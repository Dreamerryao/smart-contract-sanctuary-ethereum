// SPDX-License-Identifier: MIT
pragma solidity ^0.8.13;

//import "hardhat/console.sol";

import "@openzeppelin/contracts/access/AccessControlEnumerable.sol";
import "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";

import "../Interfaces/GoDaoInterfaces/GoDaoDataInterface.sol";

import "../Interfaces/GoDaoInterfaces/GoDaoStoreInterface.sol";

contract GoDaoStoreContract is AccessControlEnumerable, GoDaoStoreInterface {
    
    using SafeERC20 for IERC20;

    bytes32 public constant ADMIN_ROLE = keccak256("ADMIN_ROLE");
    bytes32 public constant MODERATOR_ROLE = keccak256("MODERATOR_ROLE");
    bytes32 public constant SYSTEM_CONTRACT_ROLE = keccak256("SYSTEM_CONTRACT_ROLE");

    uint private constant PLUGIN_NAME = 0;
    uint private constant PLUGIN_ADDRESS = 1;
    uint private constant PLUGIN_PRICE = 2;
    uint private constant PLUGIN_METADATAURL = 3;
    uint private constant PLUGIN_ACTIVE = 4;

    GoDaoDataInterface private _dataInstance;
    IERC20 private _baseTokenInstance;

    bool private _isActive;
    bool private _isInitialised;

    mapping(bytes32 => uint) private _pluginArrayIndex;
    bytes32[] private _pluginArray;
    mapping(bytes32 => PluginData) private _pluginDataMap;

    constructor() {
        _setupRole(DEFAULT_ADMIN_ROLE, _msgSender());
    }

    // **************************************************
    // ****************** PUBLIC REGION *****************
    // **************************************************
    function buyPlugin(address accountAddress_, bytes32 pluginId_) public override {
        require(_dataInstance.getIsAccountWhitelisted(accountAddress_), "Account is not whitelisted!");
        require(_dataInstance.getIsPluginActive(pluginId_), "Plugin is not active!");

        _dataInstance.addAccountToPlugin(accountAddress_, pluginId_);
        emit PluginBought(_msgSender(), accountAddress_, pluginId_, getPluginData(pluginId_).pluginPrice);
        if (getPluginData(pluginId_).pluginPrice > 0) {
            _baseTokenInstance.safeTransferFrom(_msgSender(), _dataInstance.getFeeAddress(pluginId_), getPluginData(pluginId_).pluginPrice);
        }
    }

    // **************************************************
    // ************** MODERATOR_ROLE REGION *************
    // **************************************************
    function addPlugin(
        bytes32 pluginId_, 
        string memory pluginName_, 
        uint pluginPrice_, 
        address pluginAddress_, 
        string memory pluginMetadataUrl_,
        bool active_) public override onlyRole(MODERATOR_ROLE) {

        require(!getPluginExists(pluginId_), "Plugin is already added!");
        _pluginArray.push(pluginId_);
        _pluginDataMap[pluginId_] = PluginData(
            pluginId_, 
            pluginName_, 
            pluginAddress_, 
            pluginPrice_, 
            pluginMetadataUrl_, 
            active_, 
            true
        );
        _pluginArrayIndex[pluginId_] = getPluginCount() - 1;

        emit PluginAdded(pluginId_, _pluginArrayIndex[pluginId_], pluginAddress_);
    }

    function removePlugin(bytes32 pluginId_) public override onlyRole(MODERATOR_ROLE) {
        require(getPluginExists(pluginId_), "Plugin does not exist!");

    }

    function switchPlugins(bytes32 firstPluginId_, bytes32 secondPluginId_) public override onlyRole(MODERATOR_ROLE) {
        require(getPluginExists(firstPluginId_), "First plugin does not exist!");
        require(getPluginExists(secondPluginId_), "Second plugin does not exist!");

        uint firstPluginArrayId =  _pluginArrayIndex[firstPluginId_];
        uint secondPluginArrayId =  _pluginArrayIndex[secondPluginId_];

        _pluginArray[firstPluginArrayId] = secondPluginId_;
        _pluginArrayIndex[secondPluginId_] = firstPluginArrayId;

        _pluginArray[secondPluginArrayId] = firstPluginId_;
        _pluginArrayIndex[firstPluginId_] = secondPluginArrayId;

        emit PluginsSwitched(firstPluginId_, secondPluginId_, firstPluginArrayId, secondPluginArrayId);
    }

    function editPluginData(bytes32 pluginId_, uint paramId_, bytes memory newValue_) public override onlyRole(MODERATOR_ROLE) {
        require(getPluginExists(pluginId_), "Plugin does not exist!");

        if (paramId_ == PLUGIN_NAME) {
            string memory pluginNameNewValue = abi.decode(newValue_, (string));
            require(keccak256(bytes(_pluginDataMap[pluginId_].pluginName)) != keccak256(newValue_), "Vaule is already set!");
            emit PluginDataChanged(pluginId_, paramId_, abi.encode(_pluginDataMap[pluginId_].pluginName), newValue_);
            _pluginDataMap[pluginId_].pluginName = pluginNameNewValue;
        } else if (paramId_ == PLUGIN_ADDRESS) {
            address pluginAddressNewValue = abi.decode(newValue_, (address));
            require(_pluginDataMap[pluginId_].pluginAddress != pluginAddressNewValue, "Vaule is already set!");
            emit PluginDataChanged(pluginId_, paramId_, abi.encode(_pluginDataMap[pluginId_].pluginAddress), newValue_);            
            _pluginDataMap[pluginId_].pluginAddress = pluginAddressNewValue;
        } else if (paramId_ == PLUGIN_PRICE) {
            uint pluginPriceNewValue = abi.decode(newValue_, (uint));
            require(_pluginDataMap[pluginId_].pluginPrice != pluginPriceNewValue, "Vaule is already set!");
            emit PluginDataChanged(pluginId_, paramId_, abi.encode(_pluginDataMap[pluginId_].pluginPrice), newValue_);            
            _pluginDataMap[pluginId_].pluginPrice = pluginPriceNewValue;
        }else if (paramId_ == PLUGIN_METADATAURL) {
            string memory pluginMetadataUrlNewValue = abi.decode(newValue_, (string));
            require(keccak256(bytes(_pluginDataMap[pluginId_].pluginMetadataUrl)) != keccak256(newValue_), "Vaule is already set!");
            emit PluginDataChanged(pluginId_, paramId_, abi.encode(_pluginDataMap[pluginId_].pluginMetadataUrl), newValue_);            
            _pluginDataMap[pluginId_].pluginMetadataUrl = pluginMetadataUrlNewValue;
        }else if (paramId_ == PLUGIN_ACTIVE) {
            bool pluginActiveNewValue = abi.decode(newValue_, (bool));
            require(_pluginDataMap[pluginId_].active != pluginActiveNewValue, "Vaule is already set!");
            emit PluginDataChanged(pluginId_, paramId_, abi.encode(_pluginDataMap[pluginId_].active), newValue_);            
            _pluginDataMap[pluginId_].active = pluginActiveNewValue;
        } else {
            revert("Param id is out of range!");
        }
    }

    // **************************************************
    // ************** DEFAULT ADMIN REGION **************
    // **************************************************
    function init(
        address defaultAdminAddress_, 
        address adminAddress_, 
        address moderatorAddress_,
        address dataAddress_,
        address baseTokenAddress_) public override onlyRole(DEFAULT_ADMIN_ROLE) {
        
        require(!getIsInitialised());

        _dataInstance = GoDaoDataInterface(dataAddress_);
        _baseTokenInstance = IERC20(baseTokenAddress_);

        _setupRole(DEFAULT_ADMIN_ROLE, defaultAdminAddress_);
        _setupRole(ADMIN_ROLE, adminAddress_);
        _setupRole(MODERATOR_ROLE, moderatorAddress_);

        _setRoleAdmin(MODERATOR_ROLE, ADMIN_ROLE);
        _setRoleAdmin(SYSTEM_CONTRACT_ROLE, ADMIN_ROLE);

        _revokeRole(DEFAULT_ADMIN_ROLE, _msgSender());

        _isActive = true;
        _isInitialised = true;
    }

    function setRoleAdmin(bytes32 role_, bytes32 adminRole_) public override onlyRole(DEFAULT_ADMIN_ROLE) {
        _setRoleAdmin(role_, adminRole_);
        emit AdminRoleSet(role_, adminRole_);
    }
    function salvageTokensFromContract(address tokenAddress_, address to_, uint amount_) public override onlyRole(DEFAULT_ADMIN_ROLE) {
        IERC20(tokenAddress_).transfer(to_, amount_);
        emit TokensSalvaged(tokenAddress_, to_, amount_);
    }
    function killContract() public override onlyRole(DEFAULT_ADMIN_ROLE) {
        emit ContractKilled();
        selfdestruct(payable(msg.sender));
    }

    // **************************************************
    // ************** PUBLIC GETTERS REGION *************
    // **************************************************
    function getIsActive() public view override returns(bool) {
        return _isActive;
    }
    function getIsInitialised() public view override returns(bool) {
        return _isInitialised;
    }

    function getPluginCount() public view override returns (uint) {
        return _pluginArray.length;
    }
    function getPlugins() public view override returns (bytes32[] memory) {
        return _pluginArray;
    }
    function getPluginsPaged(uint startIndex_, uint endIndex_) public view override returns (bytes32[] memory) {

    }

    function getPluginExists(bytes32 pluginId_) public view override returns (bool) {
        if (_pluginDataMap[pluginId_].exists) { return true; }
        return false;
    }
    function getPluginData(bytes32 pluginId_) public view override returns (PluginData memory) {
        require(getPluginExists(pluginId_), "Plugin does not exists!");
        return _pluginDataMap[pluginId_];
    }
    function getPluginsDataPaged(bytes32 pluginId_, uint startIndex_, uint endIndex_) public view override returns (PluginData memory) {
        require(getPluginExists(pluginId_), "Plugin does not exists!");
        return _pluginDataMap[pluginId_];
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (access/AccessControlEnumerable.sol)

pragma solidity ^0.8.0;

import "./IAccessControlEnumerable.sol";
import "./AccessControl.sol";
import "../utils/structs/EnumerableSet.sol";

/**
 * @dev Extension of {AccessControl} that allows enumerating the members of each role.
 */
abstract contract AccessControlEnumerable is IAccessControlEnumerable, AccessControl {
    using EnumerableSet for EnumerableSet.AddressSet;

    mapping(bytes32 => EnumerableSet.AddressSet) private _roleMembers;

    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IAccessControlEnumerable).interfaceId || super.supportsInterface(interfaceId);
    }

    /**
     * @dev Returns one of the accounts that have `role`. `index` must be a
     * value between 0 and {getRoleMemberCount}, non-inclusive.
     *
     * Role bearers are not sorted in any particular way, and their ordering may
     * change at any point.
     *
     * WARNING: When using {getRoleMember} and {getRoleMemberCount}, make sure
     * you perform all queries on the same block. See the following
     * https://forum.openzeppelin.com/t/iterating-over-elements-on-enumerableset-in-openzeppelin-contracts/2296[forum post]
     * for more information.
     */
    function getRoleMember(bytes32 role, uint256 index) public view override returns (address) {
        return _roleMembers[role].at(index);
    }

    /**
     * @dev Returns the number of accounts that have `role`. Can be used
     * together with {getRoleMember} to enumerate all bearers of a role.
     */
    function getRoleMemberCount(bytes32 role) public view override returns (uint256) {
        return _roleMembers[role].length();
    }

    /**
     * @dev Overload {_grantRole} to track enumerable memberships
     */
    function _grantRole(bytes32 role, address account) internal virtual override {
        super._grantRole(role, account);
        _roleMembers[role].add(account);
    }

    /**
     * @dev Overload {_revokeRole} to track enumerable memberships
     */
    function _revokeRole(bytes32 role, address account) internal virtual override {
        super._revokeRole(role, account);
        _roleMembers[role].remove(account);
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC20/utils/SafeERC20.sol)

pragma solidity ^0.8.0;

import "../IERC20.sol";
import "../../../utils/Address.sol";

/**
 * @title SafeERC20
 * @dev Wrappers around ERC20 operations that throw on failure (when the token
 * contract returns false). Tokens that return no value (and instead revert or
 * throw on failure) are also supported, non-reverting calls are assumed to be
 * successful.
 * To use this library you can add a `using SafeERC20 for IERC20;` statement to your contract,
 * which allows you to call the safe operations as `token.safeTransfer(...)`, etc.
 */
library SafeERC20 {
    using Address for address;

    function safeTransfer(
        IERC20 token,
        address to,
        uint256 value
    ) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transfer.selector, to, value));
    }

    function safeTransferFrom(
        IERC20 token,
        address from,
        address to,
        uint256 value
    ) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transferFrom.selector, from, to, value));
    }

    /**
     * @dev Deprecated. This function has issues similar to the ones found in
     * {IERC20-approve}, and its usage is discouraged.
     *
     * Whenever possible, use {safeIncreaseAllowance} and
     * {safeDecreaseAllowance} instead.
     */
    function safeApprove(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        // safeApprove should only be called when setting an initial allowance,
        // or when resetting it to zero. To increase and decrease it, use
        // 'safeIncreaseAllowance' and 'safeDecreaseAllowance'
        require(
            (value == 0) || (token.allowance(address(this), spender) == 0),
            "SafeERC20: approve from non-zero to non-zero allowance"
        );
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, value));
    }

    function safeIncreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        uint256 newAllowance = token.allowance(address(this), spender) + value;
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
    }

    function safeDecreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        unchecked {
            uint256 oldAllowance = token.allowance(address(this), spender);
            require(oldAllowance >= value, "SafeERC20: decreased allowance below zero");
            uint256 newAllowance = oldAllowance - value;
            _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
        }
    }

    /**
     * @dev Imitates a Solidity high-level call (i.e. a regular function call to a contract), relaxing the requirement
     * on the return value: the return value is optional (but if data is returned, it must not be false).
     * @param token The token targeted by the call.
     * @param data The call data (encoded using abi.encode or one of its variants).
     */
    function _callOptionalReturn(IERC20 token, bytes memory data) private {
        // We need to perform a low level call here, to bypass Solidity's return data size checking mechanism, since
        // we're implementing it ourselves. We use {Address.functionCall} to perform this call, which verifies that
        // the target address contains contract code and also asserts for success in the low-level call.

        bytes memory returndata = address(token).functionCall(data, "SafeERC20: low-level call failed");
        if (returndata.length > 0) {
            // Return data is optional
            require(abi.decode(returndata, (bool)), "SafeERC20: ERC20 operation did not succeed");
        }
    }
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.13;

//import "hardhat/console.sol";

import "@openzeppelin/contracts/access/AccessControlEnumerable.sol";

interface GoDaoDataInterface is IAccessControlEnumerable {

    /* solhint-disable */
    function ADMIN_ROLE() external pure returns(bytes32);
    function MODERATOR_ROLE() external pure returns(bytes32);
    function SYSTEM_CONTRACT_ROLE() external pure returns(bytes32);
    /* solhint-enable */

    // **************************************************
    // **************** DAO CALL REGION *****************
    // **************************************************
    function removeSelfFromPlugin(bytes32 pluginId_) external;

    // **************************************************
    // *************** PLUGIN CALL REGION ***************
    // **************************************************
    function setSelfData(bytes32 pluginId_, uint dataId_, bytes memory newValue_) external;

    // **************************************************
    // ************** MODERATOR_ROLE REGION *************
    // **************************************************
    function manuallyChangeDefaultFeeAddress(address newValue_) external;
    function manuallyChangePluginFeeAddress(bytes32 pluginId_, address newValue_) external;

    function manuallyAddPlugin(address pluginAddress_, uint pluginGroupId_) external;
    function manuallyRemovePlugin(bytes32 pluginId_) external;

    function manuallyWhitelistAccount(address accountAddress_, bool newValue_) external;
    function manuallyAddAccountToPlugin(address accountAddress_, bytes32 pluginId_) external;
    function manuallyRemoveAccountFromPlugin(address accountAddress_, bytes32 pluginId_) external;
    function manuallySetPluginsData(bytes32 pluginId_, address accountAddress_, uint dataId_, bytes memory newValue_) external;

    function manuallySetDaosDataAddress(address accountAddress_, address newDataAddress_) external;

    // **************************************************
    // *********** SYSTEM_CONTRACT_ROLE REGION **********
    // **************************************************
    function whitelistAccount(address accountAddress_) external;

    function addAccountToPlugin(address accountAddress_, bytes32 pluginId_) external;
    function removeAccountFromPlugin(address accountAddress_, bytes32 pluginId_) external;
    function setPluginsData(bytes32 pluginId_, address accountAddress_, uint dataId_, bytes memory newValue_) external;


    // **************************************************
    // ************** DEFAULT ADMIN REGION **************
    // **************************************************
    function init(
        address defaultAdminAddress_,
        address adminAddress_,
        address moderatorAddress_,
        address storeAddress_,
        address deployerAddress_,
        address defaultFeeAddress_ ) external;

    function setRoleAdmin(bytes32 role_, bytes32 adminRole_) external;
    function salvageTokensFromContract(address tokenAddress_, address to_, uint amount_) external;
    function killContract() external;

    // **************************************************
    // ************** PUBLIC GETTERS REGION *************
    // **************************************************
    function getIsInitialised() external view returns (bool);
    function getFeeAddress(bytes32 pluginId_) external view returns (address);
    function getDefaultFeeAddress() external view returns (address);

    function getPluginFeeAddress(bytes32 pluginId_) external view returns (address);

    function getIsPluginActive(bytes32 pluginId_) external view returns (bool);
    function getPluginAddress(bytes32 pluginId_) external view returns (address);
    function getPluginGroup(bytes32 pluginId_) external view returns (uint);
    function getPluginData(bytes32 pluginId_, address accountAddress_, uint dataId_) external view returns (bytes memory);

    function getIsAccountWhitelisted(address accountAddress_) external view returns (bool);
    function getIsAccountWhitelistedInPlugin(address accountAddress_, bytes32 pluginId_) external view returns (bool);

    function getPlugins(uint groupId_) external view returns (bytes32[] memory);
    function getPluginsPaged(uint groupId_, uint startIndex_, uint endIndex_) external view returns (bytes32[] memory);

    function getAccountsPlugins(address accountAddress_) external view returns (bytes32[] memory);
    function getAccountPluginsPaged(address accountAddress_, uint startIndex_, uint endIndex_) external view returns (bytes32[] memory);
    function getAllAccountPluginsPaged(address accountAddress_, uint startIndex_, uint endIndex_) external view returns (bytes32[] memory, bytes32[] memory);

    // **************************************************
    // ****************** EVENTS REGION *****************
    // **************************************************
    event DefaultFeeAddressChanged(address oldValue, address newValue);
    event PluginFeeAddressChanged(bytes32 indexed pluginId, address oldValue, address newValue);

    event PluginAdded(bytes32 indexed pluginId_, address indexed pluginAddress_, uint groupId);
    event PluginRemoved(bytes32 indexed pluginId_, address indexed pluginAddress_, uint groupId);

    event PluginArrayElementAdded(uint indexed groupId, uint arrayId);
    event PluginArrayElementsSwitched(uint indexed groupId, uint firstId, uint secondId);
    event PluginArrayElementPopped(uint indexed groupId, uint arrayId);

    event IsWhitelistedChanged(address indexed accountAddress, bool oldValue, bool newValue);
    event AccountAddedToThePlugin(address indexed accountAddress, bytes32 indexed pluginId, uint arrayId);
    event PluginsSwitchedInAccount(address indexed accountAddress_, uint firstId, uint secondId );
    event AccountRemovedFromThePlugin(address indexed accountAddress, bytes32 indexed pluginId, uint arrayId);
    event PluginDataChanged(bytes32 indexed pluginId_, address indexed accountAddress_, uint indexed dataId_, bytes oldValue, bytes newValue_);


    event AdminRoleSet(bytes32 role, bytes32 adminRole);
    event TokensSalvaged(address tokenAddress, address reciever, uint amount);
    event ContractKilled();
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.13;

//import "hardhat/console.sol";

import "@openzeppelin/contracts/access/AccessControlEnumerable.sol";

struct PluginData {
    bytes32 pluginId;
    string pluginName;
    address pluginAddress;
    uint pluginPrice;
    string pluginMetadataUrl;
    bool active;
    bool exists;
}

interface GoDaoStoreInterface is IAccessControlEnumerable {
    
    /* solhint-disable */
    function ADMIN_ROLE() external pure returns(bytes32);
    function MODERATOR_ROLE() external pure returns(bytes32);
    function SYSTEM_CONTRACT_ROLE() external pure returns(bytes32);
    /* solhint-enable */

    // **************************************************
    // ****************** PUBLIC REGION *****************
    // **************************************************
    function buyPlugin(address accountAddress_, bytes32 pluginId_) external;

    // **************************************************
    // ************** MODERATOR_ROLE REGION *************
    // **************************************************
    function addPlugin(
        bytes32 pluginId_, 
        string memory pluginName_, 
        uint pluginPrice_, 
        address pluginAddress_, 
        string memory pluginMetadataUrl_,
        bool active_) external;
    function removePlugin(bytes32 pluginId_) external;
    function switchPlugins(bytes32 firstPluginId_, bytes32 secondPluginId_) external;
    function editPluginData(bytes32 pluginId_, uint paramId_, bytes memory newValue_) external;

    // **************************************************
    // ************** DEFAULT ADMIN REGION **************
    // **************************************************
    function init(
        address defaultAdminAddress_,
        address adminAddress_,
        address moderatorAddress_,
        address dataAddress_,
        address baseTokenAddress_) external;

    function setRoleAdmin(bytes32 role_, bytes32 adminRole_) external;
    function salvageTokensFromContract(address tokenAddress_, address to_, uint amount_) external;
    function killContract() external;

    // **************************************************
    // *********** SYSTEM_CONTRACT_ROLE REGION **********
    // **************************************************

    // **************************************************
    // ************** PUBLIC GETTERS REGION *************
    // **************************************************
    function getIsActive() external view returns(bool);
    function getIsInitialised() external view returns(bool);

    function getPluginCount() external view returns (uint);
    function getPlugins() external view returns (bytes32[] memory);
    function getPluginsPaged(uint startIndex_, uint endIndex_) external view returns (bytes32[] memory);

    function getPluginExists(bytes32 pluginId_) external view returns (bool);
    function getPluginData(bytes32 pluginId_) external view returns (PluginData memory);
    function getPluginsDataPaged(bytes32 pluginId_, uint startIndex_, uint endIndex_) external view returns (PluginData memory);

    // **************************************************
    // ****************** EVENTS REGION *****************
    // **************************************************
    event PluginBought(address indexed senderAddress, address indexed accountAddress, bytes32 indexed pluginId, uint price);

    event PluginAdded(bytes32 indexed pluginId, uint, address indexed pluginAddress);
    event PluginDataChanged(bytes32 indexed pluginId, uint indexed paramId, bytes oldValue, bytes newValue);            
    event PluginsSwitched(bytes32 indexed firstPluginId, bytes32 indexed secondPluginId, uint firstPluginArrayId, uint secondPluginArrayId);

    event AdminRoleSet(bytes32 role, bytes32 adminRole);
    event TokensSalvaged(address tokenAddress, address reciever, uint amount);
    event ContractKilled();
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (access/IAccessControlEnumerable.sol)

pragma solidity ^0.8.0;

import "./IAccessControl.sol";

/**
 * @dev External interface of AccessControlEnumerable declared to support ERC165 detection.
 */
interface IAccessControlEnumerable is IAccessControl {
    /**
     * @dev Returns one of the accounts that have `role`. `index` must be a
     * value between 0 and {getRoleMemberCount}, non-inclusive.
     *
     * Role bearers are not sorted in any particular way, and their ordering may
     * change at any point.
     *
     * WARNING: When using {getRoleMember} and {getRoleMemberCount}, make sure
     * you perform all queries on the same block. See the following
     * https://forum.openzeppelin.com/t/iterating-over-elements-on-enumerableset-in-openzeppelin-contracts/2296[forum post]
     * for more information.
     */
    function getRoleMember(bytes32 role, uint256 index) external view returns (address);

    /**
     * @dev Returns the number of accounts that have `role`. Can be used
     * together with {getRoleMember} to enumerate all bearers of a role.
     */
    function getRoleMemberCount(bytes32 role) external view returns (uint256);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (access/AccessControl.sol)

pragma solidity ^0.8.0;

import "./IAccessControl.sol";
import "../utils/Context.sol";
import "../utils/Strings.sol";
import "../utils/introspection/ERC165.sol";

/**
 * @dev Contract module that allows children to implement role-based access
 * control mechanisms. This is a lightweight version that doesn't allow enumerating role
 * members except through off-chain means by accessing the contract event logs. Some
 * applications may benefit from on-chain enumerability, for those cases see
 * {AccessControlEnumerable}.
 *
 * Roles are referred to by their `bytes32` identifier. These should be exposed
 * in the external API and be unique. The best way to achieve this is by
 * using `public constant` hash digests:
 *
 * ```
 * bytes32 public constant MY_ROLE = keccak256("MY_ROLE");
 * ```
 *
 * Roles can be used to represent a set of permissions. To restrict access to a
 * function call, use {hasRole}:
 *
 * ```
 * function foo() public {
 *     require(hasRole(MY_ROLE, msg.sender));
 *     ...
 * }
 * ```
 *
 * Roles can be granted and revoked dynamically via the {grantRole} and
 * {revokeRole} functions. Each role has an associated admin role, and only
 * accounts that have a role's admin role can call {grantRole} and {revokeRole}.
 *
 * By default, the admin role for all roles is `DEFAULT_ADMIN_ROLE`, which means
 * that only accounts with this role will be able to grant or revoke other
 * roles. More complex role relationships can be created by using
 * {_setRoleAdmin}.
 *
 * WARNING: The `DEFAULT_ADMIN_ROLE` is also its own admin: it has permission to
 * grant and revoke this role. Extra precautions should be taken to secure
 * accounts that have been granted it.
 */
abstract contract AccessControl is Context, IAccessControl, ERC165 {
    struct RoleData {
        mapping(address => bool) members;
        bytes32 adminRole;
    }

    mapping(bytes32 => RoleData) private _roles;

    bytes32 public constant DEFAULT_ADMIN_ROLE = 0x00;

    /**
     * @dev Modifier that checks that an account has a specific role. Reverts
     * with a standardized message including the required role.
     *
     * The format of the revert reason is given by the following regular expression:
     *
     *  /^AccessControl: account (0x[0-9a-f]{40}) is missing role (0x[0-9a-f]{64})$/
     *
     * _Available since v4.1._
     */
    modifier onlyRole(bytes32 role) {
        _checkRole(role, _msgSender());
        _;
    }

    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IAccessControl).interfaceId || super.supportsInterface(interfaceId);
    }

    /**
     * @dev Returns `true` if `account` has been granted `role`.
     */
    function hasRole(bytes32 role, address account) public view override returns (bool) {
        return _roles[role].members[account];
    }

    /**
     * @dev Revert with a standard message if `account` is missing `role`.
     *
     * The format of the revert reason is given by the following regular expression:
     *
     *  /^AccessControl: account (0x[0-9a-f]{40}) is missing role (0x[0-9a-f]{64})$/
     */
    function _checkRole(bytes32 role, address account) internal view {
        if (!hasRole(role, account)) {
            revert(
                string(
                    abi.encodePacked(
                        "AccessControl: account ",
                        Strings.toHexString(uint160(account), 20),
                        " is missing role ",
                        Strings.toHexString(uint256(role), 32)
                    )
                )
            );
        }
    }

    /**
     * @dev Returns the admin role that controls `role`. See {grantRole} and
     * {revokeRole}.
     *
     * To change a role's admin, use {_setRoleAdmin}.
     */
    function getRoleAdmin(bytes32 role) public view override returns (bytes32) {
        return _roles[role].adminRole;
    }

    /**
     * @dev Grants `role` to `account`.
     *
     * If `account` had not been already granted `role`, emits a {RoleGranted}
     * event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     */
    function grantRole(bytes32 role, address account) public virtual override onlyRole(getRoleAdmin(role)) {
        _grantRole(role, account);
    }

    /**
     * @dev Revokes `role` from `account`.
     *
     * If `account` had been granted `role`, emits a {RoleRevoked} event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     */
    function revokeRole(bytes32 role, address account) public virtual override onlyRole(getRoleAdmin(role)) {
        _revokeRole(role, account);
    }

    /**
     * @dev Revokes `role` from the calling account.
     *
     * Roles are often managed via {grantRole} and {revokeRole}: this function's
     * purpose is to provide a mechanism for accounts to lose their privileges
     * if they are compromised (such as when a trusted device is misplaced).
     *
     * If the calling account had been revoked `role`, emits a {RoleRevoked}
     * event.
     *
     * Requirements:
     *
     * - the caller must be `account`.
     */
    function renounceRole(bytes32 role, address account) public virtual override {
        require(account == _msgSender(), "AccessControl: can only renounce roles for self");

        _revokeRole(role, account);
    }

    /**
     * @dev Grants `role` to `account`.
     *
     * If `account` had not been already granted `role`, emits a {RoleGranted}
     * event. Note that unlike {grantRole}, this function doesn't perform any
     * checks on the calling account.
     *
     * [WARNING]
     * ====
     * This function should only be called from the constructor when setting
     * up the initial roles for the system.
     *
     * Using this function in any other way is effectively circumventing the admin
     * system imposed by {AccessControl}.
     * ====
     *
     * NOTE: This function is deprecated in favor of {_grantRole}.
     */
    function _setupRole(bytes32 role, address account) internal virtual {
        _grantRole(role, account);
    }

    /**
     * @dev Sets `adminRole` as ``role``'s admin role.
     *
     * Emits a {RoleAdminChanged} event.
     */
    function _setRoleAdmin(bytes32 role, bytes32 adminRole) internal virtual {
        bytes32 previousAdminRole = getRoleAdmin(role);
        _roles[role].adminRole = adminRole;
        emit RoleAdminChanged(role, previousAdminRole, adminRole);
    }

    /**
     * @dev Grants `role` to `account`.
     *
     * Internal function without access restriction.
     */
    function _grantRole(bytes32 role, address account) internal virtual {
        if (!hasRole(role, account)) {
            _roles[role].members[account] = true;
            emit RoleGranted(role, account, _msgSender());
        }
    }

    /**
     * @dev Revokes `role` from `account`.
     *
     * Internal function without access restriction.
     */
    function _revokeRole(bytes32 role, address account) internal virtual {
        if (hasRole(role, account)) {
            _roles[role].members[account] = false;
            emit RoleRevoked(role, account, _msgSender());
        }
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/structs/EnumerableSet.sol)

pragma solidity ^0.8.0;

/**
 * @dev Library for managing
 * https://en.wikipedia.org/wiki/Set_(abstract_data_type)[sets] of primitive
 * types.
 *
 * Sets have the following properties:
 *
 * - Elements are added, removed, and checked for existence in constant time
 * (O(1)).
 * - Elements are enumerated in O(n). No guarantees are made on the ordering.
 *
 * ```
 * contract Example {
 *     // Add the library methods
 *     using EnumerableSet for EnumerableSet.AddressSet;
 *
 *     // Declare a set state variable
 *     EnumerableSet.AddressSet private mySet;
 * }
 * ```
 *
 * As of v3.3.0, sets of type `bytes32` (`Bytes32Set`), `address` (`AddressSet`)
 * and `uint256` (`UintSet`) are supported.
 */
library EnumerableSet {
    // To implement this library for multiple types with as little code
    // repetition as possible, we write it in terms of a generic Set type with
    // bytes32 values.
    // The Set implementation uses private functions, and user-facing
    // implementations (such as AddressSet) are just wrappers around the
    // underlying Set.
    // This means that we can only create new EnumerableSets for types that fit
    // in bytes32.

    struct Set {
        // Storage of set values
        bytes32[] _values;
        // Position of the value in the `values` array, plus 1 because index 0
        // means a value is not in the set.
        mapping(bytes32 => uint256) _indexes;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function _add(Set storage set, bytes32 value) private returns (bool) {
        if (!_contains(set, value)) {
            set._values.push(value);
            // The value is stored at length-1, but we add 1 to all indexes
            // and use 0 as a sentinel value
            set._indexes[value] = set._values.length;
            return true;
        } else {
            return false;
        }
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function _remove(Set storage set, bytes32 value) private returns (bool) {
        // We read and store the value's index to prevent multiple reads from the same storage slot
        uint256 valueIndex = set._indexes[value];

        if (valueIndex != 0) {
            // Equivalent to contains(set, value)
            // To delete an element from the _values array in O(1), we swap the element to delete with the last one in
            // the array, and then remove the last element (sometimes called as 'swap and pop').
            // This modifies the order of the array, as noted in {at}.

            uint256 toDeleteIndex = valueIndex - 1;
            uint256 lastIndex = set._values.length - 1;

            if (lastIndex != toDeleteIndex) {
                bytes32 lastvalue = set._values[lastIndex];

                // Move the last value to the index where the value to delete is
                set._values[toDeleteIndex] = lastvalue;
                // Update the index for the moved value
                set._indexes[lastvalue] = valueIndex; // Replace lastvalue's index to valueIndex
            }

            // Delete the slot where the moved value was stored
            set._values.pop();

            // Delete the index for the deleted slot
            delete set._indexes[value];

            return true;
        } else {
            return false;
        }
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function _contains(Set storage set, bytes32 value) private view returns (bool) {
        return set._indexes[value] != 0;
    }

    /**
     * @dev Returns the number of values on the set. O(1).
     */
    function _length(Set storage set) private view returns (uint256) {
        return set._values.length;
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function _at(Set storage set, uint256 index) private view returns (bytes32) {
        return set._values[index];
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function _values(Set storage set) private view returns (bytes32[] memory) {
        return set._values;
    }

    // Bytes32Set

    struct Bytes32Set {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(Bytes32Set storage set, bytes32 value) internal returns (bool) {
        return _add(set._inner, value);
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(Bytes32Set storage set, bytes32 value) internal returns (bool) {
        return _remove(set._inner, value);
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(Bytes32Set storage set, bytes32 value) internal view returns (bool) {
        return _contains(set._inner, value);
    }

    /**
     * @dev Returns the number of values in the set. O(1).
     */
    function length(Bytes32Set storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(Bytes32Set storage set, uint256 index) internal view returns (bytes32) {
        return _at(set._inner, index);
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function values(Bytes32Set storage set) internal view returns (bytes32[] memory) {
        return _values(set._inner);
    }

    // AddressSet

    struct AddressSet {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(AddressSet storage set, address value) internal returns (bool) {
        return _add(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(AddressSet storage set, address value) internal returns (bool) {
        return _remove(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(AddressSet storage set, address value) internal view returns (bool) {
        return _contains(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Returns the number of values in the set. O(1).
     */
    function length(AddressSet storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(AddressSet storage set, uint256 index) internal view returns (address) {
        return address(uint160(uint256(_at(set._inner, index))));
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function values(AddressSet storage set) internal view returns (address[] memory) {
        bytes32[] memory store = _values(set._inner);
        address[] memory result;

        assembly {
            result := store
        }

        return result;
    }

    // UintSet

    struct UintSet {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(UintSet storage set, uint256 value) internal returns (bool) {
        return _add(set._inner, bytes32(value));
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(UintSet storage set, uint256 value) internal returns (bool) {
        return _remove(set._inner, bytes32(value));
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(UintSet storage set, uint256 value) internal view returns (bool) {
        return _contains(set._inner, bytes32(value));
    }

    /**
     * @dev Returns the number of values on the set. O(1).
     */
    function length(UintSet storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(UintSet storage set, uint256 index) internal view returns (uint256) {
        return uint256(_at(set._inner, index));
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function values(UintSet storage set) internal view returns (uint256[] memory) {
        bytes32[] memory store = _values(set._inner);
        uint256[] memory result;

        assembly {
            result := store
        }

        return result;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (access/IAccessControl.sol)

pragma solidity ^0.8.0;

/**
 * @dev External interface of AccessControl declared to support ERC165 detection.
 */
interface IAccessControl {
    /**
     * @dev Emitted when `newAdminRole` is set as ``role``'s admin role, replacing `previousAdminRole`
     *
     * `DEFAULT_ADMIN_ROLE` is the starting admin for all roles, despite
     * {RoleAdminChanged} not being emitted signaling this.
     *
     * _Available since v3.1._
     */
    event RoleAdminChanged(bytes32 indexed role, bytes32 indexed previousAdminRole, bytes32 indexed newAdminRole);

    /**
     * @dev Emitted when `account` is granted `role`.
     *
     * `sender` is the account that originated the contract call, an admin role
     * bearer except when using {AccessControl-_setupRole}.
     */
    event RoleGranted(bytes32 indexed role, address indexed account, address indexed sender);

    /**
     * @dev Emitted when `account` is revoked `role`.
     *
     * `sender` is the account that originated the contract call:
     *   - if using `revokeRole`, it is the admin role bearer
     *   - if using `renounceRole`, it is the role bearer (i.e. `account`)
     */
    event RoleRevoked(bytes32 indexed role, address indexed account, address indexed sender);

    /**
     * @dev Returns `true` if `account` has been granted `role`.
     */
    function hasRole(bytes32 role, address account) external view returns (bool);

    /**
     * @dev Returns the admin role that controls `role`. See {grantRole} and
     * {revokeRole}.
     *
     * To change a role's admin, use {AccessControl-_setRoleAdmin}.
     */
    function getRoleAdmin(bytes32 role) external view returns (bytes32);

    /**
     * @dev Grants `role` to `account`.
     *
     * If `account` had not been already granted `role`, emits a {RoleGranted}
     * event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     */
    function grantRole(bytes32 role, address account) external;

    /**
     * @dev Revokes `role` from `account`.
     *
     * If `account` had been granted `role`, emits a {RoleRevoked} event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     */
    function revokeRole(bytes32 role, address account) external;

    /**
     * @dev Revokes `role` from the calling account.
     *
     * Roles are often managed via {grantRole} and {revokeRole}: this function's
     * purpose is to provide a mechanism for accounts to lose their privileges
     * if they are compromised (such as when a trusted device is misplaced).
     *
     * If the calling account had been granted `role`, emits a {RoleRevoked}
     * event.
     *
     * Requirements:
     *
     * - the caller must be `account`.
     */
    function renounceRole(bytes32 role, address account) external;
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Strings.sol)

pragma solidity ^0.8.0;

/**
 * @dev String operations.
 */
library Strings {
    bytes16 private constant _HEX_SYMBOLS = "0123456789abcdef";

    /**
     * @dev Converts a `uint256` to its ASCII `string` decimal representation.
     */
    function toString(uint256 value) internal pure returns (string memory) {
        // Inspired by OraclizeAPI's implementation - MIT licence
        // https://github.com/oraclize/ethereum-api/blob/b42146b063c7d6ee1358846c198246239e9360e8/oraclizeAPI_0.4.25.sol

        if (value == 0) {
            return "0";
        }
        uint256 temp = value;
        uint256 digits;
        while (temp != 0) {
            digits++;
            temp /= 10;
        }
        bytes memory buffer = new bytes(digits);
        while (value != 0) {
            digits -= 1;
            buffer[digits] = bytes1(uint8(48 + uint256(value % 10)));
            value /= 10;
        }
        return string(buffer);
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation.
     */
    function toHexString(uint256 value) internal pure returns (string memory) {
        if (value == 0) {
            return "0x00";
        }
        uint256 temp = value;
        uint256 length = 0;
        while (temp != 0) {
            length++;
            temp >>= 8;
        }
        return toHexString(value, length);
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation with fixed length.
     */
    function toHexString(uint256 value, uint256 length) internal pure returns (string memory) {
        bytes memory buffer = new bytes(2 * length + 2);
        buffer[0] = "0";
        buffer[1] = "x";
        for (uint256 i = 2 * length + 1; i > 1; --i) {
            buffer[i] = _HEX_SYMBOLS[value & 0xf];
            value >>= 4;
        }
        require(value == 0, "Strings: hex length insufficient");
        return string(buffer);
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/introspection/ERC165.sol)

pragma solidity ^0.8.0;

import "./IERC165.sol";

/**
 * @dev Implementation of the {IERC165} interface.
 *
 * Contracts that want to implement ERC165 should inherit from this contract and override {supportsInterface} to check
 * for the additional interface id that will be supported. For example:
 *
 * ```solidity
 * function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
 *     return interfaceId == type(MyInterface).interfaceId || super.supportsInterface(interfaceId);
 * }
 * ```
 *
 * Alternatively, {ERC165Storage} provides an easier to use but more expensive implementation.
 */
abstract contract ERC165 is IERC165 {
    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IERC165).interfaceId;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/introspection/IERC165.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC165 standard, as defined in the
 * https://eips.ethereum.org/EIPS/eip-165[EIP].
 *
 * Implementers can declare support of contract interfaces, which can then be
 * queried by others ({ERC165Checker}).
 *
 * For an implementation, see {ERC165}.
 */
interface IERC165 {
    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30 000 gas.
     */
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC20/IERC20.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `recipient`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address recipient, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `sender` to `recipient` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address sender,
        address recipient,
        uint256 amount
    ) external returns (bool);

    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Address.sol)

pragma solidity ^0.8.0;

/**
 * @dev Collection of functions related to the address type
 */
library Address {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize, which returns 0 for contracts in
        // construction, since the code is only stored at the end of the
        // constructor execution.

        uint256 size;
        assembly {
            size := extcodesize(account)
        }
        return size > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCall(target, data, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        require(isContract(target), "Address: call to non-contract");

        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        require(isContract(target), "Address: static call to non-contract");

        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionDelegateCall(target, data, "Address: low-level delegate call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(isContract(target), "Address: delegate call to non-contract");

        (bool success, bytes memory returndata) = target.delegatecall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verifies that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly

                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}