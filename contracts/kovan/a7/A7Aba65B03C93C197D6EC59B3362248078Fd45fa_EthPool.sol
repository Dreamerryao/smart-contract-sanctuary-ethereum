//SPDX-License-Identifier: Unlicense
pragma solidity 0.8.4;

import "@openzeppelin/contracts/access/AccessControl.sol";
import "@openzeppelin/contracts/security/ReentrancyGuard.sol";
import "./interfaces/IEthPool.sol";


/**
 *  d: user deposit
 *  r: reward deposit
 *  w: first user withdrawl
 *  w*: user withdrawl now
 *
 *  timeline:
 *  ---- d ---- r ---- w ---- d ---- r ---- d ---- w* ---->
 *
 *  ---------------------------------------->                total deposit
 *  --------------------------------->                       rewards
 *  ------------------->                                     reward on last withdrawl
 *  ------------------->                                     deposit before last withdrawl
 *                                   +------>                deposit after last reward
 *                     +------------->                       deposits applicable to this part participates in user reward calculation
 *
 *  user reward =
 *     (rewards - reward on last withdrawl) *
 *     (total deposit -  deposit before last withdraw - deposit after last withdrawl) /
 *     total deposit before last rewards
 *
 */
contract EthPool is IEthPool, AccessControl, ReentrancyGuard {

  struct UserInfo {
    uint256 totalDeposit;
    uint256 depositAfterLastReward;
    uint256 depositBeforeLastWithdraw;
    uint256 lastDepositBlock;
    uint256 lastWithdrawBlock;
    /// @dev rewards deposited until the user's last withdrawal
    uint256 rewardOnLastWithdraw;
    /// @dev total deposit made from all users until the user's last withdrawl
    uint256 totalDepositBeforeLastWithdraw;
  }

  bytes32 constant TEAM_ROLE = DEFAULT_ADMIN_ROLE;

  /// @dev total rewards deposited by team members
  uint256 public rewards;

  /// @dev total deposit made by users
  uint256 public totalDeposit;

  /// @dev block where the last reward is deposited
  uint256 public lastRewardBlock;

  /// @dev total deposit made by all users until the last reward deposit
  uint256 public totalDepositBeforeLastReward;

  /// @dev user address => user info
  mapping(address => UserInfo) public userInfos;


  receive() external payable { }

  constructor() ReentrancyGuard() {
    _setupRole(TEAM_ROLE, msg.sender);
  }

  /**
   * @dev check if he is a team member
   */
  modifier onlyTeamMember() {
    require(hasRole(TEAM_ROLE, msg.sender), "Not team member");
    _;
  }

  /**
   * @dev put reward to pool
   */
  function depositRewards() payable external override onlyTeamMember {
    rewards += msg.value;
    totalDepositBeforeLastReward = totalDeposit;
    lastRewardBlock = block.number;

    emit RewardDeposited(msg.sender, msg.value);
  }

  /**
   * @dev make user deposit
   */
  function deposit() payable external override {
    UserInfo storage userInfo = userInfos[msg.sender];

    totalDeposit += msg.value; 
    userInfo.totalDeposit += msg.value;

    if (userInfo.lastWithdrawBlock < lastRewardBlock) {

      // note: this should be EQUAL or less than
      // otherwise depositAfterLastReward will never be reset
      // if reward and deposit happens on the same block
      if (userInfo.lastDepositBlock <= lastRewardBlock) {
        userInfo.depositAfterLastReward = 0;
      }

      userInfo.depositAfterLastReward += msg.value;
      userInfo.lastDepositBlock = block.number;
    }

    emit Deposited(msg.sender, msg.value);
  }

  /**
   * @dev get reward amount of user (except deposit)
   * @param user address of user
   * @return reward
   */
  function rewardBalanceOf(address user) public override view returns (uint256) {
    UserInfo storage userInfo = userInfos[user];
    uint256 depositAfterLastReward = userInfo.depositAfterLastReward;
    uint256 totalDepositSinceLastWithdraw = totalDepositBeforeLastReward - userInfo.totalDepositBeforeLastWithdraw;
    uint256 userDeposit;

    if (userInfo.lastDepositBlock < lastRewardBlock) {
      // no user deposit is made after the last reward deposit
      // hence user.depositAFterReward is not updated, we use 0
      depositAfterLastReward = 0;
    }

    // calcualte user deposit made between his last withdrawl and the last reward deposit
    // since user reward is calulated from user deposit made before the reward deposit
    if (userInfo.lastWithdrawBlock < lastRewardBlock) {
      userDeposit = userInfo.totalDeposit - userInfo.depositBeforeLastWithdraw - depositAfterLastReward;
    }

    if (totalDepositSinceLastWithdraw == 0) {
      return 0;
    }

    // if the above condition does not meet, it is when no reward deposit is made after the last withdrawl
    // we leave user deposit as 0 though user deposits are made after the last withdrawl
    // because they don't participate in user reward calculation

    // A = reward deposit after the last user withdrawl *
    // B = user deposit between his last withdrawl and the last reward deposit
    //     (user deposit made after the last reward deposit won't affect on reward calculation)
    // C = total deposit made from all users between the user's last withdrawl and the last reward deposit
    // user reward = A * B / C
    return (rewards - userInfo.rewardOnLastWithdraw) * userDeposit / totalDepositSinceLastWithdraw;
  }

  /**
   * @dev get amount available to withdraw
   * @param user address of user
   * @return value
   */
  function withdrawBalanceOf(address user) public override view returns (uint256) {
    UserInfo storage userInfo = userInfos[user];
    return userInfo.totalDeposit - userInfo.depositBeforeLastWithdraw + rewardBalanceOf(user);
  }

  /**
   * @dev withdraw user deposit and reward to recipient
   * @param recipient recipient address
   */
  function withdraw(address payable recipient) external override nonReentrant {
    uint256 amount = withdrawBalanceOf(msg.sender);

    if (amount > 0) {
      UserInfo storage userInfo = userInfos[msg.sender];

      userInfo.depositBeforeLastWithdraw = userInfo.totalDeposit;
      userInfo.lastWithdrawBlock = block.number;
      userInfo.rewardOnLastWithdraw = rewards;
      userInfo.totalDepositBeforeLastWithdraw = totalDepositBeforeLastReward;

      (bool success, ) = recipient.call{value: amount}("");
      require(success, "Failed to withdraw");
    }

    emit Withdrawn(msg.sender, amount);
  }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "../utils/Context.sol";
import "../utils/Strings.sol";
import "../utils/introspection/ERC165.sol";

/**
 * @dev External interface of AccessControl declared to support ERC165 detection.
 */
interface IAccessControl {
    function hasRole(bytes32 role, address account) external view returns (bool);
    function getRoleAdmin(bytes32 role) external view returns (bytes32);
    function grantRole(bytes32 role, address account) external;
    function revokeRole(bytes32 role, address account) external;
    function renounceRole(bytes32 role, address account) external;
}

/**
 * @dev Contract module that allows children to implement role-based access
 * control mechanisms. This is a lightweight version that doesn't allow enumerating role
 * members except through off-chain means by accessing the contract event logs. Some
 * applications may benefit from on-chain enumerability, for those cases see
 * {AccessControlEnumerable}.
 *
 * Roles are referred to by their `bytes32` identifier. These should be exposed
 * in the external API and be unique. The best way to achieve this is by
 * using `public constant` hash digests:
 *
 * ```
 * bytes32 public constant MY_ROLE = keccak256("MY_ROLE");
 * ```
 *
 * Roles can be used to represent a set of permissions. To restrict access to a
 * function call, use {hasRole}:
 *
 * ```
 * function foo() public {
 *     require(hasRole(MY_ROLE, msg.sender));
 *     ...
 * }
 * ```
 *
 * Roles can be granted and revoked dynamically via the {grantRole} and
 * {revokeRole} functions. Each role has an associated admin role, and only
 * accounts that have a role's admin role can call {grantRole} and {revokeRole}.
 *
 * By default, the admin role for all roles is `DEFAULT_ADMIN_ROLE`, which means
 * that only accounts with this role will be able to grant or revoke other
 * roles. More complex role relationships can be created by using
 * {_setRoleAdmin}.
 *
 * WARNING: The `DEFAULT_ADMIN_ROLE` is also its own admin: it has permission to
 * grant and revoke this role. Extra precautions should be taken to secure
 * accounts that have been granted it.
 */
abstract contract AccessControl is Context, IAccessControl, ERC165 {
    struct RoleData {
        mapping (address => bool) members;
        bytes32 adminRole;
    }

    mapping (bytes32 => RoleData) private _roles;

    bytes32 public constant DEFAULT_ADMIN_ROLE = 0x00;

    /**
     * @dev Emitted when `newAdminRole` is set as ``role``'s admin role, replacing `previousAdminRole`
     *
     * `DEFAULT_ADMIN_ROLE` is the starting admin for all roles, despite
     * {RoleAdminChanged} not being emitted signaling this.
     *
     * _Available since v3.1._
     */
    event RoleAdminChanged(bytes32 indexed role, bytes32 indexed previousAdminRole, bytes32 indexed newAdminRole);

    /**
     * @dev Emitted when `account` is granted `role`.
     *
     * `sender` is the account that originated the contract call, an admin role
     * bearer except when using {_setupRole}.
     */
    event RoleGranted(bytes32 indexed role, address indexed account, address indexed sender);

    /**
     * @dev Emitted when `account` is revoked `role`.
     *
     * `sender` is the account that originated the contract call:
     *   - if using `revokeRole`, it is the admin role bearer
     *   - if using `renounceRole`, it is the role bearer (i.e. `account`)
     */
    event RoleRevoked(bytes32 indexed role, address indexed account, address indexed sender);

    /**
     * @dev Modifier that checks that an account has a specific role. Reverts
     * with a standardized message including the required role.
     *
     * The format of the revert reason is given by the following regular expression:
     *
     *  /^AccessControl: account (0x[0-9a-f]{20}) is missing role (0x[0-9a-f]{32})$/
     *
     * _Available since v4.1._
     */
    modifier onlyRole(bytes32 role) {
        _checkRole(role, _msgSender());
        _;
    }

    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IAccessControl).interfaceId
            || super.supportsInterface(interfaceId);
    }

    /**
     * @dev Returns `true` if `account` has been granted `role`.
     */
    function hasRole(bytes32 role, address account) public view override returns (bool) {
        return _roles[role].members[account];
    }

    /**
     * @dev Revert with a standard message if `account` is missing `role`.
     *
     * The format of the revert reason is given by the following regular expression:
     *
     *  /^AccessControl: account (0x[0-9a-f]{20}) is missing role (0x[0-9a-f]{32})$/
     */
    function _checkRole(bytes32 role, address account) internal view {
        if(!hasRole(role, account)) {
            revert(string(abi.encodePacked(
                "AccessControl: account ",
                Strings.toHexString(uint160(account), 20),
                " is missing role ",
                Strings.toHexString(uint256(role), 32)
            )));
        }
    }

    /**
     * @dev Returns the admin role that controls `role`. See {grantRole} and
     * {revokeRole}.
     *
     * To change a role's admin, use {_setRoleAdmin}.
     */
    function getRoleAdmin(bytes32 role) public view override returns (bytes32) {
        return _roles[role].adminRole;
    }

    /**
     * @dev Grants `role` to `account`.
     *
     * If `account` had not been already granted `role`, emits a {RoleGranted}
     * event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     */
    function grantRole(bytes32 role, address account) public virtual override onlyRole(getRoleAdmin(role)) {
        _grantRole(role, account);
    }

    /**
     * @dev Revokes `role` from `account`.
     *
     * If `account` had been granted `role`, emits a {RoleRevoked} event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     */
    function revokeRole(bytes32 role, address account) public virtual override onlyRole(getRoleAdmin(role)) {
        _revokeRole(role, account);
    }

    /**
     * @dev Revokes `role` from the calling account.
     *
     * Roles are often managed via {grantRole} and {revokeRole}: this function's
     * purpose is to provide a mechanism for accounts to lose their privileges
     * if they are compromised (such as when a trusted device is misplaced).
     *
     * If the calling account had been granted `role`, emits a {RoleRevoked}
     * event.
     *
     * Requirements:
     *
     * - the caller must be `account`.
     */
    function renounceRole(bytes32 role, address account) public virtual override {
        require(account == _msgSender(), "AccessControl: can only renounce roles for self");

        _revokeRole(role, account);
    }

    /**
     * @dev Grants `role` to `account`.
     *
     * If `account` had not been already granted `role`, emits a {RoleGranted}
     * event. Note that unlike {grantRole}, this function doesn't perform any
     * checks on the calling account.
     *
     * [WARNING]
     * ====
     * This function should only be called from the constructor when setting
     * up the initial roles for the system.
     *
     * Using this function in any other way is effectively circumventing the admin
     * system imposed by {AccessControl}.
     * ====
     */
    function _setupRole(bytes32 role, address account) internal virtual {
        _grantRole(role, account);
    }

    /**
     * @dev Sets `adminRole` as ``role``'s admin role.
     *
     * Emits a {RoleAdminChanged} event.
     */
    function _setRoleAdmin(bytes32 role, bytes32 adminRole) internal virtual {
        emit RoleAdminChanged(role, getRoleAdmin(role), adminRole);
        _roles[role].adminRole = adminRole;
    }

    function _grantRole(bytes32 role, address account) private {
        if (!hasRole(role, account)) {
            _roles[role].members[account] = true;
            emit RoleGranted(role, account, _msgSender());
        }
    }

    function _revokeRole(bytes32 role, address account) private {
        if (hasRole(role, account)) {
            _roles[role].members[account] = false;
            emit RoleRevoked(role, account, _msgSender());
        }
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev Contract module that helps prevent reentrant calls to a function.
 *
 * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier
 * available, which can be applied to functions to make sure there are no nested
 * (reentrant) calls to them.
 *
 * Note that because there is a single `nonReentrant` guard, functions marked as
 * `nonReentrant` may not call one another. This can be worked around by making
 * those functions `private`, and then adding `external` `nonReentrant` entry
 * points to them.
 *
 * TIP: If you would like to learn more about reentrancy and alternative ways
 * to protect against it, check out our blog post
 * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].
 */
abstract contract ReentrancyGuard {
    // Booleans are more expensive than uint256 or any type that takes up a full
    // word because each write operation emits an extra SLOAD to first read the
    // slot's contents, replace the bits taken up by the boolean, and then write
    // back. This is the compiler's defense against contract upgrades and
    // pointer aliasing, and it cannot be disabled.

    // The values being non-zero value makes deployment a bit more expensive,
    // but in exchange the refund on every call to nonReentrant will be lower in
    // amount. Since refunds are capped to a percentage of the total
    // transaction's gas, it is best to keep them low in cases like this one, to
    // increase the likelihood of the full refund coming into effect.
    uint256 private constant _NOT_ENTERED = 1;
    uint256 private constant _ENTERED = 2;

    uint256 private _status;

    constructor () {
        _status = _NOT_ENTERED;
    }

    /**
     * @dev Prevents a contract from calling itself, directly or indirectly.
     * Calling a `nonReentrant` function from another `nonReentrant`
     * function is not supported. It is possible to prevent this from happening
     * by making the `nonReentrant` function external, and make it call a
     * `private` function that does the actual work.
     */
    modifier nonReentrant() {
        // On the first call to nonReentrant, _notEntered will be true
        require(_status != _ENTERED, "ReentrancyGuard: reentrant call");

        // Any calls to nonReentrant after this point will fail
        _status = _ENTERED;

        _;

        // By storing the original value once again, a refund is triggered (see
        // https://eips.ethereum.org/EIPS/eip-2200)
        _status = _NOT_ENTERED;
    }
}

//SPDX-License-Identifier: Unlicense
pragma solidity 0.8.4;

interface IEthPool {
  event RewardDeposited(address indexed depositor, uint256 amount);

  event Deposited(address indexed depositor, uint256 amount);

  event Withdrawn(address indexed withdrawer, uint256 amount);

  function depositRewards() payable external;

  function deposit() payable external;

  function rewardBalanceOf(address user) external view returns (uint256);

  function withdrawBalanceOf(address user) external view returns (uint256);

  function withdraw(address payable recipient) external;
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/*
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        this; // silence state mutability warning without generating bytecode - see https://github.com/ethereum/solidity/issues/2691
        return msg.data;
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev String operations.
 */
library Strings {
    bytes16 private constant alphabet = "0123456789abcdef";

    /**
     * @dev Converts a `uint256` to its ASCII `string` decimal representation.
     */
    function toString(uint256 value) internal pure returns (string memory) {
        // Inspired by OraclizeAPI's implementation - MIT licence
        // https://github.com/oraclize/ethereum-api/blob/b42146b063c7d6ee1358846c198246239e9360e8/oraclizeAPI_0.4.25.sol

        if (value == 0) {
            return "0";
        }
        uint256 temp = value;
        uint256 digits;
        while (temp != 0) {
            digits++;
            temp /= 10;
        }
        bytes memory buffer = new bytes(digits);
        while (value != 0) {
            digits -= 1;
            buffer[digits] = bytes1(uint8(48 + uint256(value % 10)));
            value /= 10;
        }
        return string(buffer);
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation.
     */
    function toHexString(uint256 value) internal pure returns (string memory) {
        if (value == 0) {
            return "0x00";
        }
        uint256 temp = value;
        uint256 length = 0;
        while (temp != 0) {
            length++;
            temp >>= 8;
        }
        return toHexString(value, length);
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation with fixed length.
     */
    function toHexString(uint256 value, uint256 length) internal pure returns (string memory) {
        bytes memory buffer = new bytes(2 * length + 2);
        buffer[0] = "0";
        buffer[1] = "x";
        for (uint256 i = 2 * length + 1; i > 1; --i) {
            buffer[i] = alphabet[value & 0xf];
            value >>= 4;
        }
        require(value == 0, "Strings: hex length insufficient");
        return string(buffer);
    }

}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "./IERC165.sol";

/**
 * @dev Implementation of the {IERC165} interface.
 *
 * Contracts that want to implement ERC165 should inherit from this contract and override {supportsInterface} to check
 * for the additional interface id that will be supported. For example:
 *
 * ```solidity
 * function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
 *     return interfaceId == type(MyInterface).interfaceId || super.supportsInterface(interfaceId);
 * }
 * ```
 *
 * Alternatively, {ERC165Storage} provides an easier to use but more expensive implementation.
 */
abstract contract ERC165 is IERC165 {
    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IERC165).interfaceId;
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC165 standard, as defined in the
 * https://eips.ethereum.org/EIPS/eip-165[EIP].
 *
 * Implementers can declare support of contract interfaces, which can then be
 * queried by others ({ERC165Checker}).
 *
 * For an implementation, see {ERC165}.
 */
interface IERC165 {
    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30 000 gas.
     */
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}