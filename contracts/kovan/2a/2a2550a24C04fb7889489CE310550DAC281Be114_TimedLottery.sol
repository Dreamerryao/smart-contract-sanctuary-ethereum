/**
 *Submitted for verification at Etherscan.io on 2022-01-18
*/

// SPDX-License-Identifier: MIT

pragma solidity 0.8.11;



// Part: OpenZeppelin/[email protected]/Context

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}

// Part: smartcontractkit/[email protected]/AggregatorV3Interface

interface AggregatorV3Interface {
  function decimals() external view returns (uint8);

  function description() external view returns (string memory);

  function version() external view returns (uint256);

  // getRoundData and latestRoundData should both raise "No data present"
  // if they do not have data to report, instead of returning unset values
  // which could be misinterpreted as actual reported values.
  function getRoundData(uint80 _roundId)
    external
    view
    returns (
      uint80 roundId,
      int256 answer,
      uint256 startedAt,
      uint256 updatedAt,
      uint80 answeredInRound
    );

  function latestRoundData()
    external
    view
    returns (
      uint80 roundId,
      int256 answer,
      uint256 startedAt,
      uint256 updatedAt,
      uint80 answeredInRound
    );
}

// Part: smartcontractkit/[email protected]/LinkTokenInterface

interface LinkTokenInterface {
  function allowance(address owner, address spender) external view returns (uint256 remaining);

  function approve(address spender, uint256 value) external returns (bool success);

  function balanceOf(address owner) external view returns (uint256 balance);

  function decimals() external view returns (uint8 decimalPlaces);

  function decreaseApproval(address spender, uint256 addedValue) external returns (bool success);

  function increaseApproval(address spender, uint256 subtractedValue) external;

  function name() external view returns (string memory tokenName);

  function symbol() external view returns (string memory tokenSymbol);

  function totalSupply() external view returns (uint256 totalTokensIssued);

  function transfer(address to, uint256 value) external returns (bool success);

  function transferAndCall(
    address to,
    uint256 value,
    bytes calldata data
  ) external returns (bool success);

  function transferFrom(
    address from,
    address to,
    uint256 value
  ) external returns (bool success);
}

// Part: smartcontractkit/[email protected]/VRFRequestIDBase

contract VRFRequestIDBase {
  /**
   * @notice returns the seed which is actually input to the VRF coordinator
   *
   * @dev To prevent repetition of VRF output due to repetition of the
   * @dev user-supplied seed, that seed is combined in a hash with the
   * @dev user-specific nonce, and the address of the consuming contract. The
   * @dev risk of repetition is mostly mitigated by inclusion of a blockhash in
   * @dev the final seed, but the nonce does protect against repetition in
   * @dev requests which are included in a single block.
   *
   * @param _userSeed VRF seed input provided by user
   * @param _requester Address of the requesting contract
   * @param _nonce User-specific nonce at the time of the request
   */
  function makeVRFInputSeed(
    bytes32 _keyHash,
    uint256 _userSeed,
    address _requester,
    uint256 _nonce
  ) internal pure returns (uint256) {
    return uint256(keccak256(abi.encode(_keyHash, _userSeed, _requester, _nonce)));
  }

  /**
   * @notice Returns the id for this request
   * @param _keyHash The serviceAgreement ID to be used for this request
   * @param _vRFInputSeed The seed to be passed directly to the VRF
   * @return The id for this request
   *
   * @dev Note that _vRFInputSeed is not the seed passed by the consuming
   * @dev contract, but the one generated by makeVRFInputSeed
   */
  function makeRequestId(bytes32 _keyHash, uint256 _vRFInputSeed) internal pure returns (bytes32) {
    return keccak256(abi.encodePacked(_keyHash, _vRFInputSeed));
  }
}

// Part: OpenZeppelin/[email protected]/Ownable

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
        _;
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}

// Part: smartcontractkit/[email protected]/VRFConsumerBase

/** ****************************************************************************
 * @notice Interface for contracts using VRF randomness
 * *****************************************************************************
 * @dev PURPOSE
 *
 * @dev Reggie the Random Oracle (not his real job) wants to provide randomness
 * @dev to Vera the verifier in such a way that Vera can be sure he's not
 * @dev making his output up to suit himself. Reggie provides Vera a public key
 * @dev to which he knows the secret key. Each time Vera provides a seed to
 * @dev Reggie, he gives back a value which is computed completely
 * @dev deterministically from the seed and the secret key.
 *
 * @dev Reggie provides a proof by which Vera can verify that the output was
 * @dev correctly computed once Reggie tells it to her, but without that proof,
 * @dev the output is indistinguishable to her from a uniform random sample
 * @dev from the output space.
 *
 * @dev The purpose of this contract is to make it easy for unrelated contracts
 * @dev to talk to Vera the verifier about the work Reggie is doing, to provide
 * @dev simple access to a verifiable source of randomness.
 * *****************************************************************************
 * @dev USAGE
 *
 * @dev Calling contracts must inherit from VRFConsumerBase, and can
 * @dev initialize VRFConsumerBase's attributes in their constructor as
 * @dev shown:
 *
 * @dev   contract VRFConsumer {
 * @dev     constuctor(<other arguments>, address _vrfCoordinator, address _link)
 * @dev       VRFConsumerBase(_vrfCoordinator, _link) public {
 * @dev         <initialization with other arguments goes here>
 * @dev       }
 * @dev   }
 *
 * @dev The oracle will have given you an ID for the VRF keypair they have
 * @dev committed to (let's call it keyHash), and have told you the minimum LINK
 * @dev price for VRF service. Make sure your contract has sufficient LINK, and
 * @dev call requestRandomness(keyHash, fee, seed), where seed is the input you
 * @dev want to generate randomness from.
 *
 * @dev Once the VRFCoordinator has received and validated the oracle's response
 * @dev to your request, it will call your contract's fulfillRandomness method.
 *
 * @dev The randomness argument to fulfillRandomness is the actual random value
 * @dev generated from your seed.
 *
 * @dev The requestId argument is generated from the keyHash and the seed by
 * @dev makeRequestId(keyHash, seed). If your contract could have concurrent
 * @dev requests open, you can use the requestId to track which seed is
 * @dev associated with which randomness. See VRFRequestIDBase.sol for more
 * @dev details. (See "SECURITY CONSIDERATIONS" for principles to keep in mind,
 * @dev if your contract could have multiple requests in flight simultaneously.)
 *
 * @dev Colliding `requestId`s are cryptographically impossible as long as seeds
 * @dev differ. (Which is critical to making unpredictable randomness! See the
 * @dev next section.)
 *
 * *****************************************************************************
 * @dev SECURITY CONSIDERATIONS
 *
 * @dev A method with the ability to call your fulfillRandomness method directly
 * @dev could spoof a VRF response with any random value, so it's critical that
 * @dev it cannot be directly called by anything other than this base contract
 * @dev (specifically, by the VRFConsumerBase.rawFulfillRandomness method).
 *
 * @dev For your users to trust that your contract's random behavior is free
 * @dev from malicious interference, it's best if you can write it so that all
 * @dev behaviors implied by a VRF response are executed *during* your
 * @dev fulfillRandomness method. If your contract must store the response (or
 * @dev anything derived from it) and use it later, you must ensure that any
 * @dev user-significant behavior which depends on that stored value cannot be
 * @dev manipulated by a subsequent VRF request.
 *
 * @dev Similarly, both miners and the VRF oracle itself have some influence
 * @dev over the order in which VRF responses appear on the blockchain, so if
 * @dev your contract could have multiple VRF requests in flight simultaneously,
 * @dev you must ensure that the order in which the VRF responses arrive cannot
 * @dev be used to manipulate your contract's user-significant behavior.
 *
 * @dev Since the ultimate input to the VRF is mixed with the block hash of the
 * @dev block in which the request is made, user-provided seeds have no impact
 * @dev on its economic security properties. They are only included for API
 * @dev compatability with previous versions of this contract.
 *
 * @dev Since the block hash of the block which contains the requestRandomness
 * @dev call is mixed into the input to the VRF *last*, a sufficiently powerful
 * @dev miner could, in principle, fork the blockchain to evict the block
 * @dev containing the request, forcing the request to be included in a
 * @dev different block with a different hash, and therefore a different input
 * @dev to the VRF. However, such an attack would incur a substantial economic
 * @dev cost. This cost scales with the number of blocks the VRF oracle waits
 * @dev until it calls responds to a request.
 */
abstract contract VRFConsumerBase is VRFRequestIDBase {
  /**
   * @notice fulfillRandomness handles the VRF response. Your contract must
   * @notice implement it. See "SECURITY CONSIDERATIONS" above for important
   * @notice principles to keep in mind when implementing your fulfillRandomness
   * @notice method.
   *
   * @dev VRFConsumerBase expects its subcontracts to have a method with this
   * @dev signature, and will call it once it has verified the proof
   * @dev associated with the randomness. (It is triggered via a call to
   * @dev rawFulfillRandomness, below.)
   *
   * @param requestId The Id initially returned by requestRandomness
   * @param randomness the VRF output
   */
  function fulfillRandomness(bytes32 requestId, uint256 randomness) internal virtual;

  /**
   * @dev In order to keep backwards compatibility we have kept the user
   * seed field around. We remove the use of it because given that the blockhash
   * enters later, it overrides whatever randomness the used seed provides.
   * Given that it adds no security, and can easily lead to misunderstandings,
   * we have removed it from usage and can now provide a simpler API.
   */
  uint256 private constant USER_SEED_PLACEHOLDER = 0;

  /**
   * @notice requestRandomness initiates a request for VRF output given _seed
   *
   * @dev The fulfillRandomness method receives the output, once it's provided
   * @dev by the Oracle, and verified by the vrfCoordinator.
   *
   * @dev The _keyHash must already be registered with the VRFCoordinator, and
   * @dev the _fee must exceed the fee specified during registration of the
   * @dev _keyHash.
   *
   * @dev The _seed parameter is vestigial, and is kept only for API
   * @dev compatibility with older versions. It can't *hurt* to mix in some of
   * @dev your own randomness, here, but it's not necessary because the VRF
   * @dev oracle will mix the hash of the block containing your request into the
   * @dev VRF seed it ultimately uses.
   *
   * @param _keyHash ID of public key against which randomness is generated
   * @param _fee The amount of LINK to send with the request
   *
   * @return requestId unique ID for this request
   *
   * @dev The returned requestId can be used to distinguish responses to
   * @dev concurrent requests. It is passed as the first argument to
   * @dev fulfillRandomness.
   */
  function requestRandomness(bytes32 _keyHash, uint256 _fee) internal returns (bytes32 requestId) {
    LINK.transferAndCall(vrfCoordinator, _fee, abi.encode(_keyHash, USER_SEED_PLACEHOLDER));
    // This is the seed passed to VRFCoordinator. The oracle will mix this with
    // the hash of the block containing this request to obtain the seed/input
    // which is finally passed to the VRF cryptographic machinery.
    uint256 vRFSeed = makeVRFInputSeed(_keyHash, USER_SEED_PLACEHOLDER, address(this), nonces[_keyHash]);
    // nonces[_keyHash] must stay in sync with
    // VRFCoordinator.nonces[_keyHash][this], which was incremented by the above
    // successful LINK.transferAndCall (in VRFCoordinator.randomnessRequest).
    // This provides protection against the user repeating their input seed,
    // which would result in a predictable/duplicate output, if multiple such
    // requests appeared in the same block.
    nonces[_keyHash] = nonces[_keyHash] + 1;
    return makeRequestId(_keyHash, vRFSeed);
  }

  LinkTokenInterface internal immutable LINK;
  address private immutable vrfCoordinator;

  // Nonces for each VRF key from which randomness has been requested.
  //
  // Must stay in sync with VRFCoordinator[_keyHash][this]
  mapping(bytes32 => uint256) /* keyHash */ /* nonce */
    private nonces;

  /**
   * @param _vrfCoordinator address of VRFCoordinator contract
   * @param _link address of LINK token contract
   *
   * @dev https://docs.chain.link/docs/link-token-contracts
   */
  constructor(address _vrfCoordinator, address _link) {
    vrfCoordinator = _vrfCoordinator;
    LINK = LinkTokenInterface(_link);
  }

  // rawFulfillRandomness is called by VRFCoordinator when it receives a valid VRF
  // proof. rawFulfillRandomness then calls fulfillRandomness, after validating
  // the origin of the call
  function rawFulfillRandomness(bytes32 requestId, uint256 randomness) external {
    require(msg.sender == vrfCoordinator, "Only VRFCoordinator can fulfill");
    fulfillRandomness(requestId, randomness);
  }
}

// Part: Lottery

contract Lottery is VRFConsumerBase, Ownable {
    // TYPES
    enum LotteryState {
        CLOSED,
        OPENED,
        CALCULATING_WINNER,
        ENTRIES_CLOSED
    }

    // VARIABLES
    AggregatorV3Interface internal m_EthUsdPriceFeed;
    uint256 public m_Fee;
    bytes32 public m_Keyhash;
    bytes32 public m_RandomRequestId;
    uint256 public m_PreviousRandomness;
    bool public m_GambleDone; // For testing purpose only

    LotteryState public m_LotteryState;
    address payable[] public m_Players;
    address payable public m_Winner;
    uint256 public m_UsdEntryFee; // Precision => 18

    mapping(address => bool) public m_PlayerToHasEntered;
    mapping(address => uint256) public m_PlayerToUnclaimedMoney;

    // EVENTS
    event RandomnessRequested(bytes32 requestId);
    event RandomnessReceived(bytes32 requestId, uint256 randomness);
    event LotteryStarted(uint256 usdEntryFee);
    event LotteryReset();
    event LotteryCurrentState(LotteryState state, LotteryState expected);

    // METHODS
    constructor(
        uint256 _usdEntryFee,
        address _priceFeedAddress,
        address _vrfCoordinator,
        address _linkToken,
        uint256 _fee,
        bytes32 _keyhash
    ) VRFConsumerBase(_vrfCoordinator, _linkToken) {
        // Set lottery entry fee.
        m_UsdEntryFee = _usdEntryFee * (10**18);
        // Setup price feed.
        m_EthUsdPriceFeed = AggregatorV3Interface(_priceFeedAddress);
        // Set lottery default state to CLOSED.
        m_LotteryState = LotteryState.CLOSED;
        // Set VRFConsumerBase variables
        m_Fee = _fee;
        m_Keyhash = _keyhash;
        m_GambleDone = false;
    }

    // Returns the lottery entrance fee in WEI
    function getEntranceFee() public view returns (uint256) {
        // The $ETH price in USD (precision 8)
        uint256 ethPrice = this._getEthPrice();
        // The precision by which the entry fee is devided
        uint256 precisionKeeper = 10**8;

        return (m_UsdEntryFee * precisionKeeper) / ethPrice;
    }

    /* START */

    /**
    This function is called just before the startLottery function
     */
    function _preStartLottery() internal virtual {}

    function startLottery() public virtual onlyOwner {
        _preStartLottery();
        // Checks if lottery is not currently opened.
        require(
            m_LotteryState == LotteryState.CLOSED,
            "Invalid lottery state : Lottery already in progress."
        );
        // Set lottery state to OPENED.
        m_LotteryState = LotteryState.OPENED;
        emit LotteryCurrentState(m_LotteryState, LotteryState.OPENED);
        m_GambleDone = false;
        _postStartLottery();
    }

    /**
    This function is called right after the startLottery function
     */
    function _postStartLottery() internal virtual {
        emit LotteryStarted(m_UsdEntryFee);
    }

    /* ENTER */

    function _preEnter() internal virtual {}

    function enter() public payable {
        _preEnter();

        // Check if the lottery is opened.
        require(
            m_LotteryState == LotteryState.OPENED,
            "Unable to enter lottery : Invalid lottery state."
        );

        // Get entrance fee in wei.
        uint256 entrance_fee = this.getEntranceFee();

        // Players can only enter once.
        // Checks if the msg.sender has already entered.
        require(
            m_PlayerToHasEntered[msg.sender] == false,
            "Player has already entered the lottery."
        );
        // Checks if the amount sent by msg.sender is superior
        // or equal to the entrance fee
        require(msg.value >= entrance_fee, "Insufficient amount of $ETH sent.");

        // Make player enter the lottery
        m_Players.push(payable(msg.sender));
        m_PlayerToHasEntered[msg.sender] = true;

        // Send back leftovers.
        uint256 leftovers = msg.value - entrance_fee;
        if (leftovers > 0) {
            bool success = _send(leftovers, msg.sender);
            if (!success) m_PlayerToUnclaimedMoney[msg.sender] += leftovers;
        }

        _postEnter();
    }

    function _postEnter() internal virtual {}

    /* END LOTTERY */

    /**
     * This function is called before endLottery()
     */
    function _preEndLottery() internal virtual onlyOwner {}

    function endLottery() public {
        _preEndLottery();

        // Change lottery state to CALCULATING_WINNER
        m_LotteryState = LotteryState.CALCULATING_WINNER;

        {
            // Bad way to generate random number.
            // uint256(
            //     keccak256(
            //         abi.encodePacked(
            //             nonce, // Predictable (no. txn)
            //             msg.sender, // Known
            //             block.difficulty, // Can be manipulated by the miners :(
            //             block.timestamp // Predictable
            //         )
            //     )
            // ) % players.length;
        } // DO NOT USE !
        // Request a random number.
        m_RandomRequestId = requestRandomness(m_Keyhash, m_Fee);

        emit RandomnessRequested(m_RandomRequestId);

        /*
         * Second part of the lottery gambling and ending takes place
         * in the callback function for requestRandomness().
         * @see fulfillRandomness() function.
         */

        _postEndLottery();
    }

    /**
     * This function is called after endLottery()
     */
    function _postEndLottery() internal virtual {}

    /**
     * This function can be called to be sent unclaimed funds if there is.
     */
    function claim() public {
        // Checks for the unclaimed amount to be > 0.
        require(m_PlayerToUnclaimedMoney[msg.sender] > 0, "Nothing to claim.");
        // Retrieves unclaimed amount of msg.sender
        uint256 unclaimedMoney = m_PlayerToUnclaimedMoney[msg.sender];
        // Unmaps msg.sender from m_PlayerToUnclaimedMoney.
        delete m_PlayerToUnclaimedMoney[msg.sender];
        // Tries sending the unclaimed amount to msg.sender.
        bool success = _send(unclaimedMoney, msg.sender);

        // Requires success of the claim, otherwise : reverts.
        require(
            success == true,
            "Could not transfer claimable funds to message sender."
        );
    }

    // Returns the price of ETH in USD with a precision of 8.
    function _getEthPrice() public view returns (uint256) {
        (, int256 answer, , , ) = m_EthUsdPriceFeed.latestRoundData();
        return uint256(answer);
    }

    // Gets lottery winner among participants and sends the prize.
    function _sendPrizeToWinner(uint256 _winnerIndex) internal {
        // Gets winners address.
        m_Winner = m_Players[_winnerIndex];

        // Sends funds to winner
        _send(address(this).balance, m_Winner);
        _resetLottery();
    }

    // Overrides the fulfillRandomness function from VRFConsumerBase class.
    function fulfillRandomness(bytes32 _requestId, uint256 _randomness)
        internal
        override
    {
        require(
            m_RandomRequestId == _requestId,
            "Random number request ID doesn't match the active request ID."
        );
        require(
            m_LotteryState == LotteryState.CALCULATING_WINNER,
            "Invalid lottery state detected."
        );
        require(
            _randomness != 0 && _randomness != m_PreviousRandomness,
            "random-not-found"
        );
        emit RandomnessReceived(_requestId, _randomness);
        m_GambleDone = true;

        _sendPrizeToWinner(_randomness % m_Players.length);

        // Update previously generated random number.
        m_PreviousRandomness = _randomness;
    }

    // Resets lottery.
    function _resetLottery() internal virtual {
        // Reset players related contract variables
        for (uint256 i = 0; i < m_Players.length; ++i)
            delete m_PlayerToHasEntered[m_Players[i]];
        delete m_Players;
        // Set lottery state to closed.
        m_LotteryState = LotteryState.CLOSED;
        emit LotteryReset();
    }

    // Sends '_amount' wei to the specified '_address'
    function _send(uint256 _amount, address _address) internal returns (bool) {
        (bool success, ) = _address.call{value: _amount}("");
        return success;
    }
}

// File: TimedLottery.sol

contract TimedLottery is Lottery {
    uint256 private m_StartTime;
    uint256 private m_EndTime;

    event TimedLotteryStarted(
        uint256 startTime,
        uint256 endTime,
        uint256 usdEntryFee
    );

    event LotteryTimeReached(
        uint256 endTime,
        uint256 currentTime,
        bool isCalculatingWinner
    );

    constructor(
        uint256 _usdEntryFee,
        address _priceFeedAddress,
        address _vrfCoordinator,
        address _linkToken,
        uint256 _fee,
        bytes32 _keyhash
    )
        Lottery(
            _usdEntryFee,
            _priceFeedAddress,
            _vrfCoordinator,
            _linkToken,
            _fee,
            _keyhash
        )
    {}

    // Lottery starts on function call.
    function startLottery(uint256 _secondsToEnd) public onlyOwner {
        require(
            _secondsToEnd >= 60,
            "Invalid parameter : Minimum lottery time is 60 seconds."
        );
        super.startLottery(); // Which _pre and _post functions will it execute ?
        m_StartTime = block.timestamp;
        m_EndTime = block.timestamp + _secondsToEnd;
    }

    function _postStartLottery() internal override {
        emit TimedLotteryStarted(m_StartTime, m_EndTime, m_UsdEntryFee);
    }

    function _preEnter() internal override {
        if (m_EndTime > block.timestamp) return;
        // Close lottery entries and emit LotteryTimeReached event.
        m_LotteryState = LotteryState.ENTRIES_CLOSED;
        emit LotteryTimeReached(
            m_EndTime,
            block.timestamp,
            m_LotteryState == LotteryState.CALCULATING_WINNER
        );
    }

    function _preEndLottery() internal override {
        require(
            m_EndTime <= block.timestamp,
            "Not allowed to end lottery before time is up."
        );
    }

    function startTime() public view returns (uint256) {
        return m_StartTime;
    }

    function endTime() public view returns (uint256) {
        return m_EndTime;
    }

    function isTimeUp() public returns (bool) {
        require(m_LotteryState != LotteryState.CLOSED, "Lottery is not opened");
        if (m_EndTime > block.timestamp) return false;

        emit LotteryTimeReached(
            m_EndTime,
            block.timestamp,
            bool(m_LotteryState == LotteryState.CALCULATING_WINNER)
        );
        return true;
    }

    function _resetLottery() internal override {
        m_StartTime = 0;
        m_EndTime = 0;
        super._resetLottery();
    }
}