/**
 *Submitted for verification at Etherscan.io on 2021-10-03
*/

// SPDX-License-Identifier: Apache-2.0
// File: @chainlink/contracts/src/v0.8/interfaces/AggregatorV3Interface.sol


pragma solidity ^0.8.0;

interface AggregatorV3Interface {

  function decimals()
    external
    view
    returns (
      uint8
    );

  function description()
    external
    view
    returns (
      string memory
    );

  function version()
    external
    view
    returns (
      uint256
    );

  // getRoundData and latestRoundData should both raise "No data present"
  // if they do not have data to report, instead of returning unset values
  // which could be misinterpreted as actual reported values.
  function getRoundData(
    uint80 _roundId
  )
    external
    view
    returns (
      uint80 roundId,
      int256 answer,
      uint256 startedAt,
      uint256 updatedAt,
      uint80 answeredInRound
    );

  function latestRoundData()
    external
    view
    returns (
      uint80 roundId,
      int256 answer,
      uint256 startedAt,
      uint256 updatedAt,
      uint80 answeredInRound
    );

}

// File: @chainlink/contracts/src/v0.8/VRFRequestIDBase.sol


pragma solidity ^0.8.0;

contract VRFRequestIDBase {

  /**
   * @notice returns the seed which is actually input to the VRF coordinator
   *
   * @dev To prevent repetition of VRF output due to repetition of the
   * @dev user-supplied seed, that seed is combined in a hash with the
   * @dev user-specific nonce, and the address of the consuming contract. The
   * @dev risk of repetition is mostly mitigated by inclusion of a blockhash in
   * @dev the final seed, but the nonce does protect against repetition in
   * @dev requests which are included in a single block.
   *
   * @param _userSeed VRF seed input provided by user
   * @param _requester Address of the requesting contract
   * @param _nonce User-specific nonce at the time of the request
   */
  function makeVRFInputSeed(
    bytes32 _keyHash,
    uint256 _userSeed,
    address _requester,
    uint256 _nonce
  )
    internal
    pure
    returns (
      uint256
    )
  {
    return uint256(keccak256(abi.encode(_keyHash, _userSeed, _requester, _nonce)));
  }

  /**
   * @notice Returns the id for this request
   * @param _keyHash The serviceAgreement ID to be used for this request
   * @param _vRFInputSeed The seed to be passed directly to the VRF
   * @return The id for this request
   *
   * @dev Note that _vRFInputSeed is not the seed passed by the consuming
   * @dev contract, but the one generated by makeVRFInputSeed
   */
  function makeRequestId(
    bytes32 _keyHash,
    uint256 _vRFInputSeed
  )
    internal
    pure
    returns (
      bytes32
    )
  {
    return keccak256(abi.encodePacked(_keyHash, _vRFInputSeed));
  }
}
// File: @chainlink/contracts/src/v0.8/interfaces/LinkTokenInterface.sol


pragma solidity ^0.8.0;

interface LinkTokenInterface {

  function allowance(
    address owner,
    address spender
  )
    external
    view
    returns (
      uint256 remaining
    );

  function approve(
    address spender,
    uint256 value
  )
    external
    returns (
      bool success
    );

  function balanceOf(
    address owner
  )
    external
    view
    returns (
      uint256 balance
    );

  function decimals()
    external
    view
    returns (
      uint8 decimalPlaces
    );

  function decreaseApproval(
    address spender,
    uint256 addedValue
  )
    external
    returns (
      bool success
    );

  function increaseApproval(
    address spender,
    uint256 subtractedValue
  ) external;

  function name()
    external
    view
    returns (
      string memory tokenName
    );

  function symbol()
    external
    view
    returns (
      string memory tokenSymbol
    );

  function totalSupply()
    external
    view
    returns (
      uint256 totalTokensIssued
    );

  function transfer(
    address to,
    uint256 value
  )
    external
    returns (
      bool success
    );

  function transferAndCall(
    address to,
    uint256 value,
    bytes calldata data
  )
    external
    returns (
      bool success
    );

  function transferFrom(
    address from,
    address to,
    uint256 value
  )
    external
    returns (
      bool success
    );

}

// File: @chainlink/contracts/src/v0.8/VRFConsumerBase.sol


pragma solidity ^0.8.0;



/** ****************************************************************************
 * @notice Interface for contracts using VRF randomness
 * *****************************************************************************
 * @dev PURPOSE
 *
 * @dev Reggie the Random Oracle (not his real job) wants to provide randomness
 * @dev to Vera the verifier in such a way that Vera can be sure he's not
 * @dev making his output up to suit himself. Reggie provides Vera a public key
 * @dev to which he knows the secret key. Each time Vera provides a seed to
 * @dev Reggie, he gives back a value which is computed completely
 * @dev deterministically from the seed and the secret key.
 *
 * @dev Reggie provides a proof by which Vera can verify that the output was
 * @dev correctly computed once Reggie tells it to her, but without that proof,
 * @dev the output is indistinguishable to her from a uniform random sample
 * @dev from the output space.
 *
 * @dev The purpose of this contract is to make it easy for unrelated contracts
 * @dev to talk to Vera the verifier about the work Reggie is doing, to provide
 * @dev simple access to a verifiable source of randomness.
 * *****************************************************************************
 * @dev USAGE
 *
 * @dev Calling contracts must inherit from VRFConsumerBase, and can
 * @dev initialize VRFConsumerBase's attributes in their constructor as
 * @dev shown:
 *
 * @dev   contract VRFConsumer {
 * @dev     constuctor(<other arguments>, address _vrfCoordinator, address _link)
 * @dev       VRFConsumerBase(_vrfCoordinator, _link) public {
 * @dev         <initialization with other arguments goes here>
 * @dev       }
 * @dev   }
 *
 * @dev The oracle will have given you an ID for the VRF keypair they have
 * @dev committed to (let's call it keyHash), and have told you the minimum LINK
 * @dev price for VRF service. Make sure your contract has sufficient LINK, and
 * @dev call requestRandomness(keyHash, fee, seed), where seed is the input you
 * @dev want to generate randomness from.
 *
 * @dev Once the VRFCoordinator has received and validated the oracle's response
 * @dev to your request, it will call your contract's fulfillRandomness method.
 *
 * @dev The randomness argument to fulfillRandomness is the actual random value
 * @dev generated from your seed.
 *
 * @dev The requestId argument is generated from the keyHash and the seed by
 * @dev makeRequestId(keyHash, seed). If your contract could have concurrent
 * @dev requests open, you can use the requestId to track which seed is
 * @dev associated with which randomness. See VRFRequestIDBase.sol for more
 * @dev details. (See "SECURITY CONSIDERATIONS" for principles to keep in mind,
 * @dev if your contract could have multiple requests in flight simultaneously.)
 *
 * @dev Colliding `requestId`s are cryptographically impossible as long as seeds
 * @dev differ. (Which is critical to making unpredictable randomness! See the
 * @dev next section.)
 *
 * *****************************************************************************
 * @dev SECURITY CONSIDERATIONS
 *
 * @dev A method with the ability to call your fulfillRandomness method directly
 * @dev could spoof a VRF response with any random value, so it's critical that
 * @dev it cannot be directly called by anything other than this base contract
 * @dev (specifically, by the VRFConsumerBase.rawFulfillRandomness method).
 *
 * @dev For your users to trust that your contract's random behavior is free
 * @dev from malicious interference, it's best if you can write it so that all
 * @dev behaviors implied by a VRF response are executed *during* your
 * @dev fulfillRandomness method. If your contract must store the response (or
 * @dev anything derived from it) and use it later, you must ensure that any
 * @dev user-significant behavior which depends on that stored value cannot be
 * @dev manipulated by a subsequent VRF request.
 *
 * @dev Similarly, both miners and the VRF oracle itself have some influence
 * @dev over the order in which VRF responses appear on the blockchain, so if
 * @dev your contract could have multiple VRF requests in flight simultaneously,
 * @dev you must ensure that the order in which the VRF responses arrive cannot
 * @dev be used to manipulate your contract's user-significant behavior.
 *
 * @dev Since the ultimate input to the VRF is mixed with the block hash of the
 * @dev block in which the request is made, user-provided seeds have no impact
 * @dev on its economic security properties. They are only included for API
 * @dev compatability with previous versions of this contract.
 *
 * @dev Since the block hash of the block which contains the requestRandomness
 * @dev call is mixed into the input to the VRF *last*, a sufficiently powerful
 * @dev miner could, in principle, fork the blockchain to evict the block
 * @dev containing the request, forcing the request to be included in a
 * @dev different block with a different hash, and therefore a different input
 * @dev to the VRF. However, such an attack would incur a substantial economic
 * @dev cost. This cost scales with the number of blocks the VRF oracle waits
 * @dev until it calls responds to a request.
 */
abstract contract VRFConsumerBase is VRFRequestIDBase {

  /**
   * @notice fulfillRandomness handles the VRF response. Your contract must
   * @notice implement it. See "SECURITY CONSIDERATIONS" above for important
   * @notice principles to keep in mind when implementing your fulfillRandomness
   * @notice method.
   *
   * @dev VRFConsumerBase expects its subcontracts to have a method with this
   * @dev signature, and will call it once it has verified the proof
   * @dev associated with the randomness. (It is triggered via a call to
   * @dev rawFulfillRandomness, below.)
   *
   * @param requestId The Id initially returned by requestRandomness
   * @param randomness the VRF output
   */
  function fulfillRandomness(
    bytes32 requestId,
    uint256 randomness
  )
    internal
    virtual;

  /**
   * @dev In order to keep backwards compatibility we have kept the user
   * seed field around. We remove the use of it because given that the blockhash
   * enters later, it overrides whatever randomness the used seed provides.
   * Given that it adds no security, and can easily lead to misunderstandings,
   * we have removed it from usage and can now provide a simpler API.
   */
  uint256 constant private USER_SEED_PLACEHOLDER = 0;

  /**
   * @notice requestRandomness initiates a request for VRF output given _seed
   *
   * @dev The fulfillRandomness method receives the output, once it's provided
   * @dev by the Oracle, and verified by the vrfCoordinator.
   *
   * @dev The _keyHash must already be registered with the VRFCoordinator, and
   * @dev the _fee must exceed the fee specified during registration of the
   * @dev _keyHash.
   *
   * @dev The _seed parameter is vestigial, and is kept only for API
   * @dev compatibility with older versions. It can't *hurt* to mix in some of
   * @dev your own randomness, here, but it's not necessary because the VRF
   * @dev oracle will mix the hash of the block containing your request into the
   * @dev VRF seed it ultimately uses.
   *
   * @param _keyHash ID of public key against which randomness is generated
   * @param _fee The amount of LINK to send with the request
   *
   * @return requestId unique ID for this request
   *
   * @dev The returned requestId can be used to distinguish responses to
   * @dev concurrent requests. It is passed as the first argument to
   * @dev fulfillRandomness.
   */
  function requestRandomness(
    bytes32 _keyHash,
    uint256 _fee
  )
    internal
    returns (
      bytes32 requestId
    )
  {
    LINK.transferAndCall(vrfCoordinator, _fee, abi.encode(_keyHash, USER_SEED_PLACEHOLDER));
    // This is the seed passed to VRFCoordinator. The oracle will mix this with
    // the hash of the block containing this request to obtain the seed/input
    // which is finally passed to the VRF cryptographic machinery.
    uint256 vRFSeed  = makeVRFInputSeed(_keyHash, USER_SEED_PLACEHOLDER, address(this), nonces[_keyHash]);
    // nonces[_keyHash] must stay in sync with
    // VRFCoordinator.nonces[_keyHash][this], which was incremented by the above
    // successful LINK.transferAndCall (in VRFCoordinator.randomnessRequest).
    // This provides protection against the user repeating their input seed,
    // which would result in a predictable/duplicate output, if multiple such
    // requests appeared in the same block.
    nonces[_keyHash] = nonces[_keyHash] + 1;
    return makeRequestId(_keyHash, vRFSeed);
  }

  LinkTokenInterface immutable internal LINK;
  address immutable private vrfCoordinator;

  // Nonces for each VRF key from which randomness has been requested.
  //
  // Must stay in sync with VRFCoordinator[_keyHash][this]
  mapping(bytes32 /* keyHash */ => uint256 /* nonce */) private nonces;

  /**
   * @param _vrfCoordinator address of VRFCoordinator contract
   * @param _link address of LINK token contract
   *
   * @dev https://docs.chain.link/docs/link-token-contracts
   */
  constructor(
    address _vrfCoordinator,
    address _link
  ) {
    vrfCoordinator = _vrfCoordinator;
    LINK = LinkTokenInterface(_link);
  }

  // rawFulfillRandomness is called by VRFCoordinator when it receives a valid VRF
  // proof. rawFulfillRandomness then calls fulfillRandomness, after validating
  // the origin of the call
  function rawFulfillRandomness(
    bytes32 requestId,
    uint256 randomness
  )
    external
  {
    require(msg.sender == vrfCoordinator, "Only VRFCoordinator can fulfill");
    fulfillRandomness(requestId, randomness);
  }
}

// File: @openzeppelin/contracts/security/ReentrancyGuard.sol



pragma solidity ^0.8.0;

/**
 * @dev Contract module that helps prevent reentrant calls to a function.
 *
 * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier
 * available, which can be applied to functions to make sure there are no nested
 * (reentrant) calls to them.
 *
 * Note that because there is a single `nonReentrant` guard, functions marked as
 * `nonReentrant` may not call one another. This can be worked around by making
 * those functions `private`, and then adding `external` `nonReentrant` entry
 * points to them.
 *
 * TIP: If you would like to learn more about reentrancy and alternative ways
 * to protect against it, check out our blog post
 * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].
 */
abstract contract ReentrancyGuard {
    // Booleans are more expensive than uint256 or any type that takes up a full
    // word because each write operation emits an extra SLOAD to first read the
    // slot's contents, replace the bits taken up by the boolean, and then write
    // back. This is the compiler's defense against contract upgrades and
    // pointer aliasing, and it cannot be disabled.

    // The values being non-zero value makes deployment a bit more expensive,
    // but in exchange the refund on every call to nonReentrant will be lower in
    // amount. Since refunds are capped to a percentage of the total
    // transaction's gas, it is best to keep them low in cases like this one, to
    // increase the likelihood of the full refund coming into effect.
    uint256 private constant _NOT_ENTERED = 1;
    uint256 private constant _ENTERED = 2;

    uint256 private _status;

    constructor() {
        _status = _NOT_ENTERED;
    }

    /**
     * @dev Prevents a contract from calling itself, directly or indirectly.
     * Calling a `nonReentrant` function from another `nonReentrant`
     * function is not supported. It is possible to prevent this from happening
     * by making the `nonReentrant` function external, and make it call a
     * `private` function that does the actual work.
     */
    modifier nonReentrant() {
        // On the first call to nonReentrant, _notEntered will be true
        require(_status != _ENTERED, "ReentrancyGuard: reentrant call");

        // Any calls to nonReentrant after this point will fail
        _status = _ENTERED;

        _;

        // By storing the original value once again, a refund is triggered (see
        // https://eips.ethereum.org/EIPS/eip-2200)
        _status = _NOT_ENTERED;
    }
}

// File: @openzeppelin/contracts/utils/Counters.sol



pragma solidity ^0.8.0;

/**
 * @title Counters
 * @author Matt Condon (@shrugs)
 * @dev Provides counters that can only be incremented, decremented or reset. This can be used e.g. to track the number
 * of elements in a mapping, issuing ERC721 ids, or counting request ids.
 *
 * Include with `using Counters for Counters.Counter;`
 */
library Counters {
    struct Counter {
        // This variable should never be directly accessed by users of the library: interactions must be restricted to
        // the library's function. As of Solidity v0.5.2, this cannot be enforced, though there is a proposal to add
        // this feature: see https://github.com/ethereum/solidity/issues/4637
        uint256 _value; // default: 0
    }

    function current(Counter storage counter) internal view returns (uint256) {
        return counter._value;
    }

    function increment(Counter storage counter) internal {
        unchecked {
            counter._value += 1;
        }
    }

    function decrement(Counter storage counter) internal {
        uint256 value = counter._value;
        require(value > 0, "Counter: decrement overflow");
        unchecked {
            counter._value = value - 1;
        }
    }

    function reset(Counter storage counter) internal {
        counter._value = 0;
    }
}

// File: @openzeppelin/contracts/token/ERC20/IERC20.sol



pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `recipient`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address recipient, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `sender` to `recipient` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address sender,
        address recipient,
        uint256 amount
    ) external returns (bool);

    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);
}

// File: @openzeppelin/contracts/utils/introspection/IERC165.sol



pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC165 standard, as defined in the
 * https://eips.ethereum.org/EIPS/eip-165[EIP].
 *
 * Implementers can declare support of contract interfaces, which can then be
 * queried by others ({ERC165Checker}).
 *
 * For an implementation, see {ERC165}.
 */
interface IERC165 {
    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30 000 gas.
     */
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}

// File: @openzeppelin/contracts/utils/introspection/ERC165.sol



pragma solidity ^0.8.0;


/**
 * @dev Implementation of the {IERC165} interface.
 *
 * Contracts that want to implement ERC165 should inherit from this contract and override {supportsInterface} to check
 * for the additional interface id that will be supported. For example:
 *
 * ```solidity
 * function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
 *     return interfaceId == type(MyInterface).interfaceId || super.supportsInterface(interfaceId);
 * }
 * ```
 *
 * Alternatively, {ERC165Storage} provides an easier to use but more expensive implementation.
 */
abstract contract ERC165 is IERC165 {
    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IERC165).interfaceId;
    }
}

// File: @openzeppelin/contracts/utils/Strings.sol



pragma solidity ^0.8.0;

/**
 * @dev String operations.
 */
library Strings {
    bytes16 private constant _HEX_SYMBOLS = "0123456789abcdef";

    /**
     * @dev Converts a `uint256` to its ASCII `string` decimal representation.
     */
    function toString(uint256 value) internal pure returns (string memory) {
        // Inspired by OraclizeAPI's implementation - MIT licence
        // https://github.com/oraclize/ethereum-api/blob/b42146b063c7d6ee1358846c198246239e9360e8/oraclizeAPI_0.4.25.sol

        if (value == 0) {
            return "0";
        }
        uint256 temp = value;
        uint256 digits;
        while (temp != 0) {
            digits++;
            temp /= 10;
        }
        bytes memory buffer = new bytes(digits);
        while (value != 0) {
            digits -= 1;
            buffer[digits] = bytes1(uint8(48 + uint256(value % 10)));
            value /= 10;
        }
        return string(buffer);
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation.
     */
    function toHexString(uint256 value) internal pure returns (string memory) {
        if (value == 0) {
            return "0x00";
        }
        uint256 temp = value;
        uint256 length = 0;
        while (temp != 0) {
            length++;
            temp >>= 8;
        }
        return toHexString(value, length);
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation with fixed length.
     */
    function toHexString(uint256 value, uint256 length) internal pure returns (string memory) {
        bytes memory buffer = new bytes(2 * length + 2);
        buffer[0] = "0";
        buffer[1] = "x";
        for (uint256 i = 2 * length + 1; i > 1; --i) {
            buffer[i] = _HEX_SYMBOLS[value & 0xf];
            value >>= 4;
        }
        require(value == 0, "Strings: hex length insufficient");
        return string(buffer);
    }
}

// File: @openzeppelin/contracts/utils/Context.sol



pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}

// File: @openzeppelin/contracts/access/IAccessControl.sol



pragma solidity ^0.8.0;

/**
 * @dev External interface of AccessControl declared to support ERC165 detection.
 */
interface IAccessControl {
    /**
     * @dev Emitted when `newAdminRole` is set as ``role``'s admin role, replacing `previousAdminRole`
     *
     * `DEFAULT_ADMIN_ROLE` is the starting admin for all roles, despite
     * {RoleAdminChanged} not being emitted signaling this.
     *
     * _Available since v3.1._
     */
    event RoleAdminChanged(bytes32 indexed role, bytes32 indexed previousAdminRole, bytes32 indexed newAdminRole);

    /**
     * @dev Emitted when `account` is granted `role`.
     *
     * `sender` is the account that originated the contract call, an admin role
     * bearer except when using {AccessControl-_setupRole}.
     */
    event RoleGranted(bytes32 indexed role, address indexed account, address indexed sender);

    /**
     * @dev Emitted when `account` is revoked `role`.
     *
     * `sender` is the account that originated the contract call:
     *   - if using `revokeRole`, it is the admin role bearer
     *   - if using `renounceRole`, it is the role bearer (i.e. `account`)
     */
    event RoleRevoked(bytes32 indexed role, address indexed account, address indexed sender);

    /**
     * @dev Returns `true` if `account` has been granted `role`.
     */
    function hasRole(bytes32 role, address account) external view returns (bool);

    /**
     * @dev Returns the admin role that controls `role`. See {grantRole} and
     * {revokeRole}.
     *
     * To change a role's admin, use {AccessControl-_setRoleAdmin}.
     */
    function getRoleAdmin(bytes32 role) external view returns (bytes32);

    /**
     * @dev Grants `role` to `account`.
     *
     * If `account` had not been already granted `role`, emits a {RoleGranted}
     * event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     */
    function grantRole(bytes32 role, address account) external;

    /**
     * @dev Revokes `role` from `account`.
     *
     * If `account` had been granted `role`, emits a {RoleRevoked} event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     */
    function revokeRole(bytes32 role, address account) external;

    /**
     * @dev Revokes `role` from the calling account.
     *
     * Roles are often managed via {grantRole} and {revokeRole}: this function's
     * purpose is to provide a mechanism for accounts to lose their privileges
     * if they are compromised (such as when a trusted device is misplaced).
     *
     * If the calling account had been granted `role`, emits a {RoleRevoked}
     * event.
     *
     * Requirements:
     *
     * - the caller must be `account`.
     */
    function renounceRole(bytes32 role, address account) external;
}

// File: @openzeppelin/contracts/access/AccessControl.sol



pragma solidity ^0.8.0;





/**
 * @dev Contract module that allows children to implement role-based access
 * control mechanisms. This is a lightweight version that doesn't allow enumerating role
 * members except through off-chain means by accessing the contract event logs. Some
 * applications may benefit from on-chain enumerability, for those cases see
 * {AccessControlEnumerable}.
 *
 * Roles are referred to by their `bytes32` identifier. These should be exposed
 * in the external API and be unique. The best way to achieve this is by
 * using `public constant` hash digests:
 *
 * ```
 * bytes32 public constant MY_ROLE = keccak256("MY_ROLE");
 * ```
 *
 * Roles can be used to represent a set of permissions. To restrict access to a
 * function call, use {hasRole}:
 *
 * ```
 * function foo() public {
 *     require(hasRole(MY_ROLE, msg.sender));
 *     ...
 * }
 * ```
 *
 * Roles can be granted and revoked dynamically via the {grantRole} and
 * {revokeRole} functions. Each role has an associated admin role, and only
 * accounts that have a role's admin role can call {grantRole} and {revokeRole}.
 *
 * By default, the admin role for all roles is `DEFAULT_ADMIN_ROLE`, which means
 * that only accounts with this role will be able to grant or revoke other
 * roles. More complex role relationships can be created by using
 * {_setRoleAdmin}.
 *
 * WARNING: The `DEFAULT_ADMIN_ROLE` is also its own admin: it has permission to
 * grant and revoke this role. Extra precautions should be taken to secure
 * accounts that have been granted it.
 */
abstract contract AccessControl is Context, IAccessControl, ERC165 {
    struct RoleData {
        mapping(address => bool) members;
        bytes32 adminRole;
    }

    mapping(bytes32 => RoleData) private _roles;

    bytes32 public constant DEFAULT_ADMIN_ROLE = 0x00;

    /**
     * @dev Modifier that checks that an account has a specific role. Reverts
     * with a standardized message including the required role.
     *
     * The format of the revert reason is given by the following regular expression:
     *
     *  /^AccessControl: account (0x[0-9a-f]{40}) is missing role (0x[0-9a-f]{64})$/
     *
     * _Available since v4.1._
     */
    modifier onlyRole(bytes32 role) {
        _checkRole(role, _msgSender());
        _;
    }

    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IAccessControl).interfaceId || super.supportsInterface(interfaceId);
    }

    /**
     * @dev Returns `true` if `account` has been granted `role`.
     */
    function hasRole(bytes32 role, address account) public view override returns (bool) {
        return _roles[role].members[account];
    }

    /**
     * @dev Revert with a standard message if `account` is missing `role`.
     *
     * The format of the revert reason is given by the following regular expression:
     *
     *  /^AccessControl: account (0x[0-9a-f]{40}) is missing role (0x[0-9a-f]{64})$/
     */
    function _checkRole(bytes32 role, address account) internal view {
        if (!hasRole(role, account)) {
            revert(
                string(
                    abi.encodePacked(
                        "AccessControl: account ",
                        Strings.toHexString(uint160(account), 20),
                        " is missing role ",
                        Strings.toHexString(uint256(role), 32)
                    )
                )
            );
        }
    }

    /**
     * @dev Returns the admin role that controls `role`. See {grantRole} and
     * {revokeRole}.
     *
     * To change a role's admin, use {_setRoleAdmin}.
     */
    function getRoleAdmin(bytes32 role) public view override returns (bytes32) {
        return _roles[role].adminRole;
    }

    /**
     * @dev Grants `role` to `account`.
     *
     * If `account` had not been already granted `role`, emits a {RoleGranted}
     * event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     */
    function grantRole(bytes32 role, address account) public virtual override onlyRole(getRoleAdmin(role)) {
        _grantRole(role, account);
    }

    /**
     * @dev Revokes `role` from `account`.
     *
     * If `account` had been granted `role`, emits a {RoleRevoked} event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     */
    function revokeRole(bytes32 role, address account) public virtual override onlyRole(getRoleAdmin(role)) {
        _revokeRole(role, account);
    }

    /**
     * @dev Revokes `role` from the calling account.
     *
     * Roles are often managed via {grantRole} and {revokeRole}: this function's
     * purpose is to provide a mechanism for accounts to lose their privileges
     * if they are compromised (such as when a trusted device is misplaced).
     *
     * If the calling account had been granted `role`, emits a {RoleRevoked}
     * event.
     *
     * Requirements:
     *
     * - the caller must be `account`.
     */
    function renounceRole(bytes32 role, address account) public virtual override {
        require(account == _msgSender(), "AccessControl: can only renounce roles for self");

        _revokeRole(role, account);
    }

    /**
     * @dev Grants `role` to `account`.
     *
     * If `account` had not been already granted `role`, emits a {RoleGranted}
     * event. Note that unlike {grantRole}, this function doesn't perform any
     * checks on the calling account.
     *
     * [WARNING]
     * ====
     * This function should only be called from the constructor when setting
     * up the initial roles for the system.
     *
     * Using this function in any other way is effectively circumventing the admin
     * system imposed by {AccessControl}.
     * ====
     */
    function _setupRole(bytes32 role, address account) internal virtual {
        _grantRole(role, account);
    }

    /**
     * @dev Sets `adminRole` as ``role``'s admin role.
     *
     * Emits a {RoleAdminChanged} event.
     */
    function _setRoleAdmin(bytes32 role, bytes32 adminRole) internal virtual {
        bytes32 previousAdminRole = getRoleAdmin(role);
        _roles[role].adminRole = adminRole;
        emit RoleAdminChanged(role, previousAdminRole, adminRole);
    }

    function _grantRole(bytes32 role, address account) private {
        if (!hasRole(role, account)) {
            _roles[role].members[account] = true;
            emit RoleGranted(role, account, _msgSender());
        }
    }

    function _revokeRole(bytes32 role, address account) private {
        if (hasRole(role, account)) {
            _roles[role].members[account] = false;
            emit RoleRevoked(role, account, _msgSender());
        }
    }
}

// File: contracts/token.sol



/*
 *  Copyright DecentralizedVentures
 *
 *  Licensed under the Apache License, Version 2.0 (the "License"); you may not use
 *  this file except in compliance with the License. You may obtain a copy of the
 *  License at http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software distributed
 *  under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
 *  CONDITIONS OF ANY KIND, either express or implied. See the License for the
 *  specific language governing permissions and limitations under the License.
 *
 */

pragma solidity ^0.8.7;
/*
 *
 *
 *            __________________________ ________   __
 *     |     |     |  |      |   |______|_____/  \_/
 *     |_____|_____|  |      |   |______|    \_   |
 *
 *
 *  A decentralized gaming SmartContract as a PowerBall,
 *  and use-cases designed to decentralize offline markets.
 *
 *
 *
 *
 */




// chainlink goodies



/*
 * @interface IUniswapV2Router01
 *
 *
 *
 */
interface IUniswapV2Router01 {
    function factory() external pure returns (address);

    function WETH() external pure returns (address);

    function addLiquidity(
        address tokenA,
        address tokenB,
        uint256 amountADesired,
        uint256 amountBDesired,
        uint256 amountAMin,
        uint256 amountBMin,
        address to,
        uint256 deadline
    )
        external
        returns (
            uint256 amountA,
            uint256 amountB,
            uint256 liquidity
        );

    function addLiquidityETH(
        address token,
        uint256 amountTokenDesired,
        uint256 amountTokenMin,
        uint256 amountETHMin,
        address to,
        uint256 deadline
    )
        external
        payable
        returns (
            uint256 amountToken,
            uint256 amountETH,
            uint256 liquidity
        );

    function removeLiquidity(
        address tokenA,
        address tokenB,
        uint256 liquidity,
        uint256 amountAMin,
        uint256 amountBMin,
        address to,
        uint256 deadline
    ) external returns (uint256 amountA, uint256 amountB);

    function removeLiquidityETH(
        address token,
        uint256 liquidity,
        uint256 amountTokenMin,
        uint256 amountETHMin,
        address to,
        uint256 deadline
    ) external returns (uint256 amountToken, uint256 amountETH);

    function removeLiquidityWithPermit(
        address tokenA,
        address tokenB,
        uint256 liquidity,
        uint256 amountAMin,
        uint256 amountBMin,
        address to,
        uint256 deadline,
        bool approveMax,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) external returns (uint256 amountA, uint256 amountB);

    function removeLiquidityETHWithPermit(
        address token,
        uint256 liquidity,
        uint256 amountTokenMin,
        uint256 amountETHMin,
        address to,
        uint256 deadline,
        bool approveMax,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) external returns (uint256 amountToken, uint256 amountETH);

    function swapExactTokensForTokens(
        uint256 amountIn,
        uint256 amountOutMin,
        address[] calldata path,
        address to,
        uint256 deadline
    ) external returns (uint256[] memory amounts);

    function swapTokensForExactTokens(
        uint256 amountOut,
        uint256 amountInMax,
        address[] calldata path,
        address to,
        uint256 deadline
    ) external returns (uint256[] memory amounts);

    function swapExactETHForTokens(
        uint256 amountOutMin,
        address[] calldata path,
        address to,
        uint256 deadline
    ) external payable returns (uint256[] memory amounts);

    function swapTokensForExactETH(
        uint256 amountOut,
        uint256 amountInMax,
        address[] calldata path,
        address to,
        uint256 deadline
    ) external returns (uint256[] memory amounts);

    function swapExactTokensForETH(
        uint256 amountIn,
        uint256 amountOutMin,
        address[] calldata path,
        address to,
        uint256 deadline
    ) external returns (uint256[] memory amounts);

    function swapETHForExactTokens(
        uint256 amountOut,
        address[] calldata path,
        address to,
        uint256 deadline
    ) external payable returns (uint256[] memory amounts);

    function quote(
        uint256 amountA,
        uint256 reserveA,
        uint256 reserveB
    ) external pure returns (uint256 amountB);

    function getAmountOut(
        uint256 amountIn,
        uint256 reserveIn,
        uint256 reserveOut
    ) external pure returns (uint256 amountOut);

    function getAmountIn(
        uint256 amountOut,
        uint256 reserveIn,
        uint256 reserveOut
    ) external pure returns (uint256 amountIn);

    function getAmountsOut(uint256 amountIn, address[] calldata path)
        external
        view
        returns (uint256[] memory amounts);

    function getAmountsIn(uint256 amountOut, address[] calldata path)
        external
        view
        returns (uint256[] memory amounts);
}

/*
 * @interface IUniswapV2Router02
 *
 *
 *
 */
interface IUniswapV2Router02 is IUniswapV2Router01 {
    function removeLiquidityETHSupportingFeeOnTransferTokens(
        address token,
        uint256 liquidity,
        uint256 amountTokenMin,
        uint256 amountETHMin,
        address to,
        uint256 deadline
    ) external returns (uint256 amountETH);

    function removeLiquidityETHWithPermitSupportingFeeOnTransferTokens(
        address token,
        uint256 liquidity,
        uint256 amountTokenMin,
        uint256 amountETHMin,
        address to,
        uint256 deadline,
        bool approveMax,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) external returns (uint256 amountETH);

    function swapExactTokensForTokensSupportingFeeOnTransferTokens(
        uint256 amountIn,
        uint256 amountOutMin,
        address[] calldata path,
        address to,
        uint256 deadline
    ) external;

    function swapExactETHForTokensSupportingFeeOnTransferTokens(
        uint256 amountOutMin,
        address[] calldata path,
        address to,
        uint256 deadline
    ) external payable;

    function swapExactTokensForETHSupportingFeeOnTransferTokens(
        uint256 amountIn,
        uint256 amountOutMin,
        address[] calldata path,
        address to,
        uint256 deadline
    ) external;
}

contract Lottery is AccessControl, VRFConsumerBase, ReentrancyGuard {
    event Winner(
        address indexed winner,
        uint256 amount,
        uint256 timestamp,
        uint256 epoch
    );
    event UserClaimed(address winner, uint256 amount);
    event WinnerDrawn(
        uint256 amount,
        uint256[] ticket,
        uint256 epoch,
        uint256 timestamp,
        uint256 ticketCount,
        uint256 winnerCount
    );
    event TicketBought(
        address indexed user,
        uint256[6] ticket,
        uint256 amount,
        uint256 timestamp
    );
    event LotteryStarted(uint256 openTime);
    event SwapETHForTokens(uint256 amountIn, address[] path);
    event SwapETHForTokensWithFee(uint256 amountIn, address[] path);
    event ViceTransferredMarketing(uint256 amount);
    event ViceBurned(uint256 amount);
    event EthSwappedForLink(uint256 amount, address[] path);
    event EthLinkAmount(uint256 amount);

    using Counters for Counters.Counter;

    Counters.Counter public ticketCount;
    Counters.Counter public winnerCount;
    Counters.Counter public epoch;

    address vicex;
    address link;

    address public immutable deadAddress =
        0x000000000000000000000000000000000000dEaD;

    IUniswapV2Router02 public uniswapV2Router;

    address public uniswapV2Pair;

    address public ops; // dev wallet
    address public divs; // divs wallet
    address public viceRwards;

    // lotto start end and delta time
    uint256 public startTime;
    uint256 public startOffset;
    uint256 public lotteryLength = 300;
    // lotto state
    bool public open;
    bool processing;

    uint256 public ticketPrice = 12; // USD

    /**
        @dev powerball split percentages
     */

    uint256 public pb = 110;
    uint256 public pbOne = 55;
    uint256 public pbTwo = 12;
    uint256 public noPbThree = 13;
    uint256 public pbThree = 11;
    uint256 public noPbFour = 11;
    uint256 public pbFour = 22;
    uint256 public noPbFive = 86;
    uint256 public jackpot = 600;

    address[] winnersPb;
    address[] winnersPbOne;
    address[] winnersPbTwo;
    address[] winnersNoPbThree;
    address[] winnersPbThree;
    address[] winnersNoPbFour;
    address[] winnersPbFour;
    address[] winnersnoPbFive;
    address[] winnersJackpot;

    uint256 public reserve = 80; // reserve to fund next prize pool
    uint256 public opsFee = 50; // marketing wallet
    uint256 public lFee = 100; // liquidity fee
    uint256 public divFee = 100; // div fee
    uint256 public affiliateFee = 250;

    uint256 public n = 1000; // normalizer

    uint256 public dividendThreshold = 1 ether; //

    mapping(address => uint256[6][]) tickets;

    address[] users;
    mapping(address => uint256) balances;
    /**
        @dev Create the admin role, with `root` as a member.
        @dev link, coordinator, price feed, vicex address all needed for chainlink in production
    */

    // min amount of vicex in contract before an LP swap
    uint256 public swapThreshold = 250000000000 * (10**9);

    bool public swapEnabled = true;

    bytes32 internal keyHash;
    uint256 internal fee;
    uint256 internal linkThreshold = 12;

    uint256[] public winningTicket;

    AggregatorV3Interface internal ethUSDPriceFeed;
    AggregatorV3Interface internal ethLINKPriceFeed;

    /*
     *
     *  MAINNET
     *  LINK Token Address:  0x514910771AF9Ca656af840dff83E8264EcF986CA
     *  VRF Coordinator: 0xf0d54349aDdcf704F77AE15b96510dEA15cb7952
     *  Key Hash:    0xAA77729D3466CA35AE8D28B3BBAC7CC36A5031EFDC430821C02BC31A238AF445
     *  Fee: 2 LINK
     *
     *  KOVAN
     *  LINK Token Address:  0xa36085F69e2889c224210F603D836748e7dC0088
     *  VRF Coordinator: 0xdD3782915140c8f3b190B5D67eAc6dc5760C46E9
     *  Key Hash:    0x6c3699283bda56ad74f6b855546325b68d482e983852a7a82979cc4807b641f4
     *  Fee: 0.1 LINK
     *
     *  RINKEBY
     *  LINK Token Address:  0x01BE23585060835E02B77ef475b0Cc51aA1e0709
     *  VRF Coordinator: 0xb3dCcb4Cf7a26f6cf6B120Cf5A73875B7BBc655B
     *  Key Hash:    0x2ed0feb3e7fd2022120aa84fab1945545a9f2ffc9076fd6156fa96eaff4c1311
     *  Fee: 0.1 LINK
     *
     *   -------------
     *   VICEX
     *   -   Kovan Address: 0x974dcb29e3d24637bff14dcaccd32a742892c32f
     *
     *   ------------
     *  UNISWAP
     *  -   Address: 0x7a250d5630B4cF539739dF2C5dAcb4c659F2488D
     *
     */
    // VRFConsumerBase(VRFCoordinator, LINK token address)
    constructor(
        address _ethUSDPriceFeed,
        address _ethLINKPriceFeed,
        address _dex,
        address _vicex,
        address _link,
        address _ops,
        address _divs,
        uint256 _fee // decimals - 18
    )
        VRFConsumerBase(
            0xdD3782915140c8f3b190B5D67eAc6dc5760C46E9,
            0xa36085F69e2889c224210F603D836748e7dC0088
        )
    {
        _setupRole(DEFAULT_ADMIN_ROLE, msg.sender);

        link = _link;
        vicex = _vicex;

        IUniswapV2Router02 _uniswapV2Router = IUniswapV2Router02(_dex); // uniswap router (Mainnet & Kovan)

        // set Router for use by utility functions
        uniswapV2Router = _uniswapV2Router;

        /*  ETH/USD Pricefeed (decimals -8)
         *
         *  Mainnet: 0x5f4eC3Df9cbd43714FE2740f5E3616155c5b8419
         *  Rinkeby: 0x8A753747A1Fa494EC906cE90E9f37563A8AF630e
         *  Kovan: 0x9326BFA02ADD2366b30bacB125260Af641031331
         *
         *  Select appropriate address for pricefeedAggregator
         *
         *  -----------------
         *  LINK/ETH (decimals - 18)
         *  Mainnet: 0xDC530D9457755926550b59e8ECcdaE7624181557
         *  Kovan: 0x3Af8C569ab77af5230596Acf0E8c2F9351d24C38
         */
        ethUSDPriceFeed = AggregatorV3Interface(_ethUSDPriceFeed);
        ethLINKPriceFeed = AggregatorV3Interface(_ethLINKPriceFeed);

        keyHash = 0x6c3699283bda56ad74f6b855546325b68d482e983852a7a82979cc4807b641f4;
        fee = _fee; // 0.1 LINK (Varies by network)

        // dev wallet
        ops = _ops;
        // dividends wallet
        divs = _divs;
        // initial conditions
        open = false;
    }

    function depositEth(uint256 amount) payable external onlyAdmin {
        balances[address(this)] = amount;
    }
    /**
     * Returns the latest price
     */
    function getLatestEthUsdPrice() internal view returns (int256) {
        (, int256 price, , , ) = ethUSDPriceFeed.latestRoundData();
        return price;
    }

    function getLatestEthLinkPrice() internal view returns (int256) {
        // ETH/INK pricefeed are 10^18 based
        (, int256 price, , , ) = ethLINKPriceFeed.latestRoundData();
        return price;
    }

    function getLinkEthBuyAmount() internal view returns (uint256) {
        uint256 ratio = uint256(getLatestEthLinkPrice());
        ratio = ratio;
        return linkThreshold * ratio;
    }

    function setLinkThreshold(uint256 amount) external onlyAdmin {
        linkThreshold = amount;
    }

    function getTicketPrice() public view returns (uint256) {
        // ETH/USD pricefeed are 10^8 based
        uint256 price = uint256(getLatestEthUsdPrice());
        price = price / 10**8;
        return (ticketPrice * (10**18)) / price;
    }

    function getRandomNumber() public returns (bytes32 requestId) {
        require(
            IERC20(link).balanceOf(address(this)) >= fee,
            "Not enough LINK"
        );
        return requestRandomness(keyHash, fee);
    }

    function getWinningTicketLength() public view returns (uint256) {
        return winningTicket.length;
    }

    // draw
    function draw() external {
        require(open, "lottery is closed");
        require(
            startTime + lotteryLength <= block.timestamp,
            "sale is ongoing"
        );
        if (IERC20(link).balanceOf(address(this)) <= linkThreshold * 10**18) {
            uint256 amount = getLinkEthBuyAmount();
            emit EthLinkAmount(amount);
            require(
                balances[address(this)] > amount,
                "Not enough ether to buy link"
            );
            swapETHForTokens(link, address(this), amount);
        }
        // set open to false while winners are found
        open = false;

        for (uint256 i = 0; i < 7; i++) {
            getRandomNumber();
        }
    }

    function fulfillRandomness(bytes32 requestId, uint256 randomness)
        internal
        override
    {
        // time require
        // check if powerball
        uint256 randomNumber;
        if (winningTicket.length < 5) {
            randomNumber = (randomness % 69) + 1;
            winningTicket.push(randomNumber);
        } else if (winningTicket.length == 5) {
            randomNumber = (randomness % 26) + 1;
            winningTicket.push(randomNumber);
        }
    }

    function getUserTickets(address account)
        external
        view
        returns (uint256[6][] memory)
    {
        require(open, "lotto is closed, no tickets available");
        return tickets[account];
    }

    /*
     *  @function setLotteryLength
     *
     *  Set the length of time the lottery will
     *  take place in second to draw the RNG.
     *
     *    Human Readable Time  Seconds
     *    1 Hour   3600 Seconds
     *    1 Day    86400 Seconds
     *    1 Week   604800 Seconds
     *    1 Month (30.44 days) 2629743 Seconds
     */
    function setLotteryLength(uint256 length) external onlyAdmin {
        require(!open, "lotto is open, cannot set length");
        lotteryLength = length;
    }

    function setLotteryState(bool state) external onlyAdmin {
        open = state;
    }

    // start lotto
    function start() external {
        require(!open, "lotto is open, cannot start again");
        require(winningTicket.length == 0, "Must find winners first");
        startTime = block.timestamp;
        open = true;
        emit LotteryStarted(startTime);
    }

    function setTicketPrice(uint256 price) external onlyAdmin {
        ticketPrice = price;
    }

    /// @dev Restricted to members of the community.
    modifier onlyAdmin() {
        require(isAdmin(msg.sender), "Restricted to members.");
        _;
    }

    /// @dev Return `true` if the `account` belongs to the community.
    function isAdmin(address account) public view virtual returns (bool) {
        return hasRole(DEFAULT_ADMIN_ROLE, account);
    }

    function addAdmin(address account) public onlyAdmin {
        _setupRole(DEFAULT_ADMIN_ROLE, account);
    }

    function setDividendsDestination(address destination) public onlyAdmin {
        divs = destination;
    }

    // powerball percentage settings
    function setPb(uint256 percentage) public onlyAdmin {
        pb = percentage;
    }

    function setPbOne(uint256 percentage) public onlyAdmin {
        pbOne = percentage;
    }

    function setPbTwo(uint256 percentage) public onlyAdmin {
        pbTwo = percentage;
    }

    function setPbThree(uint256 percentage) public onlyAdmin {
        pbThree = percentage;
    }

    function setPbFour(uint256 percentage) public onlyAdmin {
        pbFour = percentage;
    }

    function setJackpot(uint256 percentage) public onlyAdmin {
        jackpot = percentage;
    }

    // no powerball percentage settings
    function setNoPbThree(uint256 percentage) public onlyAdmin {
        noPbThree = percentage;
    }

    function setNoPbFour(uint256 percentage) public onlyAdmin {
        noPbFour = percentage;
    }

    function setNoPbFive(uint256 percentage) public onlyAdmin {
        noPbFive = percentage;
    }

    function setLinkFee(uint256 _fee) public onlyAdmin {
        fee = _fee;
    }

    function claim() external {
        require(!processing, "Paying winners...please try again later");
        require(balances[msg.sender] > 0, "No balance for caller");

        uint256 balance = balances[msg.sender];
        payable(msg.sender).transfer(balance);

        balances[msg.sender] = 0;
        emit UserClaimed(msg.sender, balance);
    }

    receive() external payable {}

    // Affiliate will double as ops if no affiliate is specified
    function takeFees(uint256 amount, address affiliate)
        internal
        returns (uint256)
    {
        uint256 mFee = (amount * opsFee) / n;
        uint256 aFee = (amount * affiliateFee) / n;
        uint256 liquidityFee = (amount * lFee) / n;
        uint256 dFee = (amount * divFee) / n;
        // swap rewards amount and send to the staking contract
        // pay dividends
        if (balances[divs] >= dividendThreshold) {
            payable(divs).transfer(balances[divs]);
            balances[divs] = 0;
        } else {
            balances[divs] = balances[divs] + dFee;
        }

        // pay affiliate
        if (affiliate == ops) {
            balances[ops] = balances[ops] + aFee + mFee;
        } else {
            balances[affiliate] = balances[affiliate] + aFee;
            balances[ops] = balances[ops] + mFee;
        }

        swapETHForTokensWithFee(vicex, deadAddress, liquidityFee);

        return amount - mFee - aFee - liquidityFee - dFee;
    }

    function swapETHForTokens(
        address token,
        address destination,
        uint256 amount
    ) private {
        // generate the uniswap pair path of token -> weth
        address[] memory path = new address[](2);
        path[0] = uniswapV2Router.WETH();
        path[1] = token;

        // make the swap
        uniswapV2Router.swapExactETHForTokens{value: amount}(
            0, // accept any amount of Tokens
            path,
            destination, // Burn address
            block.timestamp + 300
        );

        emit EthSwappedForLink(amount, path);
    }

    function swapETHForTokensWithFee(
        address token,
        address destination,
        uint256 amount
    ) private {
        // generate the uniswap pair path of token -> weth
        address[] memory path = new address[](2);
        path[0] = uniswapV2Router.WETH();
        path[1] = token;

        // make the swap
        uniswapV2Router.swapExactETHForTokensSupportingFeeOnTransferTokens{
            value: amount
        }(
            0, // accept any amount of Tokens
            path,
            destination, // Burn address
            block.timestamp + 300
        );

        emit SwapETHForTokensWithFee(amount, path);
    }

    function setSwapBackSettings(uint256 _amount) external onlyAdmin {
        swapThreshold = _amount;
    }

    function buyTicket(uint256[6] memory ticket, address affiliate)
        public
        payable
        nonReentrant
    {
        require(
            msg.value >= getTicketPrice(),
            "Not enough ether sent to buy a ticket"
        );
        require(startTime + lotteryLength >= block.timestamp, "sale is over");
        require(open, "lottery has not started yet");
        require(ticket.length == 6, "Incorrect ticket format");
        require(
            tickets[msg.sender].length < 100,
            "Max amount of tickets reached"
        );

        users.push(msg.sender);
        // @dev - add fee check conditions
        uint256 jackpotAmount = takeFees(msg.value, affiliate);

        tickets[msg.sender].push(ticket);

        ticketCount.increment();

        balances[address(this)] = balances[address(this)] + jackpotAmount;
        emit TicketBought(msg.sender, ticket, msg.value, block.timestamp);
    }

    function findWinner(
        address ticketOwner,
        uint256[] memory wTicket,
        uint256[6] memory uTicket
    ) internal {
        require(wTicket.length == uTicket.length, "Incorrect Ticket Format");
        bool powerball;
        // last entry is powerball
        if (wTicket[5] == uTicket[5]) powerball = true;
        // for the first 5 number entries, any match constitutes a win
        uint256 matches = 0;
        for (uint256 i = 0; i < 5; i++) {
            uint256 uNumber = uTicket[i];
            // check if the number is a match
            for (uint256 j = 0; j < 5; j++) {
                if (uNumber == wTicket[j]) matches = matches + 1;
            }
        }
        // condition - no matches, no powerball
        if (!powerball && matches == 0) {
            return;
        }

        // condition - jackpot
        if (powerball && matches == 5) {
            winnersJackpot.push(ticketOwner);
            winnerCount.increment();
            return;
        }
        // condition - if five match
        else if (!powerball && matches == 5) {
            winnersnoPbFive.push(ticketOwner);
            winnerCount.increment();
            return;
        }
        // condition - if four match + powerball
        else if (powerball && matches == 4) {
            winnersPbFour.push(ticketOwner);
            winnerCount.increment();
            return;
        }
        // condition - if four match
        else if (!powerball && matches == 4) {
            winnersNoPbFour.push(ticketOwner);
            winnerCount.increment();
            return;
        }
        // condition - if three match + powerball
        else if (powerball && matches == 3) {
            winnersPbThree.push(ticketOwner);
            winnerCount.increment();
            return;
        }
        // condition - if three match
        else if (!powerball && matches == 3) {
            winnersNoPbThree.push(ticketOwner);
            winnerCount.increment();
            return;
        }
        // condition - if two match + powerball
        else if (powerball && matches == 2) {
            winnersPbTwo.push(ticketOwner);
            winnerCount.increment();
            return;
        }
        // condition - if one match + powerball
        else if (powerball && matches == 1) {
            winnersPbOne.push(ticketOwner);
            winnerCount.increment();
            return;
        } else if (powerball && matches == 0) {
            winnersPb.push(ticketOwner);
            winnerCount.increment();
            return;
        }
    }

    function setShares(address[] memory winners, uint256 percentage) internal {
        if (winners.length == 0) return;
        uint256 allWinners = winners.length;
        // payout is 50% of contract balance
        uint256 payoutPerWinner = (balances[address(this)] * percentage) /
            n /
            allWinners;
        for (uint256 i = 0; i < allWinners; i++) {
            if (balances[winners[i]] == 0)
                balances[winners[i]] = payoutPerWinner;
            else {
                balances[winners[i]] + payoutPerWinner;
            }
            emit Winner(
                winners[i],
                payoutPerWinner,
                block.timestamp,
                epoch.current()
            );
        }
    }

    function removeTicket(address ticketHolder) internal {
        delete tickets[ticketHolder];
    }

    function findWinners() external {
        // need better role system
        require(
            startTime + lotteryLength <= block.timestamp,
            "lottery is ongoing, cannot draw winning ticket."
        );
        require(!open, "lottery has not ended");
        require(
            winningTicket.length == 6,
            "Winning Ticket is being drawn or has not been called."
        );
        require(!processing, "Paying winners...please try again later");
        // emit winner event, at this point all chainlink callbacks have returned
        emit WinnerDrawn(
            getETHBalance(),
            winningTicket,
            epoch.current(),
            block.timestamp,
            ticketCount.current(),
            winnerCount.current()
        );
        // set processsing to true so no claims can be made until the balances are actualized
        processing = true;

        // loop over all known tickets
        for (uint256 i = 0; i < users.length; i++) {
            uint256[6][] memory userTickets = tickets[users[i]];
            // iterate through user tickets and find the highest win for a user
            for (uint256 j = 0; j < userTickets.length; j++) {
                findWinner(users[i], winningTicket, userTickets[j]);
            }
            removeTicket(users[i]);
        }
        setShares(winnersPb, pb);
        setShares(winnersPbOne, pbOne);
        setShares(winnersPbTwo, pbTwo);
        setShares(winnersNoPbThree, noPbThree);
        setShares(winnersPbThree, pbThree);
        setShares(winnersNoPbFour, noPbFour);
        setShares(winnersPbFour, pbFour);
        setShares(winnersnoPbFive, noPbFive);
        setShares(winnersJackpot, jackpot);

        // reset lottery state so the lottery can be started again
        rollOver();
        // claims actualized
        processing = false;
    }

    function reset() internal {
        // reset winner arrays
        delete winnersPb;
        delete winnersPbOne;
        delete winnersPbTwo;
        delete winnersPbThree;
        delete winnersNoPbThree;
        delete winnersPbFour;
        delete winnersnoPbFive;
        delete winnersJackpot;
        // reset participants array
        delete users;
        // reset winningTicket
        delete winningTicket;
        // reset ticket count
        ticketCount.reset();
        // reset winner count
        winnerCount.reset();
    }

    function getUserBalance(address account) public view returns (uint256) {
        return balances[account];
    }

    function rollOver() internal {
        require(
            startTime + lotteryLength <= block.timestamp,
            "sale has not ended"
        );
        require(!open, "sale is still active");
        require(startTime < block.timestamp, "sale has not started");
        reset();
        epoch.increment();
    }

    function withdrawLink(address recipient, uint256 amount) public onlyAdmin {
        IERC20(link).transfer(recipient, amount);
    }

    /*
     *  @function getETHBalance
     *
     *  Check the balance of ETH in the contract.
     *
     *  @returns address(this).balance <uint>
     *
     */
    function getETHBalance() public view returns (uint256) {
        return address(this).balance;
    }

    /*
     *  @function withdrawOverFlowETH
     *
     *  Withdraw the balance of ETH in the contract
     *  and send to the contract owner's wallet.
     *
     */
    function withdrawOverFlowETH() public onlyAdmin {
        address payable to = payable(msg.sender);
        to.transfer(getETHBalance());
    }
}