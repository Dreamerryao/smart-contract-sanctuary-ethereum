/**
 *Submitted for verification at Etherscan.io on 2022-02-09
*/

// File: @openzeppelin/contracts/utils/math/SafeMath.sol



pragma solidity ^0.8.0;

// CAUTION
// This version of SafeMath should only be used with Solidity 0.8 or later,
// because it relies on the compiler's built in overflow checks.

/**
 * @dev Wrappers over Solidity's arithmetic operations.
 *
 * NOTE: `SafeMath` is no longer needed starting with Solidity 0.8. The compiler
 * now has built in overflow checking.
 */
library SafeMath {
    /**
     * @dev Returns the addition of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function tryAdd(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            uint256 c = a + b;
            if (c < a) return (false, 0);
            return (true, c);
        }
    }

    /**
     * @dev Returns the substraction of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function trySub(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            if (b > a) return (false, 0);
            return (true, a - b);
        }
    }

    /**
     * @dev Returns the multiplication of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function tryMul(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            // Gas optimization: this is cheaper than requiring 'a' not being zero, but the
            // benefit is lost if 'b' is also tested.
            // See: https://github.com/OpenZeppelin/openzeppelin-contracts/pull/522
            if (a == 0) return (true, 0);
            uint256 c = a * b;
            if (c / a != b) return (false, 0);
            return (true, c);
        }
    }

    /**
     * @dev Returns the division of two unsigned integers, with a division by zero flag.
     *
     * _Available since v3.4._
     */
    function tryDiv(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            if (b == 0) return (false, 0);
            return (true, a / b);
        }
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers, with a division by zero flag.
     *
     * _Available since v3.4._
     */
    function tryMod(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            if (b == 0) return (false, 0);
            return (true, a % b);
        }
    }

    /**
     * @dev Returns the addition of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `+` operator.
     *
     * Requirements:
     *
     * - Addition cannot overflow.
     */
    function add(uint256 a, uint256 b) internal pure returns (uint256) {
        return a + b;
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting on
     * overflow (when the result is negative).
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     *
     * - Subtraction cannot overflow.
     */
    function sub(uint256 a, uint256 b) internal pure returns (uint256) {
        return a - b;
    }

    /**
     * @dev Returns the multiplication of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `*` operator.
     *
     * Requirements:
     *
     * - Multiplication cannot overflow.
     */
    function mul(uint256 a, uint256 b) internal pure returns (uint256) {
        return a * b;
    }

    /**
     * @dev Returns the integer division of two unsigned integers, reverting on
     * division by zero. The result is rounded towards zero.
     *
     * Counterpart to Solidity's `/` operator.
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function div(uint256 a, uint256 b) internal pure returns (uint256) {
        return a / b;
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * reverting when dividing by zero.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function mod(uint256 a, uint256 b) internal pure returns (uint256) {
        return a % b;
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting with custom message on
     * overflow (when the result is negative).
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {trySub}.
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     *
     * - Subtraction cannot overflow.
     */
    function sub(
        uint256 a,
        uint256 b,
        string memory errorMessage
    ) internal pure returns (uint256) {
        unchecked {
            require(b <= a, errorMessage);
            return a - b;
        }
    }

    /**
     * @dev Returns the integer division of two unsigned integers, reverting with custom message on
     * division by zero. The result is rounded towards zero.
     *
     * Counterpart to Solidity's `/` operator. Note: this function uses a
     * `revert` opcode (which leaves remaining gas untouched) while Solidity
     * uses an invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function div(
        uint256 a,
        uint256 b,
        string memory errorMessage
    ) internal pure returns (uint256) {
        unchecked {
            require(b > 0, errorMessage);
            return a / b;
        }
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * reverting with custom message when dividing by zero.
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {tryMod}.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function mod(
        uint256 a,
        uint256 b,
        string memory errorMessage
    ) internal pure returns (uint256) {
        unchecked {
            require(b > 0, errorMessage);
            return a % b;
        }
    }
}

// File: @openzeppelin/contracts/utils/Counters.sol



pragma solidity ^0.8.0;

/**
 * @title Counters
 * @author Matt Condon (@shrugs)
 * @dev Provides counters that can only be incremented, decremented or reset. This can be used e.g. to track the number
 * of elements in a mapping, issuing ERC721 ids, or counting request ids.
 *
 * Include with `using Counters for Counters.Counter;`
 */
library Counters {
    struct Counter {
        // This variable should never be directly accessed by users of the library: interactions must be restricted to
        // the library's function. As of Solidity v0.5.2, this cannot be enforced, though there is a proposal to add
        // this feature: see https://github.com/ethereum/solidity/issues/4637
        uint256 _value; // default: 0
    }

    function current(Counter storage counter) internal view returns (uint256) {
        return counter._value;
    }

    function increment(Counter storage counter) internal {
        unchecked {
            counter._value += 1;
        }
    }

    function decrement(Counter storage counter) internal {
        uint256 value = counter._value;
        require(value > 0, "Counter: decrement overflow");
        unchecked {
            counter._value = value - 1;
        }
    }

    function reset(Counter storage counter) internal {
        counter._value = 0;
    }
}

// File: @openzeppelin/contracts/utils/Context.sol



pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}

// File: @openzeppelin/contracts/access/Ownable.sol



pragma solidity ^0.8.0;


/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _setOwner(_msgSender());
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
        _;
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _setOwner(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _setOwner(newOwner);
    }

    function _setOwner(address newOwner) private {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}

// File: @openzeppelin/contracts/token/ERC20/IERC20.sol



pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `recipient`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address recipient, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `sender` to `recipient` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address sender,
        address recipient,
        uint256 amount
    ) external returns (bool);

    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);
}

// File: contracts/presail.sol


pragma solidity ^0.8.0;





contract StakeVault is Ownable {
    using SafeMath for uint256;
    using Counters for Counters.Counter;

    struct StakeInfo {
        uint256 id;
        uint256 dealId;
        address staker;
        uint256 amount;
        uint256 restAmount;
        bool isClaimed;
    }

    struct DealPrice {
        uint256 price;
        uint256 startDate;
        uint256 updateDate;
    }

    struct LeadInvestor {
        address investor;
        bool isStaked;
    }

    struct DealInfo {
        string name;
        string url;
        address sponsor;
        address stakingToken;
        uint256 startBonus;
        uint256 endBonus;
        uint256 preSaleAmount;
        uint256 minSaleAmount;
        uint256 maxSaleAmount;
        uint256[] stakeIds;
        LeadInvestor lead;
        DealPrice dealPrice;
        DealStatus status;
    }
    
    uint256 public offerPeriod;
    uint256[] private dealIds; 
    enum DealStatus { NotDisplaying, Staking, Offering, Delivering, Claiming, Closed }

    Counters.Counter private _dealIds;
    Counters.Counter private _stakeIds;

    mapping (uint256 => StakeInfo) public stakeInfo;
    mapping (uint256 => DealInfo) public dealInfo;
    mapping (address => bool) public allowedTokenList;
    mapping (address => uint256[]) private stakesByInvestor;
    mapping (address => uint256[]) private dealsBySponsor;

    event AddDeal(uint256, address);
    event UpdateDeal(uint256, address);
    event SetDealPrice(uint256, address);
    event UpdateDealPrice(uint256, address);
    event Deposit(uint256, uint256, address);
    event Withdraw(uint256, uint256, address);
    event Claim(uint256, uint256, address);

    constructor(address _token, uint256 _offerPeriod) {
        offerPeriod = _offerPeriod;
        allowedTokenList[_token] = true;
    }

    modifier allowedToken(address _token) {
        require(allowedTokenList[_token], "The staking token is not allowed");
        _;
    }

    modifier existDeal(uint256 _dealId) {
        require(_dealId <= _dealIds.current(), "The Deal is not exist.");
        _;
    }

    function addDeal(
        string memory _name,
        string memory _url,
        address _leadInvestor,
        uint256 _startBonus,
        uint256 _endBonus,
        uint256 _preSaleAmount,
        uint256 _minSaleAmount,
        uint256 _maxSaleAmount,
        address _stakingToken
    ) public allowedToken(_stakingToken) {
        _dealIds.increment();
        uint256 dealId = _dealIds.current();
        dealIds.push(dealId);
        dealsBySponsor[msg.sender].push(dealId);
        DealInfo storage deal = dealInfo[dealId];
        deal.name = _name;
        deal.url = _url;
        deal.lead.investor = _leadInvestor;
        deal.startBonus = _startBonus;
        deal.endBonus = _endBonus;
        deal.preSaleAmount = _preSaleAmount;
        deal.minSaleAmount = _minSaleAmount;
        deal.maxSaleAmount = _maxSaleAmount;
        deal.sponsor = msg.sender;
        deal.stakingToken = _stakingToken;
        updateDealStatus(dealId, DealStatus.NotDisplaying);

        emit AddDeal(dealId, msg.sender);
    }

    function updateDeal(
        uint256 _dealId,
        address _leadInvestor,
        uint256 _startBonus,
        uint256 _endBonus,
        uint256 _preSaleAmount,
        address _stakingToken
    ) public allowedToken(_stakingToken) {
        DealInfo storage deal = dealInfo[_dealId];
        require(deal.sponsor == msg.sender, "Only sponsor can update the deal.");
        require(deal.status == DealStatus.NotDisplaying || deal.status == DealStatus.Staking, "The deal status should be NotDisplaying or Staking.");
        require(deal.stakeIds.length < 1, "The deal should be empty.");
        deal.lead.investor = _leadInvestor;
        deal.startBonus = _startBonus;
        deal.endBonus = _endBonus;
        deal.preSaleAmount = _preSaleAmount;
        deal.stakingToken = _stakingToken;

        emit UpdateDeal(_dealId, msg.sender);
    }

    function updateDealStatus(
        uint256 _dealId,
        DealStatus _status
    ) public {
        DealInfo storage deal = dealInfo[_dealId];
        require(deal.sponsor == msg.sender || owner() == msg.sender, "You have no permission to update deal status.");
        require(deal.status != DealStatus.Closed, "You can not change status when the deal status is Closed.");

        if(_status == DealStatus.NotDisplaying) {
            require(deal.stakeIds.length < 1, "Set NotDisplaying: The deal should not have a stake.");
        } else if(_status == DealStatus.Staking) {
            require(deal.status < DealStatus.Delivering, "Set Staking: The deal status should not be Delivering, Claiming, Closed.");
        } else if(_status == DealStatus.Offering) {
            require(deal.status < DealStatus.Offering, "Set Offering: The deal status should be Staking.");
        } else if(_status == DealStatus.Delivering) {
            require(deal.status == DealStatus.Offering, "Set Delivering: The deal status should be Offering.");
            
            uint256[] memory stakeIds = deal.stakeIds;
            uint256 stakedAmount;
            for(uint256 i=0; i<stakeIds.length; i++) {
                StakeInfo storage stake = stakeInfo[stakeIds[i]];
                stakedAmount = stakedAmount.add(stake.amount);
            }
            require(deal.minSaleAmount <= stakedAmount, "Set Delivering: The staked amount should be over minSaleAmount.");

            if(owner() != msg.sender) {
                require(deal.dealPrice.startDate.add(offerPeriod) < block.timestamp, "Set Delivering: The sponsor cannot set status as a Delivering until 7 days after post a deal price.");
            }
        } else if(_status == DealStatus.Claiming) {
            require(owner() == msg.sender && deal.status == DealStatus.Delivering, "Set Claiming: Only owner can set Claiming status when the deal status is Delivering.");
        } else if(_status != DealStatus.Closed) {
            revert("You cannot change the deal status.");
        }
        
        deal.status = _status;
    }

    function checkDealStatus(
        uint256 _dealId,
        DealStatus _status
    ) public view existDeal(_dealId) returns(bool) {
        DealInfo storage deal = dealInfo[_dealId];
        return deal.status == _status;
    }

    function setDealPrice(
        uint256 _dealId,
        uint256 _price
    ) public existDeal(_dealId) {
        DealInfo storage deal = dealInfo[_dealId];
        require(deal.sponsor == msg.sender, "Only sponsor can set the price");
        updateDealStatus(_dealId, DealStatus.Offering);
        deal.dealPrice.price = _price;
        deal.dealPrice.startDate = block.timestamp;

        emit SetDealPrice(_dealId, msg.sender);
    }

    function updateDealPrice(
        uint256 _dealId,
        uint256 _price
    ) public existDeal(_dealId) {
        DealInfo storage deal = dealInfo[_dealId];
        require(deal.sponsor == msg.sender, "Only sponsor can set the price");
        require(deal.status == DealStatus.Offering, "The deal status should be Offering.");
        deal.dealPrice.updateDate = block.timestamp;
        deal.dealPrice.price = _price;

        emit UpdateDealPrice(_dealId, msg.sender);
    }

    function deposite(
        uint256 _dealId,
        uint256 _amount
    ) public existDeal(_dealId) {
        require(checkDealStatus(_dealId, DealStatus.NotDisplaying) || checkDealStatus(_dealId, DealStatus.Staking) || checkDealStatus(_dealId, DealStatus.Offering), "The deal status should be NotDisplaying, Staking or Offering.");
        require(_amount > 0, "The deposite amount is not enough.");
        DealInfo storage deal = dealInfo[_dealId];
        require(deal.lead.investor != address(0), "The deal should have a lead investor.");
        if(deal.lead.investor != msg.sender) {
            require(deal.lead.isStaked, "The lead investor should stake at first.");
        } else {
            if(!deal.lead.isStaked) {
                deal.lead.isStaked = true;
            }
        }
        _stakeIds.increment();
        uint256 stakeId = _stakeIds.current();
        address staker = msg.sender;
        StakeInfo storage stake = stakeInfo[stakeId];
        stake.id = stakeId;
        stake.dealId = _dealId;
        stake.staker = staker;
        stake.amount = _amount;
        stakesByInvestor[staker].push(stakeId);
        if(deal.stakeIds.length < 1) {
            deal.status = DealStatus.Staking;
        }
        deal.stakeIds.push(stakeId);
        IERC20(deal.stakingToken).transferFrom(staker, address(this), _amount);

        emit Deposit(_dealId, _amount, staker);
    }

    function withdraw(
        uint256 _stakeId
    ) public {
        StakeInfo storage stake = stakeInfo[_stakeId];
        uint256 _dealId = stake.dealId;
        require(!(checkDealStatus(_dealId, DealStatus.Delivering) && checkDealStatus(_dealId, DealStatus.Claiming)), "The deal status should not be a Delivering or Claiming.");
        require(stake.staker == msg.sender, "Must be a staker");
        require(stake.amount > 0, "The withdraw amount is not enough.");
        DealInfo storage deal = dealInfo[_dealId];
        uint256 _amount = stake.amount;
        stake.amount = 0;
        IERC20(deal.stakingToken).transfer(msg.sender, _amount);

        emit Withdraw(_dealId, _amount, msg.sender);
    }

    function claim(
        uint256 _dealId
    ) external {
        require(checkDealStatus(_dealId, DealStatus.Claiming), "The deal status should be Claiming.");
        DealInfo storage deal = dealInfo[_dealId];
        require(deal.sponsor == msg.sender, "Must be a sponsor of this deal");
        updateDealStatus(_dealId, DealStatus.Closed);
        
        uint256[] memory stakeIds = deal.stakeIds;
        uint256 claimAmount;
        
        for(uint256 i=0; i<stakeIds.length; i++) {
            StakeInfo storage stake = stakeInfo[stakeIds[i]];
            
            if(!stake.isClaimed) {
                claimAmount = claimAmount.add(stake.amount);
                stake.isClaimed = true;

                if(claimAmount > deal.maxSaleAmount) {
                    uint256 diffAmount = claimAmount.sub(deal.maxSaleAmount);
                    stake.restAmount = diffAmount;
                    stake.amount = stake.amount.sub(diffAmount);
                    claimAmount = deal.maxSaleAmount;
                    break;
                } else {
                    stake.amount = 0;
                }
            }
        }
        
        IERC20(deal.stakingToken).transfer(msg.sender, claimAmount);

        emit Claim(_dealId, claimAmount, msg.sender);
    }

    function sendBack(
        uint256 _stakeId
    ) public {
        StakeInfo storage stake = stakeInfo[_stakeId];
        DealInfo storage deal = dealInfo[stake.dealId];
        require(deal.status == DealStatus.Closed, "The deal status should be a Closed.");
        require(deal.sponsor == msg.sender || owner() == msg.sender, "You have no permission to send back the staked amount.");
        require(stake.amount > 0, "The withdraw amount is not enough.");
        uint256 _amount = stake.amount;
        stake.amount = 0;
        IERC20(deal.stakingToken).transfer(stake.staker, _amount);
    }

    function getBonus(
        uint256 _stakeId
    ) public view returns(uint256) {
        StakeInfo memory stake = stakeInfo[_stakeId];
        DealInfo memory deal = dealInfo[stake.dealId];
        uint256[] memory stakeIds = deal.stakeIds;
        uint256 stakedAmount; // total staked amount in the deal before _staker stake 
        uint256 bonus; // the average bonus of the _staker after staking
        uint256 _amount = stake.amount; // staked amount while in the presale
        
        for(uint256 i=0; i<stakeIds.length; i++) {
            if(_stakeId <= stakeIds[i]) {
                break;
            }
            StakeInfo memory _stake = stakeInfo[stakeIds[i]];
            if(_stake.amount > 0) {
                stakedAmount = stakedAmount.add(_stake.amount);
            }
        }

        if(deal.preSaleAmount < stakedAmount) {
            return 0;
        }

        if(deal.preSaleAmount < stakedAmount.add(_amount)) {
            _amount = deal.preSaleAmount.sub(stakedAmount);
        }

        bonus = deal.startBonus.sub(deal.endBonus).mul(deal.preSaleAmount.sub(stakedAmount).sub(_amount.div(2))).div(deal.preSaleAmount).add(deal.endBonus);

        return bonus;
    }

    function getEstimateBonus(
        uint256 _dealId,
        uint256 _amount
    ) public view returns(uint256) {
        if(_amount <= 0) {
            return 0;
        }

        DealInfo memory deal = dealInfo[_dealId];
        uint256[] memory stakeIds = deal.stakeIds;
        uint256 stakedAmount; // total staked amount in the deal before _staker stake 
        uint256 bonus; // the average bonus of the _staker after staking
        
        for(uint256 i=0; i<stakeIds.length; i++) {
            StakeInfo memory _stake = stakeInfo[stakeIds[i]];
            if(_stake.amount > 0) {
                stakedAmount = stakedAmount.add(_stake.amount);
            }
        }

        if(deal.preSaleAmount < stakedAmount.add(_amount.div(2))) {
            return 0;
        }

        bonus = deal.startBonus.sub(deal.endBonus).mul(deal.preSaleAmount.sub(stakedAmount).sub(_amount.div(2))).div(deal.preSaleAmount).add(deal.endBonus);

        return bonus;
    }

    function getDealIds() public view returns(uint256[] memory) {
        return dealIds;
    } 

    function getStakes (uint256 _dealId) public view returns(uint256 [] memory) {
        DealInfo storage deal = dealInfo[_dealId];
        return deal.stakeIds;
    }
    
    function getInvetorStakes (address _investor) public view returns(uint256 [] memory) {
        return stakesByInvestor[_investor];
    }
    
    function getSponsorDeals (address _sponsor) public view returns(uint256 [] memory) {
        return dealsBySponsor[_sponsor];
    }
    
    function addAllowedToken(
        address _token
    ) public onlyOwner {
        allowedTokenList[_token] = true;
    }

    function isAllowedToken(
        address _token
    ) public view returns(bool) {
        return allowedTokenList[_token];
    }

    function currentBlockTime() public view returns (uint256) {
        return block.timestamp;
    }
}