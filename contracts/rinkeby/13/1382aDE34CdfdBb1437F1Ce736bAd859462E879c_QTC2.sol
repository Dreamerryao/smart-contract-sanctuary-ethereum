// SPDX-License-Identifier: GPL-3.0
// 
// WBBQBQHBgRWBgWRHRHWHQgBHQQRQgHHgRgWHHHWQRWBRRQQWRgHBggQWWgQRRRWQRWggRQBQWBBWQRWRHBgRHRgWHBHgQgRWBRQRQBWQHWBWHHgWHHQWQggBHHBgRWRgggWHBgRggWBQWgWWBHQRBRHgXv"<"[email protected]
// RQRQHgRBQQgQgBRBHQWgBQQHgggQRHWHBggHRWBWQHRgHBBRRHgQHQQWHggHWHgBBWBRgHHRgRWBBBHgHQgWWHBHHHWBBgRWWRQRHWRHHHWRHHBgRWHRBWHWWRWHRWWHQWRWRggQRBQWHRBWRQgQWX''`- . cB
// BWgWRgggQWBHWWggWggRBBQRQBBRWHWgHBBHRgRWHRWRWWggRHBBWWWBgRHRWHHHWHBBQRQQRWHHgWWHHBBWHQRBgQQBBHggRWWQQggQgHBBRWWHgWHWRRBBBWHRHBBRRgggBQRgBQQgHBRHRBgWRWB~`  . .yQ
// BBRWHBQgWWBQBQWQgggHBgHWRBWgQgQBRBHRQRRHHgggRHgRQQRRWWBgHBWBBWHRRQQgHRHQRWHHHWWBgQWHWWgWRHRRRgWBWWRRWWHBQRWWHBHWgRHHgBQBHgBQQWgQRQgBHWHBWQgWWRRRHQHWWgg'.'  v W
// RRBRRgQBBgWBgHHRRQRBWHgQHRBRBRQHggWHggBRWHHQHRRBWRRBgWgHBWBBHHQBRRHBBWHWHBHQQgRRRRggHQHHHWRHRBWgQWQQQQWBQHBBBRQBggQgBRHBBOPMO&WHWRBQBWRWWgWHRQgRgQWHRO].-- TsyqH
// BRWQBWBWgRWBBQWQBgHQQHRBggWRRRgHWgRBWBRgWBRHWRgWgQQQgWRgHHRQHWBWBWgRQRHWRHRQWHWRQWQWQBBQBgQQRRQBgQQHWQWWQRRWBRBWgHWgQgQar `.`_vBRgBBgQBBWRWRBRBWHBHGi''   [email protected]
// QQRBQWHHBRRWBRWWRBgHRQRRgHgBHHQHHBBgWQRHWgHHWQWWWBWgH[email protected]GT.' ``  :MggRgHHgHQHQgBQBBgRQBD''    `LHgQB
// RgBRgWRQgHRQWgQWRHRHQHWQRHgWRWQBRRRHQBRRHBWQBWQQHgQWBHQRRRBgRgWQgWHHQWHBRgWBWHBHH$<,><<;YWQWBQBBRBBRRBHQWWRHgWWBRH%T<:-`..'.. '*@QQHgRHBRRgRBHHWRggH '''-    XBRR
// WRQRHWRgWHHRRQBBgQBHBWBRQWgHQQQQRRHWBBHRHBHWWQWWWHHBRQRgggHBWgggBWQBWHHgRBBgWBHBU#x`  `' ^[email protected]'...``~.  ` zHRHBHBWgQHBBgRQQWBBO ' -  ..}WRB
// QBRBRWWHBBRRQR[email protected]f~ '--'`''.- [email protected]''`  ' ..1h8efOgHRHQRgBggRQggBBQHWi.  .  - ^%gBB
// RgggWgWBRgQWWQWBgWWBgRHggQRWHgBQHRWQRQWBRRBRRRQRHQBHRBQgWWBHQQRWQQHWWHBgBBQHgO='  -  -' ,}yCpRBWWBRWRWWRRQQHWWWQBT`.-   .--.CggRgHWgBWBQBgRQRBBHBWWQ3 '`.,''' KggHB
// [email protected]WggWHBHRWHQOf:'`.`.'.'`._&BHHBWgWRggQWRHgBgRWWgR3_ . - ` ``'DgQWHBRWBQgRBQWRggHWRBHRM"- >v  ' >@gHHR
// WRWRWgWBRWRQHRgWWgBBQQHgWRgWWWgRBBgRBHgg&&0I}-,+?EBRHWHQWQQgRWRQRHQgBRgW&n(.` .-'.``>`-'{gQQRQHHgQHWWQBRQQBHgHWT'--' '`.'`- BQRBBgHWBggggQBHQggWQBQBu>  <.'. YHHRgB
// gHBgWRBBBQHHHQHHQHQWBBgQWBHBWRBBRHQOC\<'..'-.-- .4RgHWgHBHBWRBQWgBBHQQ$! '  -` -.`-;-  cgggHgBHQRBRBQBQHRHHggY ` .  `,.. '~HQHBHBgRHRHWQQRHRRRBWWgG:'...`.  |NBQBQR
// QBgRBWRQgRWHRHgWBHHWWQHgRgRgRMXzYi>.-.''-  <Yz1YeNWRggWQWRgHHWHWQBRQB3,-- -'  <=::Tn_   iOBHBggQRHRRHgQBHQQBH!-   `'_]! '`1RgRgHRHgWQRBBBWBQHHWWQHHDr -` ' ``:XHHBBB
// QWWRBgRgHgggWHRBgRRRBgQWWHRgHHgbi!-``'```'^ -` (BBQRRWBWRgggQBRRRQBBQHg8= '_:_ ..`.-!([email protected]`-nHgHWWWWBRgRQRggQHgBi. --``.(`'' ;qBRBHBgWHgRBRWRBHQRQgBQ.`   -'~ZFSD
// ggDOBb4foo$&WHRHHgHRHkL|\/= `` '-.=Jw5+ `~ 'kQBBHRQgRBWWgHRgBR&5sxv!' -`rTAKXv..  TgWgy -|RRQRRHggBWRHHWHQgBHD`-. `-``!.' .,OWHHHRggQRBgQRRWWRgHRRHa` `-   .`rOHBWF
// \)",.-`. --- <ZRBQQBWRm_spM6z^` .-,1MBRa  [email protected]^```>=**xpQBBB$`- ^>[email protected]' . ''  _y'>'`*@RBHHggBWQQWQHWRQHQgRRV.-``. ' .')@g
// ` .-`.-`+>}3SOgBWRgBBH4EM4Y~:C|[email protected] `>lCXSMRWQWWQWQHBg%.."y^vBQHHgK >KHRHHgQBBBBgBHgRWBN ``!' .'-='xd"`cQRWBgBWgHgRHQQBQBHQHBV'```;`.   3QW
// )Ynen=.*n,"zbRgBWgQgHWtr<[email protected]:s9UHmv_&QBBWBRBi`2NBHgRWQQWBWQWgH&| .FXu9BHQBD9r xRHWQgBBRBQQBQQBHQ& '.~i<.'''=wHK .$RHgRRBHRWRHBQgQQQWBR'-  uO   -|RW
// t44MQBdeCsyp&BgWRHWggWgOf1jNWB4v^~}ZNQBgWRB2*i\YB&ORRQRHgBW$ytoPWHgBWWgggRQWg$,_^,"LYPWBWNv=;[email protected]].`` vF- .'}M/( >&HBBWHQBHgBgQgWHROtv' `'/&. '.:IgW
// RQBRRQHBQgBBggRWHQWQBRWRgHBWBBWWQD&&[email protected]&BRQHHRWRHRBRgWQQRQHBH5|>. `-- "v'.'[email protected]@RBQWgBQggBgWBRBWu'  ' |JMRg. ''-oHgg
// QRgRBgWWWQHggQQQggQRBRRRQBWBgWBHWgHQgBgggggWQBRQBRWRQQBQBBQWBRgQQHWBBgBWHQWQQHRBQHWRHQHWHRHWQHRgWWQWRgRWWf'  -`ryXmH'` .:&BBQgBgHHRQRRggHRHBWgWD> ' iKHRHWRv  .|gBWg
// QBRRHHBgHQWWQHggHHBQQgBggRRHgQQBBRggBgWggWHHHRQgBRRgWQRgBQQHWQQHRQWWQWWWRQggWHBggWQgQggHgRQBWgQgQWHRWgBHQj`-`/CMQQRBgj'--JQHRHBRWBgQWgHWHRBgRgQB1  "hWBRRWgW$ ` bBWHQ
// WgHHQQWWRWBRRBgQHWWgQgQBBQQBgHWRRWgQHBHRQWgHBWRBHWWWQBQQBQBBgQQWRgRHgQHHgHHWHBgHWQggHBQHBBBBgHRggHWQBgNer'=2OQRQWBHWHO.- @RgQWWRgBRWWBgWRBBQDXn|' -ngWBWHHQQW4 -{HHWHR
// WRHHWRBRWBWWBRgRBgQgQBggHgRQHBRBBHQRHgWBQHgHRgQBRBRgBgWgWQBQWRWBRWWRQgQgWRRggWRBBHBRgRggRBBQBgBgHBBHQQHh'`)DHRHHHWBHWgM '-&gHBBgHWBWBBHHRBQR&"-' -eRHQggBRQWH1 `LRBBWR
// QHHHHHWWWRRgHWBRRRQgBBBWWQRQHWHRBQWgBHWHHQBBHHWWRHQQWgQWQRWWBWWgRBWQggHgHHHBQgWgRHRHQHgRQgRHWgBWRBQHRHQO*-<[email protected]=-``i%OWRRWBQBBBBWRHWHgNX gBggRRHgWgRHQQBBR QQH
// QQWRBWQQQHHQWgRgBWRgBHRHggRWWHRggRgBgRWBRBRBRBBRRQWBWBRggWBgHQHgWBgQWRWRBRHgWQWBQWgRWQgHHBQBgQHRggRRRHHBWXoe#hQBHBBgQOi;/[v;|/fRgBgQgWWQgWBQQRHHDMqM0HWgRgBR2cl[xl;{]k
// 
// 
// The Quest to Consciousness - Stage 2

pragma solidity ^0.8.16;

import "erc721a/contracts/ERC721A.sol";
import "@openzeppelin/contracts/utils/Strings.sol";
import "@openzeppelin/contracts/utils/Base64.sol";
import "@openzeppelin/contracts/security/ReentrancyGuard.sol";
import "@openzeppelin/contracts/access/Ownable.sol";

interface iSVGManip {
    function add_gradients(uint256 tokenId, uint256 seed, bytes memory output, bool eukaryote, uint256 _type, uint64 entropy) view external returns (bytes memory);
    function add_filters(uint256 tokenId, bytes memory output, bool eukaryote, uint64 _type, uint64 repros, uint64 entropy, bool blur) view external returns (bytes memory);
    function add_nucleus(uint256 tokenId, bytes memory output, uint64 _type) view external returns (bytes memory);
    function add_cell_body(uint256 tokenId, bytes memory output, uint64 _type) external view returns (bytes memory);
    function add_spooky_overlay(uint256 tokenId, bytes memory output, uint64 _type) view external returns (bytes memory);
    function get_cell_entropy(uint256 tokenId, uint64 entropy) external view returns (uint64);
    function get_cell_core_color(uint256 tokenId) external view returns (uint256);
    function get_scale_value(uint256 tokenId, uint64 _type, bool eukaryote) external view returns (bytes memory);
    function breedEukaryotes(uint256 tokenId1, uint256 tokenId2, uint64 tokenGen1, uint64 tokenGen2, uint64 hunger1, uint64 hunger2) external view returns (uint256, uint256, uint256, uint256);
    function breedProkaryote(uint256 tokenId, uint64 tokenGen, uint64 hunger) external view returns (uint256);
}

interface iUtils {
    function random(bytes memory input) external pure returns (uint256);
    function get_seed_phrase() external view returns (string memory);
    function get_rand_in_range_toStr(uint256 rand, uint256 min, uint256 range) external pure returns (string memory);
    function add_trait(bytes memory json, string memory trait_type, string memory trait_value) external pure returns (bytes memory);
}

interface iQTC1 {
    function _ownerOf(uint256 tokenId) external view returns (address);
    function _isAlive(uint256 tokenId) external view returns (bool);
    function _isMutant(uint256 tokenId) external view returns (bool);
    function _isSponsored(uint256 tokenId) external view returns (bool);
    function pluck_number_AA(uint256 tokenId) external view returns (uint256);
    function sacrifice(uint256 tokenId) external;
}

contract QTC2 is ERC721A, ReentrancyGuard, Ownable {
    event ProkaryoteReproductionCycle(uint256 cycles, uint256 mutations, uint256 deaths);
    event ProkaryoteMutation(uint256 tokenId);
    event EukaryoteReproductionCycle(string new_type, string new_entropy, uint256 new_seed, uint256 new_tokenId);
    
    struct CellData {
        bool eukaryote;
        bool sponsored;
        uint64 _type;
        uint64 repros;
        uint64 entropy;
        uint64 hunger;
        uint256 seed;
    }

    struct ReproData {
        uint64 deaths;
        uint64 mutations;
        uint64 cycles;
        uint64 loop_idx;
        uint64 repro_count;
    }

    struct TokenOutput {
        bytes json;
        bytes svg_data_bytes;
    }

    address public SVGManip_ADDY;
    address public QTC1_ADDY;
    address public UTILS_ADDY;

    string public _seed_phrase;

    uint256 public PRICE_PER_AA_EVOLUTION = 10000000 gwei; // 10000000 gwei == 0.01 ether
    uint256 public PRICE_PER_PRO_REPRO = 1000000 gwei; // 1000000 gwei == 0.001 ether
    uint256 public PRICE_PER_EU_REPRO = 10000000 gwei; // 10000000 == 0.01 ether
    uint256 public PRICE_PER_INST_FEEDING = 100000000 gwei; // 10000000 == 0.1 ether
    uint256 public PRICE_PER_LAB_MINT = 100000000 gwei; // 10000000 == 0.1 ether
    uint256 public FLOOR_PRICE = 1 ether;

    bool public MINT_ACTIVE = true; // replace with false
    bool public LAB_MINT_ACTIVE = true; // replace with false
    uint256 public TOKENS_MINTED = 0;
    
    string[4] internal cell_types = ["Eukaryote", "Epithelial Cell", "Muscle Cell", "Nervous Cell"];
    string[3] internal entropies = ["Low", "Medium", "High"];

    iSVGManip internal SVGManip;
    iQTC1 internal QTC1;
    iUtils internal Utils;

    mapping(uint256 => CellData) internal cells;
    mapping(uint256 => uint256) internal SPONSORED_REPROS;

    constructor(address _QTC1_ADDY, address _SVGManip_ADDY, address _UTILS_ADDY) ERC721A("Life - Cells", "L2") {
        SVGManip_ADDY = _SVGManip_ADDY;
        SVGManip = iSVGManip(SVGManip_ADDY);
        QTC1_ADDY = _QTC1_ADDY;
        QTC1 = iQTC1(QTC1_ADDY);
        UTILS_ADDY = _UTILS_ADDY;
        Utils = iUtils(_UTILS_ADDY);
        _seed_phrase = Utils.get_seed_phrase();
    }

    function _evolve_AA(uint256 tokenId, bool sponsored) internal {
        require(QTC1._ownerOf(tokenId) == msg.sender, "don't own");
        require(QTC1._isAlive(tokenId) == true, "not alive");
        cells[TOKENS_MINTED] = CellData({
            eukaryote: QTC1._isMutant(tokenId), 
            repros: 1, 
            _type: 0, 
            entropy: 0,
            sponsored: sponsored,
            seed: TOKENS_MINTED,
            hunger: 0
        });
        QTC1.sacrifice(tokenId);
        _safeMint(msg.sender, 1);
        ++TOKENS_MINTED;
    }

    function _mint_lab_cell() external payable {
        require(LAB_MINT_ACTIVE, "not open");
        require(msg.sender == tx.origin, "sender != origin");
        require(msg.value == PRICE_PER_LAB_MINT);
        cells[TOKENS_MINTED] = CellData({
            eukaryote: false, 
            repros: 1, 
            _type: 0, 
            entropy: 0,
            sponsored: false,
            seed: TOKENS_MINTED,
            hunger: 500
        });
        _safeMint(msg.sender, 1);
        ++TOKENS_MINTED;
    }

    function _evolve_proteins(uint256[] memory tokenIds) external nonReentrant payable {
        require(MINT_ACTIVE, "not open");
        require(msg.sender == tx.origin, "sender != origin");
        require(msg.value == PRICE_PER_AA_EVOLUTION * tokenIds.length);
        for (uint256 i = 0; i < tokenIds.length; ++i) {
            require(QTC1._isSponsored(tokenIds[i]) == false, "Protein is sponsored!");
            _evolve_AA(tokenIds[i], false);
        }
    }

    function _evolve_sponsored_protein(uint256 tokenId) external nonReentrant {
        require(MINT_ACTIVE, "not open");
        require(msg.sender == tx.origin, "sender != origin");
        require(QTC1._isSponsored(tokenId) == true, "Not sponsored");
        _evolve_AA(tokenId, true);
    }

    function _make_svg(uint256 tokenId, uint256 seed, bool eukaryote, uint64 _type, uint64 repros, uint64 entropy) internal view returns (bytes memory) {
        bytes memory output;
        output = abi.encodePacked('<svg xmlns="http://www.w3.org/2000/svg" xmlns:xlink="http://www.w3.org/1999/xlink" preserveAspectRatio="xMinYMin meet" viewBox="0 0 350 350">\n<rect width="100%" height="100%" fill="black" />');
        output = SVGManip.add_gradients(tokenId, seed, output, eukaryote, _type, entropy);
        output = SVGManip.add_filters(tokenId, output, eukaryote, _type, repros, entropy, false);
        if (eukaryote == true) {output = SVGManip.add_filters(tokenId, output, eukaryote, _type, repros, entropy, true);}
        
        output = abi.encodePacked(
                output, '\n<g style="transform: rotate(', 
                Utils.get_rand_in_range_toStr(Utils.random(abi.encodePacked("CELLS SVG ROTATION", _seed_phrase, tokenId)), 0, 1000), 
                'deg) ', SVGManip.get_scale_value(tokenId, _type, eukaryote), ';transform-origin:175px 175px;">'
            );

        output = SVGManip.add_cell_body(tokenId, output, _type);

        // Nucleus
        output = SVGManip.add_spooky_overlay(tokenId, output, _type);
        output = abi.encodePacked(output, '\n</g>\n<rect x="25" y="335" width="300" height="5" style="fill: url(#line_grad); "/>\n</svg>');

        return output;
    }

    function _make_json(uint256 tokenId, bool eukaryote, uint64 _type, uint64 repros, uint64 entropy, uint64 hunger, bool sponsored) internal view returns (bytes memory) {
        bytes memory output;
        output = abi.encodePacked('{"name": "Cell #', Strings.toString(tokenId),
            '", "description": "Cells are randomly generated from alive proteins and then stored on-chain.", "attributes": [');

        output = Utils.add_trait(output, "Core Color", (SVGManip.get_cell_core_color(tokenId) == 1) ? "Dark" : "Light");
        output = Utils.add_trait(output, "Entropy", entropies[entropy]);
        output = Utils.add_trait(output, "Sponsored", (sponsored == true) ? "Yes" : "No");
        output = Utils.add_trait(output, "Cell Type", (eukaryote == true) ? cell_types[_type] : "Prokaryote");
        output = Utils.add_trait(output, "Found yet?", (tokenId + 1 > TOKENS_MINTED) ? "No" : "Yes");
        output = Utils.add_trait(output, "Age", Strings.toString(1 + repros / 5));
        output = Utils.add_trait(output, "Hunger", string.concat(Strings.toString(hunger / 1000), "%"));
        output = abi.encodePacked(
            output, '{"trait_type": "Reproductions", "value": "',  
            Strings.toString(repros), '"}'
        );
        return output;
    }

    function tokenURI(uint256 tokenId) override public view returns (string memory) {
        TokenOutput memory output;
        CellData memory cell = cells[tokenId];

        output.svg_data_bytes = _make_svg(tokenId, cell.seed, cell.eukaryote, cell._type, cell.repros, SVGManip.get_cell_entropy(tokenId, cell.entropy));
        output.json = _make_json(tokenId, cell.eukaryote, cell._type, cell.repros, SVGManip.get_cell_entropy(tokenId, cell.entropy), cell.hunger, cell.sponsored);
        output.json = bytes(string(abi.encodePacked(
            output.json, 
            '], "image": "data:image/svg+xml;base64,', 
            Base64.encode(bytes(string(output.svg_data_bytes))), '"}'
        )));
        return string(abi.encodePacked('data:application/json;base64,', Base64.encode(output.json)));
    }

    function sacrifice(uint256 tokenId) external {
        _burn(tokenId);
    }

    function get_cell_type(uint256 tokenId) external view returns (uint256) {return cells[tokenId]._type;}
    function get_cell_entropy(uint256 tokenId) external view returns (uint256) {return cells[tokenId].entropy;}
    function get_cell_age(uint256 tokenId) external view returns (uint256) {return cells[tokenId].repros;}

    function _breed_prokaryotes(uint256[] memory tokenIds) external nonReentrant payable {
        require(msg.sender == tx.origin, "sender != origin");
        ReproData memory repro;
        CellData memory cell;
        repro.cycles = 0;
        uint256 _TOTAL_PRICE = 0;

        while (repro.cycles < tokenIds.length) {
            require(ownerOf(tokenIds[repro.cycles]) == msg.sender, "not owner");
            require(cell.eukaryote == false, "not prokaryote");

            cell = cells[tokenIds[repro.cycles]];
            uint256 repro_outcome = SVGManip.breedProkaryote(tokenIds[repro.cycles], cell.repros, cell.hunger);
            if (repro_outcome == 1) {
                _burn(tokenIds[repro.cycles]);
                ++repro.deaths;
            } else if (repro_outcome == 2) {
                cell.eukaryote = true;
                ++repro.mutations;
                emit ProkaryoteMutation(tokenIds[repro.cycles]);
            }

            if ((cell.sponsored == true) && (SPONSORED_REPROS[tokenIds[repro.cycles]] < 5)) {
                SPONSORED_REPROS[tokenIds[repro.cycles]]++;
            } else {
                _TOTAL_PRICE = _TOTAL_PRICE + PRICE_PER_PRO_REPRO;
            }
            ++cell.repros;
            cell.hunger = (cell.hunger > 100) ? cell.hunger - 100 : 0;
            cells[tokenIds[repro.cycles]] = cell;
            ++repro.cycles;
        }
        require(msg.value == _TOTAL_PRICE);
        emit ProkaryoteReproductionCycle(repro.cycles, repro.mutations, repro.deaths);

    }

    function _breed_eukaryotes(uint256 tokenId1, uint256 tokenId2) external nonReentrant payable {
        require(msg.sender == tx.origin, "sender != origin");
        require(tokenId1 != tokenId2, "same cells");
        require(ownerOf(tokenId1) == msg.sender, "not owner");
        require(ownerOf(tokenId2) == msg.sender, "not owner");
        // Breeding eukaryotes has a 10% chance of a cell dying and it has a 20% chance to produce a new cell
        CellData memory cell1 = cells[tokenId1];
        CellData memory cell2 = cells[tokenId2];
        require(cell1.eukaryote == true, "not eukaryote");
        require(cell2.eukaryote == true, "not eukaryote");

        if ((cell1.sponsored == true) && (SPONSORED_REPROS[tokenId1] < 5)) {
            SPONSORED_REPROS[tokenId1]++;
        } else if ((cell2.sponsored == true) && (SPONSORED_REPROS[tokenId2] < 5)) {
            SPONSORED_REPROS[tokenId2]++;
        } else {
            require(msg.value == PRICE_PER_EU_REPRO, "wrong value");
        }

        // dead_cell is in [0, 1, 2] and value > 0 specifies which parent cell died
        // new_type is in [0, 1, 2], and value > 0 specifies whether new cell inherits parent _type or mutates a new _type
        // new_entropy is in [0, 1, 2], and value > 0 specifies whether new cell inherits parent entropy or mutates a new entropy
        (uint256 dead_cell, uint256 new_type, uint256 new_entropy, uint256 new_seed) = SVGManip.breedEukaryotes(tokenId1, tokenId2, cell1.repros, cell2.repros, cell1.hunger, cell2.hunger);

        if (dead_cell > 0) {
            // Burn the dead cell
            _burn([tokenId1, tokenId2][dead_cell - 1]);
            emit EukaryoteReproductionCycle("ded", "ded", 0, [tokenId1, tokenId2][dead_cell - 1]);
        } else if (new_type > 0) {
            // Make a new cell for you! 
            CellData memory new_cell = CellData({eukaryote: true, repros: 1, _type: 0, entropy: uint64(new_entropy), sponsored: false, seed: TOKENS_MINTED, hunger: 0});
            uint256 rand_type = Utils.random(abi.encodePacked("TYPE", _seed_phrase, tokenId1, tokenId2));
            uint256 rand_entropy = Utils.random(abi.encodePacked("ENTROPY", _seed_phrase, tokenId1, tokenId2));
            uint256 rand_seed = Utils.random(abi.encodePacked("SEED", _seed_phrase, tokenId1, tokenId2));
            if (new_type == 1) {
                new_cell._type = [cell1._type, cell2._type][rand_type % 2];
            } else {
                new_cell._type = uint64(rand_type % 3 + 1);
            }

            if (new_entropy == 1) {
                new_cell.entropy = [cell1.entropy, cell2.entropy][rand_entropy % 2];
            } else {
                new_cell.entropy = uint64(rand_entropy % 3);
            }

            if (new_seed == 1) {
                new_cell.seed = [cell1.seed, cell2.seed][rand_seed % 2];
            } else {
                new_cell.seed = rand_seed;
            }
            
            new_cell.repros = 1;
            cells[TOKENS_MINTED] = new_cell;
            _safeMint(msg.sender, 1);
            emit EukaryoteReproductionCycle(cell_types[new_cell._type], entropies[new_cell.entropy], new_cell.seed, TOKENS_MINTED);
            ++TOKENS_MINTED;
        }

        ++cell1.repros;
        ++cell2.repros;

        cell1.hunger = cell1.hunger - 100;
        cell2.hunger = cell2.hunger - 100;

        cells[tokenId1] = cell1;
        cells[tokenId2] = cell2;
    }

    function _feed_cell(uint256 cell_tokenId, uint256[] memory AA_tokenIds) external {
        require(msg.sender == tx.origin, "sender != origin");
        require(ownerOf(cell_tokenId) == msg.sender, "don't own");
        CellData memory cell = cells[cell_tokenId];
        uint256 total_AAs;

        for (uint256 i = 0; i < AA_tokenIds.length; ++i) {
            require(QTC1._ownerOf(AA_tokenIds[i]) == msg.sender, "don't own");
            total_AAs = total_AAs + QTC1.pluck_number_AA(AA_tokenIds[i]);
            QTC1.sacrifice(cell_tokenId);
        }
        cell.hunger = uint64((total_AAs + cell.hunger > 1000) ? 1000 : cell.hunger + total_AAs);
        cells[cell_tokenId] = cell;
    }

    function _institute_feeds_cell(uint256[] memory cell_tokenIds) external payable {
        require(msg.sender == tx.origin, "sender != origin");
        require(msg.value == PRICE_PER_INST_FEEDING * cell_tokenIds.length);
        for (uint256 i = 0; i < cell_tokenIds.length; ++i) {
            require(ownerOf(cell_tokenIds[i]) == msg.sender, "don't own");
            cells[cell_tokenIds[i]].hunger = 1000;
        }
    }

    // Owner Only
    function z_flipMintable() external onlyOwner {MINT_ACTIVE = !MINT_ACTIVE;}
    function z_flipLabMintable() external onlyOwner {LAB_MINT_ACTIVE = !LAB_MINT_ACTIVE;}
    function z_updateSVGContract(address new_address) external onlyOwner {
        SVGManip_ADDY = new_address;
        SVGManip = iSVGManip(SVGManip_ADDY);
    }
    
    function z_updateAAContract(address new_address) external onlyOwner {
        QTC1_ADDY = new_address;
        QTC1 = iQTC1(QTC1_ADDY);
    }

    function z_updateEvolutionPrice(uint256 _PRICE_PER_AA_EVOLUTION) external onlyOwner {PRICE_PER_AA_EVOLUTION = _PRICE_PER_AA_EVOLUTION * 1e9;}
    function z_updateProReproPrice(uint256 _PRICE_PER_PRO_REPRO) external onlyOwner {PRICE_PER_PRO_REPRO = _PRICE_PER_PRO_REPRO * 1e9;}
    function z_updateEuReproPrice(uint256 _PRICE_PER_EU_REPRO) external onlyOwner {PRICE_PER_EU_REPRO = _PRICE_PER_EU_REPRO * 1e9;}
    function z_updateInstFeedingPrice(uint256 _PRICE_PER_INST_FEEDING) external onlyOwner {PRICE_PER_INST_FEEDING = _PRICE_PER_INST_FEEDING * 1e9;}
    function z_changeSeedPhrase(string memory seed_phrase) external onlyOwner {_seed_phrase = seed_phrase;}
    function z_godMint(uint256 amount, bool eukaryote, uint64 _type, uint64 entropy, bool sponsored) external onlyOwner {
        _safeMint(msg.sender, amount);
        while (amount != 0) {
            cells[TOKENS_MINTED] = CellData({eukaryote: eukaryote, repros: 1, _type: _type, entropy: entropy, sponsored: sponsored, seed: TOKENS_MINTED, hunger: 0});
            --amount;
            ++TOKENS_MINTED;
        }
    }

    function withdrawFunds() external payable onlyOwner {
        (bool success, ) = payable(msg.sender).call{value: address(this).balance}("");
        require(success, "fail");
    }

}

// SPDX-License-Identifier: MIT
// ERC721A Contracts v4.2.2
// Creator: Chiru Labs

pragma solidity ^0.8.4;

/**
 * @dev Interface of ERC721A.
 */
interface IERC721A {
    /**
     * The caller must own the token or be an approved operator.
     */
    error ApprovalCallerNotOwnerNorApproved();

    /**
     * The token does not exist.
     */
    error ApprovalQueryForNonexistentToken();

    /**
     * The caller cannot approve to their own address.
     */
    error ApproveToCaller();

    /**
     * Cannot query the balance for the zero address.
     */
    error BalanceQueryForZeroAddress();

    /**
     * Cannot mint to the zero address.
     */
    error MintToZeroAddress();

    /**
     * The quantity of tokens minted must be more than zero.
     */
    error MintZeroQuantity();

    /**
     * The token does not exist.
     */
    error OwnerQueryForNonexistentToken();

    /**
     * The caller must own the token or be an approved operator.
     */
    error TransferCallerNotOwnerNorApproved();

    /**
     * The token must be owned by `from`.
     */
    error TransferFromIncorrectOwner();

    /**
     * Cannot safely transfer to a contract that does not implement the
     * ERC721Receiver interface.
     */
    error TransferToNonERC721ReceiverImplementer();

    /**
     * Cannot transfer to the zero address.
     */
    error TransferToZeroAddress();

    /**
     * The token does not exist.
     */
    error URIQueryForNonexistentToken();

    /**
     * The `quantity` minted with ERC2309 exceeds the safety limit.
     */
    error MintERC2309QuantityExceedsLimit();

    /**
     * The `extraData` cannot be set on an unintialized ownership slot.
     */
    error OwnershipNotInitializedForExtraData();

    // =============================================================
    //                            STRUCTS
    // =============================================================

    struct TokenOwnership {
        // The address of the owner.
        address addr;
        // Stores the start time of ownership with minimal overhead for tokenomics.
        uint64 startTimestamp;
        // Whether the token has been burned.
        bool burned;
        // Arbitrary data similar to `startTimestamp` that can be set via {_extraData}.
        uint24 extraData;
    }

    // =============================================================
    //                         TOKEN COUNTERS
    // =============================================================

    /**
     * @dev Returns the total number of tokens in existence.
     * Burned tokens will reduce the count.
     * To get the total number of tokens minted, please see {_totalMinted}.
     */
    function totalSupply() external view returns (uint256);

    // =============================================================
    //                            IERC165
    // =============================================================

    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * [EIP section](https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified)
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30000 gas.
     */
    function supportsInterface(bytes4 interfaceId) external view returns (bool);

    // =============================================================
    //                            IERC721
    // =============================================================

    /**
     * @dev Emitted when `tokenId` token is transferred from `from` to `to`.
     */
    event Transfer(address indexed from, address indexed to, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables `approved` to manage the `tokenId` token.
     */
    event Approval(address indexed owner, address indexed approved, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables or disables
     * (`approved`) `operator` to manage all of its assets.
     */
    event ApprovalForAll(address indexed owner, address indexed operator, bool approved);

    /**
     * @dev Returns the number of tokens in `owner`'s account.
     */
    function balanceOf(address owner) external view returns (uint256 balance);

    /**
     * @dev Returns the owner of the `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function ownerOf(uint256 tokenId) external view returns (address owner);

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`,
     * checking first that contract recipients are aware of the ERC721 protocol
     * to prevent tokens from being forever locked.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If the caller is not `from`, it must be have been allowed to move
     * this token by either {approve} or {setApprovalForAll}.
     * - If `to` refers to a smart contract, it must implement
     * {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId,
        bytes calldata data
    ) external;

    /**
     * @dev Equivalent to `safeTransferFrom(from, to, tokenId, '')`.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId
    ) external;

    /**
     * @dev Transfers `tokenId` from `from` to `to`.
     *
     * WARNING: Usage of this method is discouraged, use {safeTransferFrom}
     * whenever possible.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must be owned by `from`.
     * - If the caller is not `from`, it must be approved to move this token
     * by either {approve} or {setApprovalForAll}.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address from,
        address to,
        uint256 tokenId
    ) external;

    /**
     * @dev Gives permission to `to` to transfer `tokenId` token to another account.
     * The approval is cleared when the token is transferred.
     *
     * Only a single account can be approved at a time, so approving the
     * zero address clears previous approvals.
     *
     * Requirements:
     *
     * - The caller must own the token or be an approved operator.
     * - `tokenId` must exist.
     *
     * Emits an {Approval} event.
     */
    function approve(address to, uint256 tokenId) external;

    /**
     * @dev Approve or remove `operator` as an operator for the caller.
     * Operators can call {transferFrom} or {safeTransferFrom}
     * for any token owned by the caller.
     *
     * Requirements:
     *
     * - The `operator` cannot be the caller.
     *
     * Emits an {ApprovalForAll} event.
     */
    function setApprovalForAll(address operator, bool _approved) external;

    /**
     * @dev Returns the account approved for `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function getApproved(uint256 tokenId) external view returns (address operator);

    /**
     * @dev Returns if the `operator` is allowed to manage all of the assets of `owner`.
     *
     * See {setApprovalForAll}.
     */
    function isApprovedForAll(address owner, address operator) external view returns (bool);

    // =============================================================
    //                        IERC721Metadata
    // =============================================================

    /**
     * @dev Returns the token collection name.
     */
    function name() external view returns (string memory);

    /**
     * @dev Returns the token collection symbol.
     */
    function symbol() external view returns (string memory);

    /**
     * @dev Returns the Uniform Resource Identifier (URI) for `tokenId` token.
     */
    function tokenURI(uint256 tokenId) external view returns (string memory);

    // =============================================================
    //                           IERC2309
    // =============================================================

    /**
     * @dev Emitted when tokens in `fromTokenId` to `toTokenId`
     * (inclusive) is transferred from `from` to `to`, as defined in the
     * [ERC2309](https://eips.ethereum.org/EIPS/eip-2309) standard.
     *
     * See {_mintERC2309} for more details.
     */
    event ConsecutiveTransfer(uint256 indexed fromTokenId, uint256 toTokenId, address indexed from, address indexed to);
}

// SPDX-License-Identifier: MIT
// ERC721A Contracts v4.2.2
// Creator: Chiru Labs

pragma solidity ^0.8.4;

import './IERC721A.sol';

/**
 * @dev Interface of ERC721 token receiver.
 */
interface ERC721A__IERC721Receiver {
    function onERC721Received(
        address operator,
        address from,
        uint256 tokenId,
        bytes calldata data
    ) external returns (bytes4);
}

/**
 * @title ERC721A
 *
 * @dev Implementation of the [ERC721](https://eips.ethereum.org/EIPS/eip-721)
 * Non-Fungible Token Standard, including the Metadata extension.
 * Optimized for lower gas during batch mints.
 *
 * Token IDs are minted in sequential order (e.g. 0, 1, 2, 3, ...)
 * starting from `_startTokenId()`.
 *
 * Assumptions:
 *
 * - An owner cannot have more than 2**64 - 1 (max value of uint64) of supply.
 * - The maximum token ID cannot exceed 2**256 - 1 (max value of uint256).
 */
contract ERC721A is IERC721A {
    // Reference type for token approval.
    struct TokenApprovalRef {
        address value;
    }

    // =============================================================
    //                           CONSTANTS
    // =============================================================

    // Mask of an entry in packed address data.
    uint256 private constant _BITMASK_ADDRESS_DATA_ENTRY = (1 << 64) - 1;

    // The bit position of `numberMinted` in packed address data.
    uint256 private constant _BITPOS_NUMBER_MINTED = 64;

    // The bit position of `numberBurned` in packed address data.
    uint256 private constant _BITPOS_NUMBER_BURNED = 128;

    // The bit position of `aux` in packed address data.
    uint256 private constant _BITPOS_AUX = 192;

    // Mask of all 256 bits in packed address data except the 64 bits for `aux`.
    uint256 private constant _BITMASK_AUX_COMPLEMENT = (1 << 192) - 1;

    // The bit position of `startTimestamp` in packed ownership.
    uint256 private constant _BITPOS_START_TIMESTAMP = 160;

    // The bit mask of the `burned` bit in packed ownership.
    uint256 private constant _BITMASK_BURNED = 1 << 224;

    // The bit position of the `nextInitialized` bit in packed ownership.
    uint256 private constant _BITPOS_NEXT_INITIALIZED = 225;

    // The bit mask of the `nextInitialized` bit in packed ownership.
    uint256 private constant _BITMASK_NEXT_INITIALIZED = 1 << 225;

    // The bit position of `extraData` in packed ownership.
    uint256 private constant _BITPOS_EXTRA_DATA = 232;

    // Mask of all 256 bits in a packed ownership except the 24 bits for `extraData`.
    uint256 private constant _BITMASK_EXTRA_DATA_COMPLEMENT = (1 << 232) - 1;

    // The mask of the lower 160 bits for addresses.
    uint256 private constant _BITMASK_ADDRESS = (1 << 160) - 1;

    // The maximum `quantity` that can be minted with {_mintERC2309}.
    // This limit is to prevent overflows on the address data entries.
    // For a limit of 5000, a total of 3.689e15 calls to {_mintERC2309}
    // is required to cause an overflow, which is unrealistic.
    uint256 private constant _MAX_MINT_ERC2309_QUANTITY_LIMIT = 5000;

    // The `Transfer` event signature is given by:
    // `keccak256(bytes("Transfer(address,address,uint256)"))`.
    bytes32 private constant _TRANSFER_EVENT_SIGNATURE =
        0xddf252ad1be2c89b69c2b068fc378daa952ba7f163c4a11628f55a4df523b3ef;

    // =============================================================
    //                            STORAGE
    // =============================================================

    // The next token ID to be minted.
    uint256 private _currentIndex;

    // The number of tokens burned.
    uint256 private _burnCounter;

    // Token name
    string private _name;

    // Token symbol
    string private _symbol;

    // Mapping from token ID to ownership details
    // An empty struct value does not necessarily mean the token is unowned.
    // See {_packedOwnershipOf} implementation for details.
    //
    // Bits Layout:
    // - [0..159]   `addr`
    // - [160..223] `startTimestamp`
    // - [224]      `burned`
    // - [225]      `nextInitialized`
    // - [232..255] `extraData`
    mapping(uint256 => uint256) private _packedOwnerships;

    // Mapping owner address to address data.
    //
    // Bits Layout:
    // - [0..63]    `balance`
    // - [64..127]  `numberMinted`
    // - [128..191] `numberBurned`
    // - [192..255] `aux`
    mapping(address => uint256) private _packedAddressData;

    // Mapping from token ID to approved address.
    mapping(uint256 => TokenApprovalRef) private _tokenApprovals;

    // Mapping from owner to operator approvals
    mapping(address => mapping(address => bool)) private _operatorApprovals;

    // =============================================================
    //                          CONSTRUCTOR
    // =============================================================

    constructor(string memory name_, string memory symbol_) {
        _name = name_;
        _symbol = symbol_;
        _currentIndex = _startTokenId();
    }

    // =============================================================
    //                   TOKEN COUNTING OPERATIONS
    // =============================================================

    /**
     * @dev Returns the starting token ID.
     * To change the starting token ID, please override this function.
     */
    function _startTokenId() internal view virtual returns (uint256) {
        return 0;
    }

    /**
     * @dev Returns the next token ID to be minted.
     */
    function _nextTokenId() internal view virtual returns (uint256) {
        return _currentIndex;
    }

    /**
     * @dev Returns the total number of tokens in existence.
     * Burned tokens will reduce the count.
     * To get the total number of tokens minted, please see {_totalMinted}.
     */
    function totalSupply() public view virtual override returns (uint256) {
        // Counter underflow is impossible as _burnCounter cannot be incremented
        // more than `_currentIndex - _startTokenId()` times.
        unchecked {
            return _currentIndex - _burnCounter - _startTokenId();
        }
    }

    /**
     * @dev Returns the total amount of tokens minted in the contract.
     */
    function _totalMinted() internal view virtual returns (uint256) {
        // Counter underflow is impossible as `_currentIndex` does not decrement,
        // and it is initialized to `_startTokenId()`.
        unchecked {
            return _currentIndex - _startTokenId();
        }
    }

    /**
     * @dev Returns the total number of tokens burned.
     */
    function _totalBurned() internal view virtual returns (uint256) {
        return _burnCounter;
    }

    // =============================================================
    //                    ADDRESS DATA OPERATIONS
    // =============================================================

    /**
     * @dev Returns the number of tokens in `owner`'s account.
     */
    function balanceOf(address owner) public view virtual override returns (uint256) {
        if (owner == address(0)) revert BalanceQueryForZeroAddress();
        return _packedAddressData[owner] & _BITMASK_ADDRESS_DATA_ENTRY;
    }

    /**
     * Returns the number of tokens minted by `owner`.
     */
    function _numberMinted(address owner) internal view returns (uint256) {
        return (_packedAddressData[owner] >> _BITPOS_NUMBER_MINTED) & _BITMASK_ADDRESS_DATA_ENTRY;
    }

    /**
     * Returns the number of tokens burned by or on behalf of `owner`.
     */
    function _numberBurned(address owner) internal view returns (uint256) {
        return (_packedAddressData[owner] >> _BITPOS_NUMBER_BURNED) & _BITMASK_ADDRESS_DATA_ENTRY;
    }

    /**
     * Returns the auxiliary data for `owner`. (e.g. number of whitelist mint slots used).
     */
    function _getAux(address owner) internal view returns (uint64) {
        return uint64(_packedAddressData[owner] >> _BITPOS_AUX);
    }

    /**
     * Sets the auxiliary data for `owner`. (e.g. number of whitelist mint slots used).
     * If there are multiple variables, please pack them into a uint64.
     */
    function _setAux(address owner, uint64 aux) internal virtual {
        uint256 packed = _packedAddressData[owner];
        uint256 auxCasted;
        // Cast `aux` with assembly to avoid redundant masking.
        assembly {
            auxCasted := aux
        }
        packed = (packed & _BITMASK_AUX_COMPLEMENT) | (auxCasted << _BITPOS_AUX);
        _packedAddressData[owner] = packed;
    }

    // =============================================================
    //                            IERC165
    // =============================================================

    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * [EIP section](https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified)
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30000 gas.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        // The interface IDs are constants representing the first 4 bytes
        // of the XOR of all function selectors in the interface.
        // See: [ERC165](https://eips.ethereum.org/EIPS/eip-165)
        // (e.g. `bytes4(i.functionA.selector ^ i.functionB.selector ^ ...)`)
        return
            interfaceId == 0x01ffc9a7 || // ERC165 interface ID for ERC165.
            interfaceId == 0x80ac58cd || // ERC165 interface ID for ERC721.
            interfaceId == 0x5b5e139f; // ERC165 interface ID for ERC721Metadata.
    }

    // =============================================================
    //                        IERC721Metadata
    // =============================================================

    /**
     * @dev Returns the token collection name.
     */
    function name() public view virtual override returns (string memory) {
        return _name;
    }

    /**
     * @dev Returns the token collection symbol.
     */
    function symbol() public view virtual override returns (string memory) {
        return _symbol;
    }

    /**
     * @dev Returns the Uniform Resource Identifier (URI) for `tokenId` token.
     */
    function tokenURI(uint256 tokenId) public view virtual override returns (string memory) {
        if (!_exists(tokenId)) revert URIQueryForNonexistentToken();

        string memory baseURI = _baseURI();
        return bytes(baseURI).length != 0 ? string(abi.encodePacked(baseURI, _toString(tokenId))) : '';
    }

    /**
     * @dev Base URI for computing {tokenURI}. If set, the resulting URI for each
     * token will be the concatenation of the `baseURI` and the `tokenId`. Empty
     * by default, it can be overridden in child contracts.
     */
    function _baseURI() internal view virtual returns (string memory) {
        return '';
    }

    // =============================================================
    //                     OWNERSHIPS OPERATIONS
    // =============================================================

    /**
     * @dev Returns the owner of the `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function ownerOf(uint256 tokenId) public view virtual override returns (address) {
        return address(uint160(_packedOwnershipOf(tokenId)));
    }

    /**
     * @dev Gas spent here starts off proportional to the maximum mint batch size.
     * It gradually moves to O(1) as tokens get transferred around over time.
     */
    function _ownershipOf(uint256 tokenId) internal view virtual returns (TokenOwnership memory) {
        return _unpackedOwnership(_packedOwnershipOf(tokenId));
    }

    /**
     * @dev Returns the unpacked `TokenOwnership` struct at `index`.
     */
    function _ownershipAt(uint256 index) internal view virtual returns (TokenOwnership memory) {
        return _unpackedOwnership(_packedOwnerships[index]);
    }

    /**
     * @dev Initializes the ownership slot minted at `index` for efficiency purposes.
     */
    function _initializeOwnershipAt(uint256 index) internal virtual {
        if (_packedOwnerships[index] == 0) {
            _packedOwnerships[index] = _packedOwnershipOf(index);
        }
    }

    /**
     * Returns the packed ownership data of `tokenId`.
     */
    function _packedOwnershipOf(uint256 tokenId) private view returns (uint256) {
        uint256 curr = tokenId;

        unchecked {
            if (_startTokenId() <= curr)
                if (curr < _currentIndex) {
                    uint256 packed = _packedOwnerships[curr];
                    // If not burned.
                    if (packed & _BITMASK_BURNED == 0) {
                        // Invariant:
                        // There will always be an initialized ownership slot
                        // (i.e. `ownership.addr != address(0) && ownership.burned == false`)
                        // before an unintialized ownership slot
                        // (i.e. `ownership.addr == address(0) && ownership.burned == false`)
                        // Hence, `curr` will not underflow.
                        //
                        // We can directly compare the packed value.
                        // If the address is zero, packed will be zero.
                        while (packed == 0) {
                            packed = _packedOwnerships[--curr];
                        }
                        return packed;
                    }
                }
        }
        revert OwnerQueryForNonexistentToken();
    }

    /**
     * @dev Returns the unpacked `TokenOwnership` struct from `packed`.
     */
    function _unpackedOwnership(uint256 packed) private pure returns (TokenOwnership memory ownership) {
        ownership.addr = address(uint160(packed));
        ownership.startTimestamp = uint64(packed >> _BITPOS_START_TIMESTAMP);
        ownership.burned = packed & _BITMASK_BURNED != 0;
        ownership.extraData = uint24(packed >> _BITPOS_EXTRA_DATA);
    }

    /**
     * @dev Packs ownership data into a single uint256.
     */
    function _packOwnershipData(address owner, uint256 flags) private view returns (uint256 result) {
        assembly {
            // Mask `owner` to the lower 160 bits, in case the upper bits somehow aren't clean.
            owner := and(owner, _BITMASK_ADDRESS)
            // `owner | (block.timestamp << _BITPOS_START_TIMESTAMP) | flags`.
            result := or(owner, or(shl(_BITPOS_START_TIMESTAMP, timestamp()), flags))
        }
    }

    /**
     * @dev Returns the `nextInitialized` flag set if `quantity` equals 1.
     */
    function _nextInitializedFlag(uint256 quantity) private pure returns (uint256 result) {
        // For branchless setting of the `nextInitialized` flag.
        assembly {
            // `(quantity == 1) << _BITPOS_NEXT_INITIALIZED`.
            result := shl(_BITPOS_NEXT_INITIALIZED, eq(quantity, 1))
        }
    }

    // =============================================================
    //                      APPROVAL OPERATIONS
    // =============================================================

    /**
     * @dev Gives permission to `to` to transfer `tokenId` token to another account.
     * The approval is cleared when the token is transferred.
     *
     * Only a single account can be approved at a time, so approving the
     * zero address clears previous approvals.
     *
     * Requirements:
     *
     * - The caller must own the token or be an approved operator.
     * - `tokenId` must exist.
     *
     * Emits an {Approval} event.
     */
    function approve(address to, uint256 tokenId) public virtual override {
        address owner = ownerOf(tokenId);

        if (_msgSenderERC721A() != owner)
            if (!isApprovedForAll(owner, _msgSenderERC721A())) {
                revert ApprovalCallerNotOwnerNorApproved();
            }

        _tokenApprovals[tokenId].value = to;
        emit Approval(owner, to, tokenId);
    }

    /**
     * @dev Returns the account approved for `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function getApproved(uint256 tokenId) public view virtual override returns (address) {
        if (!_exists(tokenId)) revert ApprovalQueryForNonexistentToken();

        return _tokenApprovals[tokenId].value;
    }

    /**
     * @dev Approve or remove `operator` as an operator for the caller.
     * Operators can call {transferFrom} or {safeTransferFrom}
     * for any token owned by the caller.
     *
     * Requirements:
     *
     * - The `operator` cannot be the caller.
     *
     * Emits an {ApprovalForAll} event.
     */
    function setApprovalForAll(address operator, bool approved) public virtual override {
        if (operator == _msgSenderERC721A()) revert ApproveToCaller();

        _operatorApprovals[_msgSenderERC721A()][operator] = approved;
        emit ApprovalForAll(_msgSenderERC721A(), operator, approved);
    }

    /**
     * @dev Returns if the `operator` is allowed to manage all of the assets of `owner`.
     *
     * See {setApprovalForAll}.
     */
    function isApprovedForAll(address owner, address operator) public view virtual override returns (bool) {
        return _operatorApprovals[owner][operator];
    }

    /**
     * @dev Returns whether `tokenId` exists.
     *
     * Tokens can be managed by their owner or approved accounts via {approve} or {setApprovalForAll}.
     *
     * Tokens start existing when they are minted. See {_mint}.
     */
    function _exists(uint256 tokenId) internal view virtual returns (bool) {
        return
            _startTokenId() <= tokenId &&
            tokenId < _currentIndex && // If within bounds,
            _packedOwnerships[tokenId] & _BITMASK_BURNED == 0; // and not burned.
    }

    /**
     * @dev Returns whether `msgSender` is equal to `approvedAddress` or `owner`.
     */
    function _isSenderApprovedOrOwner(
        address approvedAddress,
        address owner,
        address msgSender
    ) private pure returns (bool result) {
        assembly {
            // Mask `owner` to the lower 160 bits, in case the upper bits somehow aren't clean.
            owner := and(owner, _BITMASK_ADDRESS)
            // Mask `msgSender` to the lower 160 bits, in case the upper bits somehow aren't clean.
            msgSender := and(msgSender, _BITMASK_ADDRESS)
            // `msgSender == owner || msgSender == approvedAddress`.
            result := or(eq(msgSender, owner), eq(msgSender, approvedAddress))
        }
    }

    /**
     * @dev Returns the storage slot and value for the approved address of `tokenId`.
     */
    function _getApprovedSlotAndAddress(uint256 tokenId)
        private
        view
        returns (uint256 approvedAddressSlot, address approvedAddress)
    {
        TokenApprovalRef storage tokenApproval = _tokenApprovals[tokenId];
        // The following is equivalent to `approvedAddress = _tokenApprovals[tokenId]`.
        assembly {
            approvedAddressSlot := tokenApproval.slot
            approvedAddress := sload(approvedAddressSlot)
        }
    }

    // =============================================================
    //                      TRANSFER OPERATIONS
    // =============================================================

    /**
     * @dev Transfers `tokenId` from `from` to `to`.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must be owned by `from`.
     * - If the caller is not `from`, it must be approved to move this token
     * by either {approve} or {setApprovalForAll}.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address from,
        address to,
        uint256 tokenId
    ) public virtual override {
        uint256 prevOwnershipPacked = _packedOwnershipOf(tokenId);

        if (address(uint160(prevOwnershipPacked)) != from) revert TransferFromIncorrectOwner();

        (uint256 approvedAddressSlot, address approvedAddress) = _getApprovedSlotAndAddress(tokenId);

        // The nested ifs save around 20+ gas over a compound boolean condition.
        if (!_isSenderApprovedOrOwner(approvedAddress, from, _msgSenderERC721A()))
            if (!isApprovedForAll(from, _msgSenderERC721A())) revert TransferCallerNotOwnerNorApproved();

        if (to == address(0)) revert TransferToZeroAddress();

        _beforeTokenTransfers(from, to, tokenId, 1);

        // Clear approvals from the previous owner.
        assembly {
            if approvedAddress {
                // This is equivalent to `delete _tokenApprovals[tokenId]`.
                sstore(approvedAddressSlot, 0)
            }
        }

        // Underflow of the sender's balance is impossible because we check for
        // ownership above and the recipient's balance can't realistically overflow.
        // Counter overflow is incredibly unrealistic as `tokenId` would have to be 2**256.
        unchecked {
            // We can directly increment and decrement the balances.
            --_packedAddressData[from]; // Updates: `balance -= 1`.
            ++_packedAddressData[to]; // Updates: `balance += 1`.

            // Updates:
            // - `address` to the next owner.
            // - `startTimestamp` to the timestamp of transfering.
            // - `burned` to `false`.
            // - `nextInitialized` to `true`.
            _packedOwnerships[tokenId] = _packOwnershipData(
                to,
                _BITMASK_NEXT_INITIALIZED | _nextExtraData(from, to, prevOwnershipPacked)
            );

            // If the next slot may not have been initialized (i.e. `nextInitialized == false`) .
            if (prevOwnershipPacked & _BITMASK_NEXT_INITIALIZED == 0) {
                uint256 nextTokenId = tokenId + 1;
                // If the next slot's address is zero and not burned (i.e. packed value is zero).
                if (_packedOwnerships[nextTokenId] == 0) {
                    // If the next slot is within bounds.
                    if (nextTokenId != _currentIndex) {
                        // Initialize the next slot to maintain correctness for `ownerOf(tokenId + 1)`.
                        _packedOwnerships[nextTokenId] = prevOwnershipPacked;
                    }
                }
            }
        }

        emit Transfer(from, to, tokenId);
        _afterTokenTransfers(from, to, tokenId, 1);
    }

    /**
     * @dev Equivalent to `safeTransferFrom(from, to, tokenId, '')`.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId
    ) public virtual override {
        safeTransferFrom(from, to, tokenId, '');
    }

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If the caller is not `from`, it must be approved to move this token
     * by either {approve} or {setApprovalForAll}.
     * - If `to` refers to a smart contract, it must implement
     * {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId,
        bytes memory _data
    ) public virtual override {
        transferFrom(from, to, tokenId);
        if (to.code.length != 0)
            if (!_checkContractOnERC721Received(from, to, tokenId, _data)) {
                revert TransferToNonERC721ReceiverImplementer();
            }
    }

    /**
     * @dev Hook that is called before a set of serially-ordered token IDs
     * are about to be transferred. This includes minting.
     * And also called before burning one token.
     *
     * `startTokenId` - the first token ID to be transferred.
     * `quantity` - the amount to be transferred.
     *
     * Calling conditions:
     *
     * - When `from` and `to` are both non-zero, `from`'s `tokenId` will be
     * transferred to `to`.
     * - When `from` is zero, `tokenId` will be minted for `to`.
     * - When `to` is zero, `tokenId` will be burned by `from`.
     * - `from` and `to` are never both zero.
     */
    function _beforeTokenTransfers(
        address from,
        address to,
        uint256 startTokenId,
        uint256 quantity
    ) internal virtual {}

    /**
     * @dev Hook that is called after a set of serially-ordered token IDs
     * have been transferred. This includes minting.
     * And also called after one token has been burned.
     *
     * `startTokenId` - the first token ID to be transferred.
     * `quantity` - the amount to be transferred.
     *
     * Calling conditions:
     *
     * - When `from` and `to` are both non-zero, `from`'s `tokenId` has been
     * transferred to `to`.
     * - When `from` is zero, `tokenId` has been minted for `to`.
     * - When `to` is zero, `tokenId` has been burned by `from`.
     * - `from` and `to` are never both zero.
     */
    function _afterTokenTransfers(
        address from,
        address to,
        uint256 startTokenId,
        uint256 quantity
    ) internal virtual {}

    /**
     * @dev Private function to invoke {IERC721Receiver-onERC721Received} on a target contract.
     *
     * `from` - Previous owner of the given token ID.
     * `to` - Target address that will receive the token.
     * `tokenId` - Token ID to be transferred.
     * `_data` - Optional data to send along with the call.
     *
     * Returns whether the call correctly returned the expected magic value.
     */
    function _checkContractOnERC721Received(
        address from,
        address to,
        uint256 tokenId,
        bytes memory _data
    ) private returns (bool) {
        try ERC721A__IERC721Receiver(to).onERC721Received(_msgSenderERC721A(), from, tokenId, _data) returns (
            bytes4 retval
        ) {
            return retval == ERC721A__IERC721Receiver(to).onERC721Received.selector;
        } catch (bytes memory reason) {
            if (reason.length == 0) {
                revert TransferToNonERC721ReceiverImplementer();
            } else {
                assembly {
                    revert(add(32, reason), mload(reason))
                }
            }
        }
    }

    // =============================================================
    //                        MINT OPERATIONS
    // =============================================================

    /**
     * @dev Mints `quantity` tokens and transfers them to `to`.
     *
     * Requirements:
     *
     * - `to` cannot be the zero address.
     * - `quantity` must be greater than 0.
     *
     * Emits a {Transfer} event for each mint.
     */
    function _mint(address to, uint256 quantity) internal virtual {
        uint256 startTokenId = _currentIndex;
        if (quantity == 0) revert MintZeroQuantity();

        _beforeTokenTransfers(address(0), to, startTokenId, quantity);

        // Overflows are incredibly unrealistic.
        // `balance` and `numberMinted` have a maximum limit of 2**64.
        // `tokenId` has a maximum limit of 2**256.
        unchecked {
            // Updates:
            // - `balance += quantity`.
            // - `numberMinted += quantity`.
            //
            // We can directly add to the `balance` and `numberMinted`.
            _packedAddressData[to] += quantity * ((1 << _BITPOS_NUMBER_MINTED) | 1);

            // Updates:
            // - `address` to the owner.
            // - `startTimestamp` to the timestamp of minting.
            // - `burned` to `false`.
            // - `nextInitialized` to `quantity == 1`.
            _packedOwnerships[startTokenId] = _packOwnershipData(
                to,
                _nextInitializedFlag(quantity) | _nextExtraData(address(0), to, 0)
            );

            uint256 toMasked;
            uint256 end = startTokenId + quantity;

            // Use assembly to loop and emit the `Transfer` event for gas savings.
            assembly {
                // Mask `to` to the lower 160 bits, in case the upper bits somehow aren't clean.
                toMasked := and(to, _BITMASK_ADDRESS)
                // Emit the `Transfer` event.
                log4(
                    0, // Start of data (0, since no data).
                    0, // End of data (0, since no data).
                    _TRANSFER_EVENT_SIGNATURE, // Signature.
                    0, // `address(0)`.
                    toMasked, // `to`.
                    startTokenId // `tokenId`.
                )

                for {
                    let tokenId := add(startTokenId, 1)
                } iszero(eq(tokenId, end)) {
                    tokenId := add(tokenId, 1)
                } {
                    // Emit the `Transfer` event. Similar to above.
                    log4(0, 0, _TRANSFER_EVENT_SIGNATURE, 0, toMasked, tokenId)
                }
            }
            if (toMasked == 0) revert MintToZeroAddress();

            _currentIndex = end;
        }
        _afterTokenTransfers(address(0), to, startTokenId, quantity);
    }

    /**
     * @dev Mints `quantity` tokens and transfers them to `to`.
     *
     * This function is intended for efficient minting only during contract creation.
     *
     * It emits only one {ConsecutiveTransfer} as defined in
     * [ERC2309](https://eips.ethereum.org/EIPS/eip-2309),
     * instead of a sequence of {Transfer} event(s).
     *
     * Calling this function outside of contract creation WILL make your contract
     * non-compliant with the ERC721 standard.
     * For full ERC721 compliance, substituting ERC721 {Transfer} event(s) with the ERC2309
     * {ConsecutiveTransfer} event is only permissible during contract creation.
     *
     * Requirements:
     *
     * - `to` cannot be the zero address.
     * - `quantity` must be greater than 0.
     *
     * Emits a {ConsecutiveTransfer} event.
     */
    function _mintERC2309(address to, uint256 quantity) internal virtual {
        uint256 startTokenId = _currentIndex;
        if (to == address(0)) revert MintToZeroAddress();
        if (quantity == 0) revert MintZeroQuantity();
        if (quantity > _MAX_MINT_ERC2309_QUANTITY_LIMIT) revert MintERC2309QuantityExceedsLimit();

        _beforeTokenTransfers(address(0), to, startTokenId, quantity);

        // Overflows are unrealistic due to the above check for `quantity` to be below the limit.
        unchecked {
            // Updates:
            // - `balance += quantity`.
            // - `numberMinted += quantity`.
            //
            // We can directly add to the `balance` and `numberMinted`.
            _packedAddressData[to] += quantity * ((1 << _BITPOS_NUMBER_MINTED) | 1);

            // Updates:
            // - `address` to the owner.
            // - `startTimestamp` to the timestamp of minting.
            // - `burned` to `false`.
            // - `nextInitialized` to `quantity == 1`.
            _packedOwnerships[startTokenId] = _packOwnershipData(
                to,
                _nextInitializedFlag(quantity) | _nextExtraData(address(0), to, 0)
            );

            emit ConsecutiveTransfer(startTokenId, startTokenId + quantity - 1, address(0), to);

            _currentIndex = startTokenId + quantity;
        }
        _afterTokenTransfers(address(0), to, startTokenId, quantity);
    }

    /**
     * @dev Safely mints `quantity` tokens and transfers them to `to`.
     *
     * Requirements:
     *
     * - If `to` refers to a smart contract, it must implement
     * {IERC721Receiver-onERC721Received}, which is called for each safe transfer.
     * - `quantity` must be greater than 0.
     *
     * See {_mint}.
     *
     * Emits a {Transfer} event for each mint.
     */
    function _safeMint(
        address to,
        uint256 quantity,
        bytes memory _data
    ) internal virtual {
        _mint(to, quantity);

        unchecked {
            if (to.code.length != 0) {
                uint256 end = _currentIndex;
                uint256 index = end - quantity;
                do {
                    if (!_checkContractOnERC721Received(address(0), to, index++, _data)) {
                        revert TransferToNonERC721ReceiverImplementer();
                    }
                } while (index < end);
                // Reentrancy protection.
                if (_currentIndex != end) revert();
            }
        }
    }

    /**
     * @dev Equivalent to `_safeMint(to, quantity, '')`.
     */
    function _safeMint(address to, uint256 quantity) internal virtual {
        _safeMint(to, quantity, '');
    }

    // =============================================================
    //                        BURN OPERATIONS
    // =============================================================

    /**
     * @dev Equivalent to `_burn(tokenId, false)`.
     */
    function _burn(uint256 tokenId) internal virtual {
        _burn(tokenId, false);
    }

    /**
     * @dev Destroys `tokenId`.
     * The approval is cleared when the token is burned.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     *
     * Emits a {Transfer} event.
     */
    function _burn(uint256 tokenId, bool approvalCheck) internal virtual {
        uint256 prevOwnershipPacked = _packedOwnershipOf(tokenId);

        address from = address(uint160(prevOwnershipPacked));

        (uint256 approvedAddressSlot, address approvedAddress) = _getApprovedSlotAndAddress(tokenId);

        if (approvalCheck) {
            // The nested ifs save around 20+ gas over a compound boolean condition.
            if (!_isSenderApprovedOrOwner(approvedAddress, from, _msgSenderERC721A()))
                if (!isApprovedForAll(from, _msgSenderERC721A())) revert TransferCallerNotOwnerNorApproved();
        }

        _beforeTokenTransfers(from, address(0), tokenId, 1);

        // Clear approvals from the previous owner.
        assembly {
            if approvedAddress {
                // This is equivalent to `delete _tokenApprovals[tokenId]`.
                sstore(approvedAddressSlot, 0)
            }
        }

        // Underflow of the sender's balance is impossible because we check for
        // ownership above and the recipient's balance can't realistically overflow.
        // Counter overflow is incredibly unrealistic as `tokenId` would have to be 2**256.
        unchecked {
            // Updates:
            // - `balance -= 1`.
            // - `numberBurned += 1`.
            //
            // We can directly decrement the balance, and increment the number burned.
            // This is equivalent to `packed -= 1; packed += 1 << _BITPOS_NUMBER_BURNED;`.
            _packedAddressData[from] += (1 << _BITPOS_NUMBER_BURNED) - 1;

            // Updates:
            // - `address` to the last owner.
            // - `startTimestamp` to the timestamp of burning.
            // - `burned` to `true`.
            // - `nextInitialized` to `true`.
            _packedOwnerships[tokenId] = _packOwnershipData(
                from,
                (_BITMASK_BURNED | _BITMASK_NEXT_INITIALIZED) | _nextExtraData(from, address(0), prevOwnershipPacked)
            );

            // If the next slot may not have been initialized (i.e. `nextInitialized == false`) .
            if (prevOwnershipPacked & _BITMASK_NEXT_INITIALIZED == 0) {
                uint256 nextTokenId = tokenId + 1;
                // If the next slot's address is zero and not burned (i.e. packed value is zero).
                if (_packedOwnerships[nextTokenId] == 0) {
                    // If the next slot is within bounds.
                    if (nextTokenId != _currentIndex) {
                        // Initialize the next slot to maintain correctness for `ownerOf(tokenId + 1)`.
                        _packedOwnerships[nextTokenId] = prevOwnershipPacked;
                    }
                }
            }
        }

        emit Transfer(from, address(0), tokenId);
        _afterTokenTransfers(from, address(0), tokenId, 1);

        // Overflow not possible, as _burnCounter cannot be exceed _currentIndex times.
        unchecked {
            _burnCounter++;
        }
    }

    // =============================================================
    //                     EXTRA DATA OPERATIONS
    // =============================================================

    /**
     * @dev Directly sets the extra data for the ownership data `index`.
     */
    function _setExtraDataAt(uint256 index, uint24 extraData) internal virtual {
        uint256 packed = _packedOwnerships[index];
        if (packed == 0) revert OwnershipNotInitializedForExtraData();
        uint256 extraDataCasted;
        // Cast `extraData` with assembly to avoid redundant masking.
        assembly {
            extraDataCasted := extraData
        }
        packed = (packed & _BITMASK_EXTRA_DATA_COMPLEMENT) | (extraDataCasted << _BITPOS_EXTRA_DATA);
        _packedOwnerships[index] = packed;
    }

    /**
     * @dev Called during each token transfer to set the 24bit `extraData` field.
     * Intended to be overridden by the cosumer contract.
     *
     * `previousExtraData` - the value of `extraData` before transfer.
     *
     * Calling conditions:
     *
     * - When `from` and `to` are both non-zero, `from`'s `tokenId` will be
     * transferred to `to`.
     * - When `from` is zero, `tokenId` will be minted for `to`.
     * - When `to` is zero, `tokenId` will be burned by `from`.
     * - `from` and `to` are never both zero.
     */
    function _extraData(
        address from,
        address to,
        uint24 previousExtraData
    ) internal view virtual returns (uint24) {}

    /**
     * @dev Returns the next extra data for the packed ownership data.
     * The returned result is shifted into position.
     */
    function _nextExtraData(
        address from,
        address to,
        uint256 prevOwnershipPacked
    ) private view returns (uint256) {
        uint24 extraData = uint24(prevOwnershipPacked >> _BITPOS_EXTRA_DATA);
        return uint256(_extraData(from, to, extraData)) << _BITPOS_EXTRA_DATA;
    }

    // =============================================================
    //                       OTHER OPERATIONS
    // =============================================================

    /**
     * @dev Returns the message sender (defaults to `msg.sender`).
     *
     * If you are writing GSN compatible contracts, you need to override this function.
     */
    function _msgSenderERC721A() internal view virtual returns (address) {
        return msg.sender;
    }

    /**
     * @dev Converts a uint256 to its ASCII string decimal representation.
     */
    function _toString(uint256 value) internal pure virtual returns (string memory str) {
        assembly {
            // The maximum value of a uint256 contains 78 digits (1 byte per digit),
            // but we allocate 0x80 bytes to keep the free memory pointer 32-byte word aliged.
            // We will need 1 32-byte word to store the length,
            // and 3 32-byte words to store a maximum of 78 digits. Total: 0x20 + 3 * 0x20 = 0x80.
            str := add(mload(0x40), 0x80)
            // Update the free memory pointer to allocate.
            mstore(0x40, str)

            // Cache the end of the memory to calculate the length later.
            let end := str

            // We write the string from rightmost digit to leftmost digit.
            // The following is essentially a do-while loop that also handles the zero case.
            // prettier-ignore
            for { let temp := value } 1 {} {
                str := sub(str, 1)
                // Write the character to the pointer.
                // The ASCII index of the '0' character is 48.
                mstore8(str, add(48, mod(temp, 10)))
                // Keep dividing `temp` until zero.
                temp := div(temp, 10)
                // prettier-ignore
                if iszero(temp) { break }
            }

            let length := sub(end, str)
            // Move the pointer 32 bytes leftwards to make room for the length.
            str := sub(str, 0x20)
            // Store the length.
            mstore(str, length)
        }
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (utils/Strings.sol)

pragma solidity ^0.8.0;

/**
 * @dev String operations.
 */
library Strings {
    bytes16 private constant _HEX_SYMBOLS = "0123456789abcdef";
    uint8 private constant _ADDRESS_LENGTH = 20;

    /**
     * @dev Converts a `uint256` to its ASCII `string` decimal representation.
     */
    function toString(uint256 value) internal pure returns (string memory) {
        // Inspired by OraclizeAPI's implementation - MIT licence
        // https://github.com/oraclize/ethereum-api/blob/b42146b063c7d6ee1358846c198246239e9360e8/oraclizeAPI_0.4.25.sol

        if (value == 0) {
            return "0";
        }
        uint256 temp = value;
        uint256 digits;
        while (temp != 0) {
            digits++;
            temp /= 10;
        }
        bytes memory buffer = new bytes(digits);
        while (value != 0) {
            digits -= 1;
            buffer[digits] = bytes1(uint8(48 + uint256(value % 10)));
            value /= 10;
        }
        return string(buffer);
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation.
     */
    function toHexString(uint256 value) internal pure returns (string memory) {
        if (value == 0) {
            return "0x00";
        }
        uint256 temp = value;
        uint256 length = 0;
        while (temp != 0) {
            length++;
            temp >>= 8;
        }
        return toHexString(value, length);
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation with fixed length.
     */
    function toHexString(uint256 value, uint256 length) internal pure returns (string memory) {
        bytes memory buffer = new bytes(2 * length + 2);
        buffer[0] = "0";
        buffer[1] = "x";
        for (uint256 i = 2 * length + 1; i > 1; --i) {
            buffer[i] = _HEX_SYMBOLS[value & 0xf];
            value >>= 4;
        }
        require(value == 0, "Strings: hex length insufficient");
        return string(buffer);
    }

    /**
     * @dev Converts an `address` with fixed length of 20 bytes to its not checksummed ASCII `string` hexadecimal representation.
     */
    function toHexString(address addr) internal pure returns (string memory) {
        return toHexString(uint256(uint160(addr)), _ADDRESS_LENGTH);
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (utils/Base64.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides a set of functions to operate with Base64 strings.
 *
 * _Available since v4.5._
 */
library Base64 {
    /**
     * @dev Base64 Encoding/Decoding Table
     */
    string internal constant _TABLE = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz0123456789+/";

    /**
     * @dev Converts a `bytes` to its Bytes64 `string` representation.
     */
    function encode(bytes memory data) internal pure returns (string memory) {
        /**
         * Inspired by Brecht Devos (Brechtpd) implementation - MIT licence
         * https://github.com/Brechtpd/base64/blob/e78d9fd951e7b0977ddca77d92dc85183770daf4/base64.sol
         */
        if (data.length == 0) return "";

        // Loads the table into memory
        string memory table = _TABLE;

        // Encoding takes 3 bytes chunks of binary data from `bytes` data parameter
        // and split into 4 numbers of 6 bits.
        // The final Base64 length should be `bytes` data length multiplied by 4/3 rounded up
        // - `data.length + 2`  -> Round up
        // - `/ 3`              -> Number of 3-bytes chunks
        // - `4 *`              -> 4 characters for each chunk
        string memory result = new string(4 * ((data.length + 2) / 3));

        /// @solidity memory-safe-assembly
        assembly {
            // Prepare the lookup table (skip the first "length" byte)
            let tablePtr := add(table, 1)

            // Prepare result pointer, jump over length
            let resultPtr := add(result, 32)

            // Run over the input, 3 bytes at a time
            for {
                let dataPtr := data
                let endPtr := add(data, mload(data))
            } lt(dataPtr, endPtr) {

            } {
                // Advance 3 bytes
                dataPtr := add(dataPtr, 3)
                let input := mload(dataPtr)

                // To write each character, shift the 3 bytes (18 bits) chunk
                // 4 times in blocks of 6 bits for each character (18, 12, 6, 0)
                // and apply logical AND with 0x3F which is the number of
                // the previous character in the ASCII table prior to the Base64 Table
                // The result is then added to the table to get the character to write,
                // and finally write it in the result pointer but with a left shift
                // of 256 (1 byte) - 8 (1 ASCII char) = 248 bits

                mstore8(resultPtr, mload(add(tablePtr, and(shr(18, input), 0x3F))))
                resultPtr := add(resultPtr, 1) // Advance

                mstore8(resultPtr, mload(add(tablePtr, and(shr(12, input), 0x3F))))
                resultPtr := add(resultPtr, 1) // Advance

                mstore8(resultPtr, mload(add(tablePtr, and(shr(6, input), 0x3F))))
                resultPtr := add(resultPtr, 1) // Advance

                mstore8(resultPtr, mload(add(tablePtr, and(input, 0x3F))))
                resultPtr := add(resultPtr, 1) // Advance
            }

            // When data `bytes` is not exactly 3 bytes long
            // it is padded with `=` characters at the end
            switch mod(mload(data), 3)
            case 1 {
                mstore8(sub(resultPtr, 1), 0x3d)
                mstore8(sub(resultPtr, 2), 0x3d)
            }
            case 2 {
                mstore8(sub(resultPtr, 1), 0x3d)
            }
        }

        return result;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (security/ReentrancyGuard.sol)

pragma solidity ^0.8.0;

/**
 * @dev Contract module that helps prevent reentrant calls to a function.
 *
 * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier
 * available, which can be applied to functions to make sure there are no nested
 * (reentrant) calls to them.
 *
 * Note that because there is a single `nonReentrant` guard, functions marked as
 * `nonReentrant` may not call one another. This can be worked around by making
 * those functions `private`, and then adding `external` `nonReentrant` entry
 * points to them.
 *
 * TIP: If you would like to learn more about reentrancy and alternative ways
 * to protect against it, check out our blog post
 * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].
 */
abstract contract ReentrancyGuard {
    // Booleans are more expensive than uint256 or any type that takes up a full
    // word because each write operation emits an extra SLOAD to first read the
    // slot's contents, replace the bits taken up by the boolean, and then write
    // back. This is the compiler's defense against contract upgrades and
    // pointer aliasing, and it cannot be disabled.

    // The values being non-zero value makes deployment a bit more expensive,
    // but in exchange the refund on every call to nonReentrant will be lower in
    // amount. Since refunds are capped to a percentage of the total
    // transaction's gas, it is best to keep them low in cases like this one, to
    // increase the likelihood of the full refund coming into effect.
    uint256 private constant _NOT_ENTERED = 1;
    uint256 private constant _ENTERED = 2;

    uint256 private _status;

    constructor() {
        _status = _NOT_ENTERED;
    }

    /**
     * @dev Prevents a contract from calling itself, directly or indirectly.
     * Calling a `nonReentrant` function from another `nonReentrant`
     * function is not supported. It is possible to prevent this from happening
     * by making the `nonReentrant` function external, and making it call a
     * `private` function that does the actual work.
     */
    modifier nonReentrant() {
        // On the first call to nonReentrant, _notEntered will be true
        require(_status != _ENTERED, "ReentrancyGuard: reentrant call");

        // Any calls to nonReentrant after this point will fail
        _status = _ENTERED;

        _;

        // By storing the original value once again, a refund is triggered (see
        // https://eips.ethereum.org/EIPS/eip-2200)
        _status = _NOT_ENTERED;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (access/Ownable.sol)

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        _checkOwner();
        _;
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if the sender is not the owner.
     */
    function _checkOwner() internal view virtual {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}