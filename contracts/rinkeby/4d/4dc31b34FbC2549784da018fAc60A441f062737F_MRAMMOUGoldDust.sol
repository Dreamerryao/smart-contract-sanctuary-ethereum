// SPDX-License-Identifier: MIT

/*
OOO00O000O00OOkO0OOOOOOOOOOO00000OO0OO0OOOOO00000OOO000OO00OOO000000KK00OO00KK0000O0KK00000OOOO00K0000O0OOOO0000000OO000OOO00000000KKKK0OOOO000000000OkOO0O
0OOO00000000OkkOOOOOOO00OO0000OO0OOOOOOOOO00000000000OOO000000000000000OO0KKXXXXXXKKKK00000OO0000000000000000000000000000OO0OOOO000000K000OO0000000OOOOOOOO
OOO0OOOOOOOOOOkkOOOOOOOOOOOOOOOOOOOOOOOOOOO000OO00000OO0OOOO0000OOO0OO00KK0kxxxddxOKNXXK00000000000KKKKK000OOO00000O0000OOO0OOOO00O00000000000000OOOOOOOOOO
OOOOOOOOkOOOOOO0OOOOOOOOOOOOOOOOOOOOOOOOkOOOOOOOO0OO00000000000O000O00KXOddddxxxdooodOXNXKK00000000K000000OO0000K0000K000OOOO00000OO00000000OOOOOOOOOOOO0OO
OOO0000OOO0OOO000O00000OOOOOOOO00OOOO0OOOOOOOOOO00OO000000000OOO000KKKOd:cddoooloodxocxXXXK0K00000KK000OO000000KK00000000OOOO0000OO000000000OO0OOOO0OOOOO00
OOO0000000OOOOOOO0000000O00OOOOOOOO0OOOO00000OOOO000000000000000KKKOoc;;;:cc::;,,;::c',kXXXK0000000KKK00000000KKK00KK000000000000000O0OO00OOOO00OOOOOOOOO00
0000O000K00OOOOOO000KK000OOOOOkOOO00O00000O0000000000KK00000000KKOl;;cc::;;;::;,;;:::;,;lOXK0000KKKK000000000000000KK0000K000OO00000O00000OOOOO0OOOOOkOOO0K
000OO00KK000OkO00000KK00OOOOOOOOO000O000OOO00OOO0000000K0O00KXX0o::::c:::;;;;;::c::;;:c:::d0K00000000000000000KK0O000OOO0000K00000000000K0O00000000OOOkkO00
KKK0OO00000OOOO000OO00OOOOOOOOOO0000OOO00OOOOOO0000000KK00KXXN0c,lddolc::::::::::;,',;;;:::cOXK00OOOO00OO00000KK0000OOOOOOO000000000000KK0000000000000OOOOO
KK00O0000000OOOOOOOO0OO00OOOOO000OOOO00000000OO00O0000KKKXXKkdclO0Okkkxdoc::ccc::::odxxxdoo;;xXK000OOOOO0000O0KKK000O0OOOOO00KK00000000KKK000000000000K000O
KKK0O000000000OOOO00OOOOOO000000OOOO0000000K00O000O000KXN0dol;cOkoc:;;:col,;ccc;':oolc:cccll,,xKXKK0OOO0000000KK00K00000O0000KK0000000KK0K000O00OO0000000OO
KK0OO0000000OO00000000O00000K00000O0000OO0O00OO000OOO0KNKox0OdcldxkOOkdl;,:loodl,';lddxdoc;;;;oxxOXX0OOO00K0O00000KK00000K0O0000000000K0000000OO000000000OO
KK0OO00KKKK00K0000000O00000000000OOO00O000OOOOO000000KXNOdO00klkX00KXXNXk::olloo:cOXNNNNXKx,.lOOOdkXXK00000OOO00K00KK000000OO000K00O0KK0000OOOOOO000O00000O
K00OO0O00000O000KK0KK000000000000OOO00000000OO00000000XNOdOKKklkX0kkkOk0Kxcllllc:xKOOOOOO0Xo.cO00kdKNKKKK0OOO0000000KKKK00K0KK000000O00000K0000OOO0O000000O
000K00OOOO000000KK00K0000000KKK0000000K00000OkO0000000XNKdx00O:;dOXXNXXKxc:ccclc:o0KKKKKKX0l'l000xxKXK0KK00OOOOOOO000000K000000000OO0000KKKK000OO00O000O000
00KK00OO000OO00O0KK0KK00K000000O00KKK0K000KK0OO00K0000KXN0ddkOo;;;cllllc',llllool;;dxkkxxdlccd00OdONX0000000000OO0K000O0000KOOO0OOOO0K00000KK00000000O00000
000OOOOO00OO00OO0KK00KKKKK000OO00K000000K0000OOO0000KK0KXXXOkxl;:;,,;;;,,clllllooc,;:::::clclxOxx0XX000OO00KK00OOO000O00000K0O00OOkO00O0000K000000000000000
00OOOO00O000OOOO000KKKKKKKK00O00K000000K00KK00OOO00O000000KXNXOc;;;;:::,,colllolol;;:clcllc;,lxkKXK00OOO000000000000000000000OOOOOOO0O0K0000O00O000000000K0
00OOOOOOO00OOOOOOO00000000000000000KKKK0KKKKK00OO00OOOO0000KXXX0l;:;;:;,';:;,';:c:;;:c:ccc:,c0XXK0000OOOOO000OO000000000OOOO0000000000000000O00O00OO00000OO
00OOOOOOOOOOkOOOOO0KK0000000000O000KKKKKK00OOOO0000000OO000000KX0c;:;,,::::;;:::::::;;;:c:;oKXKK0000OOOOOOOOOOO0O0000000kkO0000000KK00OO00O00000000OOO0OOOO
00OOOOO0OOOOOOOOOO0000000000O00OO0000KKK00OOOO000OOOOOOO000000O0XKd::;:ooododddddddo:,,cc;dKKK000000OkkkkkO00OO000000O00OOOOOOOO0000K00000000OO0OO00OO0O00O
000OOOOOOOOOOOO00OO0000000000000OO0000000O0000000OOO0OO000000OOOKXXkc;;:loddxdddoll:;;;;ckXXK0000000OO00OOOOOOOO0000000O00000OOOOOOO00O00OO00OOOOOOOOkOO000
000OOOOOOO0OOOOO0K0O00KK0OO000KK0OOOOO0OOOO0000OO0OOOO0000000000KXXX0d;'';;::;;,,,,;,:okKXK0000K000OO0OOO0OOOOOOOOO0000O000000OOOkkkkOOOOOOO00O00OOOOkOO0OO
00000000OOOOO0000K0OO000OO00000000OOOOOO0OO00OOkO000O0K0KKKKXXXXNNNXKKx..,,;;,,','..c0XXXXK00000000OO0O00000K0OOOOOO0K00000000OOOOOkOOOkkOOOO000O00OOkOO00O
00KKK0OOO0000000000O0KK000OOOOOO00OOkOOOO0000OOO00K00KKKKXXXKOkxddooll:'.','''',,,,'cxxk00KKKK00OOOOOkO0000000OOO00000000K0000OO000O00OOOOOOOO00000OkkOO00O
0KKXK0OOOO00000000000KKKKK0OOOOOOOOOOOO0000OO00O000KKXX0kdoc:;;;;;;;:::,'',,,,,;;;,,',;:cclodk0XKK0OOOOOO000OOkkOOO0OOOO000KKK0O00OO00OOkkkOkkO0OOOOOkOOOOO
KKKKK0O000000OO0000000KKKK0OOOOO00OkOOO00OO0K00O0KXXOdl;,,;;;;;::::ldxxl:cl::lodxo;.';::::::;,:oOXXKK00OOO0OOkkkOO0OOOOO0000K00000OOOOO0OOOO00OOOOO00OO0OOO
000KKK000000O0O0000K0O0000OO000000OkOOO00O00000KXKko:,,:;;;;;;;;;,:xxxkdoOKdlxxxxxlldc,cc:;;,;;,;o0XXK00000000OOOOOOO00OO0OO0000000OOOOO0000O0OOOO0KK00000O
000000000OOOO00OOO00OO000000000000OOOOOO00000KXKkl::::::;;::;,,;coollllclxkxlclllloO0ooOkkxc,;;::;:xKNX0000K00000OOOO000O000000OO00OOOOOO0000O000O0KK000KKK
OOO000OO000O000OkOOOO00OO00000KK00OOOOO0OOO0XX0occc:;::;;;::;',cxK0xdddxkxoddooddxkkxoldddo;',,:c:;;o0NX000KKK00OOkOOOOO0000000O00OO0OOOOO0000000OO000000KK
OOO0K0OOOOOOOOOOOOOO0000O0000000000OO00OOOKXXkcclcccc::;;,',cdkkxdxxxkkkd:coooodkkkkkxdooddd;';::::;;l0NX0OO0000OOkkOOOOO00OOOOO000OO000OOO000O0000O000000K
000000OOOOOOOOOOOO000K0000K00000000O000OO0XKo:ccccclcc:;,:;,lxoodloxxxdl;;odxxxookkddxxdxOkxdolcccc:::oKNX0OO000OOOOOOOOOOOOO00000OO00000OOOO000000K000O000
00000000OOOOOO00O0K00K0OO00K00000O000000KXKo:ccccccc:;;lkKXd:lllcldxxccooddolldxxo;lkkkdodldkkkdlllc::;dXXKOOO000OOOOOOOOOOOOO000OO0000K0OOOO000000KK0OOO00
00000OOOOO000OO0000OKKOOO00K00000000000KXNkcllllccc:;:kXXXXxd0OxxxxooloolokkxxocoOdldxkxodoclooc;:c:;;,c0NK0OOO00OOO00OOOOOOOOO00O000000OOOOOOOO00O00OO0000
0000OOOkkOO0000000OO00O00000000000O00O0KNKololllllc::kX0OKXdlkkxxOd;lxdxxxolxOOdcokocododkO0Od:;,;;::::;dXX0O000000O0000O00OOOO00000000OOOOOOOOOOOOOOO00000
00O0OOOOkOOOO0000OO00000K000K0000OO000KXNOlodooololcxXKOOKkodkkxxkc:dxxdodkxlloollooldxxkkxkkl,;;:;;ccc,cKNK00000000000OO0K00O0KKKK00000OO0OOOOOOOOOOOOO000
00000OOOOOOkOOO00OO0OO00K00000000O00KKKXNxldddddddoo0X0OK0ldOkkkddo:cldkOxlolccloollccxkkookkxo:::ccc::,cKNXK000K000000OO000000KKK00KK000000O0O000OOOOO0OOO
000O000OOOOO00000O000O000KKK0000000KKKKXNkldddddddll0X0OO0koooolokkdolc:colccccclolllccdOdoxxkdc:ccc:;:;oKNK00KK000000000000KKK0KK00KK0O000000000000O000OOO
000OO0OOOO0000000000000000K0000000K00KKXN0lodddoool:kX0OkOOOOdcdK0kxxkkdlc;:ccclllllcc::ldc;;;;::::::;c:oXNK00K00000000000KKKK00000000000000000O000K0000000
000OOOOOO000OOO00000O0OOOO000K00000OO0KKNXdcoooolll;c0XKOOkkOKOddodkkkxkOkdc::ccccccc::::;'...;c::cccll:dXX0O00OO00KKXKKKKKKK0000000000KKK00000000KK00000KK
O000OOOOOO0000000000O0000000KKK00K00OO0KKXOccoollllc:lOXK0OOOOKXd:x0OOxok00kc;;;:ccccc:;;;;,,:cllclllcc:kNK0OO0O0KK00KKKKKKK00000000000000000O0OO000KKK00KK
OO00OkOO00000000000000K0000000000000OkO0KXXx:lollccccc:oOXK0OOOXKxloooc;okdldOko:;:::::::;,;:::cccllcc;lKNK0O0OO0KKKKK000KKKKKK00O00OOOOO000O0000000KK0000K
OOO0OOO000000K0000000KK0000000000OOOOOkO0KNKl;lllcccccc::ldkOOOOOOd,'''',,''cddxd;.,::;;::;:cc:cllolc;:kNX0000OO0KKKK000KKKKKK0000000000000000K000000O00000
O00OOO0O00K00000000O0K00000000000OO000OO00KX0c;lllcclllllllodxkxxxxo,,,,,',,,,;;,'..'',;:ccccccloool::xXXK0KK000KKKK00000000000K00K0000000KK0KKKK0000000000
000OO00000000000O00000OOO0KK000O00000OOO000KXKo;clllloooolloodxkkOOk:';;,;;;,',,,,,,'''.',;:::clll:;lOXK0000000KK0KKKK0000000K000000000000000KKK00000K00000
OOOOOOOO00000K00000KK0000000000OOO000OOOO000KXXx:coooooooooxkxxxdddo,,cc:;;;;;;;;;;:;,'':clolooooodOXX0OO00000000000KKK0K000K0O000KKK00000000KKKKK0000000K0
OOOOOOkkkOOO00OO00K0000000OO000OOOO00OOOO000O0XXOocloolllloddxxkkOOklc::c:;;;:::;,;::::oxdONNXNNNNXK00OO0000000000O0KKKK0000000000KKKK0O0KK00KKKKK00K0000K0
OOOkkOOOO0OO0OOO0K0K00K0OOOO00000OO000O0000OOOO0XKklcollllldxxxxxko;d0Odc,';xOkl;,;:ldOKKoo0XNNXKK0OOOOOO000O0KK00000KK000000O00K00KK0000000000KKK0O0000000
OOOkkkOOOOO00O00OO0000OOOOO00000000K000000OOOOOO0KXKxllloodddddddxxoxkkOOx:l00KOccdk0KKOdxOdONXKOOOOkOkkkOK0000000K0000KK00OO00KKKKK00KKK0KKKKKKKK00OO00000
000OOOOOOOOOOOOOO0K00OOO00OOOO000000K000OOOOOOOkkOOKXKkxlloxkkOOx:ck00OOOOxokO0Odx000Okk0KkldKNK0OOkkOOOkO00000000KK00000O0000000KKKKKKKKKKK000K000000O00O0
000OO0OO0OOOOOOO00000OOO0OOOOOOO0000KK0OO000OOO0OOOO00KK0kdollddldOOOOkO0OkxkOOOkO000000OkOOoONXOkOOO00OOOO0KK000000000000000O00000KKKKKK00000000000K00000K
00000000OOOOOOOOOOOO00OOOOOOO0OO00O000000000OO00000OOOOO0KXXK00d':xkO0OOOOOOOOOOOOO0K0OO00kokNNKOO00O00000OO00K0000KKKKK000000000KKKKKKK000000000KKKKKKK000
O00000000000000000OOOOOOOOOO0000KK0000KKK00KKKK0000OOOOOO00KXNNO,.,,;:cc:okOOOOO00OOOkkko:oKNNX00000000000OOOKKK0KK000KKKKK0000KKKKKKKKKKKKKKKKKKKKXXK0K000
OOO0000K000O0000OOO000OkOOO00KKKKK000KKK000KXKKKK0OOO0OOO000KNNx,,;,,,;;;:dOOOOKKo;:::;:;lXWNKKKK0000KK000O000KKKKK0000K00000000K0KKK0KKKKK0KKKKK00KK0000O0
0000000K0000000O000OOOOkOOOO00KKKK000KK000KKKKK000000000OO00KNXo,;,,,,;;:c:dO00kl;,;:;;lldNNX00000O000KK00OO000000000K00000000KKKKK000KKKKKKKKKK000000000O0
00K00O000OOOO0OOOOOOOOOOOO0000K0K0000000000000K0O00000000O0KXW0:,;'',;:;lo;lO0d'';,,,,;olxNNK0000OOOO00KK000000000OO0KK00K00000KK00000000KKKKK000000000OOO0
000000000OOkOOOOOOOOOOOOOOOOO00KK000000000O00O00O00KK0O0000KNNk;',,,,,,:xdxNWWO,':,;;;cxokNX000OOO0OOOOO0000000000KK0KKK0000KKKKK000000000000000000K000000O
K0000000000OOO000OOOOOO0000O00000K00O00OOOO0KK00000000000OOKNNx,',,,,,,lkxONNWO;,;,;:;lkdONXK0OOOO0000000OOOOOOO0000KKKK000KXXKKK0000000000000O0OO00O00000O
00000000000OOO000OOkO00000000000000OOOOOOOOO0KKK0O00OOOOOO0XNXl,,,;;,,,dOxKNNWO;',,,;;dOokNXK000O000000000OOOOOOOO000KK00000KKK000OO00000000000000O000000K0
0000000K00000OOOO00O000K00K000000OOOOOO000000KKK0KKOOOOOkOKNNO:,,,,,,,cOkkXNNWO:',;;:lO0oONX000000000K00000000000000O0K0000000K00000000OO0000000000O0K000KK
00000000KK0OOOOOO000000000OOOO0KK00000000000KKK0000O00OOO0KNNx;;;;;,;;oKkkNNNWO:,;;ccoK0o0NK00000O0000000K0000000000OO0000000K00O0000000OO00K000K00000KK00K
KKKK00K0000OOOOOO0000000OOO0000KK000000KK00O000O0000000O0KXNKl,::;;:cckKk0NNNWO:,;:ccdK0dKNKKK0000000KKK0000O00000OOO00000000K0OO000000000000000000KK0KKKKK
KKKX0000000OOkkO00000000OOOO0000000KKKKK00000O000O0O000O0KXNO:;::::c:o0KkKNNNNk:;:cclkXOdKNXK0O0000000KK000000OO00000O000OOO00OOO00O00KKKK00000000KKK0K0O0K
00KKK00000OOOO00KK00000OOOOOOOO000KKK0KK00O00000000000000KNNx;:clllccxX0kXNNNNk:cclclONkoKWXKK0000000000000000000K0000000OO000OOO00OOO00KK00O000000000000KK
K0KKKK0KK00OO0K000000OOkkOOOOOOOO000000000O0KKK0000000000XNXd:lloolllkXOxXNNNNx:llcclONkoKNK000000OO00000O0000O0KKK000OO00O0000000OOO00O0K0OO000000000000K0
00KKKKKKKK00000000O00OOOOkOOOOOOOO0000000000KKKKKK000000KXNXdloooollokXxdXNNNXd:lllloOXkl0WX000000OO0000000000000K0KK0OOO0000000000OO0OOOOO00OOO00OO00KKKKK
0KKKKK00000OOO0000000OOkkkkOOOOOOO00000000000KKKK0000000KNNKooxdddllldXkl0NNXXd:lllllxXOl0NXXKK00000000000000KK00K00OOO00000000000OO00OOOOOOOOO0000O0000000
KKK0KK00OOOOOOOOO000OOOOOkOOOOOOOO000000000000000000KK00KNW0oldddolllo00coXNKOocoooloxKKldXNNNNXK00KKKK000K00K000000OOO000KKKK00KKOO000OOOOOOkO000000000OO0
KKK00KK00OOOOO00000OOOOOOOO00OOO0KKK0000000O000000000KK0KXW0lloxdolollxKOodkxocldoooodxKOoxkkkOKNNXKKKK00KKK0000000000000000000KK000000000OOOOOOOOOOO00OO00
KKKK000K0OOOOOO000000000000OO0KKKKK0000K00000000OO00KK00KNW0lldxdoollllodddxl,;ldlloooodOOOkkkkxkXNXK000000KKKK0OOO00000000OO00KK0000000OOOOOOOkOOO0OOOOOO0
K000KK00K00OOOO0OOOOOO00O0000KKKKK00000K00000O00O000000OKXNKolooddoolccokkddol:coclloloodoloooxdo0WXK000K000KK0OOO0000000000OO0000KKK000OOOOOOOOOO000O0000K
00000K00K00OOOO0OOO0OOOOO000KKKKKK000O00000OO000O0OOO0OOOKNXocodddollodxodKk;,,:lllooooodoodxxol0NNK00000000000OO00000000000OOOOO00KKK0OOOO000000000KK0OO0K
00000000000OOO00OOOOOOO0000KKKKKK000000KKK0000KK00OOOO0OO0XNOccooxkxxlo00loxOXKocoodxdllddxdlodONXK0000000000K00000000OO00KK0OOOO000000OOO00000KK000000O000
0000000O0OOO000OOOO00000O000KK0KK000K0KKKK0KKKKK00OOOO00OO0XNKxdollokdlxkk0NNNNKxlloxkko:lxxokKNXK000000KK000K000O0OO0000K00OOOOOO000O00000K0KK0000O00O000K
K0O00K000OOO0000000OOOO0000KKK0KK00000000KKKKKK000000OOOO0000KXXK0OkkOKXXNXXXXXXXKkdooddoxkOXNNXKK0KKK00KKKK0000OOOOO000000OOOOOOOOOOO00000KKK000000000000K
00OO000OOO00K00000OOOOOO000KK0KKK000000K00KKKKKKK000OOOO000000OO00KKKKK0000000000KXXK00KXNNXXK00KKKKKK00K0KKKK0OOOOOOO0O00000000O000000000KKK00K00000K0O000
0000000000000OO0K000000000000KKK00K00000000000000OOOOOOOO00K00OOOOOOOOOO000OOOOOOOO00000000KKKKKKKK0KKKKK0000000000O0000O000K00KK0000000KKKKKKKK00000K0O000
00000K0000000OO000000K000O00000K00K00KKK00KK0O0000O00OOOO0K0000OOO00O000000000000000O0OOO00KXXXX00000KKK0000000KK0000K000000000KK0O00OOO0KKKKKKK00KKKK00KKK
dev by @bitcoinski
*/


pragma solidity ^0.8.4;

import "@openzeppelin/contracts/token/ERC1155/ERC1155.sol";
import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/security/Pausable.sol";
import "@openzeppelin/contracts/token/ERC1155/extensions/ERC1155Burnable.sol";

contract MRAMMOUGoldDust is ERC1155, Ownable, Pausable, ERC1155Burnable {
    constructor() ERC1155("ipfs://") {}

    function setURI(string memory newuri) public onlyOwner {
        _setURI(newuri);
    }

    function pause() public onlyOwner {
        _pause();
    }

    function unpause() public onlyOwner {
        _unpause();
    }

    function mint(address account, uint256 id, uint256 amount)
        public
        onlyOwner
    {
        _mint(account, id, amount, "");
    }

    function mintBatch(address to, uint256[] memory ids, uint256[] memory amounts)
        public
        onlyOwner
    {
        _mintBatch(to, ids, amounts, "");
    }

    function _beforeTokenTransfer(address operator, address from, address to, uint256[] memory ids, uint256[] memory amounts, bytes memory data)
        internal
        whenNotPaused
        override
    {
        super._beforeTokenTransfer(operator, from, to, ids, amounts, data);
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "./IERC1155.sol";
import "./IERC1155Receiver.sol";
import "./extensions/IERC1155MetadataURI.sol";
import "../../utils/Address.sol";
import "../../utils/Context.sol";
import "../../utils/introspection/ERC165.sol";

/**
 * @dev Implementation of the basic standard multi-token.
 * See https://eips.ethereum.org/EIPS/eip-1155
 * Originally based on code by Enjin: https://github.com/enjin/erc-1155
 *
 * _Available since v3.1._
 */
contract ERC1155 is Context, ERC165, IERC1155, IERC1155MetadataURI {
    using Address for address;

    // Mapping from token ID to account balances
    mapping(uint256 => mapping(address => uint256)) private _balances;

    // Mapping from account to operator approvals
    mapping(address => mapping(address => bool)) private _operatorApprovals;

    // Used as the URI for all token types by relying on ID substitution, e.g. https://token-cdn-domain/{id}.json
    string private _uri;

    /**
     * @dev See {_setURI}.
     */
    constructor(string memory uri_) {
        _setURI(uri_);
    }

    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override(ERC165, IERC165) returns (bool) {
        return
            interfaceId == type(IERC1155).interfaceId ||
            interfaceId == type(IERC1155MetadataURI).interfaceId ||
            super.supportsInterface(interfaceId);
    }

    /**
     * @dev See {IERC1155MetadataURI-uri}.
     *
     * This implementation returns the same URI for *all* token types. It relies
     * on the token type ID substitution mechanism
     * https://eips.ethereum.org/EIPS/eip-1155#metadata[defined in the EIP].
     *
     * Clients calling this function must replace the `\{id\}` substring with the
     * actual token type ID.
     */
    function uri(uint256) public view virtual override returns (string memory) {
        return _uri;
    }

    /**
     * @dev See {IERC1155-balanceOf}.
     *
     * Requirements:
     *
     * - `account` cannot be the zero address.
     */
    function balanceOf(address account, uint256 id) public view virtual override returns (uint256) {
        require(account != address(0), "ERC1155: balance query for the zero address");
        return _balances[id][account];
    }

    /**
     * @dev See {IERC1155-balanceOfBatch}.
     *
     * Requirements:
     *
     * - `accounts` and `ids` must have the same length.
     */
    function balanceOfBatch(address[] memory accounts, uint256[] memory ids)
        public
        view
        virtual
        override
        returns (uint256[] memory)
    {
        require(accounts.length == ids.length, "ERC1155: accounts and ids length mismatch");

        uint256[] memory batchBalances = new uint256[](accounts.length);

        for (uint256 i = 0; i < accounts.length; ++i) {
            batchBalances[i] = balanceOf(accounts[i], ids[i]);
        }

        return batchBalances;
    }

    /**
     * @dev See {IERC1155-setApprovalForAll}.
     */
    function setApprovalForAll(address operator, bool approved) public virtual override {
        require(_msgSender() != operator, "ERC1155: setting approval status for self");

        _operatorApprovals[_msgSender()][operator] = approved;
        emit ApprovalForAll(_msgSender(), operator, approved);
    }

    /**
     * @dev See {IERC1155-isApprovedForAll}.
     */
    function isApprovedForAll(address account, address operator) public view virtual override returns (bool) {
        return _operatorApprovals[account][operator];
    }

    /**
     * @dev See {IERC1155-safeTransferFrom}.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 id,
        uint256 amount,
        bytes memory data
    ) public virtual override {
        require(
            from == _msgSender() || isApprovedForAll(from, _msgSender()),
            "ERC1155: caller is not owner nor approved"
        );
        _safeTransferFrom(from, to, id, amount, data);
    }

    /**
     * @dev See {IERC1155-safeBatchTransferFrom}.
     */
    function safeBatchTransferFrom(
        address from,
        address to,
        uint256[] memory ids,
        uint256[] memory amounts,
        bytes memory data
    ) public virtual override {
        require(
            from == _msgSender() || isApprovedForAll(from, _msgSender()),
            "ERC1155: transfer caller is not owner nor approved"
        );
        _safeBatchTransferFrom(from, to, ids, amounts, data);
    }

    /**
     * @dev Transfers `amount` tokens of token type `id` from `from` to `to`.
     *
     * Emits a {TransferSingle} event.
     *
     * Requirements:
     *
     * - `to` cannot be the zero address.
     * - `from` must have a balance of tokens of type `id` of at least `amount`.
     * - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155Received} and return the
     * acceptance magic value.
     */
    function _safeTransferFrom(
        address from,
        address to,
        uint256 id,
        uint256 amount,
        bytes memory data
    ) internal virtual {
        require(to != address(0), "ERC1155: transfer to the zero address");

        address operator = _msgSender();

        _beforeTokenTransfer(operator, from, to, _asSingletonArray(id), _asSingletonArray(amount), data);

        uint256 fromBalance = _balances[id][from];
        require(fromBalance >= amount, "ERC1155: insufficient balance for transfer");
        unchecked {
            _balances[id][from] = fromBalance - amount;
        }
        _balances[id][to] += amount;

        emit TransferSingle(operator, from, to, id, amount);

        _doSafeTransferAcceptanceCheck(operator, from, to, id, amount, data);
    }

    /**
     * @dev xref:ROOT:erc1155.adoc#batch-operations[Batched] version of {_safeTransferFrom}.
     *
     * Emits a {TransferBatch} event.
     *
     * Requirements:
     *
     * - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155BatchReceived} and return the
     * acceptance magic value.
     */
    function _safeBatchTransferFrom(
        address from,
        address to,
        uint256[] memory ids,
        uint256[] memory amounts,
        bytes memory data
    ) internal virtual {
        require(ids.length == amounts.length, "ERC1155: ids and amounts length mismatch");
        require(to != address(0), "ERC1155: transfer to the zero address");

        address operator = _msgSender();

        _beforeTokenTransfer(operator, from, to, ids, amounts, data);

        for (uint256 i = 0; i < ids.length; ++i) {
            uint256 id = ids[i];
            uint256 amount = amounts[i];

            uint256 fromBalance = _balances[id][from];
            require(fromBalance >= amount, "ERC1155: insufficient balance for transfer");
            unchecked {
                _balances[id][from] = fromBalance - amount;
            }
            _balances[id][to] += amount;
        }

        emit TransferBatch(operator, from, to, ids, amounts);

        _doSafeBatchTransferAcceptanceCheck(operator, from, to, ids, amounts, data);
    }

    /**
     * @dev Sets a new URI for all token types, by relying on the token type ID
     * substitution mechanism
     * https://eips.ethereum.org/EIPS/eip-1155#metadata[defined in the EIP].
     *
     * By this mechanism, any occurrence of the `\{id\}` substring in either the
     * URI or any of the amounts in the JSON file at said URI will be replaced by
     * clients with the token type ID.
     *
     * For example, the `https://token-cdn-domain/\{id\}.json` URI would be
     * interpreted by clients as
     * `https://token-cdn-domain/000000000000000000000000000000000000000000000000000000000004cce0.json`
     * for token type ID 0x4cce0.
     *
     * See {uri}.
     *
     * Because these URIs cannot be meaningfully represented by the {URI} event,
     * this function emits no events.
     */
    function _setURI(string memory newuri) internal virtual {
        _uri = newuri;
    }

    /**
     * @dev Creates `amount` tokens of token type `id`, and assigns them to `account`.
     *
     * Emits a {TransferSingle} event.
     *
     * Requirements:
     *
     * - `account` cannot be the zero address.
     * - If `account` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155Received} and return the
     * acceptance magic value.
     */
    function _mint(
        address account,
        uint256 id,
        uint256 amount,
        bytes memory data
    ) internal virtual {
        require(account != address(0), "ERC1155: mint to the zero address");

        address operator = _msgSender();

        _beforeTokenTransfer(operator, address(0), account, _asSingletonArray(id), _asSingletonArray(amount), data);

        _balances[id][account] += amount;
        emit TransferSingle(operator, address(0), account, id, amount);

        _doSafeTransferAcceptanceCheck(operator, address(0), account, id, amount, data);
    }

    /**
     * @dev xref:ROOT:erc1155.adoc#batch-operations[Batched] version of {_mint}.
     *
     * Requirements:
     *
     * - `ids` and `amounts` must have the same length.
     * - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155BatchReceived} and return the
     * acceptance magic value.
     */
    function _mintBatch(
        address to,
        uint256[] memory ids,
        uint256[] memory amounts,
        bytes memory data
    ) internal virtual {
        require(to != address(0), "ERC1155: mint to the zero address");
        require(ids.length == amounts.length, "ERC1155: ids and amounts length mismatch");

        address operator = _msgSender();

        _beforeTokenTransfer(operator, address(0), to, ids, amounts, data);

        for (uint256 i = 0; i < ids.length; i++) {
            _balances[ids[i]][to] += amounts[i];
        }

        emit TransferBatch(operator, address(0), to, ids, amounts);

        _doSafeBatchTransferAcceptanceCheck(operator, address(0), to, ids, amounts, data);
    }

    /**
     * @dev Destroys `amount` tokens of token type `id` from `account`
     *
     * Requirements:
     *
     * - `account` cannot be the zero address.
     * - `account` must have at least `amount` tokens of token type `id`.
     */
    function _burn(
        address account,
        uint256 id,
        uint256 amount
    ) internal virtual {
        require(account != address(0), "ERC1155: burn from the zero address");

        address operator = _msgSender();

        _beforeTokenTransfer(operator, account, address(0), _asSingletonArray(id), _asSingletonArray(amount), "");

        uint256 accountBalance = _balances[id][account];
        require(accountBalance >= amount, "ERC1155: burn amount exceeds balance");
        unchecked {
            _balances[id][account] = accountBalance - amount;
        }

        emit TransferSingle(operator, account, address(0), id, amount);
    }

    /**
     * @dev xref:ROOT:erc1155.adoc#batch-operations[Batched] version of {_burn}.
     *
     * Requirements:
     *
     * - `ids` and `amounts` must have the same length.
     */
    function _burnBatch(
        address account,
        uint256[] memory ids,
        uint256[] memory amounts
    ) internal virtual {
        require(account != address(0), "ERC1155: burn from the zero address");
        require(ids.length == amounts.length, "ERC1155: ids and amounts length mismatch");

        address operator = _msgSender();

        _beforeTokenTransfer(operator, account, address(0), ids, amounts, "");

        for (uint256 i = 0; i < ids.length; i++) {
            uint256 id = ids[i];
            uint256 amount = amounts[i];

            uint256 accountBalance = _balances[id][account];
            require(accountBalance >= amount, "ERC1155: burn amount exceeds balance");
            unchecked {
                _balances[id][account] = accountBalance - amount;
            }
        }

        emit TransferBatch(operator, account, address(0), ids, amounts);
    }

    /**
     * @dev Hook that is called before any token transfer. This includes minting
     * and burning, as well as batched variants.
     *
     * The same hook is called on both single and batched variants. For single
     * transfers, the length of the `id` and `amount` arrays will be 1.
     *
     * Calling conditions (for each `id` and `amount` pair):
     *
     * - When `from` and `to` are both non-zero, `amount` of ``from``'s tokens
     * of token type `id` will be  transferred to `to`.
     * - When `from` is zero, `amount` tokens of token type `id` will be minted
     * for `to`.
     * - when `to` is zero, `amount` of ``from``'s tokens of token type `id`
     * will be burned.
     * - `from` and `to` are never both zero.
     * - `ids` and `amounts` have the same, non-zero length.
     *
     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].
     */
    function _beforeTokenTransfer(
        address operator,
        address from,
        address to,
        uint256[] memory ids,
        uint256[] memory amounts,
        bytes memory data
    ) internal virtual {}

    function _doSafeTransferAcceptanceCheck(
        address operator,
        address from,
        address to,
        uint256 id,
        uint256 amount,
        bytes memory data
    ) private {
        if (to.isContract()) {
            try IERC1155Receiver(to).onERC1155Received(operator, from, id, amount, data) returns (bytes4 response) {
                if (response != IERC1155Receiver.onERC1155Received.selector) {
                    revert("ERC1155: ERC1155Receiver rejected tokens");
                }
            } catch Error(string memory reason) {
                revert(reason);
            } catch {
                revert("ERC1155: transfer to non ERC1155Receiver implementer");
            }
        }
    }

    function _doSafeBatchTransferAcceptanceCheck(
        address operator,
        address from,
        address to,
        uint256[] memory ids,
        uint256[] memory amounts,
        bytes memory data
    ) private {
        if (to.isContract()) {
            try IERC1155Receiver(to).onERC1155BatchReceived(operator, from, ids, amounts, data) returns (
                bytes4 response
            ) {
                if (response != IERC1155Receiver.onERC1155BatchReceived.selector) {
                    revert("ERC1155: ERC1155Receiver rejected tokens");
                }
            } catch Error(string memory reason) {
                revert(reason);
            } catch {
                revert("ERC1155: transfer to non ERC1155Receiver implementer");
            }
        }
    }

    function _asSingletonArray(uint256 element) private pure returns (uint256[] memory) {
        uint256[] memory array = new uint256[](1);
        array[0] = element;

        return array;
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _setOwner(_msgSender());
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
        _;
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _setOwner(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _setOwner(newOwner);
    }

    function _setOwner(address newOwner) private {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which allows children to implement an emergency stop
 * mechanism that can be triggered by an authorized account.
 *
 * This module is used through inheritance. It will make available the
 * modifiers `whenNotPaused` and `whenPaused`, which can be applied to
 * the functions of your contract. Note that they will not be pausable by
 * simply including this module, only once the modifiers are put in place.
 */
abstract contract Pausable is Context {
    /**
     * @dev Emitted when the pause is triggered by `account`.
     */
    event Paused(address account);

    /**
     * @dev Emitted when the pause is lifted by `account`.
     */
    event Unpaused(address account);

    bool private _paused;

    /**
     * @dev Initializes the contract in unpaused state.
     */
    constructor() {
        _paused = false;
    }

    /**
     * @dev Returns true if the contract is paused, and false otherwise.
     */
    function paused() public view virtual returns (bool) {
        return _paused;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is not paused.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    modifier whenNotPaused() {
        require(!paused(), "Pausable: paused");
        _;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is paused.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    modifier whenPaused() {
        require(paused(), "Pausable: not paused");
        _;
    }

    /**
     * @dev Triggers stopped state.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    function _pause() internal virtual whenNotPaused {
        _paused = true;
        emit Paused(_msgSender());
    }

    /**
     * @dev Returns to normal state.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    function _unpause() internal virtual whenPaused {
        _paused = false;
        emit Unpaused(_msgSender());
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "../ERC1155.sol";

/**
 * @dev Extension of {ERC1155} that allows token holders to destroy both their
 * own tokens and those that they have been approved to use.
 *
 * _Available since v3.1._
 */
abstract contract ERC1155Burnable is ERC1155 {
    function burn(
        address account,
        uint256 id,
        uint256 value
    ) public virtual {
        require(
            account == _msgSender() || isApprovedForAll(account, _msgSender()),
            "ERC1155: caller is not owner nor approved"
        );

        _burn(account, id, value);
    }

    function burnBatch(
        address account,
        uint256[] memory ids,
        uint256[] memory values
    ) public virtual {
        require(
            account == _msgSender() || isApprovedForAll(account, _msgSender()),
            "ERC1155: caller is not owner nor approved"
        );

        _burnBatch(account, ids, values);
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "../../utils/introspection/IERC165.sol";

/**
 * @dev Required interface of an ERC1155 compliant contract, as defined in the
 * https://eips.ethereum.org/EIPS/eip-1155[EIP].
 *
 * _Available since v3.1._
 */
interface IERC1155 is IERC165 {
    /**
     * @dev Emitted when `value` tokens of token type `id` are transferred from `from` to `to` by `operator`.
     */
    event TransferSingle(address indexed operator, address indexed from, address indexed to, uint256 id, uint256 value);

    /**
     * @dev Equivalent to multiple {TransferSingle} events, where `operator`, `from` and `to` are the same for all
     * transfers.
     */
    event TransferBatch(
        address indexed operator,
        address indexed from,
        address indexed to,
        uint256[] ids,
        uint256[] values
    );

    /**
     * @dev Emitted when `account` grants or revokes permission to `operator` to transfer their tokens, according to
     * `approved`.
     */
    event ApprovalForAll(address indexed account, address indexed operator, bool approved);

    /**
     * @dev Emitted when the URI for token type `id` changes to `value`, if it is a non-programmatic URI.
     *
     * If an {URI} event was emitted for `id`, the standard
     * https://eips.ethereum.org/EIPS/eip-1155#metadata-extensions[guarantees] that `value` will equal the value
     * returned by {IERC1155MetadataURI-uri}.
     */
    event URI(string value, uint256 indexed id);

    /**
     * @dev Returns the amount of tokens of token type `id` owned by `account`.
     *
     * Requirements:
     *
     * - `account` cannot be the zero address.
     */
    function balanceOf(address account, uint256 id) external view returns (uint256);

    /**
     * @dev xref:ROOT:erc1155.adoc#batch-operations[Batched] version of {balanceOf}.
     *
     * Requirements:
     *
     * - `accounts` and `ids` must have the same length.
     */
    function balanceOfBatch(address[] calldata accounts, uint256[] calldata ids)
        external
        view
        returns (uint256[] memory);

    /**
     * @dev Grants or revokes permission to `operator` to transfer the caller's tokens, according to `approved`,
     *
     * Emits an {ApprovalForAll} event.
     *
     * Requirements:
     *
     * - `operator` cannot be the caller.
     */
    function setApprovalForAll(address operator, bool approved) external;

    /**
     * @dev Returns true if `operator` is approved to transfer ``account``'s tokens.
     *
     * See {setApprovalForAll}.
     */
    function isApprovedForAll(address account, address operator) external view returns (bool);

    /**
     * @dev Transfers `amount` tokens of token type `id` from `from` to `to`.
     *
     * Emits a {TransferSingle} event.
     *
     * Requirements:
     *
     * - `to` cannot be the zero address.
     * - If the caller is not `from`, it must be have been approved to spend ``from``'s tokens via {setApprovalForAll}.
     * - `from` must have a balance of tokens of type `id` of at least `amount`.
     * - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155Received} and return the
     * acceptance magic value.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 id,
        uint256 amount,
        bytes calldata data
    ) external;

    /**
     * @dev xref:ROOT:erc1155.adoc#batch-operations[Batched] version of {safeTransferFrom}.
     *
     * Emits a {TransferBatch} event.
     *
     * Requirements:
     *
     * - `ids` and `amounts` must have the same length.
     * - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155BatchReceived} and return the
     * acceptance magic value.
     */
    function safeBatchTransferFrom(
        address from,
        address to,
        uint256[] calldata ids,
        uint256[] calldata amounts,
        bytes calldata data
    ) external;
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "../../utils/introspection/IERC165.sol";

/**
 * @dev _Available since v3.1._
 */
interface IERC1155Receiver is IERC165 {
    /**
        @dev Handles the receipt of a single ERC1155 token type. This function is
        called at the end of a `safeTransferFrom` after the balance has been updated.
        To accept the transfer, this must return
        `bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)"))`
        (i.e. 0xf23a6e61, or its own function selector).
        @param operator The address which initiated the transfer (i.e. msg.sender)
        @param from The address which previously owned the token
        @param id The ID of the token being transferred
        @param value The amount of tokens being transferred
        @param data Additional data with no specified format
        @return `bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)"))` if transfer is allowed
    */
    function onERC1155Received(
        address operator,
        address from,
        uint256 id,
        uint256 value,
        bytes calldata data
    ) external returns (bytes4);

    /**
        @dev Handles the receipt of a multiple ERC1155 token types. This function
        is called at the end of a `safeBatchTransferFrom` after the balances have
        been updated. To accept the transfer(s), this must return
        `bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"))`
        (i.e. 0xbc197c81, or its own function selector).
        @param operator The address which initiated the batch transfer (i.e. msg.sender)
        @param from The address which previously owned the token
        @param ids An array containing ids of each token being transferred (order and length must match values array)
        @param values An array containing amounts of each token being transferred (order and length must match ids array)
        @param data Additional data with no specified format
        @return `bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"))` if transfer is allowed
    */
    function onERC1155BatchReceived(
        address operator,
        address from,
        uint256[] calldata ids,
        uint256[] calldata values,
        bytes calldata data
    ) external returns (bytes4);
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "../IERC1155.sol";

/**
 * @dev Interface of the optional ERC1155MetadataExtension interface, as defined
 * in the https://eips.ethereum.org/EIPS/eip-1155#metadata-extensions[EIP].
 *
 * _Available since v3.1._
 */
interface IERC1155MetadataURI is IERC1155 {
    /**
     * @dev Returns the URI for token type `id`.
     *
     * If the `\{id\}` substring is present in the URI, it must be replaced by
     * clients with the actual token type ID.
     */
    function uri(uint256 id) external view returns (string memory);
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev Collection of functions related to the address type
 */
library Address {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize, which returns 0 for contracts in
        // construction, since the code is only stored at the end of the
        // constructor execution.

        uint256 size;
        assembly {
            size := extcodesize(account)
        }
        return size > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCall(target, data, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        require(isContract(target), "Address: call to non-contract");

        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        require(isContract(target), "Address: static call to non-contract");

        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionDelegateCall(target, data, "Address: low-level delegate call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(isContract(target), "Address: delegate call to non-contract");

        (bool success, bytes memory returndata) = target.delegatecall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verifies that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly

                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "./IERC165.sol";

/**
 * @dev Implementation of the {IERC165} interface.
 *
 * Contracts that want to implement ERC165 should inherit from this contract and override {supportsInterface} to check
 * for the additional interface id that will be supported. For example:
 *
 * ```solidity
 * function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
 *     return interfaceId == type(MyInterface).interfaceId || super.supportsInterface(interfaceId);
 * }
 * ```
 *
 * Alternatively, {ERC165Storage} provides an easier to use but more expensive implementation.
 */
abstract contract ERC165 is IERC165 {
    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IERC165).interfaceId;
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC165 standard, as defined in the
 * https://eips.ethereum.org/EIPS/eip-165[EIP].
 *
 * Implementers can declare support of contract interfaces, which can then be
 * queried by others ({ERC165Checker}).
 *
 * For an implementation, see {ERC165}.
 */
interface IERC165 {
    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30 000 gas.
     */
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}