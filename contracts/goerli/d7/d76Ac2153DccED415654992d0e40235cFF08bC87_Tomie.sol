// SPDX-License-Identifier: MIT
/*
N# .dMMMMN,  .(JNM#= .(gMMMMB""TYT"WNg+gQHWm..UMR.d`jb.MF(j#uMkYG+#=   ..v"1(+gHHHNNmggJJgNNHHNgda(..THMMMMMMMMMMMMMMMMMMF !j._._ >./TNMMMNM9`(#MMMM#! .(gMM#!gJJJ..?WNMN(mMMMMMrWMMMgMM=.MMMMmMMM[
MMBUMMMt([email protected]! .JMMM9C!  ..JNMgHQg,?TNJ7TMg,7MN# [email protected](Mj%dNMB'dY(,=.([email protected]!~<?zWJ_:.(TMMNg"MM#! .!! .?H9^` (HHMMMMM#""MMMMMMm, MMMMMN,,MMM"WMMM]
MMMMMMNNMMmjMY  .dMM5_!  .J#"! ..gNMMMMMMMMMMMMMNMM,HNdMd~MMDoZ+MF(dMD(MMN#=.ZQgMM"^(JMSqMMMMMMMMMMMMNm77TNNMNmMMMMMMMMMF_(X<i.. ! `?4o( ,?N>  dMI--?!`  ..-+(...((&+..J(T" .,.HMMMMMMD` .M#  JMMM]
MNh.JgNN,`,WM%.dMMC!  ..#= ..MMMMMMMMMMMMMMMMMMMMMMMMMdMN(MMJ~dM#JdMt.M1M$.jMMMM#WYMMMMMMMMMMKHHMMNgVHMNkn.?WNgMNMMNMMMMb--d).. (_    -G.:  WJ"!    ..JXBY=+?!        .W5..dB^  (TMB!  .MMMh. JMMM]
MMM9"77MMR.  (MM"   .JD` .dMMMMMMMMMMMMMMMMMMMMMMMMMMMMMNBdMDdMM0dM$d#g8dM#QV5zYUHWM8YTOJTGJ7WMNg7MMMNgTMMNxTuVMMNdMMMMMN,.?b ..1``  `  (e?! ?&..vY=~-(J7!....Jz?<[email protected]         `..(7"!dVMNJMMMt
     [email protected]?7`.dM=   .dY`.([email protected]#zH=(Vdf7?=Y7<JWai~/(7+T&1JMMm?MNZWNdMMNx,4ZMMMNNMMMb??M,,} ~  ..+7` `..v9N+(gkY5(+dY">-._~``   ,"=..    ..-?=!      .N/MMbMM[
([email protected]`  .M@`  .1J~(qMMM#QMMMH"(D(gNMMMMMMMMMMMMMMMMMMMMMMMKN([email protected]#ZZ4h_(<`.i.IO.>J<~<Yuc  ?MG?WMMm7MR,TNdMMN,(l ?HMNMMM< ,N < .?!.! ...dYT+JHM4jMM9=-~~` ..(ggNMMMMMMMMMMMMMHNNg...`  TMN, ?jMM]
MMM>_~-  .M$  .v(K!.MMM5gMMMBiJgM#[email protected]#dw!dUdHHTTTur?TZZCwJ_!/  .7+  4eG(MMNeM#i 7NdMHh,n  ?NNMMN, ?b?`..wzuJgMWkWH8qgMBI.++JgMMMMMMMMNNNMMMMNNmg+-... ` _?TWHg..(TNgMF.]
MMF .!  .M\  ,c.f -MM=.MMMMQMMM5dMMM5+JMMMMMMMMMMMMHgggggWMMMHkMMM3J<gWVO$<_z(7(x?X~~?j.7    4, WWWA-TdMMe4,.Wmh7ZWb  .h?HsdmJNY=v-.dMMHMMNJgMMHMNgMMMMMMMMMMMMMMMMMNNMMMHY"UQav""BHgJ....??TuV} .]
M3 /`  .M% .!.Of`+M#(dMMMMM8vugMUMgNMMMMMMMMMMMMMMMMMBMMNHMMMM#dECJ:,j>JWyzJ:_ uWW$.Jy^.i    (?n.O?Nd&,BVHWWo.?HN,1?h,  4,/WxTWRUMNNMMMMHMMMAHNMMMMMMMB"=+X"!    `  _?"THMMMNJ,_TW..  _?THMWgaJ.?7]
N,!.MMLMt .o(\d .MF.MMMMM5/(gdY(MMMMMMMMMMMMMMMMMNNMMMMMMMNM#jX0z?<.kzJ(7OWVw{(v$+dMNR?<(1  .=G.1.T+TRd+z?HVa?,.4[ ?,NS+-dH1JCdNWgMD=~.dMNgMMMMMMMMQQQMMMMMMMMMMMMMMNNMMMNNMMMMMNNZ7Tm-  .&MMM]  (]
M\.MMMWF .%,I.t.MNdM#uM#y1/(BgMMMMMMMMMMMMMMMMMMMMMMMMB9"7MH3<qX<YhJIXC%(z<>7qQVdM^_<?_  Tz,l,Z) 1.(XJUmzX,?ZUkG,?;,?.WWd#[email protected](/4,,M#9MMN..]
N(MMM##(:>(j!d~.MMM#IM#ZJ<dNMMMMMMMBdMMMWMMMWMMMM#(HkXg#[email protected](uQS=.1-d5Jd(<JjJ.TQM#4&..(1X6.jWdpkL+ (, ?G/9m&?7o,TxJTmIjHM#=qgMMMMNVWMMMMBTqgMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMNgJTMMMMMNgz1S, JMMM!.]
MMMM#jI!,%[email protected](F.MMMWtjM#v.MMMMMMMMMHMMMMNMMNNMMMMNdQMMMNdNMkdfJd!1CNm43f<O~<6(-d#$ v Wm.1jpzNgNMNMg$vi   7JHHX-?42(Y4HHSHUNMMMMMY(dMMNMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMNHHHHMMMNmTMmJ\     .]
MMMM^yIj% [email protected]#[email protected]<dHh|,!ujWJg+V/.1!-.d#[(:,(WS(?d&MMWMMMMMNR?-., 1d$dTB43&NWXBjMMMMMMuMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMNgMMMMMMMMMN&.. ?TMMm,?TN&,  .]
MM% .K`J .M#[email protected]@-MMMMMNMMdMtMOUHDdbcdHdW].w+jNW=(2J},:([email protected]=zdL (HdHMMMB"C?Yjlv/ujvJ/qTh9jgMMHK(NMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMNNavTWMMMMMMMNQMMMMNHMMMMNNJ,TMMm,  ~"6(]
M#  J!.% JM#dMM(MHPMMMMMMMM#MMMMMM#jMNdM#jMMMM#[email protected]#<?biN/(>m6(b+SIg!.-!MN,~ [email protected]`.< `- jNg&sW+(s .=,[email protected](MMNMMMMMMMMMMMMgMMMMMMMMMMMNmgggmWMNMNNMNMNgMMMMMMMMNmMMMMMMMMMMMMMMMMMm,   .]
#! .j (\ XM\(#[email protected]@d#uMMMMMMWNWMM#uMMMMWMM#[email protected];.WMM2((MydzN[,vW>.+MNc?Tk+<(MM3.?1+-  ?<jM?4gXMM~2 [email protected](.??vTNNMMMMMMMMMMMMMMMMMMMNJTMMMNJ7TMMMm...]
  .\( M) kMvMNVJ#ZNMMMd#dM5MMMMM#dd#MEMNMMMMMdMM6WZ#>JW~HNwK(2r?S(#d#,j9.2 V=,TMKnc -?HMP.`  (0.   dK=`.([email protected]&aJWNyMMMMMMMMMMMMMMMMMMNJTMMMNx. 7MM#W[
  2 <.d] M#idM%JF MMMMMWM#(MNMMMNhWd#dMMMMM#MdM#jMdk<tdJ?WJ0,?;(3XdB#mO.?n~~<-(d#{__-J.Mx~..  .S,(X#=!~??2(MHM#dMMMMMMWMMNMMMMMMMMMMMMMMMMMMMMHWH""MNHM9MMMNXMNJTNgWMNJMMMMMMMMMMMMMMMNMMMMMm, 7MM%
N(! <-dbvM#(@MLd$ [email protected](3hh;JNnJ(/_S961fTMh(>G:+:_.M#e(..+Mb~!  ` 7zIdM&.(!(J#+(MNMMAMMM#(MNMMMMMMMMMMMMMMMHagMMMMMMMX&.(gMMMNdMNNkMmMNkkTNdNHMNdMMMMMMMmMMMMMMMMNJ.?]
!r  >((N>dMdkMMNR MMMMMMOMUMMM#dMSWWNMMMMMM]dNHbjMdNJ(dvh:dN<di((Nuj>Cv+TNJNX_-.JMMHh.THNJ.. <  (MNm0wt.R-kjdM#dMdMMMNMMMMMMMMMMMMMMMMMMMMMMMH97771uJdMMMMMyTMNMMeHNMMNxMNMNMMMMMMMMMMNsMMMMMNe.7W)
,`  >,jN(JMMDMdMM/MMMMM#(MwMMMDdMJJ(MMMMMMMNjWdUdtHMk(.W?UZTNJJHkSZ.><v.W/(?MN;s?WMNWHxXdMe< (.ude.(VWC(W6s(,MNdMZMMMMMMMMMMMMMMMMMMMMMMMB9961?7zdMMNNHMMMMMNgsMNMMmMMNMNMMMMMMMMMMMMMMMNvMMMMMN,,[
Na` d!cMy(MMPMMMMrMMMMMMJNNMMMNX#I(_dMMMMMMM#f#MK.JNdEUMn(7WsdNH?HUj_C((H_(-Z4xk% ("BwWl??MNJMDObJmTC.deJH24.MMKMbMMMNMMMMMMMMMMMMMMMMMNNHko_  .?TMMMMMMMNMMmdMNMMNWMMMNMMNdMMNMMMMMMMMMMMmo?WMMMg%
MN .r`qM#(MMbMNJMNdMNMMMkNMMMMNJNI(}(MMMNdMKMbdHN1(MVBsvMn+iWMMMNHW.1(1_U`.!.<?j>..~,_~61.?MMHNmZn. >_(X<OXdHdMNdNJMMMMMMNMMMMMMMMMMMMHkA+-.-!??XMMNmMNgMMMNNNHMMNMMMMMMMHMMMMNMNMMMMMMMMMMMN,(4?M)
M# J{ (M#.MMNdM4MMMMMMMMMMMMMMMHMc.R,MMMMpdNJMKHMZ(NROuzd1HdXv7jDYUdvGz_./ -.R: <1j&J-~._4,??[`.?(v74uWXhJWNN+MMMMNMMMMMMMMMMMMMMKOvVTI-..._?1WHgMMMMMMMMNHHMNdMMNMmMMMMMMmHMMMMHMMMMMMMMMMMNYh, j[
M'.%} <?M{MMM#MZMMMMMMMMMMMMMMMNMb.dxMMMMMxdNvM#M#(7N{?%?MmYU+:,D&z(WWdJJ-,>1d.j77O.?ii.~.J7kY(! j_,~jo.MMMMMMMMMMMMMMMMMMMMMMMMMMMMM#9XgJ&d64&dHHMMNNMMMMmHMMMNVMNMNWHMNMMNyHMHMMkWMMMMMNMMMNe,?([
t ,_} { M#7MMNdbMMMMMMMMMMMMNMMNMMW(NdMMMMMRdNJMMNe_1b(bJ/WHh<.?TTUQkNkHWH&,  l (. ,h,4/<,-.(dA/_.?WHMMMMMBwdMMMMMMMMMMNQMMMW&J(&WMMMMMNJ(ThJ8G+JMMNMMNKMMMMNHHWMmdMMNkNMMMMMbdMMMMm?TMNMMMNMMMNC([
} {.: } JM{?MMMNdMMMMMMMMMMMMWMMMMRNdNMMHMNdp?MKdMN+4#+K1(z#Y.: <i(f}1  .! (J&(vj.(-,++J-(l !.zUJzJqMMMhMWdMM5AMMMMMMMMMNHHMMMNg+magTWHWHMNa,TQeJ4dHMMNMNyHMMNkXNdMNMMMMNKWMMMMkMMNMNXJMMRMMMNdMNg%
`,`.  \ ?Mb dMMMfMMMMMMMMMMMM#MMMMbJMMNMMdMN7h,HNdMN23(f+dUb <!.uJHMMMMMMWNdaJJJ.1h(,1<<1v<J&dMk(ljMM#wM8NMMNMMMN#HMMMMMMMMMMNmHMNsTWNa,?6JTMMadMNgTmJMMNMNdMMMmHMNMM#MMNRIvMMMMNXMMMMsJMM#QMMMm7M]
.\ (  >.$,Mp(HMMN?MMMMMMMMMMMMMMMMMpMMMNMMMMNd(WJMJMNdWZJn1dMM#97Y!`?vc`(YTMkMMNN,  <(/=(7__!jM#"jMMMkd8dMMMNdMMNBMMNMMNHNMMWmMMNgJBa,7MNa.?1JTMMMNkWVWHMMMMNMNMNNdMMMNJMMNmjMMMMMNJMMMNJWNJWN/TMN[
J  <  (/< MMndMMMbdMMMMMMMMMMMMMMMMNJMWMNMMMMNp$?IMR8HgJjYW#T5....i(.j(+-(, ?WmHVW;~?iZXYui(XndnJMMMQd#CM#MMMMM#MXMMNMMNRONWMvWNHMMKGMN,(HNS, 7hJMm/ThRVHMMMMNMNdMNJTMMNJ?MMMMgHMMMNkHMMN(dMmdN,?Wt
~.[< `._> {dKhHMMMrMMMMMMMMMMMMWMMMMN?N?M#UMMMMN+.T9Mp(4r.K!Ii(+J,.(WggNJddm...zI_.`_jKdm(vd4JMHM#M#[email protected]#WMMHMWJMrWHJdNZVWNe?Me.?Mm6, ?UNWNp7hHWkMMMMMNdM#H,WMHmr7MMMMMMMMMbdMMN&dMRUMK([
"M|< .!;) (,HR3WMMNdMMMMMMMMMMMKMMNMMRHNdMKHJMM#HmxJdU/<(Wl1Jo?nwQMNNMMMMH#"""MMMM9}  ?<[email protected]#<jMMMMMMMDHHDXMM#MNhXb(XN(dS4ZdMNeWN, ?Me4,_([email protected][
.MF_.: (} .__WmvMMMMMMMMMMMMMMMMdMM2MMNHNdMKXJxWNKTNJnW[J%  JS,(MNMMD ?,.--_- ..MY-jl. _.dMMdMkMMHMUMP.MMMMMMMNrHMPXMMNKMZDM/jJtj](vvn?R4,7h..WMeW,}.?HhMm2WHXMMMMMmMNdvdMMMMN24dMNg[dMMMNMNkMMNJT[
j#!.>` .|  l:(?N?dMMHMMMMMMMMMMMMMMNdMMNMMMMNdMmW#NmZThd(9. ,`1dMMN>?  (`.u..4..Mr.dH,J>jN#(M3MM#dMMMb.dMMMMM#dLdUN(WfMXd#9Ub nk+I  11z(Hv..H& ??Mgh,++UWSYNVHWdMMHMNMNvn?MMMMMMNM#7MNMMMMMMNdN?hJ[
MN./_.(J(. .+. ,Nd?MNMMMMMMMMMMMNMMMMMMMMMMMMMMMMMNHMJ-XQ/(<?7WWMMMn_>    .zX.(K^ >d$J' [email protected](NJMMMMN(NJZWc4jdbjM#.H,([email protected]   ?zJIU,  7N,.i?Ngh+<VnCOTNNWkMMMMNdMmWPWMJMNfMMNJ/dMMMMMNMMR T%
M#< .d~( (. 1(. (dnJMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMNdN(xTk-<- +MMMk_?'  .-..JHN[ O2xx(dMMWMfdM#HtMMMNMMJMMMMMLMNGzN-l.M/4Mp!W(d#@s.  .+11z,  .He.?,?NdmI:Cc=x?MNkMMNMMNN#dh?/SJMMN(fMMbMM#MMMMM[,%
 .  VMN[  ~. O~  Rj~IWNMMMMMMMMMMMMMMmMMMMMMMMMMMMMMMMMMv??MmdY=(MMMHm--.,u.JU _4WSHJ'.dMMWMQMMX#(dMMMMMvMMMMMbdMNpWW,l,b.HKh.WdD$Swc_. 1(zJ1.  ?N,.i,ZNNo:?n/6{vMkUMNKMMNNTdy<4jMMNbJb#WMNMfMMMN.[
J...eJuNNJ..-.l  z {(.(MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMNKS(-,4m, -ZW?WWMMMMHh,>.(HJ(fdMMM#dMMDKdNdkMMrMM[MMNdMMJKWN2N4,l,tWd/4-WHmJWI<1_ ~.?GJ-   TN,?(IMNp>_7&(1<[email protected][
MMMMMMMMMM!  <r1 d -J _.WMMMMMMMMMMMMMMMMMMB""HMMMMMMMMMNR-?i(f4?<_?jvdfBOdN/?-(>.JONMMMM#KMM$KHMMZMMbMMbdMMdMMKW/dNxzd,I.4>4 114MHXk>zI_ .` (3v-  .WN.Hp>TNx(jCJZq?wV4MNKWNJ</7HMM2?MNNMMMk#,b(MN\
MMM#[email protected]_V-?Z_ j.>  ?MMMMMMMMMMMMMM#<=~. .-<,_.JfWWMMNsI,?Gg,`,(7,HR/  Um,v<:.MMMNMMNkMMPNMMMbdM#XMNMMMMMMNvb(dN/.Wen.; > 1(zHdNNdwo. m...(OzJ: ,MN-bo<TNm+ATJ([email protected]$jNMMMMMN# M/MM)
MMM7MM#`(MMMb c ~Vn .G(    ?MMMMMMMMMMM#Tejz?7=CQmg,dn,CWWWMHJ?iJE.!~  ?Mn.~-JTMm?MMM#dMMMKJMbMNWM#qMMwMMMMMM#MMbO[(d#, q2l1  ! >./zvUMkN+,MMMMNeJv4S, .TN,7A(SWN,(?nvOWdJ<?W24h,[email protected]#RMMMMHN.MbJM)
MD` [email protected]:  !%1,_u_    [email protected](G   _!_. `[email protected],  (4SJ.(eJ9jMMNgNMMM$wMNdW#MNdMMKdMMdMNHJMM[4/1Mk..NJ(;   .- !?SGd9JY`?"MMMNNf.J7i, ?9mJHJI>YW&JS2jMo/,ZHo([email protected][email protected])
M,.gJM#(MMM^ .    !_J+ h.  ` I.+TMMMMMMb..J.! _?!~~-.6)[.?MMXWSJOI,jMg$vQJVYQggMMMMMMMMYMNhvMMM/NMMbMM#QMMNMMKnHMNJWJd#1 (NI(.   .._ (4wv$      7MNMb.<7!?~ jM8(?<+z(<(+4Hp?WyZ:?d#nM#MMNdM#M9&dbH%
..MMMMMN7j[  ( .J.(, 11{?,   .l [email protected],1<-_-.    .1r1 _dMNkkZOvG7a(n.ZTmMMMMMMMMMMMMNjMKydMMb,MMNMMNdMMMMM#XGdMN,WdN(_ MN,1    i..  .2C        ?MMN+gc<..zNK(?1(>vI=<z>[email protected])
MMN+,.MMNMMN.: (MMM# __I. <.  ~1 (oTMsMK!?j/     !-. .Zj.I?dMM#HI+?z-T=Z(QMMMMMW#MMMMMMMR#MH+ZWM[(MMMMMdMMMNMMe24jMb(MM_  JN] 1   dC `,^(n`         WMM93..._?Wn{_(~(jI(I<((pJJ7GWHdMmM#_MMMUd#MKM\
MMMMMMM#7^.7(   (MN.,  ~?< .~.~ ?.((6TWM$?! .._~!!!  `4(>< .JMHNmo1<?hk(WMMMNM#HmkMdMMMHNM2MNzXdN,dMMMMMMMMMM#HPl1d#h1M)  ,Mb  1  .w- .?.T,         .MbAv~  . TMb~2(1l>z++ USJJjVJSMMdM#.MM#.M#dNd\
.....gaJJ(-.J.....?"`  _  <.  ?- .i<.v-,b~?! l,!_.....(,:.. ?JNkNW(&JJNNMMMMMM##M#W0MNMfMsH/HNcMWb.W#MM#MMXMMF WyluHJNMb   MN   ~. (h_`.`.E. ` `    .MMUD     ,M#.\z1v(I1I zKzZJ>vHMMdMFJMMb.MDdMM}
MMMNNg+J..._7""HMMMMMNNg-...-    ~.~. ~ (h.(771/!-.  .,1_<1..17Nhvr 4gMWMMMdMMN#dM>SMMWmdNyW.TNJ4ML(dJMNHMWZHN. WJlZNkdR1  M#;     .N/  .?_p     `  .MN2??!~_~jNF((Izvz11>.j$IIR(:NMMdM],MMF.NlMMX}
""""HMMMMMMMNNgJ.._7TMMMMMMNaa,.   _.<.~..H,./!?(~~~?!~~>  _.  .<1c-.#K(MM##HMMN([email protected],1X/ZMR$.JbI      ,Sk/! zY1.`   `.MM#C??!~` (Mt;(v(jZ<=:Jd_zVr( MMMKM]JMd$d#j#dd}
           ?"TMMMMMNJ,. _7TMMMMMNg,   .- ` ?NJ! ..  .~_.?i.. !   .euMNdMMMHNWdMM_qVJNWM/HHo(vN,(dNXhZMNMNGlMz?Hk.,N.1WJdrG` N.;  `    XR-,! d4-....MHW2??!~~`(dMU~0zvJ>>d>rO.y(>{.MM##MbXHWQMDdNNd}
_!_...`          _7WMMMNg,.  _THMMMMNJ.      ?Sm< ,,` .-"!`?h   jMNMMNM#dMNdHKMM],JJMNMb WHp.1Wp ?NZ?WM#MXZdI4?b1 JR.44_4.h ?|(.       ?HW,.'.4#NQ#1xC<?7?< .JOI(,zj1Z(jZj1!J!J.`.MMWHM#W6=d#1MMMd}
     `_~..            ?WMMMNa,   ?WMMMMNg,.     ?9Q...Y    .U! (MMMMNMMM3MMRWWWMN.1iJMHMh.TWL ?Mm.?NmWdNdV/ZS>?Jhi W<,2h I.l 4 1.        7MNg.g.>?JZ`  .v_~(vvOv>+>v(Iv1v?_.\.%/ JMMdvJNK (MDdMMMH}
  `     `.<. .  `        ?TMMMNN,   7MMMMMMN,      .Vdo. `.2J`.NM#WM#dMMKdMMNXrUM[?sNWMNR?,.Wh.,Mm+XNWJM# GCd2-.dSo,[(zJl 3,, l z    `  `_.(?dwdmxwOwz1<-.(zZO1&71<<v+1l::.C.zz\ CMNdb1M\.M#jMMMMM}
     `      ?_!    `         7MMMNJ.  (WMMMMMMm, .-= Tm-.+%.t(#ddwdM#WMMMkMMMmW.XN,dMMHMN..i.TW, WRGHNyM#..y(X(. 4Xo( ?nl(. (.   1. `    (VdU=zVVdXyWkkOAAw$jIzZO?(z.<.<  (:_J( ._WMdH/MoMM#1MMMMM{
                    `    `    `(TMMMm.  (WMMMMMM#Y      ~`.Y(MMN#dMMNHMMNMN?MMN1,MNWMMMMW2  _..7n.WNdM#M#1 H.(%<  ?Xl: .z.?. 3    ?,   `.?X4O&&JuAAQHSOwfOUZXv?:JggMNHaa, J.!n:.ddMMNHNrMMM#(MMMMM}
. `   `        `  `    `          TMMMm,  (MMMM#`.       .=(d#MMtHMMMNdMMHMb?MMNN,MNdMMMMMeTY .L,0+HpWNM# >j1 (._  .4G. ><  `,.    .l  .x(wS<+uw0dX0TXH9=!udk9ZY`  ?? (TMNN-Jr [email protected]}
`` `   `  `   ` ..        `  `      ?MMMm.  7MM%   `   .J(kMMWMMdNMMMMAMMNJMbOMMML(MMMMMMD.%   ?~ <(HsMMH  j.- !(    </~(`~.  % `    1. . !(+<?(OCIdW0>  JWF?!    .~ .!  dMNJ`.MMMMMMMb(MMM#(6vX?J}
vj,        `   ( .4/?<-.    `        `?MMMe  .B`     `.Ngr-S#MdNdMMMMMMHMMMMMmXMNMmdM#MM$ z    ,`  (,MdMM, .`.-       (+.! .- (       .i        _jWV=   [email protected]  ..+Jn.++, ._C,Nb MMMMMMMkNldMME1dh !,:
.1J3.`   `     (.  .4, ?h.              7MMN,.%       JMMMNjM#MNdMMMMMMMMMMMMMNXMNMKMNMF  X.. .!     W?NRf-   /+      `(i.   `._  `     _`     _?T>`    jM\(!``_?"YM#H+..l.WIdqNMMMMMXMmdM/J%.2h ,{
  .3(+      ` ` L    ?   7,`  `  `       .MMMK`    `  (MMMMMNbwUWMMMMMMMMMM#HMMMvMMMMMF    (4.b    ` ,[(qp    SWHHg,`    >     \     `      `          (MM_(,       ?MMN.(&[email protected]#~b. S,l,:
     ?t  `      ?x  `.l   (L       `       TMF       .MMMMMMMMNWMMMNHMMMMMMNWMMM#MMMW#       .NL      S WB,   .WNMMM;    `     !`       `            ` dMH-?k,       JMMHNMMO##dMMMMjMMM1C_]  .n(J:
      .{`        ?p `.J`   .h.             `J`      `(MMMMMMMMMMMMM#gbMMMMMMkdMMNdMHH!       sdNG.    ,<,wl     ?#NdMN,           `  (..`   `          ?0db.?H,   ` .WdNMMMHdd#WMMM^(MMNJNmr:   `d:
     ` .,   `     ?iMD       4.         `  .\ `      dMMMMMMMMMMMMMHHMMMMNM#CdMMMMNV^       +#/Mb1     C 6O.    .HKWXHNe.`         .. ?MN,     `  `     (wU}.ZWo...dHNWdMMMdddXdMMn.?MMMAdMN,    ,:
        (.         .~,<   `  .b`   `       ,;.1.   `.% dMMMMMMMMMMMNdWMMMNMN~JM#NJ^      ` .#B},Ms{      .s1    <,NKWNWMNu..  `  .JT= ./"=           `  .zdm>1{SZgNMMMMNMMMkJdKdM9; ?"TMkHMMMh,..,{
  `  `   t     `  ` },-      `(,     `   ` .{       d, [email protected] dM#=        ! dHWr (Mk.      j>l   (-dNWHHXWWHn.      .~ .NJ,.`` `        ` .OTr  qMMMMMMMMMMN#dNnMM.(z-.. Q]   `  ?W{
         (. `       z,`        h  `    `   ,`    `  gMmH~MMMMMMMMMMMMMMMMMM\.MF       `   JMkRb  dN-       I,:` &JwMNUXV=1JMgk..  .{.Jf"!?TMN; .   `       .: (MMMMMMMMMMMMNMMMMMN,   _""!       ,{
   `    `.)     `   k.   `  `  d          `J        dMMN.MMMMMMMMMMMMMMMMM# J^       ` ..#\d#HN  ,M)  `..  .1.i.MgZWMmXUS/T-.YWdDi(.K^     (Z7"`            .JMMMMMMMMMMM#MMMMBMMMMU+,           ,:
    `  `  I  `    `.P        . Z       `   2   `   `dMMMMMNMMMMMMMMMMMMMMMm=      `  .JMMNzMN#H. .M]   ?Tn-+,`  GdHkHMNXh<0cW<(v7He.       `     `  `  `   .= (MMMMMMMMMMMMG#MbzMMNb.MHMQJ.. . . ,{
          (        ,L `       ?N        ` .\        K?MMMMMMM5dMMMMMMMMMMM'   `  ` .JM#NMNkMMbN`  W)    ..JQZ8Q.(u9udMMNvdUiZd1,3<dp    `               `.v`  (MMMMMMMMMQMq#(MN,TMMM-(MNL,NdHMF  ,:
 `  `     ._  `    .N.  `      ,\   `    `(`  `     b ?MMMMMNjMMMMMMMMMM"7.     ` .MMdMWM#MMMWM   K)  .J^.^ T?7Yqeo-?dHMNgNaggJge-(d`  `     `  `    `  .=    [email protected]## MMb .?4} .MM;JKNUb  ,{
&    `  ` .)   `  ` d:    `   `,{         Z        .F  MMMMMMMM#JJHMM#^     `    ([email protected]#NMMWK   W\ (^ (` .>   `vmgAQdWMMMBzJ(1ud#^                `  (^    JMNMMMMMM##dMDb dMMu>. ?77JWb.HM8Wx ,{
UZ,        t        ,t         J       ` J!   `    ,] .#JMMMMMMNs1gY!          [email protected]  .C.J` .X..J      (NJ6dXMHMMMNNW^t      `     `     `.Y    `.MMM#[email protected],MNH.1.    (9?=?THWh,}
XkU-.`     (.  `    .\  `   `  (-    `  .) ``   .` .] .t} MMMMMMM"`      `  `.JMMMNMMHMM#[email protected]  .uf    .nJ!       TMYWVWMMD^..z ?               ``.J^    `.MMNdMMMMMMN#(MN(|JRNWh.-"Tu.,%      4d{
VkWJn  `    [    `  J[         ,[     .J=   J      -[`.R~ >.WMM=    `  `    (MMMMMMMNMMMjMMMN#(  (%      .^     `     .TSY!   C       `   ` `  ` .J^     `.MZMW#XMMNMMM#.Mdcb.MWNMMa.            T{
?wWvW,`  `  1       d\   `    `.b   `.Y!   .%     `jl  W. 1.-Y           `.dMMMMMMMMMMM1MMM8dMm .%  `  `,!                                   ` .Y!     ` [email protected]#MMM(MMbX.dMMKHX?Um-...   .  ,:
 .?HNWm.    ,-    ` W   `  `    W. `.^   ...``     MrTQ,-.gD        `   `.MMMMMMMMMMMMNMMq#X6}JnP  `   .'     `   ` `             `  `  `   .+Y`   `    .MMMNJPJXdMMMd#dHMMMM:,DVSZ(` Nb  ?"MF   ,{
. ?WQMm+.    t `   .#           .b J!  .NM}   .` `d%  ?(d#!           ` -SMMMMMMMMMMMMMMNHy!dY"_`      Z   `    `    `  `    `        ` ..gMMF        ` (MMMMKNdNMMM#ZNJNMMNMi\ I ?+  J#.,i.2G,  ,:
Va. .TMNe`  `j     .%         `  JZ`   .MZ   :!   M\   J=         `   .dMMMMMMMMMMMMMMMMMMe=         `.N                       ` ` ..JNMMMMM#`          [email protected][dN?MMMN(_ z, .h.db.ndN] W, ,{
gWyl   (4-   .{    d`    `  `  .J"     .2!  ,  (  `         `   `  /.+MMMMMMMMM]dMMMMMMM#!       ` ` .fj          ` `  `  `  ...JgMMMMMMMMMME7    `  ` .MMMNMMMnMM#dH]djb?NMMk[  ?n <[email protected]^   4.,{
dmZ4.    7o   1   .P          .=     `._.`  ` .      ` `-.       .K&NK?MMMMMMMMDJMMMMMM^       `   .Y`)(.  `   `    `   ..+NMMMMMMMMMMMMMMD`        ` >,MMMMMMMmNd#MH$f.W;dMMNX,   !?`.Wb ..   .h.{
 ?4m,1i.  .`  .-  (!  ` `  .(Y      ` :(!              .2` `  ` .YdMt?MnMMMMMMB"`  _!`       `  ..=   CJ    `     ..(gMMMMMMMMMMMMMMMMMMB!           1. MMMMMMMMNMXHMH#JdhMMMN#M,       .Tm.    (M{
    _TG,~     .! .F       .=          .%            ` .d    `  _  J\   (Y"!`                  .+bk     XMg-.(JggMMMMMMMMMMMMMMMMMMMMMMMY           `  .,,MMMMMMMMN>j#d#dMMMMMNNNMN,       .4,  ,HW}
      ` ?o.<.    d   `  .v`          .v      `     ` -Z'    .`.`  '                        _.(MMMM;     WkOzX0ZdNMMMMMMMMMMMMMMMMMMMMM'      `  `       ?TdMMMMWMMAH9"^!  . (YY""#WW+      .#.J! ,{
  `       T,`  `('    `.=       `  ` ,`  -`    `   ..1`   `.v(>     `        `        ...JgMMMMMM#^     .mmgZ1zZ#MMMMMMMMMMMMMMMMMMMD`    `                             .! =   .H2      `` (F`   ,{
    `      (n. .F   `       `      `.! `.`       ` /!     (V(!    `        `    ` .gMMMMMMMMMM#"`  `  ` v ` ?TNJ#[email protected]!        `      ` `.     `       ` ._-.     ,D        .Z`   ..d{
       `     ?a.[       `     `    .!   `   `     !   ` .V1`       !   `  `  ` [email protected]!       ` .%      ?N([email protected]!      `     `  `  .,!   `` .  ``.?`     ?, ..{      ` .P .(dY^ ,{
   `     `     ?]`  `           ` .!         `       `.JC  `            ` Jm.7^     ?7"""HMY   `  ` .JR>l       JsMMMMMMMMMMMMM^      `           .+=       `....!         (MMh.       .Mk"!`    ,{
      `        .]        `  `        .    `    `   ` JY  ~          `  [email protected]!                   `  .dMMMb{  ` .< ,LMMMMMMMMMMMB`            `     .!      ` !  _`  .,`   ..Y=!       ` .F         ,:
    `          .\   `  `     `      .`   `         .Z'    ` `  `  `` .wC1'                     `.MMMMMMMb   zZIz,bdMMMMMMMMM^           `     ``  ``     .   ``.J/    .=`         `  .\        ` ,{
       ` `  `  .:`    `           `.:        `  ``.=      `     ` .J=,! !     `  ``  `      ` .vHrMMMMMMM|  (Sv?z#[email protected]`         ` `.....--_??`   ` `?TSJ,Z&(?G..Y`          ` .J=  `  ` `  ..{
   `        `  (}        `  `   ` .<   `  `     .>   ` `    `  `.V=        ` ..-+ggNNNQg&J++gMMNHrMMMMMMMN.  .Z+d#dMMMMMMD       `   . ?                     .^  T"`       ` ``..g9^  `   ...JdY=(:
    `  `      `()  `              c     `    ` .>        `  .?-J^     `  ..gMR(NMdM#MMMMH/kMMMMMMLMMMMMMMMb   1z?FMMMMMMF        `.J=       `                             `  .dM"    ..dT""<dM]  ,:
      `  `     -\    `       `   ,`        `  .6_         `  _`        (Q#MMMC`HNMM#MMMMHbjMMMMMMMMMMPMMMMM;    (bMMMMMM:     ` `.>     `            `    `    `      `     +#^   .J"!....MMMM%  ,:
-.-.....    `  (}      `  `   ` .%   `  `    .C!   ` ` `          ` `..f!rMMM(_(MMMNd#MM#N,MMzMMMMMMMDwMMMMN.   .DMMMMM#       ./`          `          `     `          ` .MD   .#ML  [email protected]\  ,:
z1<1<1<?1O}... J!`  `           J`        `                 `      .J" l jdMML} MMMNWWdMNd_MM>MdMMMMMmf>?MMMN.  .hMMMMMF    `  !    `  `     `    `     `           `     ..(JYM_.gMm.  .d#HM#`.R,:
<<<>1OTUUUXUUUCX        `  `   ,<   `                  `       `  .F   (..vMMNj.J#MMykrTMNrdMgMM5TMMMMNJ,.TMMb  -OMMMMM%                 `          `       `       ...g#HN/Mb (NNMb(Wx,MMMMMb.dMH:
    ___<<<<<?+7K               z{       `  `.       `      `     J=     t (JMMp% ?VMNvZ+TRldMtN#([email protected]: (MMb`J?MMMMM\       `            `          `       ``..H#JMMb(JN,HN,,MMM- dMNx (MMMN!,:
          .,=zOK    `       ` .j         ` ,` `        `      ` J^     `(. ldMNJ, (JMNJZ,?NMt.SR(D dHMMMMe   WN,VJMMMMM}    `     `  `  `        `         `  ..gMNHMmMM#;1.TmMMgMMMN+M>?MNNgJMM~,:
     `      !~?]  `  `  ` `   J<   `  `   ,`       `    `  `  .J!        1 .;WMN2. .wM#(z,.Ml(XMD  t ?MMMMN.  WNKMMMMMM[  `                  `     ` `   ``..NMMMMKWMMmMMJ/4,.UMMM#MMMMNJ,[email protected]_,:
  `    `      ,>             .\          ,` `        `      `.X!    `   ` ! .-?MMm.  7My((J!.OOU:  (. ZMMMMN, .MbMMMD` W.     `  `    `  `  `          ` .JMMMNNWMMNdMMMNWko?l .TMb ??MMD`,MM#,M[,!
    `    `    J   `  `  `  ``J` `      `.``  ` ` `     `  ` .K`      `     `  ~!4J$:  (b<  ~.j:J   .| .:WMMMM;.dM#^    ,h.  `     `     `     `  `   ` .JMMMMMMNNHdMMkMN,7QMN,3, .MMMNdMb..dM%.M#,;
...` ...-+d9USN.         `   {     `   .:           `    ` .f`   `       `       ?z.    <     (`    (.` ~dMMMM[J%   `.(MMN,          `                `JMMMMMN(NMNN7MNZMb .4?MKdNMB"?7TMMMMMN(MMF,:
XUUUUTHHHkZ0UX#     `        `       ` /   `         `  ` .#-....  `               ?.              ` -!   (MMMMN: `.+MMMMMN, `  `                      (MMMNNdNJW7Nk,UTmMNx 7,dm.7BQ..  `.MMM9"<J,_
<!?<!(.JvY"!~Jr   `   `          `    .!      `        ` (#1+(++<1++.....             `......   `          .MMMM:.JMMMMMMM9N.    `     `    `  `    `   ?MMMNM,TNn,THe..We.!.MMMMMNk=?4a.,MM# .MF(_
""""!        d\         `    `            `        `    (#<<<;<+(((+<?777<-..`  `    .J8UwZUVHMHa..         .HMMqMMMMMM#=  ,b       `   `                ,NHMMMp TM,[email protected]: Me.MMMMMMMMN(_
       `     K`     `     `     `   `       `  `     ` .F ` ?!`_~~!<+-___!!!!`      `drwOvvwvwwZvwTMQ..       [email protected]'     ?L                       `  ` .N,_7"NxJ/S.4, UMM=dM#`..  ,M[.MMMMMMMMM~
     .`  _` [email protected]   `   `     `  `        ` `    `   `   JD              .!~`(Ji.... ` .(NXwwwzwvuXwwOrrZVMm..    d0MMM^     `  de `  `  `  `  `  `  `        .N,   Te?YW,?h,[email protected](,.WMMMNMMNJ.,_
 _.`  _`    ?]          `            `              .d=._.                         .+zdNwywZXwQQHHNNNHmmwwVMHg,.MB3   `   ....N,                       `    ,N.   .!._1.79d#MN?MMdMNJN.,MMMMMmJM%,!
            .]      `                     `      ` [email protected]    ` .                    ` .(+x1OMXrvzuuwUXHMMMMMMMNNmQMMNkUWN-.-Z9XwwOZMe      `            `       `(N      (.>  ._7N,  .MMNMNN+N_ ?MMN,,~
         `   6   `        `  `     `     `   `    (#       `.<..              ` ._~``   JkzwwwzXuuXHMHHMMMMMMMMMMNZzdNQNMMMMMNSrMp`     `   `           `     ?R       .~ ..  7h. ?MMMMMMMNa(MMN!,:
       `     (.       `        `       `      `  .W>    `     .!    `                   dUuurzwwXZzwZXuzzX8HWSdMMMNHXMHBH8WSXvvwZMp `                     `    ?b            ?i,W, MMMMMMMMMMMMN ,{
  `  `    `  ,] `  `   ` `  `    `        `    ` Jd. `        `.<..  `  ` `      ..   ` JkuXQggmmmmQQXwXuuzuuZqMMMNXMNXkQNNNNmmXXXMp  `      ` `    `  `     `  ?b  ` `         ?Tm,XMMMMMMMM#HMb,{
         `  `,b     `        `     `  `    `    ,Z(_.    `  `                `  `.!  `   WMMMMMMMMMMMMMNNNNNN#MMMMMMMNv1=zvHNMNMNmHMx   `        `   `   `       ?k.   `  `        ?QMM#?` MMNMMNJ{
   `  `       N           `             `    `  (K.[  _~. `               `  !         ` (ZTMMMMMMHHkXWMMMHWMYMWMMNMMN#m??7"7THNMMMNN,     `              `       ?NTHBHHUWaJ..      4WWHe(MMMMM",{
         ` ` .d)    `   `     `      `    `    `,$ k<<++-. `        `            _>-....  .   _~`._7^      ...ddMSNOMMMHN,    .TMHHHWN.       `        `    `      /N/7UUVYBTHHMMNe.. .U, MMMMMF ,{
     ` ` ..v"^?N.`                       `      X\ M;?C.(->  `         `                              ..~<<(_?HMNSdR+dMMNWN,      ?7!,b.`           `          `    (N, ` ~!??779YBvT   (W,_dMM, ,{
  `   .JY^     /b    `    `  `         `     `     Jb-_<<j+<-.   `        `     `     1,       ` ...(+<<<<<--(NMM0Xb_17MMMRMm.`       (b   `             `           .N,                  .5J.HMr,{
     .D`        d[             `   `               .NJ+u&z<jV0zOo.  `        `         (6(..  .+<<<-(+z+<!?OdW#MN0wN.(zJMMMNXN,        d[        `   `     ` `        .We                   .Ta, ,{
    (Y          .N. `   `   `             `         MP!-~<(_~<-.?=?1..   `                  ?1, `~!`      +>1dHMN0tM! .&dNMMNOMb.       W.    `        `        `       7b.`          ` `   ...JN({
 `.M=    ` ` `  `JL   `  `        `  ` ` `   ` `  ` db-OOz+77<1zzVWkOaJ.  `   ` ` ` ` `       l_`~       .<+=WXMNKrd)  .~dRMMNvTN,`  `  ,b`  `    ` `   `  `      `      (N, `  ` `     ..dHWVOwWM\
.d"   `           N.          `                     (#<71OZC++usv7zXVT7O&(..             `          -`.  (<<j#dMMRvd] `_<?MkMMNmZMe   `  X,                  `      `      Tb.   ...-dY"`.NKGwVGf,:
!       `   `  `  Jb    `  `     `      `  `  `     .MYj+u+XWZT9G&<?TC++77C++-.                         `(<?dSdMNkrdb`.(&&dNXMMNkwMb     .N.   `     `   `     `     `      (MP77!`       .HsOw^ ,:
    `              N. `       `     `              ` d#~.(i(JZzlv7""""4wY77CjAuvG,    `     `  `    ``   (1j#wWMN8vwN    .<?NdMMMNmdN.    (b`         `     `           `     Tm.      `    Us`  ,{
  `    `   `      `(b    `             `  `  `       .MVC-.J?7?!(J+z1(.Jzu+(J1&-.1z(,    `      `       .1?dSwMMM8rwM.    _(J#OMMMNwVN, `  v[     `             `  `      `    ,N,    `      7p  ,:
     `    `  `  `   W,    `  `  `  `     `    `       db.ZY74V9zuZUz&gzVGazXSuzdV1++J+<.     `    `   `.+<(#vdMMMSwrM_`   .z+MkZMMMHXXN, ...W,       `  `  `         `   `       Tm.     `    ?h.J{
                    ,b                `         `  `  .MY1X+JY7?uwYzuyU1-(vC((JZ6dVURX6?^        `  ...(1cJSXd#MKwzOd|     .svNwvZMNmwdMR0wyJN               `                    ?N,`  `      ,H({
      `  `           M-     `            ` `  `      ` db74u&gkaJdMNkWMmmNNWmda+y<_<++-+z7>  `  `   jswwo(#XwdMMSzwwd] ` ....(MKZXwM#vzdMkwyid[     `    `     `    `   `     `    .Wp           U;
   `        `        Jb `     `   `                     HmzI(CO&dSUQkWWyWaJwAJ(JgkAd9TWHegJJ..      (1+<?dSuwXHH8vwvX#?wIXGvVyTMXzzU#uwzXM6J>!W,       `    `             `          ?h.   `     ,{
      `        `    `.N,  `         `  `      `   `  `  .M&uzV+(ys+TCj+Uu+JdmzXm+jwAywa+&I<?! `  `  !!j&(#vwzdWSvvwXdNUZXIzgWkWMKXwvvuvrwdb_?!.b                `    `   `            .W, `  ` ` ,{
 `   `   `        `   Jb     `           `  `            .N,(uewAeAWQXXNOH$vWsvWkswkXCvVUuw&+-+<>!    (OdSwzvXSwwzXwd#ZSO7zUwkwdNuwwwrwwwwMp   d[   `   `  `        `        `          7m.      ,{
           `  `       .N.   `    `   `    `    `     `    ,N,..-dVTAvTITC?77???=AZzOO<z7<?77??<?!      jNZwzvwOOrwzXXbO1jXKQXMH$MRzwwvrvOwwM,   N.          `  `  `     `  `   `  `   `  ,N,     ,}
[   `  `               d]                     `            ,NfT9?4yZXzWWM9TSZVi+J?71??>-...(jw+       .v#vwwwwXvwzwzdDvGVzwWadWdH#uXwwzzvwvdN   (b   `   `                                 Up    ,{
M;        `    `  ` `  ,N    `    `    `  `  `     `   `    (NxziJ<7wUi+zi(-vG&?TuadUIOO&xTH9<!  ` ` .?(#zXzwXwzwXXzdmZd0VZGkWHXXNUuwwwwwwOvM|  `W.     `            `   `          `   `   ?h.  ,{
.N,  `   `  `         ` M_ `   `     `     `        `   `    (No+1+J+([email protected]<<wmy7`  `  `.+--dKXuXzXuwXzXwWxWsdWHXZ4QWHNXXuXXXwwzvdb`  ,L `  `    `  `      `   `  `   `    `    ` .N,`,{
 ,N,   `     `          d)               `     `  `           (N:..-jJdMBUVYO6<ugkKUUIvC`         ?Hk&ud0XzuuuwXZXuXMZWW6TwZTVC??HOXXXzXwrwzwM. ..W,             `                       `    .W,,}
  (N.          `  ` `  `Jb    `  `  `        `       `  ` `   `(N,(Z?1dUnJw&ZWfWYIwXVV> .^-.`  ``((&xzAWWXwXzuXvzXud#Y<<~!-.-    dwXXXwzXwwuwMy1++Jb   `  `  `      `   `  `  `   `  `         .HJ}
  `?b   ` ` `        `  .N. `         ` `  `    `      `   `    (NJv7z+wTAv0COxZwmdW9' .?`   ` .(OYCOXOKXzuuXXXwXXXdl_!`         ,kXuwXXzuzXwWv?v1_J[           `        `             `    `    H{
    d[`                  M;        `                             (Nv1J?>(ugW9I1zzuv7`      ._<jzOI<+<__HwuXwzzuzuzXK          `   WmXwwwwXwwXN0z?1?<N.  `   `       `                            ,\
p   .N,  `      `        J]`  `           `   `             `     ,NJzwV=67=(-7^       ._<(JXkXwOOOwXMYHmXuwXZuwwuXF         ` ..~ (BH9YYUBYW56(3+7(?b                `   `  `          `      ` ,{
Mx   (N.          `      ,#          `   `         ` ` `   `  `    ,Ns<!-_!!        `?juZV0zzwwzv^(<??+(?TWMNQmmmWY       . ` ...JT+zIjzCZ71IZO?1deu+d[  `    `   `            `  `  `     `     ,{
MN.   db    `       `  `  M.     `           `          `       `   (N>       `  ` 1wVVT=<<!                   ......(AQmK9OUU0kga+<uO&vGJAdZVTOdsQsI(N,    `        `  `             `     `  ` ,}
MN> `  W[`   ` `    `     H. `  `   `  `  `   ` `    `    `  `      `(N,     ..(OTCI?7?~`       `  `   ....(++&dVWQWSdHMHHUVY6T4vOz1d0OdHSkIwwySXWWkZIjN.`   `  `   `    ` ` `  `      ` `       ,}
MMp    .Mc                H`                       `   `              (N. `  .!                   ` ...-zwIdyWw&kXjgQHJQQs8WWWY1Zv4U&ZOXwXOZVIOCCwk?o<_?b                           `          ` ,}
MMMN,   ,N,`    `   `   ` H.  `   `    `  `  `            `   `        ?N.                ` `  ` .XWXdMHHMWHHkHHkWH9WUYX96swv(xJOVzv1J-JTWd&ZI1JIGvzI!  d;  `      `  `  `              `   `    ,}
MMMMN,   ,N.  `       `   M;         `     `        `  `   `      ` `   7b       `    ` `   .(gmdMNBMMMNBwOwWywWWBATVUUTXUv(JJuZzzUWv31zO1ZOZX-YuzC<.J~` N.    `             `   `   `           ,}
MMMMMF `  (N.    ` `      J]`  `        `     ` `  `    `     `          Wb  `  `  ` ..JZYwQMT9XAadTWHHkmWHXAmmdiJJ1CvJYC!vIId&wo4ZGvVOv6OZV3+z=1zvv^    ,b     `   `   `  `  `       `  `   ` ` ,}
MMMMM]   ` ?R.  `      `  ,b  `  `  `    `   `   `        `    `  `    `  H[     ` _X&gXwVOQkZwdWVTUMMBHMWBU89y+&Cvv1VY=Z4WdSGXQxd8X71zvC1(1J=1?`         ?[ `       `    `       `        `     ,\
MMMMM%      ?R       `    [email protected]              `           `                   .N,    ` .""!?!   ...-??7?7TT9U07"OWkdGI?J3>(<?d6XSd8VOJ1JZ6<<!_! .`             4,                          `         ,}
MMMMM)  `    XR           .N           `                     `       `     (N.                 !!?^!!~:((((+-((VU&O-(zJXV1vT7'.<``.!`                `  `   N,  `     `                        ` ,)
MMMMMb        Wb    `      H_                `         `                    db                      __<<<?<7777zzzzz(z_~~!                                .,7N,    `     `            `          ,)
MMMMMMb       (Wp       `  H~            `                 `      `          H;                          !!~``                          `           `  .(1~.!,N,                          `    ` ,)
.?NMMMMN,     .rTm   `     W{          `      `   `  `  `   `  `    `   ` `  ,N.      `        `  `                           `             `  ` `..(><<<?(<! .M,`    `   `  `       `  `   `    ,)
dp/MMMMMMN,    F 7R.       d]    `  `     `    `                             `db`         `  `                    `   `    `     `   `  `  ` ..JV94zJ7z1Jv~.<_` Wx  `    `     `  `              .[
MMMMMMMMMMM^  .\  ?N. ` `  (b          `    `       `    ` `         `        .N....`           `  `      `  `  `  ` ``  ...  .....((+-..JuwXJZvCiJXv1j<_,<</~`  7p                     `    `   .[
MMMMMMBMM#`   d    /N,    `,N`  `       `    `     `  `     `  `  `     ` `    dbOOwd9&..   `        `    ``_i.(++<?<1+((?1++uggWWVBUWUWYCziJ27zV1JZ43--<<!-/     ?h.  `  `  `      ` `    `   ` .[
@      MM#   .t     ,N,    .M.    `  `    `      `     `  `     `   `  `   `   ,NZv7uJJZYTYQJ...` `    `       (zTT01dXkHHmXAggKQkU9WWmOAdavOdmJzY17-((1!       `  .H,          `                .r
NggJ...dMN.` J:     `.Wp    M|         `      `          `   `              ` ` MK0AAsZUUw+zI~.(XHk&J.... `              _"TTBWWWmkQAgVSV6ZT7iX+i?<J(x(`             4e  `             `  `  `   .]
MMMMMMMNMMm, d;  `     Ub   Jb  `   `     `         `                           JNwWYTC1+zzvCI((+swvZYY"=<?TTT71+(...           _:(?77?=v1C.j?^=j/=~`       `  `     `?h. `  `     `  `        ` .]
M8_TMMMMMMMMNJb         ?h. .N.              `        `  `  `       `  `        .N. .._!`???1ZY7<-.(<+++><+(((++z++<~?=CO+..                             `             .W,                       .]
D7M~(MMMMMMMMFW_`  ` `  `?N.`d[  ` `  `` `` ` ` ` ` `  `  ` ` ` ``  ` `  `` ` `  H}      ?! <?!_??7^!???"7711+((+zz1?=+<(+<?<_..    `         `     ` ` `  ` ` ` `` `  ` 7h.` ` ` ` ` ` `` ``  ` .t
© J.I./Asahi Shimbun Publishing
© J.I./Asahi Shimbun Publishing and Junji Ito "Maniac" Production Committee
*/

pragma solidity ^0.8.4;

import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/token/common/ERC2981.sol";
import "@openzeppelin/contracts/security/ReentrancyGuard.sol";
import "@openzeppelin/contracts/utils/Counters.sol";
import "@openzeppelin/contracts/utils/Base64.sol";
import "erc721a/contracts/ERC721A.sol";

import "@openzeppelin/contracts/utils/Strings.sol";

import {DefaultOperatorFilterer} from "./lib/OpenSea/DefaultOperatorFilterer.sol";

contract Tomie is ERC721A, ERC2981, ReentrancyGuard, Ownable, DefaultOperatorFilterer {
    event Killed(address killer, uint256 killedTokenId, uint256 birthTokenId, uint256 totalKill);

    uint256 firstBorn = 2222;
    uint256 lastPhase = 4;
    // string baseName = "Tomie ";
    string baseName = "LE TEST ";
    string description =
        unicode'Regenerating TOMIE has been airdropped to \\"TOMIE by Junji Ito\\" holders.  \\nYou can enjoy the KILL gimmick at the following website  \\nhttps://nft.ji-anime.com/kill  \\n  \\n© JI Inc./Asahi Shimbun Publications Inc.  \\n© JI/Asahi Shimbun Publications, Junji Ito Maniac Production Committee';

    mapping(uint256 => string[]) private imageURIs;
    mapping(uint256 => string[]) private titles;
    mapping(uint256 => string[]) private colors;

    uint256 private regenerateTime = 2 days;
    mapping(uint256 => uint256) private birthTimes;
    mapping(uint256 => uint256) private killTimes;

    uint256 public killCost = 0.01 ether;
    uint256 public freeKill = 1;

    mapping(address => uint256[]) public killedTokenIds;

    address public royaltyAddress;
    uint96 public royaltyFee = 1000;

    // constructor() ERC721("Tomie", "TOMIE") {
    constructor() ERC721A("LE Test", "LE Test") {
        _setDefaultRoyalty(msg.sender, royaltyFee);
    }

    function _startTokenId() internal pure override returns (uint256) {
        return 1;
    }

    function beginning(address[] memory _enchantedPeople, uint256[] memory numOfTomie)
        external
        onlyOwner
    {
        require(_enchantedPeople.length == numOfTomie.length, "No match Arguments");

        for (uint256 i = 0; i < _enchantedPeople.length; i++) {
            uint256 startTokenid = _nextTokenId();
            require(totalSupply() + numOfTomie[i] <= firstBorn, "No more births.");

            _safeMint(_enchantedPeople[i], numOfTomie[i]);

            for (uint256 j = 0; j < numOfTomie[i]; j++) {
                birthTimes[startTokenid + j] = 1;
            }
        }
    }

    function setMetadata(
        uint256 phase,
        string[] memory newImageURIs,
        string[] memory newTitles,
        string[] memory newColors
    ) external onlyOwner {
        require(
            newImageURIs.length == newTitles.length && newImageURIs.length == newColors.length,
            "Array size mismatch."
        );
        setImageURIs(phase, newImageURIs);
        setTitles(phase, newTitles);
        setColors(phase, newColors);
    }

    function setImageURIs(uint256 phase, string[] memory newImageURIs) public onlyOwner {
        imageURIs[phase] = newImageURIs;
    }

    function setTitles(uint256 phase, string[] memory newTitles) public onlyOwner {
        titles[phase] = newTitles;
    }

    function setColors(uint256 phase, string[] memory newColors) public onlyOwner {
        colors[phase] = newColors;
    }

    function setBaseName(string memory newBaseName) external onlyOwner {
        baseName = newBaseName;
    }

    function setDescription(string memory newDescription) external onlyOwner {
        description = newDescription;
    }

    function setRegenerateTime(uint256 newRegenerateTime) external onlyOwner {
        regenerateTime = newRegenerateTime;
    }

    function setFreeKill(uint256 newFreeKill) external onlyOwner {
        freeKill = newFreeKill;
    }

    function setKillCost(uint256 newKillCost) external onlyOwner {
        killCost = newKillCost;
    }

    // Kill her. But rebirth.
    function kill(uint256 tokenId) external payable nonReentrant {
        require(ownerOf(tokenId) == msg.sender, "I'm not your Tomie, boy.");

        require(
            killedTokenIds[msg.sender].length < freeKill || msg.value == killCost,
            "You need to pay the killing fee."
        );

        // kill
        _burn(tokenId);
        killTimes[tokenId] = block.timestamp;

        killedTokenIds[msg.sender].push(tokenId);
        birthTimes[_nextTokenId()] = block.timestamp;
        _safeMint(msg.sender, 1);

        emit Killed(msg.sender, tokenId, _nextTokenId(), killedTokenIds[msg.sender].length);
    }

    function getOwnTokenIds(address owner) external view returns (uint256[] memory) {
        unchecked {
            uint256 tokenIdsIdx;
            address currOwnershipAddr;
            uint256 tokenIdsLength = balanceOf(owner);
            uint256[] memory tokenIds = new uint256[](tokenIdsLength);
            TokenOwnership memory ownership;
            for (uint256 i = _startTokenId(); tokenIdsIdx != tokenIdsLength; ++i) {
                ownership = _ownershipAt(i);
                if (ownership.burned) {
                    continue;
                }
                if (ownership.addr != address(0)) {
                    currOwnershipAddr = ownership.addr;
                }
                if (currOwnershipAddr == owner) {
                    tokenIds[tokenIdsIdx++] = i;
                }
            }
            return tokenIds;
        }
    }

    function getKilledTokenIds(address walletAddress) external view returns (uint256[] memory) {
        return killedTokenIds[walletAddress];
    }

    function isBorned(uint256 tokenId) private view returns (bool) {
        return _startTokenId() <= tokenId && tokenId < _nextTokenId();
    }

    function getRegenerateLevel(uint256 tokenId) public view returns (uint256) {
        require(isBorned(tokenId), "Not yet Birthed.");

        if (birthTimes[tokenId] == 1) {
            return 0;
        }

        uint256 n = 0;
        if (killTimes[tokenId] == 0) {
            n = (block.timestamp - birthTimes[tokenId]) / regenerateTime + 1;
        } else {
            n = (killTimes[tokenId] - birthTimes[tokenId]) / regenerateTime + 1;
        }

        if (n > lastPhase) n = lastPhase;
        return n;
    }

    function _getImageIndex(uint256 tokenId) private view returns (uint256) {
        uint256 regenerateLevel = getRegenerateLevel(tokenId);
        uint256 imageNum = imageURIs[regenerateLevel].length;
        uint256 random = uint256(
            keccak256(abi.encodePacked(tokenId, birthTimes[tokenId], regenerateLevel))
        );
        uint256 imageIndex = random % imageNum;
        return imageIndex;
    }

    function tokenURI(uint256 tokenId)
        public
        view
        virtual
        override(ERC721A)
        returns (string memory)
    {
        require(isBorned(tokenId), "Not yet Birthed.");

        uint256 level = getRegenerateLevel(tokenId);
        uint256 index = _getImageIndex(tokenId);

        bytes memory attrs = abi.encodePacked(
            '"attributes":[{"trait_type":"regenerateLevel","value":L"',
            Strings.toString(level),
            '"},{"trait_type":"Title","value":"',
            titles[level][index],
            '"},{"trait_type":"Color","value":"',
            colors[level][index],
            '"}]'
        );

        return
            string(
                abi.encodePacked(
                    "data:application/json;base64,",
                    Base64.encode(
                        abi.encodePacked(
                            '{"name":"',
                            baseName,
                            "#",
                            Strings.toString(tokenId),
                            '","description":"',
                            description,
                            '","image":"',
                            imageURIs[level][index],
                            '",',
                            attrs,
                            "}"
                        )
                    )
                )
            );
    }

    //other function
    function setDefaultRoyalty(address receiver, uint96 feeNumerator) external onlyOwner {
        _setDefaultRoyalty(receiver, feeNumerator);
    }

    function withdraw() external onlyOwner {
        require(payable(msg.sender).send(address(this).balance));
    }

    function supportsInterface(bytes4 interfaceId)
        public
        view
        override(ERC721A, ERC2981)
        returns (bool)
    {
        return ERC721A.supportsInterface(interfaceId) || ERC2981.supportsInterface(interfaceId);
    }

    // DefaultOperatorFilterer
    function setApprovalForAll(address operator, bool approved)
        public
        override
        onlyAllowedOperatorApproval(operator)
    {
        super.setApprovalForAll(operator, approved);
    }

    function approve(address operator, uint256 tokenId)
        public
        payable
        override
        onlyAllowedOperatorApproval(operator)
    {
        super.approve(operator, tokenId);
    }

    function transferFrom(
        address from,
        address to,
        uint256 tokenId
    ) public payable override onlyAllowedOperator(from) {
        super.transferFrom(from, to, tokenId);
    }

    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId
    ) public payable override onlyAllowedOperator(from) {
        super.safeTransferFrom(from, to, tokenId);
    }

    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId,
        bytes memory data
    ) public payable override onlyAllowedOperator(from) {
        super.safeTransferFrom(from, to, tokenId, data);
    }
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.13;

import {OperatorFilterer} from "./OperatorFilterer.sol";

/**
 * @title  DefaultOperatorFilterer
 * @notice Inherits from OperatorFilterer and automatically subscribes to the default OpenSea subscription.
 */
abstract contract DefaultOperatorFilterer is OperatorFilterer {
    address constant DEFAULT_SUBSCRIPTION = address(0x3cc6CddA760b79bAfa08dF41ECFA224f810dCeB6);

    constructor() OperatorFilterer(DEFAULT_SUBSCRIPTION, true) {}
}

// SPDX-License-Identifier: MIT
// ERC721A Contracts v4.2.3
// Creator: Chiru Labs

pragma solidity ^0.8.4;

import './IERC721A.sol';

/**
 * @dev Interface of ERC721 token receiver.
 */
interface ERC721A__IERC721Receiver {
    function onERC721Received(
        address operator,
        address from,
        uint256 tokenId,
        bytes calldata data
    ) external returns (bytes4);
}

/**
 * @title ERC721A
 *
 * @dev Implementation of the [ERC721](https://eips.ethereum.org/EIPS/eip-721)
 * Non-Fungible Token Standard, including the Metadata extension.
 * Optimized for lower gas during batch mints.
 *
 * Token IDs are minted in sequential order (e.g. 0, 1, 2, 3, ...)
 * starting from `_startTokenId()`.
 *
 * Assumptions:
 *
 * - An owner cannot have more than 2**64 - 1 (max value of uint64) of supply.
 * - The maximum token ID cannot exceed 2**256 - 1 (max value of uint256).
 */
contract ERC721A is IERC721A {
    // Bypass for a `--via-ir` bug (https://github.com/chiru-labs/ERC721A/pull/364).
    struct TokenApprovalRef {
        address value;
    }

    // =============================================================
    //                           CONSTANTS
    // =============================================================

    // Mask of an entry in packed address data.
    uint256 private constant _BITMASK_ADDRESS_DATA_ENTRY = (1 << 64) - 1;

    // The bit position of `numberMinted` in packed address data.
    uint256 private constant _BITPOS_NUMBER_MINTED = 64;

    // The bit position of `numberBurned` in packed address data.
    uint256 private constant _BITPOS_NUMBER_BURNED = 128;

    // The bit position of `aux` in packed address data.
    uint256 private constant _BITPOS_AUX = 192;

    // Mask of all 256 bits in packed address data except the 64 bits for `aux`.
    uint256 private constant _BITMASK_AUX_COMPLEMENT = (1 << 192) - 1;

    // The bit position of `startTimestamp` in packed ownership.
    uint256 private constant _BITPOS_START_TIMESTAMP = 160;

    // The bit mask of the `burned` bit in packed ownership.
    uint256 private constant _BITMASK_BURNED = 1 << 224;

    // The bit position of the `nextInitialized` bit in packed ownership.
    uint256 private constant _BITPOS_NEXT_INITIALIZED = 225;

    // The bit mask of the `nextInitialized` bit in packed ownership.
    uint256 private constant _BITMASK_NEXT_INITIALIZED = 1 << 225;

    // The bit position of `extraData` in packed ownership.
    uint256 private constant _BITPOS_EXTRA_DATA = 232;

    // Mask of all 256 bits in a packed ownership except the 24 bits for `extraData`.
    uint256 private constant _BITMASK_EXTRA_DATA_COMPLEMENT = (1 << 232) - 1;

    // The mask of the lower 160 bits for addresses.
    uint256 private constant _BITMASK_ADDRESS = (1 << 160) - 1;

    // The maximum `quantity` that can be minted with {_mintERC2309}.
    // This limit is to prevent overflows on the address data entries.
    // For a limit of 5000, a total of 3.689e15 calls to {_mintERC2309}
    // is required to cause an overflow, which is unrealistic.
    uint256 private constant _MAX_MINT_ERC2309_QUANTITY_LIMIT = 5000;

    // The `Transfer` event signature is given by:
    // `keccak256(bytes("Transfer(address,address,uint256)"))`.
    bytes32 private constant _TRANSFER_EVENT_SIGNATURE =
        0xddf252ad1be2c89b69c2b068fc378daa952ba7f163c4a11628f55a4df523b3ef;

    // =============================================================
    //                            STORAGE
    // =============================================================

    // The next token ID to be minted.
    uint256 private _currentIndex;

    // The number of tokens burned.
    uint256 private _burnCounter;

    // Token name
    string private _name;

    // Token symbol
    string private _symbol;

    // Mapping from token ID to ownership details
    // An empty struct value does not necessarily mean the token is unowned.
    // See {_packedOwnershipOf} implementation for details.
    //
    // Bits Layout:
    // - [0..159]   `addr`
    // - [160..223] `startTimestamp`
    // - [224]      `burned`
    // - [225]      `nextInitialized`
    // - [232..255] `extraData`
    mapping(uint256 => uint256) private _packedOwnerships;

    // Mapping owner address to address data.
    //
    // Bits Layout:
    // - [0..63]    `balance`
    // - [64..127]  `numberMinted`
    // - [128..191] `numberBurned`
    // - [192..255] `aux`
    mapping(address => uint256) private _packedAddressData;

    // Mapping from token ID to approved address.
    mapping(uint256 => TokenApprovalRef) private _tokenApprovals;

    // Mapping from owner to operator approvals
    mapping(address => mapping(address => bool)) private _operatorApprovals;

    // =============================================================
    //                          CONSTRUCTOR
    // =============================================================

    constructor(string memory name_, string memory symbol_) {
        _name = name_;
        _symbol = symbol_;
        _currentIndex = _startTokenId();
    }

    // =============================================================
    //                   TOKEN COUNTING OPERATIONS
    // =============================================================

    /**
     * @dev Returns the starting token ID.
     * To change the starting token ID, please override this function.
     */
    function _startTokenId() internal view virtual returns (uint256) {
        return 0;
    }

    /**
     * @dev Returns the next token ID to be minted.
     */
    function _nextTokenId() internal view virtual returns (uint256) {
        return _currentIndex;
    }

    /**
     * @dev Returns the total number of tokens in existence.
     * Burned tokens will reduce the count.
     * To get the total number of tokens minted, please see {_totalMinted}.
     */
    function totalSupply() public view virtual override returns (uint256) {
        // Counter underflow is impossible as _burnCounter cannot be incremented
        // more than `_currentIndex - _startTokenId()` times.
        unchecked {
            return _currentIndex - _burnCounter - _startTokenId();
        }
    }

    /**
     * @dev Returns the total amount of tokens minted in the contract.
     */
    function _totalMinted() internal view virtual returns (uint256) {
        // Counter underflow is impossible as `_currentIndex` does not decrement,
        // and it is initialized to `_startTokenId()`.
        unchecked {
            return _currentIndex - _startTokenId();
        }
    }

    /**
     * @dev Returns the total number of tokens burned.
     */
    function _totalBurned() internal view virtual returns (uint256) {
        return _burnCounter;
    }

    // =============================================================
    //                    ADDRESS DATA OPERATIONS
    // =============================================================

    /**
     * @dev Returns the number of tokens in `owner`'s account.
     */
    function balanceOf(address owner) public view virtual override returns (uint256) {
        if (owner == address(0)) revert BalanceQueryForZeroAddress();
        return _packedAddressData[owner] & _BITMASK_ADDRESS_DATA_ENTRY;
    }

    /**
     * Returns the number of tokens minted by `owner`.
     */
    function _numberMinted(address owner) internal view returns (uint256) {
        return (_packedAddressData[owner] >> _BITPOS_NUMBER_MINTED) & _BITMASK_ADDRESS_DATA_ENTRY;
    }

    /**
     * Returns the number of tokens burned by or on behalf of `owner`.
     */
    function _numberBurned(address owner) internal view returns (uint256) {
        return (_packedAddressData[owner] >> _BITPOS_NUMBER_BURNED) & _BITMASK_ADDRESS_DATA_ENTRY;
    }

    /**
     * Returns the auxiliary data for `owner`. (e.g. number of whitelist mint slots used).
     */
    function _getAux(address owner) internal view returns (uint64) {
        return uint64(_packedAddressData[owner] >> _BITPOS_AUX);
    }

    /**
     * Sets the auxiliary data for `owner`. (e.g. number of whitelist mint slots used).
     * If there are multiple variables, please pack them into a uint64.
     */
    function _setAux(address owner, uint64 aux) internal virtual {
        uint256 packed = _packedAddressData[owner];
        uint256 auxCasted;
        // Cast `aux` with assembly to avoid redundant masking.
        assembly {
            auxCasted := aux
        }
        packed = (packed & _BITMASK_AUX_COMPLEMENT) | (auxCasted << _BITPOS_AUX);
        _packedAddressData[owner] = packed;
    }

    // =============================================================
    //                            IERC165
    // =============================================================

    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * [EIP section](https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified)
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30000 gas.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        // The interface IDs are constants representing the first 4 bytes
        // of the XOR of all function selectors in the interface.
        // See: [ERC165](https://eips.ethereum.org/EIPS/eip-165)
        // (e.g. `bytes4(i.functionA.selector ^ i.functionB.selector ^ ...)`)
        return
            interfaceId == 0x01ffc9a7 || // ERC165 interface ID for ERC165.
            interfaceId == 0x80ac58cd || // ERC165 interface ID for ERC721.
            interfaceId == 0x5b5e139f; // ERC165 interface ID for ERC721Metadata.
    }

    // =============================================================
    //                        IERC721Metadata
    // =============================================================

    /**
     * @dev Returns the token collection name.
     */
    function name() public view virtual override returns (string memory) {
        return _name;
    }

    /**
     * @dev Returns the token collection symbol.
     */
    function symbol() public view virtual override returns (string memory) {
        return _symbol;
    }

    /**
     * @dev Returns the Uniform Resource Identifier (URI) for `tokenId` token.
     */
    function tokenURI(uint256 tokenId) public view virtual override returns (string memory) {
        if (!_exists(tokenId)) revert URIQueryForNonexistentToken();

        string memory baseURI = _baseURI();
        return bytes(baseURI).length != 0 ? string(abi.encodePacked(baseURI, _toString(tokenId))) : '';
    }

    /**
     * @dev Base URI for computing {tokenURI}. If set, the resulting URI for each
     * token will be the concatenation of the `baseURI` and the `tokenId`. Empty
     * by default, it can be overridden in child contracts.
     */
    function _baseURI() internal view virtual returns (string memory) {
        return '';
    }

    // =============================================================
    //                     OWNERSHIPS OPERATIONS
    // =============================================================

    /**
     * @dev Returns the owner of the `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function ownerOf(uint256 tokenId) public view virtual override returns (address) {
        return address(uint160(_packedOwnershipOf(tokenId)));
    }

    /**
     * @dev Gas spent here starts off proportional to the maximum mint batch size.
     * It gradually moves to O(1) as tokens get transferred around over time.
     */
    function _ownershipOf(uint256 tokenId) internal view virtual returns (TokenOwnership memory) {
        return _unpackedOwnership(_packedOwnershipOf(tokenId));
    }

    /**
     * @dev Returns the unpacked `TokenOwnership` struct at `index`.
     */
    function _ownershipAt(uint256 index) internal view virtual returns (TokenOwnership memory) {
        return _unpackedOwnership(_packedOwnerships[index]);
    }

    /**
     * @dev Initializes the ownership slot minted at `index` for efficiency purposes.
     */
    function _initializeOwnershipAt(uint256 index) internal virtual {
        if (_packedOwnerships[index] == 0) {
            _packedOwnerships[index] = _packedOwnershipOf(index);
        }
    }

    /**
     * Returns the packed ownership data of `tokenId`.
     */
    function _packedOwnershipOf(uint256 tokenId) private view returns (uint256) {
        uint256 curr = tokenId;

        unchecked {
            if (_startTokenId() <= curr)
                if (curr < _currentIndex) {
                    uint256 packed = _packedOwnerships[curr];
                    // If not burned.
                    if (packed & _BITMASK_BURNED == 0) {
                        // Invariant:
                        // There will always be an initialized ownership slot
                        // (i.e. `ownership.addr != address(0) && ownership.burned == false`)
                        // before an unintialized ownership slot
                        // (i.e. `ownership.addr == address(0) && ownership.burned == false`)
                        // Hence, `curr` will not underflow.
                        //
                        // We can directly compare the packed value.
                        // If the address is zero, packed will be zero.
                        while (packed == 0) {
                            packed = _packedOwnerships[--curr];
                        }
                        return packed;
                    }
                }
        }
        revert OwnerQueryForNonexistentToken();
    }

    /**
     * @dev Returns the unpacked `TokenOwnership` struct from `packed`.
     */
    function _unpackedOwnership(uint256 packed) private pure returns (TokenOwnership memory ownership) {
        ownership.addr = address(uint160(packed));
        ownership.startTimestamp = uint64(packed >> _BITPOS_START_TIMESTAMP);
        ownership.burned = packed & _BITMASK_BURNED != 0;
        ownership.extraData = uint24(packed >> _BITPOS_EXTRA_DATA);
    }

    /**
     * @dev Packs ownership data into a single uint256.
     */
    function _packOwnershipData(address owner, uint256 flags) private view returns (uint256 result) {
        assembly {
            // Mask `owner` to the lower 160 bits, in case the upper bits somehow aren't clean.
            owner := and(owner, _BITMASK_ADDRESS)
            // `owner | (block.timestamp << _BITPOS_START_TIMESTAMP) | flags`.
            result := or(owner, or(shl(_BITPOS_START_TIMESTAMP, timestamp()), flags))
        }
    }

    /**
     * @dev Returns the `nextInitialized` flag set if `quantity` equals 1.
     */
    function _nextInitializedFlag(uint256 quantity) private pure returns (uint256 result) {
        // For branchless setting of the `nextInitialized` flag.
        assembly {
            // `(quantity == 1) << _BITPOS_NEXT_INITIALIZED`.
            result := shl(_BITPOS_NEXT_INITIALIZED, eq(quantity, 1))
        }
    }

    // =============================================================
    //                      APPROVAL OPERATIONS
    // =============================================================

    /**
     * @dev Gives permission to `to` to transfer `tokenId` token to another account.
     * The approval is cleared when the token is transferred.
     *
     * Only a single account can be approved at a time, so approving the
     * zero address clears previous approvals.
     *
     * Requirements:
     *
     * - The caller must own the token or be an approved operator.
     * - `tokenId` must exist.
     *
     * Emits an {Approval} event.
     */
    function approve(address to, uint256 tokenId) public payable virtual override {
        address owner = ownerOf(tokenId);

        if (_msgSenderERC721A() != owner)
            if (!isApprovedForAll(owner, _msgSenderERC721A())) {
                revert ApprovalCallerNotOwnerNorApproved();
            }

        _tokenApprovals[tokenId].value = to;
        emit Approval(owner, to, tokenId);
    }

    /**
     * @dev Returns the account approved for `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function getApproved(uint256 tokenId) public view virtual override returns (address) {
        if (!_exists(tokenId)) revert ApprovalQueryForNonexistentToken();

        return _tokenApprovals[tokenId].value;
    }

    /**
     * @dev Approve or remove `operator` as an operator for the caller.
     * Operators can call {transferFrom} or {safeTransferFrom}
     * for any token owned by the caller.
     *
     * Requirements:
     *
     * - The `operator` cannot be the caller.
     *
     * Emits an {ApprovalForAll} event.
     */
    function setApprovalForAll(address operator, bool approved) public virtual override {
        _operatorApprovals[_msgSenderERC721A()][operator] = approved;
        emit ApprovalForAll(_msgSenderERC721A(), operator, approved);
    }

    /**
     * @dev Returns if the `operator` is allowed to manage all of the assets of `owner`.
     *
     * See {setApprovalForAll}.
     */
    function isApprovedForAll(address owner, address operator) public view virtual override returns (bool) {
        return _operatorApprovals[owner][operator];
    }

    /**
     * @dev Returns whether `tokenId` exists.
     *
     * Tokens can be managed by their owner or approved accounts via {approve} or {setApprovalForAll}.
     *
     * Tokens start existing when they are minted. See {_mint}.
     */
    function _exists(uint256 tokenId) internal view virtual returns (bool) {
        return
            _startTokenId() <= tokenId &&
            tokenId < _currentIndex && // If within bounds,
            _packedOwnerships[tokenId] & _BITMASK_BURNED == 0; // and not burned.
    }

    /**
     * @dev Returns whether `msgSender` is equal to `approvedAddress` or `owner`.
     */
    function _isSenderApprovedOrOwner(
        address approvedAddress,
        address owner,
        address msgSender
    ) private pure returns (bool result) {
        assembly {
            // Mask `owner` to the lower 160 bits, in case the upper bits somehow aren't clean.
            owner := and(owner, _BITMASK_ADDRESS)
            // Mask `msgSender` to the lower 160 bits, in case the upper bits somehow aren't clean.
            msgSender := and(msgSender, _BITMASK_ADDRESS)
            // `msgSender == owner || msgSender == approvedAddress`.
            result := or(eq(msgSender, owner), eq(msgSender, approvedAddress))
        }
    }

    /**
     * @dev Returns the storage slot and value for the approved address of `tokenId`.
     */
    function _getApprovedSlotAndAddress(uint256 tokenId)
        private
        view
        returns (uint256 approvedAddressSlot, address approvedAddress)
    {
        TokenApprovalRef storage tokenApproval = _tokenApprovals[tokenId];
        // The following is equivalent to `approvedAddress = _tokenApprovals[tokenId].value`.
        assembly {
            approvedAddressSlot := tokenApproval.slot
            approvedAddress := sload(approvedAddressSlot)
        }
    }

    // =============================================================
    //                      TRANSFER OPERATIONS
    // =============================================================

    /**
     * @dev Transfers `tokenId` from `from` to `to`.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must be owned by `from`.
     * - If the caller is not `from`, it must be approved to move this token
     * by either {approve} or {setApprovalForAll}.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address from,
        address to,
        uint256 tokenId
    ) public payable virtual override {
        uint256 prevOwnershipPacked = _packedOwnershipOf(tokenId);

        if (address(uint160(prevOwnershipPacked)) != from) revert TransferFromIncorrectOwner();

        (uint256 approvedAddressSlot, address approvedAddress) = _getApprovedSlotAndAddress(tokenId);

        // The nested ifs save around 20+ gas over a compound boolean condition.
        if (!_isSenderApprovedOrOwner(approvedAddress, from, _msgSenderERC721A()))
            if (!isApprovedForAll(from, _msgSenderERC721A())) revert TransferCallerNotOwnerNorApproved();

        if (to == address(0)) revert TransferToZeroAddress();

        _beforeTokenTransfers(from, to, tokenId, 1);

        // Clear approvals from the previous owner.
        assembly {
            if approvedAddress {
                // This is equivalent to `delete _tokenApprovals[tokenId]`.
                sstore(approvedAddressSlot, 0)
            }
        }

        // Underflow of the sender's balance is impossible because we check for
        // ownership above and the recipient's balance can't realistically overflow.
        // Counter overflow is incredibly unrealistic as `tokenId` would have to be 2**256.
        unchecked {
            // We can directly increment and decrement the balances.
            --_packedAddressData[from]; // Updates: `balance -= 1`.
            ++_packedAddressData[to]; // Updates: `balance += 1`.

            // Updates:
            // - `address` to the next owner.
            // - `startTimestamp` to the timestamp of transfering.
            // - `burned` to `false`.
            // - `nextInitialized` to `true`.
            _packedOwnerships[tokenId] = _packOwnershipData(
                to,
                _BITMASK_NEXT_INITIALIZED | _nextExtraData(from, to, prevOwnershipPacked)
            );

            // If the next slot may not have been initialized (i.e. `nextInitialized == false`) .
            if (prevOwnershipPacked & _BITMASK_NEXT_INITIALIZED == 0) {
                uint256 nextTokenId = tokenId + 1;
                // If the next slot's address is zero and not burned (i.e. packed value is zero).
                if (_packedOwnerships[nextTokenId] == 0) {
                    // If the next slot is within bounds.
                    if (nextTokenId != _currentIndex) {
                        // Initialize the next slot to maintain correctness for `ownerOf(tokenId + 1)`.
                        _packedOwnerships[nextTokenId] = prevOwnershipPacked;
                    }
                }
            }
        }

        emit Transfer(from, to, tokenId);
        _afterTokenTransfers(from, to, tokenId, 1);
    }

    /**
     * @dev Equivalent to `safeTransferFrom(from, to, tokenId, '')`.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId
    ) public payable virtual override {
        safeTransferFrom(from, to, tokenId, '');
    }

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If the caller is not `from`, it must be approved to move this token
     * by either {approve} or {setApprovalForAll}.
     * - If `to` refers to a smart contract, it must implement
     * {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId,
        bytes memory _data
    ) public payable virtual override {
        transferFrom(from, to, tokenId);
        if (to.code.length != 0)
            if (!_checkContractOnERC721Received(from, to, tokenId, _data)) {
                revert TransferToNonERC721ReceiverImplementer();
            }
    }

    /**
     * @dev Hook that is called before a set of serially-ordered token IDs
     * are about to be transferred. This includes minting.
     * And also called before burning one token.
     *
     * `startTokenId` - the first token ID to be transferred.
     * `quantity` - the amount to be transferred.
     *
     * Calling conditions:
     *
     * - When `from` and `to` are both non-zero, `from`'s `tokenId` will be
     * transferred to `to`.
     * - When `from` is zero, `tokenId` will be minted for `to`.
     * - When `to` is zero, `tokenId` will be burned by `from`.
     * - `from` and `to` are never both zero.
     */
    function _beforeTokenTransfers(
        address from,
        address to,
        uint256 startTokenId,
        uint256 quantity
    ) internal virtual {}

    /**
     * @dev Hook that is called after a set of serially-ordered token IDs
     * have been transferred. This includes minting.
     * And also called after one token has been burned.
     *
     * `startTokenId` - the first token ID to be transferred.
     * `quantity` - the amount to be transferred.
     *
     * Calling conditions:
     *
     * - When `from` and `to` are both non-zero, `from`'s `tokenId` has been
     * transferred to `to`.
     * - When `from` is zero, `tokenId` has been minted for `to`.
     * - When `to` is zero, `tokenId` has been burned by `from`.
     * - `from` and `to` are never both zero.
     */
    function _afterTokenTransfers(
        address from,
        address to,
        uint256 startTokenId,
        uint256 quantity
    ) internal virtual {}

    /**
     * @dev Private function to invoke {IERC721Receiver-onERC721Received} on a target contract.
     *
     * `from` - Previous owner of the given token ID.
     * `to` - Target address that will receive the token.
     * `tokenId` - Token ID to be transferred.
     * `_data` - Optional data to send along with the call.
     *
     * Returns whether the call correctly returned the expected magic value.
     */
    function _checkContractOnERC721Received(
        address from,
        address to,
        uint256 tokenId,
        bytes memory _data
    ) private returns (bool) {
        try ERC721A__IERC721Receiver(to).onERC721Received(_msgSenderERC721A(), from, tokenId, _data) returns (
            bytes4 retval
        ) {
            return retval == ERC721A__IERC721Receiver(to).onERC721Received.selector;
        } catch (bytes memory reason) {
            if (reason.length == 0) {
                revert TransferToNonERC721ReceiverImplementer();
            } else {
                assembly {
                    revert(add(32, reason), mload(reason))
                }
            }
        }
    }

    // =============================================================
    //                        MINT OPERATIONS
    // =============================================================

    /**
     * @dev Mints `quantity` tokens and transfers them to `to`.
     *
     * Requirements:
     *
     * - `to` cannot be the zero address.
     * - `quantity` must be greater than 0.
     *
     * Emits a {Transfer} event for each mint.
     */
    function _mint(address to, uint256 quantity) internal virtual {
        uint256 startTokenId = _currentIndex;
        if (quantity == 0) revert MintZeroQuantity();

        _beforeTokenTransfers(address(0), to, startTokenId, quantity);

        // Overflows are incredibly unrealistic.
        // `balance` and `numberMinted` have a maximum limit of 2**64.
        // `tokenId` has a maximum limit of 2**256.
        unchecked {
            // Updates:
            // - `balance += quantity`.
            // - `numberMinted += quantity`.
            //
            // We can directly add to the `balance` and `numberMinted`.
            _packedAddressData[to] += quantity * ((1 << _BITPOS_NUMBER_MINTED) | 1);

            // Updates:
            // - `address` to the owner.
            // - `startTimestamp` to the timestamp of minting.
            // - `burned` to `false`.
            // - `nextInitialized` to `quantity == 1`.
            _packedOwnerships[startTokenId] = _packOwnershipData(
                to,
                _nextInitializedFlag(quantity) | _nextExtraData(address(0), to, 0)
            );

            uint256 toMasked;
            uint256 end = startTokenId + quantity;

            // Use assembly to loop and emit the `Transfer` event for gas savings.
            // The duplicated `log4` removes an extra check and reduces stack juggling.
            // The assembly, together with the surrounding Solidity code, have been
            // delicately arranged to nudge the compiler into producing optimized opcodes.
            assembly {
                // Mask `to` to the lower 160 bits, in case the upper bits somehow aren't clean.
                toMasked := and(to, _BITMASK_ADDRESS)
                // Emit the `Transfer` event.
                log4(
                    0, // Start of data (0, since no data).
                    0, // End of data (0, since no data).
                    _TRANSFER_EVENT_SIGNATURE, // Signature.
                    0, // `address(0)`.
                    toMasked, // `to`.
                    startTokenId // `tokenId`.
                )

                // The `iszero(eq(,))` check ensures that large values of `quantity`
                // that overflows uint256 will make the loop run out of gas.
                // The compiler will optimize the `iszero` away for performance.
                for {
                    let tokenId := add(startTokenId, 1)
                } iszero(eq(tokenId, end)) {
                    tokenId := add(tokenId, 1)
                } {
                    // Emit the `Transfer` event. Similar to above.
                    log4(0, 0, _TRANSFER_EVENT_SIGNATURE, 0, toMasked, tokenId)
                }
            }
            if (toMasked == 0) revert MintToZeroAddress();

            _currentIndex = end;
        }
        _afterTokenTransfers(address(0), to, startTokenId, quantity);
    }

    /**
     * @dev Mints `quantity` tokens and transfers them to `to`.
     *
     * This function is intended for efficient minting only during contract creation.
     *
     * It emits only one {ConsecutiveTransfer} as defined in
     * [ERC2309](https://eips.ethereum.org/EIPS/eip-2309),
     * instead of a sequence of {Transfer} event(s).
     *
     * Calling this function outside of contract creation WILL make your contract
     * non-compliant with the ERC721 standard.
     * For full ERC721 compliance, substituting ERC721 {Transfer} event(s) with the ERC2309
     * {ConsecutiveTransfer} event is only permissible during contract creation.
     *
     * Requirements:
     *
     * - `to` cannot be the zero address.
     * - `quantity` must be greater than 0.
     *
     * Emits a {ConsecutiveTransfer} event.
     */
    function _mintERC2309(address to, uint256 quantity) internal virtual {
        uint256 startTokenId = _currentIndex;
        if (to == address(0)) revert MintToZeroAddress();
        if (quantity == 0) revert MintZeroQuantity();
        if (quantity > _MAX_MINT_ERC2309_QUANTITY_LIMIT) revert MintERC2309QuantityExceedsLimit();

        _beforeTokenTransfers(address(0), to, startTokenId, quantity);

        // Overflows are unrealistic due to the above check for `quantity` to be below the limit.
        unchecked {
            // Updates:
            // - `balance += quantity`.
            // - `numberMinted += quantity`.
            //
            // We can directly add to the `balance` and `numberMinted`.
            _packedAddressData[to] += quantity * ((1 << _BITPOS_NUMBER_MINTED) | 1);

            // Updates:
            // - `address` to the owner.
            // - `startTimestamp` to the timestamp of minting.
            // - `burned` to `false`.
            // - `nextInitialized` to `quantity == 1`.
            _packedOwnerships[startTokenId] = _packOwnershipData(
                to,
                _nextInitializedFlag(quantity) | _nextExtraData(address(0), to, 0)
            );

            emit ConsecutiveTransfer(startTokenId, startTokenId + quantity - 1, address(0), to);

            _currentIndex = startTokenId + quantity;
        }
        _afterTokenTransfers(address(0), to, startTokenId, quantity);
    }

    /**
     * @dev Safely mints `quantity` tokens and transfers them to `to`.
     *
     * Requirements:
     *
     * - If `to` refers to a smart contract, it must implement
     * {IERC721Receiver-onERC721Received}, which is called for each safe transfer.
     * - `quantity` must be greater than 0.
     *
     * See {_mint}.
     *
     * Emits a {Transfer} event for each mint.
     */
    function _safeMint(
        address to,
        uint256 quantity,
        bytes memory _data
    ) internal virtual {
        _mint(to, quantity);

        unchecked {
            if (to.code.length != 0) {
                uint256 end = _currentIndex;
                uint256 index = end - quantity;
                do {
                    if (!_checkContractOnERC721Received(address(0), to, index++, _data)) {
                        revert TransferToNonERC721ReceiverImplementer();
                    }
                } while (index < end);
                // Reentrancy protection.
                if (_currentIndex != end) revert();
            }
        }
    }

    /**
     * @dev Equivalent to `_safeMint(to, quantity, '')`.
     */
    function _safeMint(address to, uint256 quantity) internal virtual {
        _safeMint(to, quantity, '');
    }

    // =============================================================
    //                        BURN OPERATIONS
    // =============================================================

    /**
     * @dev Equivalent to `_burn(tokenId, false)`.
     */
    function _burn(uint256 tokenId) internal virtual {
        _burn(tokenId, false);
    }

    /**
     * @dev Destroys `tokenId`.
     * The approval is cleared when the token is burned.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     *
     * Emits a {Transfer} event.
     */
    function _burn(uint256 tokenId, bool approvalCheck) internal virtual {
        uint256 prevOwnershipPacked = _packedOwnershipOf(tokenId);

        address from = address(uint160(prevOwnershipPacked));

        (uint256 approvedAddressSlot, address approvedAddress) = _getApprovedSlotAndAddress(tokenId);

        if (approvalCheck) {
            // The nested ifs save around 20+ gas over a compound boolean condition.
            if (!_isSenderApprovedOrOwner(approvedAddress, from, _msgSenderERC721A()))
                if (!isApprovedForAll(from, _msgSenderERC721A())) revert TransferCallerNotOwnerNorApproved();
        }

        _beforeTokenTransfers(from, address(0), tokenId, 1);

        // Clear approvals from the previous owner.
        assembly {
            if approvedAddress {
                // This is equivalent to `delete _tokenApprovals[tokenId]`.
                sstore(approvedAddressSlot, 0)
            }
        }

        // Underflow of the sender's balance is impossible because we check for
        // ownership above and the recipient's balance can't realistically overflow.
        // Counter overflow is incredibly unrealistic as `tokenId` would have to be 2**256.
        unchecked {
            // Updates:
            // - `balance -= 1`.
            // - `numberBurned += 1`.
            //
            // We can directly decrement the balance, and increment the number burned.
            // This is equivalent to `packed -= 1; packed += 1 << _BITPOS_NUMBER_BURNED;`.
            _packedAddressData[from] += (1 << _BITPOS_NUMBER_BURNED) - 1;

            // Updates:
            // - `address` to the last owner.
            // - `startTimestamp` to the timestamp of burning.
            // - `burned` to `true`.
            // - `nextInitialized` to `true`.
            _packedOwnerships[tokenId] = _packOwnershipData(
                from,
                (_BITMASK_BURNED | _BITMASK_NEXT_INITIALIZED) | _nextExtraData(from, address(0), prevOwnershipPacked)
            );

            // If the next slot may not have been initialized (i.e. `nextInitialized == false`) .
            if (prevOwnershipPacked & _BITMASK_NEXT_INITIALIZED == 0) {
                uint256 nextTokenId = tokenId + 1;
                // If the next slot's address is zero and not burned (i.e. packed value is zero).
                if (_packedOwnerships[nextTokenId] == 0) {
                    // If the next slot is within bounds.
                    if (nextTokenId != _currentIndex) {
                        // Initialize the next slot to maintain correctness for `ownerOf(tokenId + 1)`.
                        _packedOwnerships[nextTokenId] = prevOwnershipPacked;
                    }
                }
            }
        }

        emit Transfer(from, address(0), tokenId);
        _afterTokenTransfers(from, address(0), tokenId, 1);

        // Overflow not possible, as _burnCounter cannot be exceed _currentIndex times.
        unchecked {
            _burnCounter++;
        }
    }

    // =============================================================
    //                     EXTRA DATA OPERATIONS
    // =============================================================

    /**
     * @dev Directly sets the extra data for the ownership data `index`.
     */
    function _setExtraDataAt(uint256 index, uint24 extraData) internal virtual {
        uint256 packed = _packedOwnerships[index];
        if (packed == 0) revert OwnershipNotInitializedForExtraData();
        uint256 extraDataCasted;
        // Cast `extraData` with assembly to avoid redundant masking.
        assembly {
            extraDataCasted := extraData
        }
        packed = (packed & _BITMASK_EXTRA_DATA_COMPLEMENT) | (extraDataCasted << _BITPOS_EXTRA_DATA);
        _packedOwnerships[index] = packed;
    }

    /**
     * @dev Called during each token transfer to set the 24bit `extraData` field.
     * Intended to be overridden by the cosumer contract.
     *
     * `previousExtraData` - the value of `extraData` before transfer.
     *
     * Calling conditions:
     *
     * - When `from` and `to` are both non-zero, `from`'s `tokenId` will be
     * transferred to `to`.
     * - When `from` is zero, `tokenId` will be minted for `to`.
     * - When `to` is zero, `tokenId` will be burned by `from`.
     * - `from` and `to` are never both zero.
     */
    function _extraData(
        address from,
        address to,
        uint24 previousExtraData
    ) internal view virtual returns (uint24) {}

    /**
     * @dev Returns the next extra data for the packed ownership data.
     * The returned result is shifted into position.
     */
    function _nextExtraData(
        address from,
        address to,
        uint256 prevOwnershipPacked
    ) private view returns (uint256) {
        uint24 extraData = uint24(prevOwnershipPacked >> _BITPOS_EXTRA_DATA);
        return uint256(_extraData(from, to, extraData)) << _BITPOS_EXTRA_DATA;
    }

    // =============================================================
    //                       OTHER OPERATIONS
    // =============================================================

    /**
     * @dev Returns the message sender (defaults to `msg.sender`).
     *
     * If you are writing GSN compatible contracts, you need to override this function.
     */
    function _msgSenderERC721A() internal view virtual returns (address) {
        return msg.sender;
    }

    /**
     * @dev Converts a uint256 to its ASCII string decimal representation.
     */
    function _toString(uint256 value) internal pure virtual returns (string memory str) {
        assembly {
            // The maximum value of a uint256 contains 78 digits (1 byte per digit), but
            // we allocate 0xa0 bytes to keep the free memory pointer 32-byte word aligned.
            // We will need 1 word for the trailing zeros padding, 1 word for the length,
            // and 3 words for a maximum of 78 digits. Total: 5 * 0x20 = 0xa0.
            let m := add(mload(0x40), 0xa0)
            // Update the free memory pointer to allocate.
            mstore(0x40, m)
            // Assign the `str` to the end.
            str := sub(m, 0x20)
            // Zeroize the slot after the string.
            mstore(str, 0)

            // Cache the end of the memory to calculate the length later.
            let end := str

            // We write the string from rightmost digit to leftmost digit.
            // The following is essentially a do-while loop that also handles the zero case.
            // prettier-ignore
            for { let temp := value } 1 {} {
                str := sub(str, 1)
                // Write the character to the pointer.
                // The ASCII index of the '0' character is 48.
                mstore8(str, add(48, mod(temp, 10)))
                // Keep dividing `temp` until zero.
                temp := div(temp, 10)
                // prettier-ignore
                if iszero(temp) { break }
            }

            let length := sub(end, str)
            // Move the pointer 32 bytes leftwards to make room for the length.
            str := sub(str, 0x20)
            // Store the length.
            mstore(str, length)
        }
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (access/Ownable.sol)

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        _checkOwner();
        _;
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if the sender is not the owner.
     */
    function _checkOwner() internal view virtual {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (security/ReentrancyGuard.sol)

pragma solidity ^0.8.0;

/**
 * @dev Contract module that helps prevent reentrant calls to a function.
 *
 * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier
 * available, which can be applied to functions to make sure there are no nested
 * (reentrant) calls to them.
 *
 * Note that because there is a single `nonReentrant` guard, functions marked as
 * `nonReentrant` may not call one another. This can be worked around by making
 * those functions `private`, and then adding `external` `nonReentrant` entry
 * points to them.
 *
 * TIP: If you would like to learn more about reentrancy and alternative ways
 * to protect against it, check out our blog post
 * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].
 */
abstract contract ReentrancyGuard {
    // Booleans are more expensive than uint256 or any type that takes up a full
    // word because each write operation emits an extra SLOAD to first read the
    // slot's contents, replace the bits taken up by the boolean, and then write
    // back. This is the compiler's defense against contract upgrades and
    // pointer aliasing, and it cannot be disabled.

    // The values being non-zero value makes deployment a bit more expensive,
    // but in exchange the refund on every call to nonReentrant will be lower in
    // amount. Since refunds are capped to a percentage of the total
    // transaction's gas, it is best to keep them low in cases like this one, to
    // increase the likelihood of the full refund coming into effect.
    uint256 private constant _NOT_ENTERED = 1;
    uint256 private constant _ENTERED = 2;

    uint256 private _status;

    constructor() {
        _status = _NOT_ENTERED;
    }

    /**
     * @dev Prevents a contract from calling itself, directly or indirectly.
     * Calling a `nonReentrant` function from another `nonReentrant`
     * function is not supported. It is possible to prevent this from happening
     * by making the `nonReentrant` function external, and making it call a
     * `private` function that does the actual work.
     */
    modifier nonReentrant() {
        // On the first call to nonReentrant, _notEntered will be true
        require(_status != _ENTERED, "ReentrancyGuard: reentrant call");

        // Any calls to nonReentrant after this point will fail
        _status = _ENTERED;

        _;

        // By storing the original value once again, a refund is triggered (see
        // https://eips.ethereum.org/EIPS/eip-2200)
        _status = _NOT_ENTERED;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Counters.sol)

pragma solidity ^0.8.0;

/**
 * @title Counters
 * @author Matt Condon (@shrugs)
 * @dev Provides counters that can only be incremented, decremented or reset. This can be used e.g. to track the number
 * of elements in a mapping, issuing ERC721 ids, or counting request ids.
 *
 * Include with `using Counters for Counters.Counter;`
 */
library Counters {
    struct Counter {
        // This variable should never be directly accessed by users of the library: interactions must be restricted to
        // the library's function. As of Solidity v0.5.2, this cannot be enforced, though there is a proposal to add
        // this feature: see https://github.com/ethereum/solidity/issues/4637
        uint256 _value; // default: 0
    }

    function current(Counter storage counter) internal view returns (uint256) {
        return counter._value;
    }

    function increment(Counter storage counter) internal {
        unchecked {
            counter._value += 1;
        }
    }

    function decrement(Counter storage counter) internal {
        uint256 value = counter._value;
        require(value > 0, "Counter: decrement overflow");
        unchecked {
            counter._value = value - 1;
        }
    }

    function reset(Counter storage counter) internal {
        counter._value = 0;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (utils/Base64.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides a set of functions to operate with Base64 strings.
 *
 * _Available since v4.5._
 */
library Base64 {
    /**
     * @dev Base64 Encoding/Decoding Table
     */
    string internal constant _TABLE = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz0123456789+/";

    /**
     * @dev Converts a `bytes` to its Bytes64 `string` representation.
     */
    function encode(bytes memory data) internal pure returns (string memory) {
        /**
         * Inspired by Brecht Devos (Brechtpd) implementation - MIT licence
         * https://github.com/Brechtpd/base64/blob/e78d9fd951e7b0977ddca77d92dc85183770daf4/base64.sol
         */
        if (data.length == 0) return "";

        // Loads the table into memory
        string memory table = _TABLE;

        // Encoding takes 3 bytes chunks of binary data from `bytes` data parameter
        // and split into 4 numbers of 6 bits.
        // The final Base64 length should be `bytes` data length multiplied by 4/3 rounded up
        // - `data.length + 2`  -> Round up
        // - `/ 3`              -> Number of 3-bytes chunks
        // - `4 *`              -> 4 characters for each chunk
        string memory result = new string(4 * ((data.length + 2) / 3));

        /// @solidity memory-safe-assembly
        assembly {
            // Prepare the lookup table (skip the first "length" byte)
            let tablePtr := add(table, 1)

            // Prepare result pointer, jump over length
            let resultPtr := add(result, 32)

            // Run over the input, 3 bytes at a time
            for {
                let dataPtr := data
                let endPtr := add(data, mload(data))
            } lt(dataPtr, endPtr) {

            } {
                // Advance 3 bytes
                dataPtr := add(dataPtr, 3)
                let input := mload(dataPtr)

                // To write each character, shift the 3 bytes (18 bits) chunk
                // 4 times in blocks of 6 bits for each character (18, 12, 6, 0)
                // and apply logical AND with 0x3F which is the number of
                // the previous character in the ASCII table prior to the Base64 Table
                // The result is then added to the table to get the character to write,
                // and finally write it in the result pointer but with a left shift
                // of 256 (1 byte) - 8 (1 ASCII char) = 248 bits

                mstore8(resultPtr, mload(add(tablePtr, and(shr(18, input), 0x3F))))
                resultPtr := add(resultPtr, 1) // Advance

                mstore8(resultPtr, mload(add(tablePtr, and(shr(12, input), 0x3F))))
                resultPtr := add(resultPtr, 1) // Advance

                mstore8(resultPtr, mload(add(tablePtr, and(shr(6, input), 0x3F))))
                resultPtr := add(resultPtr, 1) // Advance

                mstore8(resultPtr, mload(add(tablePtr, and(input, 0x3F))))
                resultPtr := add(resultPtr, 1) // Advance
            }

            // When data `bytes` is not exactly 3 bytes long
            // it is padded with `=` characters at the end
            switch mod(mload(data), 3)
            case 1 {
                mstore8(sub(resultPtr, 1), 0x3d)
                mstore8(sub(resultPtr, 2), 0x3d)
            }
            case 2 {
                mstore8(sub(resultPtr, 1), 0x3d)
            }
        }

        return result;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (utils/Strings.sol)

pragma solidity ^0.8.0;

/**
 * @dev String operations.
 */
library Strings {
    bytes16 private constant _HEX_SYMBOLS = "0123456789abcdef";
    uint8 private constant _ADDRESS_LENGTH = 20;

    /**
     * @dev Converts a `uint256` to its ASCII `string` decimal representation.
     */
    function toString(uint256 value) internal pure returns (string memory) {
        // Inspired by OraclizeAPI's implementation - MIT licence
        // https://github.com/oraclize/ethereum-api/blob/b42146b063c7d6ee1358846c198246239e9360e8/oraclizeAPI_0.4.25.sol

        if (value == 0) {
            return "0";
        }
        uint256 temp = value;
        uint256 digits;
        while (temp != 0) {
            digits++;
            temp /= 10;
        }
        bytes memory buffer = new bytes(digits);
        while (value != 0) {
            digits -= 1;
            buffer[digits] = bytes1(uint8(48 + uint256(value % 10)));
            value /= 10;
        }
        return string(buffer);
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation.
     */
    function toHexString(uint256 value) internal pure returns (string memory) {
        if (value == 0) {
            return "0x00";
        }
        uint256 temp = value;
        uint256 length = 0;
        while (temp != 0) {
            length++;
            temp >>= 8;
        }
        return toHexString(value, length);
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation with fixed length.
     */
    function toHexString(uint256 value, uint256 length) internal pure returns (string memory) {
        bytes memory buffer = new bytes(2 * length + 2);
        buffer[0] = "0";
        buffer[1] = "x";
        for (uint256 i = 2 * length + 1; i > 1; --i) {
            buffer[i] = _HEX_SYMBOLS[value & 0xf];
            value >>= 4;
        }
        require(value == 0, "Strings: hex length insufficient");
        return string(buffer);
    }

    /**
     * @dev Converts an `address` with fixed length of 20 bytes to its not checksummed ASCII `string` hexadecimal representation.
     */
    function toHexString(address addr) internal pure returns (string memory) {
        return toHexString(uint256(uint160(addr)), _ADDRESS_LENGTH);
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (token/common/ERC2981.sol)

pragma solidity ^0.8.0;

import "../../interfaces/IERC2981.sol";
import "../../utils/introspection/ERC165.sol";

/**
 * @dev Implementation of the NFT Royalty Standard, a standardized way to retrieve royalty payment information.
 *
 * Royalty information can be specified globally for all token ids via {_setDefaultRoyalty}, and/or individually for
 * specific token ids via {_setTokenRoyalty}. The latter takes precedence over the first.
 *
 * Royalty is specified as a fraction of sale price. {_feeDenominator} is overridable but defaults to 10000, meaning the
 * fee is specified in basis points by default.
 *
 * IMPORTANT: ERC-2981 only specifies a way to signal royalty information and does not enforce its payment. See
 * https://eips.ethereum.org/EIPS/eip-2981#optional-royalty-payments[Rationale] in the EIP. Marketplaces are expected to
 * voluntarily pay royalties together with sales, but note that this standard is not yet widely supported.
 *
 * _Available since v4.5._
 */
abstract contract ERC2981 is IERC2981, ERC165 {
    struct RoyaltyInfo {
        address receiver;
        uint96 royaltyFraction;
    }

    RoyaltyInfo private _defaultRoyaltyInfo;
    mapping(uint256 => RoyaltyInfo) private _tokenRoyaltyInfo;

    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override(IERC165, ERC165) returns (bool) {
        return interfaceId == type(IERC2981).interfaceId || super.supportsInterface(interfaceId);
    }

    /**
     * @inheritdoc IERC2981
     */
    function royaltyInfo(uint256 _tokenId, uint256 _salePrice) public view virtual override returns (address, uint256) {
        RoyaltyInfo memory royalty = _tokenRoyaltyInfo[_tokenId];

        if (royalty.receiver == address(0)) {
            royalty = _defaultRoyaltyInfo;
        }

        uint256 royaltyAmount = (_salePrice * royalty.royaltyFraction) / _feeDenominator();

        return (royalty.receiver, royaltyAmount);
    }

    /**
     * @dev The denominator with which to interpret the fee set in {_setTokenRoyalty} and {_setDefaultRoyalty} as a
     * fraction of the sale price. Defaults to 10000 so fees are expressed in basis points, but may be customized by an
     * override.
     */
    function _feeDenominator() internal pure virtual returns (uint96) {
        return 10000;
    }

    /**
     * @dev Sets the royalty information that all ids in this contract will default to.
     *
     * Requirements:
     *
     * - `receiver` cannot be the zero address.
     * - `feeNumerator` cannot be greater than the fee denominator.
     */
    function _setDefaultRoyalty(address receiver, uint96 feeNumerator) internal virtual {
        require(feeNumerator <= _feeDenominator(), "ERC2981: royalty fee will exceed salePrice");
        require(receiver != address(0), "ERC2981: invalid receiver");

        _defaultRoyaltyInfo = RoyaltyInfo(receiver, feeNumerator);
    }

    /**
     * @dev Removes default royalty information.
     */
    function _deleteDefaultRoyalty() internal virtual {
        delete _defaultRoyaltyInfo;
    }

    /**
     * @dev Sets the royalty information for a specific token id, overriding the global default.
     *
     * Requirements:
     *
     * - `receiver` cannot be the zero address.
     * - `feeNumerator` cannot be greater than the fee denominator.
     */
    function _setTokenRoyalty(
        uint256 tokenId,
        address receiver,
        uint96 feeNumerator
    ) internal virtual {
        require(feeNumerator <= _feeDenominator(), "ERC2981: royalty fee will exceed salePrice");
        require(receiver != address(0), "ERC2981: Invalid parameters");

        _tokenRoyaltyInfo[tokenId] = RoyaltyInfo(receiver, feeNumerator);
    }

    /**
     * @dev Resets royalty information for the token id back to the global default.
     */
    function _resetTokenRoyalty(uint256 tokenId) internal virtual {
        delete _tokenRoyaltyInfo[tokenId];
    }
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.13;

import {IOperatorFilterRegistry} from "./IOperatorFilterRegistry.sol";

/**
 * @title  OperatorFilterer
 * @notice Abstract contract whose constructor automatically registers and optionally subscribes to or copies another
 *         registrant's entries in the OperatorFilterRegistry.
 * @dev    This smart contract is meant to be inherited by token contracts so they can use the following:
 *         - `onlyAllowedOperator` modifier for `transferFrom` and `safeTransferFrom` methods.
 *         - `onlyAllowedOperatorApproval` modifier for `approve` and `setApprovalForAll` methods.
 */
abstract contract OperatorFilterer {
    error OperatorNotAllowed(address operator);

    IOperatorFilterRegistry public constant OPERATOR_FILTER_REGISTRY =
        IOperatorFilterRegistry(0x000000000000AAeB6D7670E522A718067333cd4E);

    constructor(address subscriptionOrRegistrantToCopy, bool subscribe) {
        // If an inheriting token contract is deployed to a network without the registry deployed, the modifier
        // will not revert, but the contract will need to be registered with the registry once it is deployed in
        // order for the modifier to filter addresses.
        if (address(OPERATOR_FILTER_REGISTRY).code.length > 0) {
            if (subscribe) {
                OPERATOR_FILTER_REGISTRY.registerAndSubscribe(
                    address(this),
                    subscriptionOrRegistrantToCopy
                );
            } else {
                if (subscriptionOrRegistrantToCopy != address(0)) {
                    OPERATOR_FILTER_REGISTRY.registerAndCopyEntries(
                        address(this),
                        subscriptionOrRegistrantToCopy
                    );
                } else {
                    OPERATOR_FILTER_REGISTRY.register(address(this));
                }
            }
        }
    }

    modifier onlyAllowedOperator(address from) virtual {
        // Allow spending tokens from addresses with balance
        // Note that this still allows listings and marketplaces with escrow to transfer tokens if transferred
        // from an EOA.
        if (from != msg.sender) {
            _checkFilterOperator(msg.sender);
        }
        _;
    }

    modifier onlyAllowedOperatorApproval(address operator) virtual {
        _checkFilterOperator(operator);
        _;
    }

    function _checkFilterOperator(address operator) internal view virtual {
        // Check registry code length to facilitate testing in environments without a deployed registry.
        if (address(OPERATOR_FILTER_REGISTRY).code.length > 0) {
            if (!OPERATOR_FILTER_REGISTRY.isOperatorAllowed(address(this), operator)) {
                revert OperatorNotAllowed(operator);
            }
        }
    }
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.13;

interface IOperatorFilterRegistry {
    function isOperatorAllowed(address registrant, address operator) external view returns (bool);

    function register(address registrant) external;

    function registerAndSubscribe(address registrant, address subscription) external;

    function registerAndCopyEntries(address registrant, address registrantToCopy) external;

    function unregister(address addr) external;

    function updateOperator(
        address registrant,
        address operator,
        bool filtered
    ) external;

    function updateOperators(
        address registrant,
        address[] calldata operators,
        bool filtered
    ) external;

    function updateCodeHash(
        address registrant,
        bytes32 codehash,
        bool filtered
    ) external;

    function updateCodeHashes(
        address registrant,
        bytes32[] calldata codeHashes,
        bool filtered
    ) external;

    function subscribe(address registrant, address registrantToSubscribe) external;

    function unsubscribe(address registrant, bool copyExistingEntries) external;

    function subscriptionOf(address addr) external returns (address registrant);

    function subscribers(address registrant) external returns (address[] memory);

    function subscriberAt(address registrant, uint256 index) external returns (address);

    function copyEntriesOf(address registrant, address registrantToCopy) external;

    function isOperatorFiltered(address registrant, address operator) external returns (bool);

    function isCodeHashOfFiltered(address registrant, address operatorWithCode)
        external
        returns (bool);

    function isCodeHashFiltered(address registrant, bytes32 codeHash) external returns (bool);

    function filteredOperators(address addr) external returns (address[] memory);

    function filteredCodeHashes(address addr) external returns (bytes32[] memory);

    function filteredOperatorAt(address registrant, uint256 index) external returns (address);

    function filteredCodeHashAt(address registrant, uint256 index) external returns (bytes32);

    function isRegistered(address addr) external returns (bool);

    function codeHashOf(address addr) external returns (bytes32);
}

// SPDX-License-Identifier: MIT
// ERC721A Contracts v4.2.3
// Creator: Chiru Labs

pragma solidity ^0.8.4;

/**
 * @dev Interface of ERC721A.
 */
interface IERC721A {
    /**
     * The caller must own the token or be an approved operator.
     */
    error ApprovalCallerNotOwnerNorApproved();

    /**
     * The token does not exist.
     */
    error ApprovalQueryForNonexistentToken();

    /**
     * Cannot query the balance for the zero address.
     */
    error BalanceQueryForZeroAddress();

    /**
     * Cannot mint to the zero address.
     */
    error MintToZeroAddress();

    /**
     * The quantity of tokens minted must be more than zero.
     */
    error MintZeroQuantity();

    /**
     * The token does not exist.
     */
    error OwnerQueryForNonexistentToken();

    /**
     * The caller must own the token or be an approved operator.
     */
    error TransferCallerNotOwnerNorApproved();

    /**
     * The token must be owned by `from`.
     */
    error TransferFromIncorrectOwner();

    /**
     * Cannot safely transfer to a contract that does not implement the
     * ERC721Receiver interface.
     */
    error TransferToNonERC721ReceiverImplementer();

    /**
     * Cannot transfer to the zero address.
     */
    error TransferToZeroAddress();

    /**
     * The token does not exist.
     */
    error URIQueryForNonexistentToken();

    /**
     * The `quantity` minted with ERC2309 exceeds the safety limit.
     */
    error MintERC2309QuantityExceedsLimit();

    /**
     * The `extraData` cannot be set on an unintialized ownership slot.
     */
    error OwnershipNotInitializedForExtraData();

    // =============================================================
    //                            STRUCTS
    // =============================================================

    struct TokenOwnership {
        // The address of the owner.
        address addr;
        // Stores the start time of ownership with minimal overhead for tokenomics.
        uint64 startTimestamp;
        // Whether the token has been burned.
        bool burned;
        // Arbitrary data similar to `startTimestamp` that can be set via {_extraData}.
        uint24 extraData;
    }

    // =============================================================
    //                         TOKEN COUNTERS
    // =============================================================

    /**
     * @dev Returns the total number of tokens in existence.
     * Burned tokens will reduce the count.
     * To get the total number of tokens minted, please see {_totalMinted}.
     */
    function totalSupply() external view returns (uint256);

    // =============================================================
    //                            IERC165
    // =============================================================

    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * [EIP section](https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified)
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30000 gas.
     */
    function supportsInterface(bytes4 interfaceId) external view returns (bool);

    // =============================================================
    //                            IERC721
    // =============================================================

    /**
     * @dev Emitted when `tokenId` token is transferred from `from` to `to`.
     */
    event Transfer(address indexed from, address indexed to, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables `approved` to manage the `tokenId` token.
     */
    event Approval(address indexed owner, address indexed approved, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables or disables
     * (`approved`) `operator` to manage all of its assets.
     */
    event ApprovalForAll(address indexed owner, address indexed operator, bool approved);

    /**
     * @dev Returns the number of tokens in `owner`'s account.
     */
    function balanceOf(address owner) external view returns (uint256 balance);

    /**
     * @dev Returns the owner of the `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function ownerOf(uint256 tokenId) external view returns (address owner);

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`,
     * checking first that contract recipients are aware of the ERC721 protocol
     * to prevent tokens from being forever locked.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If the caller is not `from`, it must be have been allowed to move
     * this token by either {approve} or {setApprovalForAll}.
     * - If `to` refers to a smart contract, it must implement
     * {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId,
        bytes calldata data
    ) external payable;

    /**
     * @dev Equivalent to `safeTransferFrom(from, to, tokenId, '')`.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId
    ) external payable;

    /**
     * @dev Transfers `tokenId` from `from` to `to`.
     *
     * WARNING: Usage of this method is discouraged, use {safeTransferFrom}
     * whenever possible.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must be owned by `from`.
     * - If the caller is not `from`, it must be approved to move this token
     * by either {approve} or {setApprovalForAll}.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address from,
        address to,
        uint256 tokenId
    ) external payable;

    /**
     * @dev Gives permission to `to` to transfer `tokenId` token to another account.
     * The approval is cleared when the token is transferred.
     *
     * Only a single account can be approved at a time, so approving the
     * zero address clears previous approvals.
     *
     * Requirements:
     *
     * - The caller must own the token or be an approved operator.
     * - `tokenId` must exist.
     *
     * Emits an {Approval} event.
     */
    function approve(address to, uint256 tokenId) external payable;

    /**
     * @dev Approve or remove `operator` as an operator for the caller.
     * Operators can call {transferFrom} or {safeTransferFrom}
     * for any token owned by the caller.
     *
     * Requirements:
     *
     * - The `operator` cannot be the caller.
     *
     * Emits an {ApprovalForAll} event.
     */
    function setApprovalForAll(address operator, bool _approved) external;

    /**
     * @dev Returns the account approved for `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function getApproved(uint256 tokenId) external view returns (address operator);

    /**
     * @dev Returns if the `operator` is allowed to manage all of the assets of `owner`.
     *
     * See {setApprovalForAll}.
     */
    function isApprovedForAll(address owner, address operator) external view returns (bool);

    // =============================================================
    //                        IERC721Metadata
    // =============================================================

    /**
     * @dev Returns the token collection name.
     */
    function name() external view returns (string memory);

    /**
     * @dev Returns the token collection symbol.
     */
    function symbol() external view returns (string memory);

    /**
     * @dev Returns the Uniform Resource Identifier (URI) for `tokenId` token.
     */
    function tokenURI(uint256 tokenId) external view returns (string memory);

    // =============================================================
    //                           IERC2309
    // =============================================================

    /**
     * @dev Emitted when tokens in `fromTokenId` to `toTokenId`
     * (inclusive) is transferred from `from` to `to`, as defined in the
     * [ERC2309](https://eips.ethereum.org/EIPS/eip-2309) standard.
     *
     * See {_mintERC2309} for more details.
     */
    event ConsecutiveTransfer(uint256 indexed fromTokenId, uint256 toTokenId, address indexed from, address indexed to);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.6.0) (interfaces/IERC2981.sol)

pragma solidity ^0.8.0;

import "../utils/introspection/IERC165.sol";

/**
 * @dev Interface for the NFT Royalty Standard.
 *
 * A standardized way to retrieve royalty payment information for non-fungible tokens (NFTs) to enable universal
 * support for royalty payments across all NFT marketplaces and ecosystem participants.
 *
 * _Available since v4.5._
 */
interface IERC2981 is IERC165 {
    /**
     * @dev Returns how much royalty is owed and to whom, based on a sale price that may be denominated in any unit of
     * exchange. The royalty amount is denominated and should be paid in that same unit of exchange.
     */
    function royaltyInfo(uint256 tokenId, uint256 salePrice)
        external
        view
        returns (address receiver, uint256 royaltyAmount);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/introspection/ERC165.sol)

pragma solidity ^0.8.0;

import "./IERC165.sol";

/**
 * @dev Implementation of the {IERC165} interface.
 *
 * Contracts that want to implement ERC165 should inherit from this contract and override {supportsInterface} to check
 * for the additional interface id that will be supported. For example:
 *
 * ```solidity
 * function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
 *     return interfaceId == type(MyInterface).interfaceId || super.supportsInterface(interfaceId);
 * }
 * ```
 *
 * Alternatively, {ERC165Storage} provides an easier to use but more expensive implementation.
 */
abstract contract ERC165 is IERC165 {
    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IERC165).interfaceId;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/introspection/IERC165.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC165 standard, as defined in the
 * https://eips.ethereum.org/EIPS/eip-165[EIP].
 *
 * Implementers can declare support of contract interfaces, which can then be
 * queried by others ({ERC165Checker}).
 *
 * For an implementation, see {ERC165}.
 */
interface IERC165 {
    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30 000 gas.
     */
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}