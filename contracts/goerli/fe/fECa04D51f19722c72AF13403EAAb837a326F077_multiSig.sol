// SPDX-License-Identifier: MIT
pragma solidity ^0.8.13;

contract multiSig {
    ////////////////////////////////////////////////////////////////
    //EVENTS//
    ////////////////////////////////////////////////////////////////
    event Deposit(address indexed sender, uint256 amount, uint256 balance);
    event SubmitTransaction(
        address indexed owner,
        uint256 indexed txIndex,
        address indexed to,
        uint256 value,
        bytes data
    );
    event ConfirmTransaction(address indexed owner, uint256 indexed txIndex);
    event RevokeConfirmation(address indexed owner, uint256 indexed txIndex);
    event ExecuteTransaction(address indexed owner, uint256 indexed txIndex);

    ////////////////////////////////////////////////////////////////
    //state variables
    ////////////////////////////////////////////////////////////////
    ////////////////////////////////////////////////////////////////
    address[] public owners;
    mapping(address => bool) public isOwner;
    uint256 public numConfirmationsRequired;

    struct Transaction {
        address to;
        uint256 value;
        bytes data;
        bool executed;
        uint256 numConfirmations;
    }

    // mapping from tx index => owner => bool
    mapping(uint256 => mapping(address => bool)) public isConfirmed;

    Transaction[] public transactions;

    ////////////////////////////////////////////////////////////////
    //modifiers
    ////////////////////////////////////////////////////////////////
    modifier onlyOwner() {
        require(isOwner[msg.sender], "not owner");
        _;
    }

    modifier txExists(uint256 _txIndex) {
        require(_txIndex < transactions.length, "tx does not exist");
        _;
    }

    modifier notExecuted(uint256 _txIndex) {
        require(!transactions[_txIndex].executed, "tx already executed");
        _;
    }

    modifier notConfirmed(uint256 _txIndex) {
        require(!isConfirmed[_txIndex][msg.sender], "tx already confirmed");
        _;
    }

    ////////////////////////////////////////////////////////////////
    //constructor
    ////////////////////////////////////////////////////////////////
    //Checks that the owners array isnt empty, there are no repeated owners and the number
    //of confirmations is less than the owner array length
    //
    // it then pushes the owners into the owners state variable array
    ////////////////////////////////////////////////////////////////
    constructor(address[] memory _owners, uint256 _numConfirmationsRequired) {
        require(_owners.length > 0, "owners required");
        require(
            _numConfirmationsRequired > 0 && _numConfirmationsRequired <= _owners.length,
            "invalid number of required confirmations"
        );

        for (uint256 i = 0; i < _owners.length; i++) {
            address owner = _owners[i];

            require(owner != address(0), "invalid owner");
            require(!isOwner[owner], "owner not unique");

            isOwner[owner] = true;
            owners.push(owner);
        }

        numConfirmationsRequired = _numConfirmationsRequired;
    }

    ////////////////////////////////////////////////////////////////
    receive() external payable {
        emit Deposit(msg.sender, msg.value, address(this).balance);
    }

    ////////////////////////////////////////////////////////////////
    //Submit Tx
    ////////////////////////////////////////////////////////////////
    //Pushes a transaction struct into the transactions array with 0 confirmations
    ////////////////////////////////////////////////////////////////
    function submitTransaction(
        address _to,
        uint256 _value,
        bytes memory _data
    ) public onlyOwner {
        uint256 txIndex = transactions.length;

        transactions.push(
            Transaction({to: _to, value: _value, data: _data, executed: false, numConfirmations: 0})
        );

        emit SubmitTransaction(msg.sender, txIndex, _to, _value, _data);
    }

    ////////////////////////////////////////////////////////////////
    //Confirm tx
    ////////////////////////////////////////////////////////////////
    //fter checking that tx exists, isnt executed and that the owner is calling the function
    //tx is confirmed checks the mapping  tx index => owner => bool taking into account the msg.sender
    //it checks that the sender hasnt confirmed, to cancel duplicate confirmations
    //
    //It then adds 1 to numConfirmations and changes the value of the mapping to prevent double confirmations
    ////////////////////////////////////////////////////////////////
    function confirmTransaction(uint256 _txIndex)
        public
        onlyOwner
        txExists(_txIndex)
        notExecuted(_txIndex)
        notConfirmed(_txIndex)
    {
        Transaction storage transaction = transactions[_txIndex];
        transaction.numConfirmations += 1;
        isConfirmed[_txIndex][msg.sender] = true;

        emit ConfirmTransaction(msg.sender, _txIndex);
    }

    ////////////////////////////////////////////////////////////////
    //Execute transaction copies the data to a new struct, checks number of confirmations and then calls
    //a transaction with the values inside the struct.
    ////////////////////////////////////////////////////////////////
    function executeTransaction(uint256 _txIndex)
        public
        onlyOwner
        txExists(_txIndex)
        notExecuted(_txIndex)
    {
        Transaction storage transaction = transactions[_txIndex];

        require(transaction.numConfirmations >= numConfirmationsRequired, "cannot execute tx");

        transaction.executed = true;

        (bool success, ) = transaction.to.call{value: transaction.value}(transaction.data);
        require(success, "tx failed");

        emit ExecuteTransaction(msg.sender, _txIndex);
    }

    ////////////////////////////////////////////////////////////////
    ////////////////////////////////////////////////////////////////
    function revokeConfirmation(uint256 _txIndex)
        public
        onlyOwner
        txExists(_txIndex)
        notExecuted(_txIndex)
    {
        Transaction storage transaction = transactions[_txIndex];

        require(isConfirmed[_txIndex][msg.sender], "tx not confirmed");

        transaction.numConfirmations -= 1;
        isConfirmed[_txIndex][msg.sender] = false;

        emit RevokeConfirmation(msg.sender, _txIndex);
    }

    function getOwners() public view returns (address[] memory) {
        return owners;
    }

    function getTransactionCount() public view returns (uint256) {
        return transactions.length;
    }

    function getTransaction(uint256 _txIndex)
        public
        view
        returns (
            address to,
            uint256 value,
            bytes memory data,
            bool executed,
            uint256 numConfirmations
        )
    {
        Transaction storage transaction = transactions[_txIndex];

        return (
            transaction.to,
            transaction.value,
            transaction.data,
            transaction.executed,
            transaction.numConfirmations
        );
    }
}