//SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "../interfaces/IUnboundedV4.sol";
import "@openzeppelin/contracts/security/ReentrancyGuard.sol";
import "@openzeppelin/contracts/utils/escrow/Escrow.sol";

///@notice protect withdrawPayment with nonReentrant modifier in PullPayment
import "@openzeppelin/contracts/security/PullPayment.sol";
import "@openzeppelin/contracts/utils/structs/EnumerableSet.sol";

contract UnboundedControlV2 is ReentrancyGuard, PullPayment {

  event MetaverseRegistered(address metaverse, uint256 metaverseId, string metaverseName,string companyName, string metaverseMetadata);
  event MetaverseRemoved(address metaverse);
  event FundsWithdrawn(address metaverse, uint256 amount);
  event MetaverseFundsWithdrawn(address metaverse, uint256 amount);
  event FundsDeposit(uint256 amount, uint256 clientPercantage, address metaverse);
  event MintSlotFundsBatchDeposit(uint256 amount, uint256 clientPercantage, address[] metaverses, uint256[] metaversesShares);
  event BatchSlotFundsDeposit(uint256 amount, uint256 clientPercantage, address[] metaverses, uint256[] metaversesShares);
  event TemplatePricesSet(uint256[] baseIds, uint256[] prices);
  event TemplatesSelected(uint256[] baseIds, address metaverse);
  event TemplatesUnselected(uint256[] baseIds, address metaverse);
  event ClientPercentageSet(uint256 percentage);
  event SlotPriceSet(address metaverse, uint256 price);

  using EnumerableSet for EnumerableSet.AddressSet;

  modifier onlyAdmin() {
    require(admin == msg.sender, "Not Authorized");
    _;
  }

  /**
   * Struct of Registered Metaverse
   * @param wallet: Given wallet address of metaverse
   * @param id: Unique id for each metaverses generated by the contract
   * @param balance: Balance of a metaverse in this contract
   * @param name: Given name for a metaverse
   * @param metaverseMetadataTemplate: Metaverse default metadata schema
   */
  struct Metaverse {
    address wallet;
    address companyOwner;
    uint256 id;
    uint256 balance;
    string name;
    string metaverseMetadataTemplate;
  }

  //Holds Admin address of this contract
  address public admin;
  //The last registered metaverse id
  uint256 public metaverseIDs;
  //Percentage of clients when minting or metadata slot adding
  uint256 public clientPercantage;
  //ERC1155 contract interface
  IUnboundedV4 public unbounded;

  //Stores metaverses wallet address that are registered to the metaverse
  EnumerableSet.AddressSet private metaverseWallets;

  //Stores registered metaverses in struct of Metaverse, with given metaverse address, returns data in Metaverse struct
  mapping(address => Metaverse) public registeredMetaverses;

  // //metaverse address => baseId => price
  // //Stores minting price info for each metaverses for each base ids(templates)
  // //given metaverse address and baseId, returns minting price
  // mapping(address => mapping(uint256 => uint256)) public metaverseBaseIdPrice;

  //metaverse address => baseId => price
  //Stores minting price info for each metaverses for each base ids(templates)
  //given metaverse address and baseId, returns minting price
  mapping(address => mapping(uint256 => uint256)) public metaverseTemplateMintPrices;

  mapping(uint256 => uint256) templateMintPrice;

  //Stores metadata slot adding price for each metaverses
  //given metaverse address, returns metadata slot adding price
  mapping(address => uint256) public metaverseSlotAddPrice;

  constructor() {
    admin = msg.sender;
  }

  /**
   *Sets the address of deployed NFT contract
   *@param _unboundedAddress: Contract address of NFT Contract
   */
  function setUnbounded(address _unboundedAddress) external onlyAdmin {
    unbounded = IUnboundedV4(_unboundedAddress);
  }

  /**
   *Checks the validity of given parameters and whether paid ETH amount is valid
   *It makes call to NFT contract to mint single NFT.
   *@param _baseId: ID of Templates which are categorized NFTs
   *@param _metaverse: Metaverse wallet address
   */
  function mint(uint256 _baseId, address payable _metaverse) external payable nonReentrant {
    require(_baseId != 0, "Invalid ID!");
    require(registeredMetaverses[_metaverse].id != 0, "Unregistered Metaverse");
    require(msg.value == metaverseTemplateMintPrices[_metaverse][_baseId], "Please send the required amount!");
    require(msg.value != 0, "Ether value should be more than zero!");
    unbounded.mint(msg.sender, _baseId);

    registeredMetaverses[_metaverse].balance += (msg.value * clientPercantage) / 100; //8kgas
    // _asyncTransfer(_metaverse,(msg.value * clientPercantage) / 100);//20kgas
    emit FundsDeposit(msg.value, clientPercantage, _metaverse);
  }

  /**
   *Checks the validity of given parameters and whether paid ETH amount is valid
   *It makes call to NFT contract to mint multiple NFT.
   *@param _baseIds: IDs of Templates which are categorized NFTs
   *@param _amounts: Mint amount for each ID
   *@param _metaverse: Metaverse wallet address
   */
  function mintBatch(
    uint256[] calldata _baseIds,
    uint256[] calldata _amounts,
    address payable _metaverse
  ) external payable nonReentrant {
    require(_baseIds.length == _amounts.length, "Specify equal number of Base IDs and their amounts!");
    require(_baseIds.length <= 5, "You can't mint more than 5 different templates!");
    require(this.calculateBatchPrice(_baseIds, _amounts, _metaverse) == msg.value, "Please send the required amount!");
    require(msg.value != 0, "Ether value should be more than zero!");
    unbounded.mintBatch(msg.sender, _baseIds, _amounts);

    registeredMetaverses[_metaverse].balance += (msg.value * clientPercantage) / 100;
    // _asyncTransfer(_metaverse,(msg.value * clientPercantage) / 100);
    emit FundsDeposit(msg.value, clientPercantage, _metaverse);
  }

  /**
   *Checks the validity of given parameters and whether paid ETH amount is valid
   *It makes call to NFT contract to mint multiple NFTs with a single specified metadata slot
   *@param _baseIds: IDs of Templates which are categorized NFTs
   *@param _amounts: Mint amount for each ID
   *@param _metaverseWallet:Metaverse wallet address
   *@param _metaverseId: ID of the metaverse
   */
  function mintBatchWithMetadataSlot(
    uint256[] calldata _baseIds,
    uint256[] calldata _amounts,
    address payable _metaverseWallet,
    uint256 _metaverseId
  ) external payable nonReentrant {
    require(registeredMetaverses[_metaverseWallet].id == _metaverseId, "Metaverse id mismatch!");
    require(registeredMetaverses[_metaverseWallet].wallet == _metaverseWallet, "Metaverse wallet mismatch!");
    require(_baseIds.length == _amounts.length, "Specify equal number of Base IDs and and its amounts!");
    require(
      (this.calculateBatchPrice(_baseIds, _amounts, _metaverseWallet) + this.calculateBatchSlotPrice(_amounts, _metaverseWallet)) ==
        msg.value,
      "Please send the required amount!"
    );

    unbounded.mintBatchWithMetadataSlot(
      msg.sender,
      _baseIds,
      _amounts,
      _metaverseId,
      registeredMetaverses[_metaverseWallet].metaverseMetadataTemplate
    );

    registeredMetaverses[_metaverseWallet].balance += (msg.value * clientPercantage) / 100;
    // _asyncTransfer(_metaverseWallet,(msg.value * clientPercantage) / 100);
    emit FundsDeposit(msg.value, clientPercantage, _metaverseWallet);
  }

  //=======================INITIAL MINT PRICE ISSUE===================================
  /**
   *Checks the validity of given parameters and whether paid ETH amount is valid
   *It makes call to NFT contract to mint single NFT with multiple specified metadata slots
   *@param _baseId: IDs of Templates which are categorized NFTs
   *@param _metaverseIds: Number of mint from each IDs
   *@param _metaverseWallets:Metaverse wallet addresses(Address at index zero, gets the mint price)
   */
  function mintWithMetadaSlots(
    uint256 _baseId,
    uint256[] calldata _metaverseIds,
    address[] calldata _metaverseWallets
  ) external payable nonReentrant {
    require(_baseId != 0, "Invalid ID!");
    require(_metaverseIds.length == _metaverseWallets.length, "Wrong given number of parameters!");
    require(metaverseTemplateMintPrices[_metaverseWallets[0]][_baseId] > 0, "Can't mint this NFT in this metaverse!");
    string[] memory _metaverseMetadataTemplate = new string[](_metaverseIds.length);
    uint256 _totalMetadataSlotPriceAmount;
    uint256 _metadataSlotPrice;
    uint256[] memory _metaversesMetedataPriceShares = new uint256[](_metaverseWallets.length);
    for (uint256 i; i < _metaverseIds.length; i++) {
      require(registeredMetaverses[_metaverseWallets[i]].id == _metaverseIds[i], "Metaverse wallet and ID mismatch!");
      require(metaverseSlotAddPrice[_metaverseWallets[i]] > 0, "Slot for one of the selected Metaverses can't be added!");
      _metadataSlotPrice = metaverseSlotAddPrice[_metaverseWallets[i]];
      _metaverseMetadataTemplate[i] = (registeredMetaverses[_metaverseWallets[i]].metaverseMetadataTemplate);
      _totalMetadataSlotPriceAmount += _metadataSlotPrice;
      registeredMetaverses[_metaverseWallets[i]].balance += (_metadataSlotPrice * clientPercantage) / 100;
      _metaversesMetedataPriceShares[i] = _metadataSlotPrice;
    }

    require(
      msg.value == (_totalMetadataSlotPriceAmount + metaverseTemplateMintPrices[_metaverseWallets[0]][_baseId]),
      "Please send the required amount!"
    );
    registeredMetaverses[_metaverseWallets[0]].balance += ((metaverseTemplateMintPrices[_metaverseWallets[0]][_baseId] * clientPercantage) / 100);
    unbounded.mintWithMetadaSlots(msg.sender, _baseId, _metaverseIds, _metaverseMetadataTemplate);

    //base mint price should be considered at zero index!
    emit MintSlotFundsBatchDeposit(msg.value, clientPercantage, _metaverseWallets, _metaversesMetedataPriceShares);
  }

  /**
   *Checks the validity of given parameters and whether paid ETH amount is valid
   *It makes call to NFT contract to add single NFT metadata to single NFT
   *@param _tokenId: Token ID of the NFT
   *@param _metaverseId: Number of mint from each ID
   *@param _metaverseWallet:Metaverse wallet address
   */
  function addMetadataSlotToTemplate(
    uint256 _tokenId,
    uint256 _metaverseId,
    address payable _metaverseWallet
  ) external payable nonReentrant {
    require(registeredMetaverses[_metaverseWallet].id == _metaverseId, "Metaverse wallet and id mismatch!");
    require(metaverseSlotAddPrice[_metaverseWallet] == msg.value, "Please send the required amount!");
    require(msg.value != 0, "Ether value should be more than zero!");
    unbounded.addMetadataSlotToTemplate(
      msg.sender,
      _tokenId,
      _metaverseId,
      registeredMetaverses[_metaverseWallet].metaverseMetadataTemplate
    );
    registeredMetaverses[_metaverseWallet].balance += (msg.value * clientPercantage) / 100;
    // _asyncTransfer(_metaverseWallet,(msg.value * clientPercantage) / 100);
    emit FundsDeposit(msg.value, clientPercantage, _metaverseWallet);
  }

  /** Single Token => Multiple Metaverses
   *Checks the validity of given parameters and whether paid ETH amount is valid
   *It makes call to NFT contract to add multiple metadata slots to single NFT
   *@param _tokenId: Token ID of the NFT
   *@param _metaverseIds: Number of mint from each IDs
   *@param _metaverseWallets:Metaverse wallet addresses
   *@notice If reverts, costs gas!
   */
  function addBatchMetaverseSlotsToTemplate(
    uint256 _tokenId,
    uint256[] calldata _metaverseIds,
    address[] calldata _metaverseWallets
  ) external payable nonReentrant {
    require(_metaverseIds.length == _metaverseWallets.length, "Wrong given number of parameters!");
    // require(msg.value != 0,"Ether value should be more than zero!");
    string[] memory _metaverseMetadataTemplate = new string[](_metaverseIds.length);
    uint256 _price;
    uint256 _totalAmount;
    uint256[] memory _metaversesShares = new uint256[](_metaverseWallets.length);
    for (uint256 i; i < _metaverseIds.length; i++) {
      require(registeredMetaverses[_metaverseWallets[i]].id == _metaverseIds[i], "Metaverse wallet and ID mismatch!");
      require(metaverseSlotAddPrice[_metaverseWallets[i]] > 0, "Slot for one of the selected Metaverses can't be added!");
      _price = metaverseSlotAddPrice[_metaverseWallets[i]];
      _metaverseMetadataTemplate[i] = (registeredMetaverses[_metaverseWallets[i]].metaverseMetadataTemplate);
      _totalAmount += _price;
      registeredMetaverses[_metaverseWallets[i]].balance += (_price * clientPercantage) / 100;
      _metaversesShares[i] = _price;
    }
    require(msg.value == _totalAmount, "Please send the required amount!");
    unbounded.addBatchMetaverseSlotsToTemplate(msg.sender, _tokenId, _metaverseIds, _metaverseMetadataTemplate);
    emit BatchSlotFundsDeposit(msg.value, clientPercantage, _metaverseWallets, _metaversesShares);
  }

  //************************************************************************* */
  /**Multiple Tokens => Single Metaverse
   *Checks the validity of given parameters and whether paid ETH amount is valid
   *It makes call to NFT contract to add the same single metadata slot to multiple NFTs
   *@param _tokenIds: Token IDs of NFTs
   *@param _metaverseId: Number of mint from each IDs
   *@param _metaverseWallet:Metaverse wallet addresses
   */
  function addBatchMetaverseSlotToTemplates(
    uint256[] calldata _tokenIds,
    uint256 _metaverseId,
    address payable _metaverseWallet
  ) external payable nonReentrant {
    require(registeredMetaverses[_metaverseWallet].id == _metaverseId, "Metaverse wallet and ID mismatch!");
    require(_tokenIds.length <= 20, "Token numbers should be less then 20!");
    require((metaverseSlotAddPrice[_metaverseWallet] * _tokenIds.length) == msg.value, "Please send the required amount!");
    require(msg.value != 0, "Ether value should be more than zero!");
    unbounded.addBatchMetaverseSlotToTemplates(
      msg.sender,
      _tokenIds,
      _metaverseId,
      registeredMetaverses[_metaverseWallet].metaverseMetadataTemplate
    );
    //_metaverseWallet.transfer((msg.value * clientPercantage) / 100);
    registeredMetaverses[_metaverseWallet].balance += (msg.value * clientPercantage) / 100;
    emit FundsDeposit(msg.value, clientPercantage, _metaverseWallet);
    // _asyncTransfer(_metaverseWallet,(msg.value * clientPercantage) / 100);
  }

  /**Multiple Tokens => Single Metaverse
   *Checks the validity of given parameters and whether paid ETH amount is valid
   *It makes call to NFT contract to add the same single metadata slot to multiple NFTs
   *@param _signature: IDs of Templates which are categorized NFTs
   *@param _metaverseWallet: Metaverse wallet addresses
   *@param _metaverseId: Number of mint from each IDs
   *@param _tokenId: Token ID of the NFT
   *@param _newURI: New URI which will be replaced with old URI
   *@param _salt: Unique salt value
   */
  function updateURI(
    bytes calldata _signature,
    address _metaverseWallet,
    uint256 _metaverseId,
    uint256 _tokenId,
    string calldata _newURI,
    bytes calldata _salt
  ) external {
    Metaverse memory _registeredMetaverse = registeredMetaverses[_metaverseWallet];
    require(_registeredMetaverse.wallet == _metaverseWallet, "Not authorized!");
    require(_registeredMetaverse.id == _metaverseId, "Not authorized!");
    require(_registeredMetaverse.wallet != address(0), "Unregistered Metaverse");
    unbounded.updateURI(_signature, _metaverseWallet, _metaverseId, _tokenId, _newURI, _salt);
  }

  /**
   *Resets NFT's metaverse metadata URI to default
   *@notice This function is not completed yet!
   */
  function resetMetaverseURI(
    uint256 _tokenId,
    address _metaverseWallet,
    uint256 _metaverseId
  ) external {
    require(msg.sender == registeredMetaverses[_metaverseWallet].wallet, "");
    require(registeredMetaverses[_metaverseWallet].id == _metaverseId, "Invalid Metaverse Parameters!");
    unbounded.resetMetaverseURI(_tokenId, _metaverseId, registeredMetaverses[_metaverseWallet].metaverseMetadataTemplate);
  }

  /**
   *Via this function Metaverses can be registered to the contract.
   *@param _wallet: Metaverse Wallet
   *@param _name: Metaverse Name
   *@param _metaverseMetadataTemplate: Metaverse default metadata.It will be used when a m
   */
  function registerMetaverse(
    address _wallet,
    address _companyWallet,
    string calldata _name,
    string calldata _companyName,
    string calldata _metaverseMetadataTemplate
  ) external onlyAdmin {
    require(!metaverseWallets.contains(_wallet), "This Metaverse has already registered!");
    unchecked {
      uint256 id = ++metaverseIDs;
      registeredMetaverses[_wallet] = Metaverse(
        _wallet,
        _companyWallet,
        id,
        registeredMetaverses[_wallet].balance,
        _name,
        _metaverseMetadataTemplate
      );
      metaverseWallets.add(_wallet);
      emit MetaverseRegistered(_wallet, id, _name, _companyName, _metaverseMetadataTemplate);
    }
  }

  /**
   *Removes registered metaverse from the contract
   *@param _wallet: Wallet address of registered metaverse
   */
  function removeMetaverse(address _wallet) external onlyAdmin {
    require(registeredMetaverses[_wallet].wallet != address(0), "Can't delete an un-registered Metaverse");
    registeredMetaverses[_wallet].wallet = address(0);
    registeredMetaverses[_wallet].id = 0;
    registeredMetaverses[_wallet].name = "";
    registeredMetaverses[_wallet].metaverseMetadataTemplate = "";
    metaverseWallets.remove(_wallet);
    emit MetaverseRemoved(_wallet);
  }

  //==============================================================================================================
  // /**
  //  *Sets Templates mint prices for metaverses
  //  *@param _baseIds: IDs of Templates which are categorized NFTs
  //  *@param _prices: Prices of each given templates in wei unit
  //  *@param _metaverse: Metaverse address
  //  */
  // function setTemplatePrices(
  //   uint256[] calldata _baseIds,
  //   uint256[] calldata _prices,
  //   address _metaverse
  // ) external onlyAdmin {
  //   require(_baseIds.length == _prices.length, "Wrong given number of parameters!");
  //   require(registeredMetaverses[_metaverse].id != 0, "Unregistered metaverse wallet!");
  //   for (uint256 i; i < _baseIds.length; i++) {
  //     metaverseBaseIdPrice[_metaverse][_baseIds[i]] = _prices[i];
  //   }
  //   emit TemplatePricesSet(_baseIds, _prices, _metaverse);
  // }

  /**
   *Sets Templates mint prices for metaverses
   *@param _baseIds: IDs of Templates which are categorized NFTs
   *@param _prices: Prices of each given templates in wei unit
   */
  function setTemplatePrices(uint256[] calldata _baseIds, uint256[] calldata _prices) external onlyAdmin {
    require(_baseIds.length == _prices.length, "Wrong given number of parameters!");
    for (uint256 i; i < _baseIds.length; i++) {
      templateMintPrice[_baseIds[i]] = _prices[i];
    }
    emit TemplatePricesSet(_baseIds, _prices);
  }

  /**
   *Sets Templates mint prices for metaverses
   *@param _baseIds: IDs of Templates which are categorized NFTs
   */
  function selectTemplates(uint256[] calldata _baseIds) external {
    require(registeredMetaverses[msg.sender].id != 0, "Unregistered metaverse wallet!");
    for (uint256 i; i < _baseIds.length; i++) {
      metaverseTemplateMintPrices[msg.sender][_baseIds[i]] = templateMintPrice[_baseIds[i]];
    }
    emit TemplatesSelected(_baseIds, msg.sender);
  }

  /**
   *Sets Templates mint prices for metaverses
   *@param _baseIds: IDs of Templates which are categorized NFTs
   */
  function unSelectTemplates(uint256[] calldata _baseIds) external {
    require(registeredMetaverses[msg.sender].id != 0, "Unregistered metaverse wallet!");
    for (uint256 i; i < _baseIds.length; i++) {
      delete metaverseTemplateMintPrices[msg.sender][_baseIds[i]];
    }
    emit TemplatesUnselected(_baseIds, msg.sender);
  }

  /**
   *Sets metaverses percantage from minting and metadata slot purchases
   *@param _amount: Percantage amount
   *@notice Amount should be between 0-100
   */
  function setClientPercentage(uint256 _amount) external onlyAdmin {
    require(_amount <= 100, "Can't be higher than 100!");
    clientPercantage = _amount;
    emit ClientPercentageSet(_amount);
  }

  /**
   *Sets a metaverse's metadata slot adding price for each template
   *@param _metaverse: Metaverse wallet address
   *@param _price: Price in wei unit
   *@notice Amount should be between 0-100
   */
  function setSlotAddPrice(address _metaverse, uint256 _price) external onlyAdmin {
    metaverseSlotAddPrice[_metaverse] = _price;
    emit SlotPriceSet(_metaverse, _price);
  }

  /**
   *Calculates and returns templates batch mint price
   *@param _baseIds: IDs of Templates which are categorized NFTs
   *@param _amounts: Amount of each IDs
   *@return _totalAmount Calculated total amount of IDs for a metaverse
   */
  function calculateBatchPrice(
    uint256[] calldata _baseIds,
    uint256[] calldata _amounts,
    address _metaverse
  ) external view returns (uint256 _totalAmount) {
    for (uint256 i; i < _baseIds.length; i++) {
      require(_baseIds[i] > 0, "You can't mint one of the selected NFTs for this metaverse!");
      _totalAmount += (metaverseTemplateMintPrices[_metaverse][_baseIds[i]] * _amounts[i]);
    }
  }

  /**
   *Calculates and returns metaverse matadata slots
   *@param _amounts: Amounts of tokens
   *@param _metaverse: Metaverse wallet address
   */
  function calculateBatchSlotPrice(uint256[] calldata _amounts, address _metaverse) external view returns (uint256 _totalAmount) {
    require(metaverseSlotAddPrice[_metaverse] > 0, "Slot for this metaverse can't be added!");
    for (uint256 i; i < _amounts.length; i++) {
      _totalAmount += (metaverseSlotAddPrice[_metaverse] * _amounts[i]);
    }
  }

  // /**
  //  *Calculates and returns total price for an item in different metaverses
  //  */
  // function calculateBatchMetaversePrice(uint256 _baseId, address[] calldata _metaverses) public view returns (uint256 _totalAmount) {
  //   for (uint256 i; i < _metaverses.length; i++) _totalAmount += templateMintPrice[_baseId];
  // }

  /**
   *Admin of this contract withdraws all funds. Uses {PullPayment} method of Oppenzeppelin.
   *@param _wallet: Admin wallet address.
   */
  function getFunds(address payable _wallet) external onlyAdmin {
    uint256 _balanceOfContract = address(this).balance;
    uint256 _withdrawableAmount;
    // uint256 _withdrawableAmount = (_balanceOfContract - (_balanceOfContract * clientPercantage) / 100);
    // require(_amount <= _withdrawableAmount, "Insufficient balance");
    // _wallet.transfer(_amount);
    address[] memory _metaverseWallets = new address[](metaverseWallets.length());
    _metaverseWallets = metaverseWallets.values();
    uint256 currentMetaverseBalance;
    for (uint256 i; i < metaverseWallets.length(); i++) {
      currentMetaverseBalance += registeredMetaverses[_metaverseWallets[i]].balance;
    }
    _withdrawableAmount = _balanceOfContract - currentMetaverseBalance;
    _asyncTransfer(_wallet, _withdrawableAmount);
    emit FundsWithdrawn(_wallet, _withdrawableAmount);
  }

  /**
   *Metaverses can withdraw their balance using this function
   *@param _metaverse: Metaverse registered wallet address
   *@param _amount: Amount of funds that will be withdrawn in wei
   */
  function metaverseGetFunds(address payable _metaverse, uint256 _amount) external {
    require(registeredMetaverses[_metaverse].wallet == msg.sender, "Unauthorizaed Access");
    require(_amount <= registeredMetaverses[_metaverse].balance, "Insufficient balance");
    registeredMetaverses[_metaverse].balance -= _amount;
    // _metaverse.transfer(_amount);
    _asyncTransfer(_metaverse, _amount);
    emit MetaverseFundsWithdrawn(_metaverse, _amount);
  }

  //==============================================================================================================
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/structs/EnumerableSet.sol)

pragma solidity ^0.8.0;

/**
 * @dev Library for managing
 * https://en.wikipedia.org/wiki/Set_(abstract_data_type)[sets] of primitive
 * types.
 *
 * Sets have the following properties:
 *
 * - Elements are added, removed, and checked for existence in constant time
 * (O(1)).
 * - Elements are enumerated in O(n). No guarantees are made on the ordering.
 *
 * ```
 * contract Example {
 *     // Add the library methods
 *     using EnumerableSet for EnumerableSet.AddressSet;
 *
 *     // Declare a set state variable
 *     EnumerableSet.AddressSet private mySet;
 * }
 * ```
 *
 * As of v3.3.0, sets of type `bytes32` (`Bytes32Set`), `address` (`AddressSet`)
 * and `uint256` (`UintSet`) are supported.
 */
library EnumerableSet {
    // To implement this library for multiple types with as little code
    // repetition as possible, we write it in terms of a generic Set type with
    // bytes32 values.
    // The Set implementation uses private functions, and user-facing
    // implementations (such as AddressSet) are just wrappers around the
    // underlying Set.
    // This means that we can only create new EnumerableSets for types that fit
    // in bytes32.

    struct Set {
        // Storage of set values
        bytes32[] _values;
        // Position of the value in the `values` array, plus 1 because index 0
        // means a value is not in the set.
        mapping(bytes32 => uint256) _indexes;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function _add(Set storage set, bytes32 value) private returns (bool) {
        if (!_contains(set, value)) {
            set._values.push(value);
            // The value is stored at length-1, but we add 1 to all indexes
            // and use 0 as a sentinel value
            set._indexes[value] = set._values.length;
            return true;
        } else {
            return false;
        }
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function _remove(Set storage set, bytes32 value) private returns (bool) {
        // We read and store the value's index to prevent multiple reads from the same storage slot
        uint256 valueIndex = set._indexes[value];

        if (valueIndex != 0) {
            // Equivalent to contains(set, value)
            // To delete an element from the _values array in O(1), we swap the element to delete with the last one in
            // the array, and then remove the last element (sometimes called as 'swap and pop').
            // This modifies the order of the array, as noted in {at}.

            uint256 toDeleteIndex = valueIndex - 1;
            uint256 lastIndex = set._values.length - 1;

            if (lastIndex != toDeleteIndex) {
                bytes32 lastvalue = set._values[lastIndex];

                // Move the last value to the index where the value to delete is
                set._values[toDeleteIndex] = lastvalue;
                // Update the index for the moved value
                set._indexes[lastvalue] = valueIndex; // Replace lastvalue's index to valueIndex
            }

            // Delete the slot where the moved value was stored
            set._values.pop();

            // Delete the index for the deleted slot
            delete set._indexes[value];

            return true;
        } else {
            return false;
        }
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function _contains(Set storage set, bytes32 value) private view returns (bool) {
        return set._indexes[value] != 0;
    }

    /**
     * @dev Returns the number of values on the set. O(1).
     */
    function _length(Set storage set) private view returns (uint256) {
        return set._values.length;
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function _at(Set storage set, uint256 index) private view returns (bytes32) {
        return set._values[index];
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function _values(Set storage set) private view returns (bytes32[] memory) {
        return set._values;
    }

    // Bytes32Set

    struct Bytes32Set {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(Bytes32Set storage set, bytes32 value) internal returns (bool) {
        return _add(set._inner, value);
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(Bytes32Set storage set, bytes32 value) internal returns (bool) {
        return _remove(set._inner, value);
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(Bytes32Set storage set, bytes32 value) internal view returns (bool) {
        return _contains(set._inner, value);
    }

    /**
     * @dev Returns the number of values in the set. O(1).
     */
    function length(Bytes32Set storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(Bytes32Set storage set, uint256 index) internal view returns (bytes32) {
        return _at(set._inner, index);
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function values(Bytes32Set storage set) internal view returns (bytes32[] memory) {
        return _values(set._inner);
    }

    // AddressSet

    struct AddressSet {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(AddressSet storage set, address value) internal returns (bool) {
        return _add(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(AddressSet storage set, address value) internal returns (bool) {
        return _remove(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(AddressSet storage set, address value) internal view returns (bool) {
        return _contains(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Returns the number of values in the set. O(1).
     */
    function length(AddressSet storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(AddressSet storage set, uint256 index) internal view returns (address) {
        return address(uint160(uint256(_at(set._inner, index))));
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function values(AddressSet storage set) internal view returns (address[] memory) {
        bytes32[] memory store = _values(set._inner);
        address[] memory result;

        assembly {
            result := store
        }

        return result;
    }

    // UintSet

    struct UintSet {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(UintSet storage set, uint256 value) internal returns (bool) {
        return _add(set._inner, bytes32(value));
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(UintSet storage set, uint256 value) internal returns (bool) {
        return _remove(set._inner, bytes32(value));
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(UintSet storage set, uint256 value) internal view returns (bool) {
        return _contains(set._inner, bytes32(value));
    }

    /**
     * @dev Returns the number of values on the set. O(1).
     */
    function length(UintSet storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(UintSet storage set, uint256 index) internal view returns (uint256) {
        return uint256(_at(set._inner, index));
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function values(UintSet storage set) internal view returns (uint256[] memory) {
        bytes32[] memory store = _values(set._inner);
        uint256[] memory result;

        assembly {
            result := store
        }

        return result;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (security/PullPayment.sol)

pragma solidity ^0.8.0;

import "../utils/escrow/Escrow.sol";

/**
 * @dev Simple implementation of a
 * https://consensys.github.io/smart-contract-best-practices/recommendations/#favor-pull-over-push-for-external-calls[pull-payment]
 * strategy, where the paying contract doesn't interact directly with the
 * receiver account, which must withdraw its payments itself.
 *
 * Pull-payments are often considered the best practice when it comes to sending
 * Ether, security-wise. It prevents recipients from blocking execution, and
 * eliminates reentrancy concerns.
 *
 * TIP: If you would like to learn more about reentrancy and alternative ways
 * to protect against it, check out our blog post
 * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].
 *
 * To use, derive from the `PullPayment` contract, and use {_asyncTransfer}
 * instead of Solidity's `transfer` function. Payees can query their due
 * payments with {payments}, and retrieve them with {withdrawPayments}.
 */
abstract contract PullPayment {
    Escrow private immutable _escrow;

    constructor() {
        _escrow = new Escrow();
    }

    /**
     * @dev Withdraw accumulated payments, forwarding all gas to the recipient.
     *
     * Note that _any_ account can call this function, not just the `payee`.
     * This means that contracts unaware of the `PullPayment` protocol can still
     * receive funds this way, by having a separate account call
     * {withdrawPayments}.
     *
     * WARNING: Forwarding all gas opens the door to reentrancy vulnerabilities.
     * Make sure you trust the recipient, or are either following the
     * checks-effects-interactions pattern or using {ReentrancyGuard}.
     *
     * @param payee Whose payments will be withdrawn.
     *
     * Causes the `escrow` to emit a {Withdrawn} event.
     */
    function withdrawPayments(address payable payee) public virtual {
        _escrow.withdraw(payee);
    }

    /**
     * @dev Returns the payments owed to an address.
     * @param dest The creditor's address.
     */
    function payments(address dest) public view returns (uint256) {
        return _escrow.depositsOf(dest);
    }

    /**
     * @dev Called by the payer to store the sent amount as credit to be pulled.
     * Funds sent in this way are stored in an intermediate {Escrow} contract, so
     * there is no danger of them being spent before withdrawal.
     *
     * @param dest The destination address of the funds.
     * @param amount The amount to transfer.
     *
     * Causes the `escrow` to emit a {Deposited} event.
     */
    function _asyncTransfer(address dest, uint256 amount) internal virtual {
        _escrow.deposit{value: amount}(dest);
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (utils/escrow/Escrow.sol)

pragma solidity ^0.8.0;

import "../../access/Ownable.sol";
import "../Address.sol";

/**
 * @title Escrow
 * @dev Base escrow contract, holds funds designated for a payee until they
 * withdraw them.
 *
 * Intended usage: This contract (and derived escrow contracts) should be a
 * standalone contract, that only interacts with the contract that instantiated
 * it. That way, it is guaranteed that all Ether will be handled according to
 * the `Escrow` rules, and there is no need to check for payable functions or
 * transfers in the inheritance tree. The contract that uses the escrow as its
 * payment method should be its owner, and provide public methods redirecting
 * to the escrow's deposit and withdraw.
 */
contract Escrow is Ownable {
    using Address for address payable;

    event Deposited(address indexed payee, uint256 weiAmount);
    event Withdrawn(address indexed payee, uint256 weiAmount);

    mapping(address => uint256) private _deposits;

    function depositsOf(address payee) public view returns (uint256) {
        return _deposits[payee];
    }

    /**
     * @dev Stores the sent amount as credit to be withdrawn.
     * @param payee The destination address of the funds.
     *
     * Emits a {Deposited} event.
     */
    function deposit(address payee) public payable virtual onlyOwner {
        uint256 amount = msg.value;
        _deposits[payee] += amount;
        emit Deposited(payee, amount);
    }

    /**
     * @dev Withdraw accumulated balance for a payee, forwarding all gas to the
     * recipient.
     *
     * WARNING: Forwarding all gas opens the door to reentrancy vulnerabilities.
     * Make sure you trust the recipient, or are either following the
     * checks-effects-interactions pattern or using {ReentrancyGuard}.
     *
     * @param payee The address whose funds will be withdrawn and transferred to.
     *
     * Emits a {Withdrawn} event.
     */
    function withdraw(address payable payee) public virtual onlyOwner {
        uint256 payment = _deposits[payee];

        _deposits[payee] = 0;

        payee.sendValue(payment);

        emit Withdrawn(payee, payment);
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (security/ReentrancyGuard.sol)

pragma solidity ^0.8.0;

/**
 * @dev Contract module that helps prevent reentrant calls to a function.
 *
 * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier
 * available, which can be applied to functions to make sure there are no nested
 * (reentrant) calls to them.
 *
 * Note that because there is a single `nonReentrant` guard, functions marked as
 * `nonReentrant` may not call one another. This can be worked around by making
 * those functions `private`, and then adding `external` `nonReentrant` entry
 * points to them.
 *
 * TIP: If you would like to learn more about reentrancy and alternative ways
 * to protect against it, check out our blog post
 * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].
 */
abstract contract ReentrancyGuard {
    // Booleans are more expensive than uint256 or any type that takes up a full
    // word because each write operation emits an extra SLOAD to first read the
    // slot's contents, replace the bits taken up by the boolean, and then write
    // back. This is the compiler's defense against contract upgrades and
    // pointer aliasing, and it cannot be disabled.

    // The values being non-zero value makes deployment a bit more expensive,
    // but in exchange the refund on every call to nonReentrant will be lower in
    // amount. Since refunds are capped to a percentage of the total
    // transaction's gas, it is best to keep them low in cases like this one, to
    // increase the likelihood of the full refund coming into effect.
    uint256 private constant _NOT_ENTERED = 1;
    uint256 private constant _ENTERED = 2;

    uint256 private _status;

    constructor() {
        _status = _NOT_ENTERED;
    }

    /**
     * @dev Prevents a contract from calling itself, directly or indirectly.
     * Calling a `nonReentrant` function from another `nonReentrant`
     * function is not supported. It is possible to prevent this from happening
     * by making the `nonReentrant` function external, and making it call a
     * `private` function that does the actual work.
     */
    modifier nonReentrant() {
        // On the first call to nonReentrant, _notEntered will be true
        require(_status != _ENTERED, "ReentrancyGuard: reentrant call");

        // Any calls to nonReentrant after this point will fail
        _status = _ENTERED;

        _;

        // By storing the original value once again, a refund is triggered (see
        // https://eips.ethereum.org/EIPS/eip-2200)
        _status = _NOT_ENTERED;
    }
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

interface IUnboundedV4 {
  
  event NewItemtemplateCreated(uint256 baseId, uint256 minId, uint256 maxId, string uri);
  event TemplateEdited(uint256 baseId, uint256 minId, uint256 maxId, string uri);
  event MetaverseURIAdded(uint256 tokenId, uint256 metaverseId, string uri);
  event MetaverseURIReset(uint256 tokenId, uint256 metaverseId);
  event MetaverseURIUpdated(bytes signature, uint256 metaverseId, uint256 tokenId, string newUri);
  event ItemMinted(uint256 baseId, uint256 count);

  function setAuthorizedContract(address _contractAddress) external;

  function setRoyalty(address _receiver, uint256 _royaltyPercentage) external;

  function removeAuthorizedContract(address _contractAddress) external;

  function createTemplate(
    uint256 _minId,
    uint256 _maxId,
    string calldata _uri
  ) external;

  function createBatchTemplate(
    uint256[] calldata _minIds,
    uint256[] calldata _maxIds,
    string[] calldata _uris
  ) external;

  function editTemplate(
    uint256 _baseID,
    uint256 _minTokenID,
    uint256 _maxTokenID,
    string calldata _uri
  ) external;
  
  //Single Token => Single Metaverse
  function addMetadataSlotToTemplate(
    address _owner,
    uint256 _tokenId,
    uint256 _metaverseId,
    string calldata _metaverseMetadataTemplate
  ) external;

  //Single Token => Multiple Metaverses
  function addBatchMetaverseSlotsToTemplate(
    address _owner,
    uint256 _tokenId,
    uint256[] calldata _metaverseIds,
    string[] calldata _metaverseMetadataTemplate
  ) external;

  //Multiple Tokens => Single Metaverse
  function addBatchMetaverseSlotToTemplates(
    address _owner,
    uint256[] calldata _tokenIds,
    uint256 _metaverseId,
    string calldata _metaverseMetadataTemplate
  ) external;

  function updateURI(
    bytes calldata _signature,
    address _metaverseWallet,
    uint256 _metaverseId,
    uint256 _tokenId,
    string calldata _newURI,
    bytes calldata _salt
  ) external;

  function resetMetaverseURI(
    uint256 _tokenId,
    uint256 _metaverseId,
    string calldata _metaverseTemplate
  ) external;

  function recoverNFT(
    address contractAddress,
    uint256 _tokenID,
    uint256 _amount,
    bool _isERC1155
  ) external;

  //Single minting with no metadata
  function mint(address _to, uint256 _baseId) external;
  
  //Multiple Tokens with no metadata
  function mintBatch(address _to, uint256[] calldata _baseIds, uint256[] calldata _amounts) external;

  //Single Token with multiple Metadata slots
  function mintWithMetadaSlots(
    address _to,
    uint256 _baseId,
    uint256[] calldata _metaverseIds,
    string [] calldata _metaverseMetadataTemplate
  ) external;

  //Multiple Token with single metadata slot
  function mintBatchWithMetadataSlot(
    address _to,
    uint256[] calldata _baseIds,
    uint256[] calldata _amounts,
    uint256 _metaverseId,
    string calldata _metaverseMetadataTemplate
  ) external;

  function uri(uint256 _tokenId) external view returns (string memory);

  function checkItemBaseId(uint256 _tokenId) external view returns (uint256 _baseID);

  function getCreatedBaseIds() external view returns (uint256[] memory);

  function royaltyInfo(uint256 /*_tokenId*/, uint256 _salePrice) external view returns(address receiver, uint256 royaltyAmount);
  
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Address.sol)

pragma solidity ^0.8.0;

/**
 * @dev Collection of functions related to the address type
 */
library Address {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize, which returns 0 for contracts in
        // construction, since the code is only stored at the end of the
        // constructor execution.

        uint256 size;
        assembly {
            size := extcodesize(account)
        }
        return size > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCall(target, data, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        require(isContract(target), "Address: call to non-contract");

        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        require(isContract(target), "Address: static call to non-contract");

        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionDelegateCall(target, data, "Address: low-level delegate call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(isContract(target), "Address: delegate call to non-contract");

        (bool success, bytes memory returndata) = target.delegatecall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verifies that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly

                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (access/Ownable.sol)

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
        _;
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}